#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Справочники.ПретензииКлиентов.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	СозданиеНаОснованииПереопределяемый.ДобавитьКомандуСоздатьНаОснованииБизнесПроцессЗадание(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Добавляет команду создания документа "Корректировка реализации".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.КорректировкаРеализации) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.КорректировкаРеализации.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.КорректировкаРеализации);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьКорректировкиРеализаций";
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	КомандаОтчет = ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуКарточкаРасчетовСКлиентомПоДокументам(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		
	КонецЕсли;
	
	КомандаОтчет = ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСостояниеРасчетовСКлиентомПоДокументам(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		
	КонецЕсли;
	
КонецПроцедуры

#Область Серии
//Имена реквизитов, от значений которых зависят параметры указания серий
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	ИменаРеквизитов = "ДокументОснование,Склад,Статус,Дата";
	
	Возврат ИменаРеквизитов;
КонецФункции

//Возвращает параметры указания серий для товаров, указанных в документе
//
// Параметры:
//  Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
//
// Возвращаемое значение:
//  Структура - Состав полей задается в функции ОбработкаТабличнойЧастиКлиентСервер.ПараметрыУказанияСерий.
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.КорректировкаРеализации";
	
	ПараметрыСерийСклада = СкладыСервер.ИспользованиеСерийНаСкладе(Объект.Склад, Истина);
	
	ПараметрыУказанияСерий.ТолькоСерииДляСебестоимости = Истина;
	
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ПараметрыСерийСклада.УчитыватьСебестоимостьПоСериям;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ПараметрыСерийСклада.УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерий.ИмяТЧСерии = ПараметрыУказанияСерий.ИмяТЧТовары;
	
	ПараметрыУказанияСерий.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПустаяСсылка());
	
	ПараметрыУказанияСерий.ИменаПолейДополнительные.Добавить("Склад");
	
	ПараметрыУказанияСерий.ЭтоНакладная = Истина;
	
	ПараметрыУказанияСерий.Дата = Объект.Дата;
	
	Возврат ПараметрыУказанияСерий;
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий
//	Параметры:
//		ПараметрыУказанияСерий - Структура - состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий
//	Возвращаемое значение:
//		Строка - текст запроса
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Товары.Склад,
	|	Товары.Номенклатура,
	|	Товары.Серия,
	|	Товары.Количество,
	|	Товары.СтатусУказанияСерий,
	|	Товары.НомерСтроки
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|					ТОГДА ВЫБОР
	|							КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА 14
	|							ИНАЧЕ 13
	|						КОНЕЦ
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Статусы
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|			ПО ПолитикиУчетаСерий.Склад = Склады.Ссылка
	|		ПО (ПолитикиУчетаСерий.Склад = Товары.Склад)
	|			И (ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Статусы.НомерСтроки КАК НомерСтроки,
	|	Статусы.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	Статусы КАК Статусы
	|ГДЕ
	|	Статусы.СтатусУказанияСерий <> Статусы.СтарыйСтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для расчета необходимости ввода счета-фактуры выданного.
//	Возвращаемое значение:
//		Строка - текст запроса
//
Функция ТекстЗапросаДляРасчетаНеобходимостиСчетаФактуры() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка      КАК Основание,
	|	ДанныеДокумента.Дата        КАК ДатаДокумента,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Контрагент  КАК Контрагент,
	|	ДанныеДокумента.Валюта      КАК Валюта,
	|	&ТипСФВыданный              КАК ТипСчетаФактуры,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТаблицаСчетовФактуры.СчетФактура ЕСТЬ NULL
	|					И ДанныеДокумента.Проведен
	|					И %ТекстУсловия%
	|				ТОГДА ВЫБОР
	|						КОГДА УчетнаяПолитика.ПрименяетсяОсвобождениеОтУплатыНДС
	|								И ДанныеДокумента.НалогообложениеНДС <> &ПродажаОблагаетсяЕНВД
	|							ТОГДА ИСТИНА
	|						КОГДА тчТовары.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|							ТОГДА ИСТИНА
	|						КОГДА тчРасхождения.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|							ТОГДА ИСТИНА
	|						ИНАЧЕ ЛОЖЬ
	|					КОНЕЦ
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК Требуется
	|//ОператорПОМЕСТИТЬ
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСчетовФактуры КАК ТаблицаСчетовФактуры
	|		ПО ДанныеДокумента.Ссылка = ТаблицаСчетовФактуры.Основание
	|			И ДанныеДокумента.Организация = ТаблицаСчетовФактуры.Организация
	|			И ДанныеДокумента.Контрагент = ТаблицаСчетовФактуры.Контрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитика КАК УчетнаяПолитика
	|		ПО ДанныеДокумента.Ссылка = УчетнаяПолитика.Ссылка
	|			И ДанныеДокумента.Организация = УчетнаяПолитика.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Товары КАК тчТовары
	|		ПО ДанныеДокумента.Ссылка = тчТовары.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Расхождения КАК тчРасхождения
	|		ПО ДанныеДокумента.Ссылка = тчРасхождения.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивОснований)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Контрагент,
	|	ДанныеДокумента.Валюта
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ТекстУсловия%", ТекстУсловияТребуетсяСчетФактура());
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст условия запроса, по которым документу требуется счет-фактура.
// Условие применяется для таблица "ДанныеДокумента", содержащую реквизиты документа.
//
//	Возвращаемое значение:
//		Строка - текст условия запроса
//
Функция ТекстУсловияТребуетсяСчетФактура() Экспорт
	
	ТекстУсловия = "ДанныеДокумента.ВидКорректировки <>
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара)";
		
	Возврат ТекстУсловия;
	
КонецФункции

#КонецОбласти

#Область ОснованиеДляПечати

// Возвращает структуру основания по данными документа
//
// Параметры:
//	Объект - ДанныеФормыСтруктура, ДокументОбъект.КорректировкаРеализации - Объект документа, по которму необходимо получить текст основания
//
// Возвращаемое значение:
//	СтруктураОснования - Структура с наименованием, датой и номером основания
//
Функция СтруктураОснованияДляПечати(Объект) Экспорт
	
	СтруктураОснования = СтруктураОснования(Объект, Объект.ПорядокРасчетов);
	
	Возврат СтруктураОснования;
	
КонецФункции

// Возвращает таблицу значений по умолчанию для реквизита "Основание"
//
// Параметры:
//	Объект - ДанныеФормыСтруктура, ДокументОбъект.КорректировкаРеализации - Объект документа, по которму необходимо получить список выбора
//
// Возвращаемое значение:
//	ТаблицаОснований - Таблица значений с реквизитами оснований
//
Функция ТаблицаОснованийДляПечати(Объект) Экспорт
	
	ТаблицаОснований = Новый ТаблицаЗначений;
	ТаблицаОснований.Колонки.Добавить("Основание",      Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(300)));
	ТаблицаОснований.Колонки.Добавить("ОснованиеДата",  Новый ОписаниеТипов("Дата",,,,,Новый КвалификаторыДаты(ЧастиДаты.Дата))); 
	ТаблицаОснований.Колонки.Добавить("ОснованиеНомер", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(128)));
	
	СтруктураОснования = СтруктураОснования(Объект, Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов);
	Если ЗначениеЗаполнено(СтруктураОснования.Основание) Тогда
		ДобавленнаяСтрока = ТаблицаОснований.Добавить();
		ЗаполнитьЗначенияСвойств(ДобавленнаяСтрока, СтруктураОснования);
	КонецЕсли;
	
	СтруктураОснования = СтруктураОснования(Объект, Перечисления.ПорядокРасчетов.ПоЗаказамНакладным);
	Если ЗначениеЗаполнено(СтруктураОснования.Основание) Тогда
		ДобавленнаяСтрока = ТаблицаОснований.Добавить();
		ЗаполнитьЗначенияСвойств(ДобавленнаяСтрока, СтруктураОснования);
	КонецЕсли;
	
	Возврат ТаблицаОснований;
	
КонецФункции

#КонецОбласти


// Возвращает текст запроса для получениях доступных назначений
//	Параметры:
//		ПараметрыФормированияЗапроса - Структура - параметры для формирования текстов запросов
//	Возвращаемое значение:
//		Строка - текст запроса
//
Функция ТекстЗапросаДоступныхНазначений(ПараметрыФормированияЗапроса) Экспорт
	
	Возврат Справочники.Назначения.ТекстЗапросаВсехНазначений(ПараметрыФормированияЗапроса);
	
КонецФункции

Функция ОписаниеФормыДокументаДляЗаполненияРеквизитовСвязанныхСНаправлениемДеятельности() Экспорт
	
	СтруктураОбъекта = НаправленияДеятельностиСервер.СтруктураОбъекта();
	СтруктураОбъекта.Вставить("ВТЧНазначениеОтгрузки", Истина);
	Возврат СтруктураОбъекта;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента      = "Документ.КорректировкаРеализации";
	СинонимТаблицыДокумента = "";
	ВЗапросеЕстьИсточник    = Истина;
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать",        """""");
	ПереопределениеРасчетаПараметров.Вставить("ИнформацияПоДоговору", """""");
	ПереопределениеРасчетаПараметров.Вставить("ХозОперацияОснования",
		"	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ЕСТЬNULL(ДанныеДокумента.ДокументОснование, НЕОПРЕДЕЛЕНО)) = ТИП(Документ.АктВыполненныхРабот)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
		|		ИНАЧЕ
		|			ЕСТЬNULL(ДанныеДокумента.ДокументОснование.ХозяйственнаяОперация, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка))
		|	КОНЕЦ");
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента));
	
	Если ИмяРегистра = "СвободныеОстатки" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ТаблицаРасхождения";
		
	ИначеЕсли ИмяРегистра = "ОбеспечениеЗаказов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаОбеспечениеЗаказов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ТаблицаРасхождения";
		
	ИначеЕсли ИмяРегистра = "ТоварыНаСкладах" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаТоварыНаСкладах(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ТаблицаРасхождения";
		
	ИначеЕсли ИмяРегистра = "ТоварыКОформлениюИзлишковНедостач" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаТоварыКОформлениюИзлишковНедостач(Запрос, ТекстыЗапроса, ИмяРегистра); 
		СинонимТаблицыДокумента = "ТаблицаРасхождения";
		
	ИначеЕсли ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ВЗапросеЕстьИсточник,
			ПереопределениеРасчетаПараметров);
		
	Иначе
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ПереопределениеРасчетаПараметров);
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт

	// Создание запроса инициализации движений и заполенение его параметров.
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);

	// Формирование текста запроса.
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаОбеспечениеЗаказов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыНаСкладах(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыКОформлениюИзлишковНедостач(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПереданнаяВозвратнаяТара(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыОрганизацийКПередаче(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыКОформлениюОтчетовКомитенту(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаВыручкаИСебестоимостьПродаж(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРасчетыСКлиентами(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл(Запрос, ТекстыЗапроса, Регистры);

	ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДвиженияКонтрагентДоходыРасходы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаНДСЗаписиКнигиПродаж(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаНДСПредъявленный(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаУслугиКОформлениюОтчетовПринципалу(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
	
	// Исполнение запроса и выгрузка полученных таблиц для движений.
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);

КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)

	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка                                      КАК Ссылка,
		|	ДанныеДокумента.Дата                                        КАК Период,
		|	ДанныеДокумента.ДокументОснование                           КАК ДокументОснование,
		|	ЕСТЬNULL(ДанныеДокумента.ДокументОснование.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка))КАК ГруппаФинансовогоУчета,
		|	ДанныеДокумента.ДокументОснование.Дата                      КАК ПериодПродажи,
		|	ДанныеДокумента.Организация                                 КАК Организация,
		|	ДанныеДокумента.Партнер                                     КАК Партнер,
		|	ДанныеДокумента.Контрагент                                  КАК Контрагент,
		|	ДанныеДокумента.Подразделение                               КАК Подразделение,
		|	ДанныеДокумента.ПодразделениеДоходы                         КАК ПодразделениеДоходы,
		|	ДанныеДокумента.ПодразделениеРасходы                        КАК ПодразделениеРасходы,
		|	ЕСТЬNULL(ДанныеДокумента.Подразделение.ВариантОбособленногоУчетаТоваров, ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПустаяСсылка)) КАК ВариантОбособленногоУчетаТоваров,
		|	ДанныеДокумента.СтатьяРасходов                              КАК СтатьяРасходов,
		|	ЕСТЬNULL(ДанныеДокумента.СтатьяРасходов.ВариантРаспределенияРасходовРегл, ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)) КАК ВариантРаспределенияРасходовРегл,
		|	ЕСТЬNULL(ДанныеДокумента.СтатьяРасходов.ВариантРаздельногоУчетаНДС, ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)) КАК ВариантРаздельногоУчетаНДС,
		|	ДанныеДокумента.АналитикаРасходов                           КАК АналитикаРасходов,
		|	ДанныеДокумента.СтатьяДоходов                               КАК СтатьяДоходов,
		|	ДанныеДокумента.АналитикаДоходов                            КАК АналитикаДоходов,
		|	ДанныеДокумента.ВалютаВзаиморасчетов                        КАК ВалютаВзаиморасчетов,
		|	ДанныеДокумента.Валюта                                      КАК Валюта,
		|	ДанныеДокумента.ПродажаПоЗаказам                            КАК ПродажаПоЗаказам,
		|	ДанныеДокумента.НалогообложениеНДС                          КАК НалогообложениеНДС,
		|	ЕСТЬNULL(ДанныеДокумента.ДокументОснование.НалогообложениеНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) КАК ОснованиеНалогообложениеНДС,
		|	ДанныеДокумента.ДатаПлатежа                                 КАК ДатаПлатежа,
		|	ДанныеДокумента.ФормаОплаты                                 КАК ФормаОплаты,
		|	ДанныеДокумента.Соглашение                                  КАК Соглашение,
		|	ДанныеДокумента.Договор                                     КАК Договор,
		|	ДанныеДокумента.ВидКорректировки                            КАК ВидКорректировки,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ЕСТЬNULL(ДанныеДокумента.ДокументОснование, НЕОПРЕДЕЛЕНО)) = ТИП(Документ.АктВыполненныхРабот)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
		|		ИНАЧЕ
		|			ЕСТЬNULL(ДанныеДокумента.ДокументОснование.ХозяйственнаяОперация, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка))
		|	КОНЕЦ                                                       КАК ХозОперацияОснования,
		|	ДанныеДокумента.Менеджер                                    КАК Менеджер,
		|	ДанныеДокумента.Сделка 										КАК Сделка,
		|	ЕСТЬNULL(ДанныеДокумента.Сделка.ОбособленныйУчетТоваровПоСделке, ЛОЖЬ) КАК ОбособленныйУчетТоваровПоСделке,
		|	ЕСТЬNULL(ДанныеДокумента.ДокументОснование.РеализацияПоЗаказам,
		|		ЛОЖЬ)                                                   КАК РеализацияПоЗаказам,
		|	ВЫБОР
		|		КОГДА
		|			ДанныеДокумента.ДокументОснование ССЫЛКА Документ.РеализацияТоваровУслуг
		|		ТОГДА
		|			ДанныеДокумента.ДокументОснование.ВернутьМногооборотнуюТару
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ВернутьМногооборотнуюТару,
		|	ВЫБОР
		|		КОГДА
		|			ДанныеДокумента.ДокументОснование ССЫЛКА Документ.РеализацияТоваровУслуг
		|		ТОГДА
		|			ДанныеДокумента.ДокументОснование.ТребуетсяЗалогЗаТару
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ТребуетсяЗалогЗаТару,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов) ТОГДА
		|		ИСТИНА
		|	ИНАЧЕ
		|		ЛОЖЬ
		|	КОНЕЦ КАК РасчетыПоДоговорам,
		|	
		|	ВЫБОР КОГДА ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным) ТОГДА
		|		ИСТИНА
		|	ИНАЧЕ
		|		ЛОЖЬ
		|	КОНЕЦ КАК РасчетыПоНакладным,
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ДокументОснование.ХозяйственнаяОперация = 
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
		|		ТОГДА 
		|			ИСТИНА
		|		ИНАЧЕ 
		|			ЛОЖЬ
		|	КОНЕЦ КАК КорректировкаОтгрузкиБезПереходаПраваСобственности,
		|	ЕСТЬNULL(ДанныеДокумента.ДокументОснование.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		|	ВЫБОР КОГДА ДанныеДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
		|		И ЕСТЬNULL(ДанныеДокумента.Договор.ЗаданГрафикИсполнения, ЛОЖЬ) ТОГДА
		|		Истина
		|	ИНАЧЕ
		|		Ложь
		|	КОНЕЦ КАК ГрафикИсполненияВДоговоре,
		|	ДанныеДокумента.Номер                  КАК Номер,
		|	ДанныеДокумента.Склад                  КАК Склад,
		|	ДанныеДокумента.СуммаДокумента         КАК СуммаДокумента,
		|	ДанныеДокумента.Комментарий            КАК Комментарий,
		|	ДанныеДокумента.ПометкаУдаления        КАК ПометкаУдаления,
		|	ДанныеДокумента.Проведен               КАК Проведен
		|
		|ИЗ
		|	Документ.КорректировкаРеализации КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ИспользоватьУчетПрочихДоходовРасходов", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов")); 
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета"));
	Запрос.УстановитьПараметр("ИспользуетсяНазначение", ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьТоваровПоВидамЗапасов"));
	Запрос.УстановитьПараметр("ИспользоватьРасширенныеВозможностиЗаказаКлиента", 
		ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"));
	
	КорректировкаПрошлогоПериода = Ложь;
	КорректировкаТекущегоПериода = Ложь;
	Если ЗначениеЗаполнено(Реквизиты.ПериодПродажи) Тогда
		Если Реквизиты.ПериодПродажи < НачалоМесяца(Реквизиты.Период) Тогда
			КорректировкаПрошлогоПериода = Истина;
		Иначе
			КорректировкаТекущегоПериода = Истина;
		КонецЕсли;
	КонецЕсли;
	Запрос.УстановитьПараметр("КорректировкаПрошлогоПериода", КорректировкаПрошлогоПериода);
	Запрос.УстановитьПараметр("КорректировкаТекущегоПериода", КорректировкаТекущегоПериода);
	Запрос.УстановитьПараметр("ИспользоватьПродажуАгентскихУслуг", ПолучитьФункциональнуюОпцию("ИспользоватьПродажуАгентскихУслуг"));
	Если ЗначениеЗаполнено(Реквизиты.Договор) Тогда
		ИнформацияПоДоговору = НСтр("ru='По договору ""%Договор%""'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ИнформацияПоДоговору = СтрЗаменить(ИнформацияПоДоговору, "%Договор%", Реквизиты.Договор);
	КонецЕсли;
	Запрос.УстановитьПараметр("ИнформацияПоДоговору",             ИнформацияПоДоговору);
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",          ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(ДокументСсылка)));
	Запрос.УстановитьПараметр("НомерНаПечать",                    ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	
	УчетнаяПолитикаДляДокумента = РегистрыСведений.УчетнаяПолитикаОрганизаций.ПараметрыУчетнойПолитики(Реквизиты.Организация, Реквизиты.Период);
	Если УчетнаяПолитикаДляДокумента = Неопределено Тогда
		Запрос.УстановитьПараметр("ПрименяетсяОсвобождениеОтУплатыНДС", Ложь);
	Иначе	
		Запрос.УстановитьПараметр("ПрименяетсяОсвобождениеОтУплатыНДС", УчетнаяПолитикаДляДокумента.ПрименяетсяОсвобождениеОтУплатыНДС);
	КонецЕсли;
	
	УниверсальныеМеханизмыПартийИСебестоимости.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Запрос)
	
	Если Запрос.Параметры.Свойство("КлючиАналитикиУчетаНоменклатурыИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросАналитик = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Номенклатура         КАК Номенклатура,
	|	Таблица.Характеристика       КАК Характеристика,
	|	Таблица.Назначение           КАК Назначение,
	|	Таблица.Серия                КАК Серия,
	|	Таблица.Склад                КАК Склад
	|ИЗ
	|	(
	|		ВЫБРАТЬ
	|			Ключи.Номенклатура         КАК Номенклатура,
	|			Ключи.Характеристика       КАК Характеристика,
	|			&ПустоеНазначение          КАК Назначение,
	|			Ключи.Серия                КАК Серия,
	|			Ключи.Склад                КАК Склад
	|		ИЗ
	|			Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК Товары
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Ключи
	|			ПО Товары.АналитикаУчетаНоменклатуры = Ключи.КлючАналитики
	|		ГДЕ
	|			Товары.Ссылка = &Ссылка
	|			И Не &ИспользуетсяНазначение
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|	
	|		ВЫБРАТЬ
	|			Ключи.Номенклатура         КАК Номенклатура,
	|			Ключи.Характеристика       КАК Характеристика,
	|			&ПустоеНазначение          КАК Назначение,
	|			Ключи.Серия                КАК Серия,
	|			Ключи.Склад                КАК Склад
	|		ИЗ
	|			Документ.КорректировкаРеализации.Расхождения КАК Товары
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Ключи
	|			ПО Товары.АналитикаУчетаНоменклатуры = Ключи.КлючАналитики
	|		ГДЕ
	|			Товары.Ссылка = &Ссылка
	|			И Товары.Номенклатура.ТипНоменклатуры В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|			И Не &ИспользуетсяНазначение
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|	
	|		ВЫБРАТЬ
	|			Ключи.Номенклатура         КАК Номенклатура,
	|			Ключи.Характеристика       КАК Характеристика,
	|			&ПустоеНазначение          КАК Назначение,
	|			Ключи.Серия                КАК Серия,
	|			Ключи.Склад                КАК Склад
	|		ИЗ
	|			Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК Товары
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Ключи
	|			ПО Товары.АналитикаУчетаНоменклатуры = Ключи.КлючАналитики
	|		ГДЕ
	|			Товары.Ссылка = &Ссылка
	|			И Не &ИспользуетсяНазначение
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|	
	|		ВЫБРАТЬ
	|			Ключи.Номенклатура         КАК Номенклатура,
	|			Ключи.Характеристика       КАК Характеристика,
	|			&ПустоеНазначение          КАК Назначение,
	|			Ключи.Серия                КАК Серия,
	|			Товары.СкладОтгрузки       КАК Склад
	|		ИЗ
	|			Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК Товары
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Ключи
	|			ПО Товары.АналитикаУчетаНоменклатуры = Ключи.КлючАналитики
	|		ГДЕ
	|			Товары.Ссылка = &Ссылка
	|			И Не &ИспользуетсяНазначение
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|	
	|		ВЫБРАТЬ
	|			Ключи.Номенклатура         КАК Номенклатура,
	|			Ключи.Характеристика       КАК Характеристика,
	|			Ключи.Назначение           КАК Назначение,
	|			Ключи.Серия                КАК Серия,
	|			Товары.ВидЗапасов.ВладелецТовара КАК Склад
	|		ИЗ
	|			Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК Товары
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Ключи
	|			ПО Товары.АналитикаУчетаНоменклатуры = Ключи.КлючАналитики
	|		ГДЕ
	|			Товары.Ссылка = &Ссылка
	|			И Товары.ВидЗапасов.ВладелецТовара <> НЕОПРЕДЕЛЕНО
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|	
	|		ВЫБРАТЬ
	|			Ключи.Номенклатура         КАК Номенклатура,
	|			Ключи.Характеристика       КАК Характеристика,
	|			&ПустоеНазначение          КАК Назначение,
	|			Ключи.Серия                КАК Серия,
	|			Ключи.Склад                КАК Склад
	|		ИЗ
	|			Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК Товары
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Ключи
	|			ПО Товары.АналитикаУчетаНоменклатуры = Ключи.КлючАналитики
	|		ГДЕ
	|			Товары.Ссылка = &Ссылка
	|			И Не &ИспользуетсяНазначение
	|	) КАК Таблица
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО Таблица.Номенклатура = Аналитика.Номенклатура
	|		И Таблица.Характеристика = Аналитика.Характеристика
	|		И Таблица.Серия = Аналитика.Серия
	|		И Таблица.Склад = Аналитика.Склад
	|		И Таблица.Назначение = Аналитика.Назначение
	|ГДЕ
	|	Аналитика.КлючАналитики ЕСТЬ NULL
	|");
	
	ЗапросАналитик.УстановитьПараметр("Ссылка", Запрос.Параметры.Ссылка);
	ЗапросАналитик.УстановитьПараметр("ПустоеНазначение", Справочники.Назначения.ПустаяСсылка());
	ЗапросАналитик.УстановитьПараметр("ИспользуетсяНазначение", Запрос.Параметры.ИспользуетсяНазначение);
	
	Выборка = ЗапросАналитик.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КлючиАналитикиУчетаНоменклатурыИнициализированы", Истина);
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос)
	
	Если Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуУпр")
		И Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуРегл") Тогда
		Возврат;
	КонецЕсли;
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(Запрос.Параметры.Валюта,
																				Запрос.Параметры.ВалютаВзаиморасчетов,
																				Запрос.Параметры.ПериодПродажи);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",           Коэффициенты.КоэффициентПересчетаВВалютуУПР);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл",          Коэффициенты.КоэффициентПересчетаВВалютуРегл);

КонецПроцедуры

Процедура УстановитьПараметрыЗапросаАналитикаУчетаПоПартнерам(Запрос)
	
	Если Запрос.Параметры.Свойство("АналитикаУчетаПоПартнерам") Тогда
		Возврат;
	КонецЕсли;
	
	АналитикаУчетаПоПартнерам = РегистрыСведений.АналитикаУчетаПоПартнерам.ЗначениеКлючаАналитики(Запрос.Параметры);
	
	Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам", АналитикаУчетаПоПартнерам);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "СвободныеОстатки";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ТаблицаРасхождения.Склад КАК Склад,
	|	ТаблицаРасхождения.Номенклатура КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика КАК Характеристика,
	|	ТаблицаРасхождения.Количество КАК ВНаличии,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			ТОГДА ТаблицаРасхождения.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВРезервеПодЗаказ
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ТаблицаРасхождения.ВариантОтражения В (ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УвеличитьРеализациюУменьшитьСкладскиеОстатки), ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУвеличитьСкладскиеОстатки))
	|	И ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаОбеспечениеЗаказов(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ОбеспечениеЗаказов";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	ТаблицаРасхождения.Склад                    КАК Склад,
	|	ТаблицаРасхождения.Номенклатура             КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика           КАК Характеристика,
	|	ТаблицаРасхождения.Назначение               КАК Назначение,
	|	ТаблицаРасхождения.Количество               КАК НаличиеПодЗаказ,
	|	0                                           КАК КЗаказу,
	|	ВЫБОР КОГДА ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУвеличитьСкладскиеОстатки) ТОГДА
	|				ИСТИНА
	|			ИНАЧЕ
	|				ЛОЖЬ
	|		КОНЕЦ                                   КАК КонтролироватьТолькоНаличиеПодЗаказ
	|
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|	
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|	И ТаблицаРасхождения.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И ТаблицаРасхождения.ВариантОтражения В(
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УвеличитьРеализациюУменьшитьСкладскиеОстатки),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУвеличитьСкладскиеОстатки))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	ТаблицаРасхождения.Склад                    КАК Склад,
	|	ТаблицаРасхождения.Номенклатура             КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика           КАК Характеристика,
	|	ТаблицаРасхождения.Назначение               КАК Назначение,
	|	0                                           КАК НаличиеПодЗаказ,
	|	ТаблицаРасхождения.Количество               КАК КЗаказу,
	|	ВЫБОР КОГДА ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУвеличитьСкладскиеОстатки) ТОГДА
	|				ИСТИНА
	|			ИНАЧЕ
	|				ЛОЖЬ
	|		КОНЕЦ                                   КАК КонтролироватьТолькоНаличиеПодЗаказ
	|
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|	
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|	И ТаблицаРасхождения.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И ТаблицаРасхождения.ВариантОтражения В(
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УвеличитьРеализациюУменьшитьСкладскиеОстатки),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУвеличитьСкладскиеОстатки))";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаТоварыНаСкладах(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ТоварыНаСкладах";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ТаблицаРасхождения.Склад КАК Склад,
	|	ТаблицаРасхождения.Номенклатура КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика КАК Характеристика,
	|	ТаблицаРасхождения.Серия КАК Серия,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Назначение.ДвиженияПоСкладскимРегистрам
	|			ТОГДА ТаблицаРасхождения.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ КАК Назначение,
	|	ТаблицаРасхождения.Количество КАК ВНаличии,
	|	ИСТИНА КАК КонтролироватьОстатки
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ТаблицаРасхождения.ВариантОтражения В (ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УвеличитьРеализациюУменьшитьСкладскиеОстатки), ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУвеличитьСкладскиеОстатки))";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаТоварыКОформлениюИзлишковНедостач(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыКОформлениюИзлишковНедостач";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ТаблицаРасхождения.Склад КАК Склад,
	|	ТаблицаРасхождения.Номенклатура КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Назначение.ДвиженияПоСкладскимРегистрам
	|			ТОГДА ТаблицаРасхождения.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ КАК Назначение,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СтатусУказанияСерий = 14
	|			ТОГДА ТаблицаРасхождения.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Серия,
	|	-ТаблицаРасхождения.Количество КАК КОформлениюАктов
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ТаблицаРасхождения.ВариантОтражения В (ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УвеличитьРеализациюУчестьПриИнвентаризации), ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУчестьПриИнвентаризации))";
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаВтТаблицаВидыЗапасовСписание(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаВидыЗапасовСписание";
	ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Запрос);
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	ТекстЗапроса = " 
	|ВЫБРАТЬ
	|	ВидыЗапасов.НомерСтроки                  КАК НомерСтроки,
	|	Аналитика.Номенклатура                   КАК Номенклатура,
	|	Аналитика.Номенклатура.ТипНоменклатуры   КАК ТипНоменклатуры,
	|	ЕСТЬNULL(ВидыЗапасов.КодТНВЭД.СырьевойТовар,ЛОЖЬ) КАК СырьевойТовар,
	|	Аналитика.Характеристика                 КАК Характеристика,
	|	ВидыЗапасов.ВидЗапасов                   КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасов.ТипЗапасов        КАК ТипЗапасов,
	|	ВидыЗапасов.ВидЗапасов.Валюта            КАК Валюта,
	|	ВидыЗапасов.НомерГТД                     КАК НомерГТД,
	|	ВидыЗапасов.Количество                   КАК Количество,
	|	ВидыЗапасов.СуммаСНДС                    КАК СуммаСНДС,
	|	ВидыЗапасов.СуммаНДС                     КАК СуммаНДС,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВидыЗапасов.СуммаВзаиморасчетов
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((ВидыЗапасов.СуммаСНДС * &КоэффициентПересчетаВВалютуУпр) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаСНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВидыЗапасов.СуммаВзаиморасчетов - 
	|				ВЫБОР КОГДА ВидыЗапасов.СуммаСНДС <> 0 ТОГДА
	|					ВЫРАЗИТЬ(ВидыЗапасов.СуммаВзаиморасчетов * ВидыЗапасов.СуммаНДС / ВидыЗапасов.СуммаСНДС КАК ЧИСЛО(15, 2))
	|				ИНАЧЕ
	|					0
	|				КОНЕЦ
	|	ИНАЧЕ
	|		ВЫРАЗИТЬ(((ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаБезНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВидыЗапасов.СуммаВзаиморасчетов - 
	|				ВЫБОР КОГДА ВидыЗапасов.СуммаСНДС <> 0 ТОГДА
	|					ВЫРАЗИТЬ(ВидыЗапасов.СуммаВзаиморасчетов * ВидыЗапасов.СуммаНДС / ВидыЗапасов.СуммаСНДС КАК ЧИСЛО(15, 2))
	|				ИНАЧЕ
	|					0
	|				КОНЕЦ
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(((ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВЫБОР КОГДА ВидыЗапасов.СуммаСНДС <> 0 ТОГДА
	|				ВЫРАЗИТЬ(ВидыЗапасов.СуммаВзаиморасчетов * ВидыЗапасов.СуммаНДС / ВидыЗапасов.СуммаСНДС КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((ВидыЗапасов.СуммаНДС * &КоэффициентПересчетаВВалютуРегл) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК НДСРегл,
	|
	|	ВидыЗапасов.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ВЫБОР КОГДА ВидыЗапасов.СуммаСНДС <> 0 ТОГДА
	|		ВЫРАЗИТЬ(ВидыЗапасов.СуммаВзаиморасчетов * ВидыЗапасов.СуммаНДС / ВидыЗапасов.СуммаСНДС КАК ЧИСЛО(15, 2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНДСВзаиморасчетов,
	|
	|	ВидыЗапасов.Склад                        КАК Склад,
	|	ВидыЗапасов.Склад.ЦеховаяКладовая        КАК ЦеховаяКладовая,
	|	ВидыЗапасов.ЗаказКлиента                 КАК ЗаказКлиента,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры   КАК АналитикаУчетаНоменклатуры,
	|	АналитикаБезНазначения.КлючАналитики     КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	Аналитика.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВидыЗапасов.АналитикаУчетаНаборов        КАК АналитикаУчетаНаборов,
	|	Справочник.РеализацияЗапасовДругойОрганизации КАК РеализацияЗапасовДругойОрганизации,
	|	Справочник.ВидЗапасовВладельца           КАК ВидЗапасовВладельца,
	|	КлючиКомитента.КлючАналитики             КАК АналитикаКомитента,
	|	ВидыЗапасов.НаДоходыРасходы              КАК НаДоходыРасходы,
	|	ВидыЗапасов.ИдентификаторСтроки          КАК ИдентификаторСтроки,
	|	ВидыЗапасов.СтавкаНДС                    КАК СтавкаНДС,
	|
	|	(ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоСделкам И &ОбособленныйУчетТоваровПоСделке
	|			ТОГДА &Сделка
	|		КОГДА &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|		 И &ВариантОбособленногоУчетаТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоМенеджерамПодразделения)
	|			ТОГДА &Менеджер
	|		КОГДА &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|		 И &ВариантОбособленногоУчетаТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоПодразделению)
	|			ТОГДА &Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ) КАК АналитикаФинансовогоУчета,
	|	(ВЫБОР
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт) ТОГДА 
	|			ВЫБОР КОГДА ЕСТЬNULL(ВидыЗапасов.КодТНВЭД.СырьевойТовар,ЛОЖЬ)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров) КОНЕЦ
	|		КОГДА ВидыЗапасов.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|		 И &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		ИНАЧЕ &НалогообложениеНДС
	|	КОНЕЦ) КАК ВидДеятельностиНДС
	|
	|ПОМЕСТИТЬ ВтТаблицаВидыЗапасовСписание
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК ВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО 
	|		ВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК Справочник
	|	ПО
	|		ВидыЗапасов.ВидЗапасов = Справочник.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО
	|		Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
	|		И Аналитика.Серия = АналитикаБезНазначения.Серия
	|		И Аналитика.Склад = АналитикаБезНазначения.Склад
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиКомитента
	|	ПО
	|		Аналитика.Номенклатура = КлючиКомитента.Номенклатура
	|		И Аналитика.Характеристика = КлючиКомитента.Характеристика
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = КлючиКомитента.Серия
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = КлючиКомитента.Назначение
	|		И ВидыЗапасов.ВидЗапасов.ВладелецТовара = КлючиКомитента.Склад
	|		
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаРасхождения(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаРасхождения";
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаАналитикУчетаПартий", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаАналитикУчетаПартий(Запрос, ТекстыЗапроса);
	КонецЕсли; 

	ТекстЗапроса = " 
	|ВЫБРАТЬ
	|	Расхождения.НомерСтроки                  КАК НомерСтроки,
	|	Расхождения.Номенклатура КАК Номенклатура,
	|	Аналитика.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			Расхождения.СуммаВзаиморасчетов
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((Расхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуУпр) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаСНДСУпр,
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			Расхождения.СуммаВзаиморасчетов - 
	|				ВЫБОР КОГДА Расхождения.СуммаСНДС <> 0 ТОГДА
	|					ВЫРАЗИТЬ(Расхождения.СуммаВзаиморасчетов * Расхождения.СуммаНДС / Расхождения.СуммаСНДС КАК ЧИСЛО(15, 2))
	|				ИНАЧЕ
	|					0
	|				КОНЕЦ
	|	ИНАЧЕ
	|		ВЫРАЗИТЬ(((Расхождения.СуммаСНДС - Расхождения.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаБезНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			Расхождения.СуммаВзаиморасчетов - 
	|				ВЫБОР КОГДА Расхождения.СуммаСНДС <> 0 ТОГДА
	|					ВЫРАЗИТЬ(Расхождения.СуммаВзаиморасчетов * Расхождения.СуммаНДС / Расхождения.СуммаСНДС КАК ЧИСЛО(15, 2))
	|				ИНАЧЕ
	|					0
	|				КОНЕЦ
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(((Расхождения.СуммаСНДС - Расхождения.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВЫБОР КОГДА Расхождения.СуммаСНДС <> 0 ТОГДА
	|				ВЫРАЗИТЬ(Расхождения.СуммаВзаиморасчетов * Расхождения.СуммаНДС / Расхождения.СуммаСНДС КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((Расхождения.СуммаНДС * &КоэффициентПересчетаВВалютуРегл) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК НДСРегл,
	|
	|	ВЫБОР КОГДА Расхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.СписатьНаРасходы)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НаРасходы,
	|	ВЫБОР КОГДА Расхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.ОтразитьНаПрочихДоходах)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НаДоходы,
	|
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	Расхождения.ЗаказКлиента                 КАК ЗаказКлиента,
	|	Расхождения.Количество                   КАК Количество,
	|	Расхождения.СуммаСНДС                    КАК СуммаСНДС,
	|	Расхождения.СуммаНДС                     КАК СуммаНДС,
	|	Расхождения.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ВЫБОР КОГДА Расхождения.СуммаСНДС <> 0 ТОГДА
	|		ВЫРАЗИТЬ(Расхождения.СуммаВзаиморасчетов * Расхождения.СуммаНДС / Расхождения.СуммаСНДС КАК ЧИСЛО(15, 2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНДСВзаиморасчетов,
	|	Расхождения.СтавкаНДС                    КАК СтавкаНДС,
	|	Расхождения.СтатьяДоходов                КАК СтатьяДоходов,
	|	Расхождения.АналитикаДоходов             КАК АналитикаДоходов,
	|	Расхождения.Ссылка                       КАК Ссылка
	|
	|ПОМЕСТИТЬ ВтТаблицаРасхождения
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК Расхождения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО 
	|		Расхождения.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартий
	|	ПО ТаблицаАналитикУчетаПартий.НомерСтроки       = Расхождения.НомерСтроки
	|	 И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""Расхождения""
	|
	|		
	|ГДЕ
	|	Расхождения.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаАналитикУчетаПартий(Запрос, ТекстыЗапроса)
	
	// Создадим временную таблицу "ВтТаблицаАналитикУчетаПартий"
	
	ТекстВыборкаПоляАналитик =
	"ВЫБРАТЬ
	|	""ВидыЗапасовОприходование""			КАК ИмяТабличнойЧасти,
	|	ТаблицаДокумента.НомерСтроки 			КАК НомерСтроки,
	|	ДанныеДокумента.Партнер					КАК Поставщик,
	|	ДанныеДокумента.Контрагент				КАК Контрагент,
	|	ТаблицаДокумента.СтавкаНДС 				КАК СтавкаНДС,
	|	ДанныеДокумента.НалогообложениеНДС		КАК НалогообложениеНДС,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|		КОГДА СпрНоменклатура.ТипНоменклатуры В
	|		  (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	КОНЕЦ                               	КАК ВидЦенности,
	|	0										КАК КодСтроки
	|ПОМЕСТИТЬ ВТПоляАналитикУчетаПартий
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаДокумента.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|		ПО ТаблицаДокумента.АналитикаУчетаНоменклатуры = АналитикаНоменклатуры.КлючАналитики
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО СпрНоменклатура.Ссылка = АналитикаНоменклатуры.Номенклатура
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Расхождения""							КАК ИмяТабличнойЧасти,
	|	ТаблицаДокумента.НомерСтроки 			КАК НомерСтроки,
	|	ДанныеДокумента.Партнер					КАК Поставщик,
	|	ДанныеДокумента.Контрагент				КАК Контрагент,
	|	ТаблицаДокумента.СтавкаНДС 				КАК СтавкаНДС,
	|	ДанныеДокумента.НалогообложениеНДС		КАК НалогообложениеНДС,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|		КОГДА СпрНоменклатура.ТипНоменклатуры В
	|		  (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	КОНЕЦ                               	КАК ВидЦенности,
	|	0										КАК КодСтроки
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаДокумента.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|		ПО ТаблицаДокумента.АналитикаУчетаНоменклатуры = АналитикаНоменклатуры.КлючАналитики
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО СпрНоменклатура.Ссылка = АналитикаНоменклатуры.Номенклатура
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|";
	
	ТекстЗапроса = Справочники.КлючиАналитикиУчетаПартий.ТекстЗапросаВтТаблицаАналитикУчетаПартий(ТекстВыборкаПоляАналитик, Запрос, ТекстыЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаВидыЗапасовОприходование";
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Запрос);
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаАналитикУчетаПартий", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаАналитикУчетаПартий(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = " 
	|ВЫБРАТЬ
	|	ВидыЗапасов.НомерСтроки                             КАК НомерСтроки,
	|	Аналитика.Номенклатура                              КАК Номенклатура,
	|	Аналитика.Номенклатура.ТипНоменклатуры              КАК ТипНоменклатуры,
	|	ЕСТЬNULL(ВидыЗапасов.КодТНВЭД.СырьевойТовар,ЛОЖЬ)   КАК СырьевойТовар,
	|	Аналитика.Характеристика                            КАК Характеристика,
	|	Аналитика.Серия                                     КАК Серия,
	|	Аналитика.Назначение                                КАК Назначение,
	|	ВидыЗапасов.ВидЗапасов                              КАК ВидЗапасов,
	|	ТаблицаАналитикУчетаПартий.ВидЦенности				КАК ВидЦенности,
	|	ТаблицаАналитикУчетаПартий.НалогообложениеНДС		КАК ВидДеятельностиНДС,
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий 	КАК АналитикаУчетаПартий,
	|	ВидыЗапасов.ВидЗапасов.ТипЗапасов                   КАК ТипЗапасов,
	|	ВидыЗапасов.ВидЗапасов.Валюта                       КАК Валюта,
	|	ВидыЗапасов.НомерГТД                     			КАК НомерГТД,
	|	ВидыЗапасов.Количество                              КАК Количество,
	|	ВидыЗапасов.СуммаСНДС					 			КАК СуммаСНДС,
	|	ВидыЗапасов.СуммаНДС					 			КАК СуммаНДС,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВидыЗапасов.СуммаВзаиморасчетов
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((ВидыЗапасов.СуммаСНДС * &КоэффициентПересчетаВВалютуУпр) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаСНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВидыЗапасов.СуммаВзаиморасчетов - 
	|				ВЫБОР КОГДА ВидыЗапасов.СуммаСНДС <> 0 ТОГДА
	|					ВЫРАЗИТЬ(ВидыЗапасов.СуммаВзаиморасчетов * ВидыЗапасов.СуммаНДС / ВидыЗапасов.СуммаСНДС КАК ЧИСЛО(15, 2))
	|				ИНАЧЕ
	|					0
	|				КОНЕЦ
	|	ИНАЧЕ
	|		ВЫРАЗИТЬ(((ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаБезНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВидыЗапасов.СуммаВзаиморасчетов - 
	|				ВЫБОР КОГДА ВидыЗапасов.СуммаСНДС <> 0 ТОГДА
	|					ВЫРАЗИТЬ(ВидыЗапасов.СуммаВзаиморасчетов * ВидыЗапасов.СуммаНДС / ВидыЗапасов.СуммаСНДС КАК ЧИСЛО(15, 2))
	|				ИНАЧЕ
	|					0
	|				КОНЕЦ
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(((ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВЫБОР КОГДА ВидыЗапасов.СуммаСНДС <> 0 ТОГДА
	|				ВЫРАЗИТЬ(ВидыЗапасов.СуммаВзаиморасчетов * ВидыЗапасов.СуммаНДС / ВидыЗапасов.СуммаСНДС КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((ВидыЗапасов.СуммаНДС * &КоэффициентПересчетаВВалютуРегл) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК НДСРегл,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВЫБОР КОГДА ВидыЗапасов.СуммаСНДС <> 0 ТОГДА
	|				ВЫРАЗИТЬ(ВидыЗапасов.СуммаВзаиморасчетов * ВидыЗапасов.СуммаНДС / ВидыЗапасов.СуммаСНДС КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((ВидыЗапасов.СуммаНДС * &КоэффициентПересчетаВВалютуУпр) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК НДСУпр,
	|
	|	ВидыЗапасов.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ВЫБОР КОГДА ВидыЗапасов.СуммаСНДС <> 0 ТОГДА
	|		ВЫРАЗИТЬ(ВидыЗапасов.СуммаВзаиморасчетов * ВидыЗапасов.СуммаНДС / ВидыЗапасов.СуммаСНДС КАК ЧИСЛО(15, 2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНДСВзаиморасчетов,
	|
	|	ВидыЗапасов.Склад                                    КАК Склад,
	|	ВидыЗапасов.Склад.ЦеховаяКладовая                    КАК ЦеховаяКладовая,
	|	ВидыЗапасов.СкладОтгрузки                            КАК СкладОтгрузки,
	|	ВидыЗапасов.ЗаказКлиента                             КАК ЗаказКлиента,
	|	ВидыЗапасов.ВидЗапасовОтгрузки                       КАК ВидЗапасовОтгрузки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры               КАК АналитикаУчетаНоменклатуры,
	|	АналитикаБезНазначения.КлючАналитики                 КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	АналитикаОтгрузки.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВидыЗапасов.АналитикаУчетаНаборов                    КАК АналитикаУчетаНаборов,
	|	АналитикаОтгрузки.КлючАналитики                      КАК АналитикаРеализации,
	|	АналитикаОтгрузкиБезНазначения.КлючАналитики         КАК АналитикаРеализацииБезНазначения,
	|	Справочник.РеализацияЗапасовДругойОрганизации        КАК РеализацияЗапасовДругойОрганизации,
	|	Справочник.ВидЗапасовВладельца                       КАК ВидЗапасовВладельца,
	|	КлючиКомитента.КлючАналитики                         КАК АналитикаКомитента,
	|	ВидыЗапасов.НаДоходыРасходы                          КАК НаДоходыРасходы,
	|	ВидыЗапасов.ИдентификаторСтроки                      КАК ИдентификаторСтроки,
	|	ВидыЗапасов.СтавкаНДС                                КАК СтавкаНДС,
	|	Операция.СтатьяРасходов                              КАК СтатьяРасходов,
	|	Операция.АналитикаРасходов                           КАК АналитикаРасходов,
	|
	|	(ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоСделкам И &ОбособленныйУчетТоваровПоСделке
	|			ТОГДА &Сделка
	|		КОГДА &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|		 И &ВариантОбособленногоУчетаТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоМенеджерамПодразделения)
	|			ТОГДА &Менеджер
	|		КОГДА &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|		 И &ВариантОбособленногоУчетаТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоПодразделению)
	|			ТОГДА &Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ) КАК АналитикаФинансовогоУчета
	|
	|ПОМЕСТИТЬ ВтТаблицаВидыЗапасовОприходование
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК Операция
	|		ПО Операция.Ссылка = ВидыЗапасов.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО 
	|		ВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО 
	|		Аналитика.Номенклатура		= АналитикаБезНазначения.Номенклатура
	|		И Аналитика.Характеристика	= АналитикаБезНазначения.Характеристика
	|		И Аналитика.Серия			= АналитикаБезНазначения.Серия
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|		И Аналитика.Склад = АналитикаБезНазначения.Склад
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаОтгрузки
	|	ПО 
	|		Аналитика.Номенклатура		= АналитикаОтгрузки.Номенклатура
	|		И Аналитика.Характеристика	= АналитикаОтгрузки.Характеристика
	|		И Аналитика.Серия			= АналитикаОтгрузки.Серия
	|		И Аналитика.Назначение		= АналитикаОтгрузки.Назначение
	|		И ВидыЗапасов.СкладОтгрузки	= АналитикаОтгрузки.Склад
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаОтгрузкиБезНазначения
	|	ПО 
	|		АналитикаОтгрузки.Номенклатура		= АналитикаОтгрузкиБезНазначения.Номенклатура
	|		И АналитикаОтгрузки.Характеристика	= АналитикаОтгрузкиБезНазначения.Характеристика
	|		И АналитикаОтгрузки.Серия			= АналитикаОтгрузкиБезНазначения.Серия
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаОтгрузкиБезНазначения.Назначение
	|		И АналитикаОтгрузки.Склад = АналитикаОтгрузкиБезНазначения.Склад
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК Справочник
	|	ПО
	|		ВидыЗапасов.ВидЗапасов = Справочник.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиКомитента
	|	ПО
	|		Аналитика.Номенклатура				= КлючиКомитента.Номенклатура
	|		И Аналитика.Характеристика			= КлючиКомитента.Характеристика
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = КлючиКомитента.Серия
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = КлючиКомитента.Назначение
	|		И ВидыЗапасов.ВидЗапасов.ВладелецТовара	= КлючиКомитента.Склад
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартий
	|	ПО ТаблицаАналитикУчетаПартий.НомерСтроки 		= ВидыЗапасов.НомерСтроки
	|	 И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""ВидыЗапасовОприходование""
	|
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтВидыЗапасовКорректировкаВыручки(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтВидыЗапасовКорректировкаВыручки";
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Запрос);
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки                                  КАК НомерСтроки,
	|	ТаблицаВидыЗапасов.ВидЗапасов                                   КАК ВидЗапасов,
	|	Справочник.ТипЗапасов                                           КАК ТипЗапасов,
	|	Справочник.ВидЗапасовВладельца                                  КАК ВидЗапасовВладельца,
	|	Справочник.Валюта                                               КАК Валюта,
	|	Аналитика.Номенклатура                                          КАК Номенклатура,
	|	Аналитика.Номенклатура.ТипНоменклатуры                          КАК ТипНоменклатуры,
	|	ЕСТЬNULL(ТаблицаВидыЗапасов.КодТНВЭД.СырьевойТовар,ЛОЖЬ)        КАК СырьевойТовар,
	|	Аналитика.Характеристика                                        КАК Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД                                     КАК НомерГТД,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры                   КАК АналитикаУчетаНоменклатуры,
	|	АналитикаБезНазначения.КлючАналитики                            КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНаборов                        КАК АналитикаУчетаНаборов,
	|	ТаблицаВидыЗапасов.ЗаказКлиента                                 КАК ЗаказКлиента,
	|
	|	ТаблицаВидыЗапасов.СуммаСНДС                                    КАК СуммаСНДС,
	|	ТаблицаВидыЗапасов.СуммаНДС                                     КАК СуммаНДС,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВЫБОР
	|				КОГДА ТаблицаВидыЗапасов.СуммаВзаиморасчетов = 0 ТОГДА
	|					ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаСНДС * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15,2))
	|				ИНАЧЕ
	|					ТаблицаВидыЗапасов.СуммаВзаиморасчетов
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((ТаблицаВидыЗапасов.СуммаСНДС * &КоэффициентПересчетаВВалютуУпр) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаСНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВЫБОР
	|				КОГДА ТаблицаВидыЗапасов.СуммаВзаиморасчетов = 0 ТОГДА
	|					ВЫРАЗИТЬ((ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15,2))
	|				ИНАЧЕ
	|					ТаблицаВидыЗапасов.СуммаВзаиморасчетов - 
	|						ВЫБОР КОГДА ТаблицаВидыЗапасов.СуммаСНДС <> 0 ТОГДА
	|							ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаВзаиморасчетов * ТаблицаВидыЗапасов.СуммаНДС / ТаблицаВидыЗапасов.СуммаСНДС КАК ЧИСЛО(15, 2))
	|						ИНАЧЕ
	|							0
	|						КОНЕЦ
	|			КОНЕЦ
	|	ИНАЧЕ
	|		ВЫРАЗИТЬ(((ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаБезНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВЫБОР
	|				КОГДА ТаблицаВидыЗапасов.СуммаВзаиморасчетов = 0 ТОГДА
	|					ВЫРАЗИТЬ((ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15,2))
	|				ИНАЧЕ ТаблицаВидыЗапасов.СуммаВзаиморасчетов - 
	|					ВЫБОР КОГДА ТаблицаВидыЗапасов.СуммаСНДС <> 0 ТОГДА
	|						ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаВзаиморасчетов * ТаблицаВидыЗапасов.СуммаНДС / ТаблицаВидыЗапасов.СуммаСНДС КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ
	|						0
	|					КОНЕЦ
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(((ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВЫБОР
	|				КОГДА ТаблицаВидыЗапасов.СуммаВзаиморасчетов = 0 ТОГДА
	|					ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15,2))
	|				ИНАЧЕ
	|					ВЫБОР КОГДА ТаблицаВидыЗапасов.СуммаСНДС <> 0 ТОГДА
	|						ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаВзаиморасчетов * ТаблицаВидыЗапасов.СуммаНДС / ТаблицаВидыЗапасов.СуммаСНДС КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ
	|						0
	|					КОНЕЦ
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((ТаблицаВидыЗапасов.СуммаНДС * &КоэффициентПересчетаВВалютуРегл) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК НДСРегл,
	|
	|	ТаблицаВидыЗапасов.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.СуммаСНДС <> 0 ТОГДА
	|		ВЫРАЗИТЬ(ТаблицаВидыЗапасов.СуммаВзаиморасчетов * ТаблицаВидыЗапасов.СуммаНДС / ТаблицаВидыЗапасов.СуммаСНДС КАК ЧИСЛО(15, 2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНДСВзаиморасчетов,
	|
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки                          КАК ИдентификаторСтроки,
	|	ТаблицаВидыЗапасов.СтавкаНДС                                    КАК СтавкаНДС,
	|	Аналитика.Склад													КАК Склад,
	|	Аналитика.Склад.ЦеховаяКладовая                    				КАК ЦеховаяКладовая,
	|	(ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоСделкам И &ОбособленныйУчетТоваровПоСделке
	|			ТОГДА &Сделка
	|		КОГДА &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|		 И &ВариантОбособленногоУчетаТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоМенеджерамПодразделения)
	|			ТОГДА &Менеджер
	|		КОГДА &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|		 И &ВариантОбособленногоУчетаТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоПодразделению)
	|			ТОГДА &Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ) КАК АналитикаФинансовогоУчета,
	|	(ВЫБОР
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт) ТОГДА 
	|			ВЫБОР КОГДА ЕСТЬNULL(ТаблицаВидыЗапасов.КодТНВЭД.СырьевойТовар,ЛОЖЬ)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров) КОНЕЦ
	|		КОГДА ТаблицаВидыЗапасов.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|		 И &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		ИНАЧЕ &НалогообложениеНДС
	|	КОНЕЦ) КАК ВидДеятельностиНДС
	|
	|ПОМЕСТИТЬ ВтВидыЗапасовКорректировкаВыручки
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК ТаблицаВидыЗапасов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО
	|		Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
	|		И Аналитика.Серия = АналитикаБезНазначения.Серия
	|		И Аналитика.Склад = АналитикаБезНазначения.Склад
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК Справочник
	|	ПО
	|		ТаблицаВидыЗапасов.ВидЗапасов = Справочник.Ссылка
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПереданнаяВозвратнаяТара(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПереданнаяВозвратнаяТара";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовСписание", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовСписание(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	0                                               КАК Порядок,
	|	ВтТаблицаВидыЗапасовСписание.НомерСтроки        КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	&Период                                         КАК Период,
	|	ВтТаблицаВидыЗапасовСписание.Номенклатура       КАК Номенклатура,
	|	ВтТаблицаВидыЗапасовСписание.Характеристика     КАК Характеристика,
	|	ВтТаблицаВидыЗапасовСписание.Количество         КАК Количество,
	|	ВтТаблицаВидыЗапасовСписание.СуммаСНДС          КАК Сумма,
	|	ВтТаблицаВидыЗапасовСписание.ВидЗапасов         КАК ВидЗапасов,
	|	ВтТаблицаВидыЗапасовСписание.НомерГТД           КАК НомерГТД,
	|	&Партнер                                        КАК Партнер,
	|	&ДокументОснование                              КАК ДокументПередачи,
	|	&ТребуетсяЗалогЗаТару                           КАК ПредусмотренЗалог
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ВтТаблицаВидыЗапасовСписание
	|ГДЕ
	|	НЕ ВтТаблицаВидыЗапасовСписание.НаДоходыРасходы
	|	И ВтТаблицаВидыЗапасовСписание.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	И &ВернутьМногооборотнуюТару
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1                                                    КАК Порядок,
	|	ВтТаблицаВидыЗапасовОприходование.НомерСтроки        КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)               КАК ВидДвижения,
	|	&Период                                              КАК Период,
	|	ВтТаблицаВидыЗапасовОприходование.Номенклатура       КАК Номенклатура,
	|	ВтТаблицаВидыЗапасовОприходование.Характеристика     КАК Характеристика,
	|	-ВтТаблицаВидыЗапасовОприходование.Количество        КАК Количество,
	|	-ВтТаблицаВидыЗапасовОприходование.СуммаСНДС         КАК Сумма,
	|	ВтТаблицаВидыЗапасовОприходование.ВидЗапасов         КАК ВидЗапасов,
	|	ВтТаблицаВидыЗапасовОприходование.НомерГТД           КАК НомерГТД,
	|	&Партнер                                             КАК Партнер,
	|	&ДокументОснование                                   КАК ДокументПередачи,
	|	&ТребуетсяЗалогЗаТару                                КАК ПредусмотренЗалог
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ВтТаблицаВидыЗапасовОприходование
	|
	|ГДЕ
	|	НЕ ВтТаблицаВидыЗапасовОприходование.НаДоходыРасходы
	|	И ВтТаблицаВидыЗапасовОприходование.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	И &ВернутьМногооборотнуюТару
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2                                                           КАК Порядок,
	|	ВтТаблицаВидыЗапасовКорректировкаВыручки.НомерСтроки        КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                      КАК ВидДвижения,
	|	&Период                                                     КАК Период,
	|	Аналитика.Номенклатура                                      КАК Номенклатура,
	|	Аналитика.Характеристика                                    КАК Характеристика,
	|	0                                                           КАК Количество,
	|	ВтТаблицаВидыЗапасовКорректировкаВыручки.СуммаСНДС          КАК Сумма,
	|	ВтТаблицаВидыЗапасовКорректировкаВыручки.ВидЗапасов         КАК ВидЗапасов,
	|	ВтТаблицаВидыЗапасовКорректировкаВыручки.НомерГТД           КАК НомерГТД,
	|	&Партнер                                                    КАК Партнер,
	|	&ДокументОснование                                          КАК ДокументПередачи,
	|	&ТребуетсяЗалогЗаТару                                       КАК ПредусмотренЗалог
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК ВтТаблицаВидыЗапасовКорректировкаВыручки
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ВтТаблицаВидыЗапасовКорректировкаВыручки.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|ГДЕ
	|	Аналитика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	И &ВернутьМногооборотнуюТару
	|	И ВтТаблицаВидыЗапасовКорректировкаВыручки.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыОрганизаций";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовСписание", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовСписание(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК ОрганизацияОтгрузки,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.РеализацияЗапасовДругойОрганизации ТОГДА
	|		ТаблицаВидыЗапасов.ВидЗапасовВладельца.Организация
	|	ИНАЧЕ
	|		&Организация
	|	КОНЕЦ КАК Организация,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.РеализацияЗапасовДругойОрганизации ТОГДА
	|		ТаблицаВидыЗапасов.ВидЗапасовВладельца
	|	ИНАЧЕ
	|		ТаблицаВидыЗапасов.ВидЗапасов
	|	КОНЕЦ КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	&ДокументОснование КАК ДокументРеализации,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
	|	&ВидКорректировки КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|			ТОГДА 
	|				ВЫБОР КОГДА ТаблицаВидыЗапасов.СырьевойТовар
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
	|				КОНЕЦ
	|		ИНАЧЕ &НалогообложениеНДС
	|	КОНЕЦ КАК НалогообложениеНДС,
	|	ЛОЖЬ КАК Первичное
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|
	|ГДЕ
	|	НЕ ТаблицаВидыЗапасов.НаДоходыРасходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Период,
	|	&Организация,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.РеализацияЗапасовДругойОрганизации ТОГДА
	|		ТаблицаВидыЗапасов.ВидЗапасовВладельца.Организация
	|	ИНАЧЕ
	|		&Организация
	|	КОНЕЦ,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.РеализацияЗапасовДругойОрганизации ТОГДА
	|		ТаблицаВидыЗапасов.ВидЗапасовВладельца
	|	ИНАЧЕ
	|		ТаблицаВидыЗапасов.ВидЗапасов
	|	КОНЕЦ,
	|	ТаблицаВидыЗапасов.Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД,
	|	&ДокументОснование,
	|	-ТаблицаВидыЗапасов.Количество,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.АналитикаРеализации КАК КорАналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасовОтгрузки <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|   		ТаблицаВидыЗапасов.ВидЗапасовОтгрузки
	|		ИНАЧЕ
	|   		ТаблицаВидыЗапасов.ВидЗапасов
	|		КОНЕЦ КАК КорВидЗапасов,
	|	&ВидКорректировки,
	|	ВЫБОР
	|		КОГДА &ОснованиеНалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|			ТОГДА 
	|				ВЫБОР КОГДА ТаблицаВидыЗапасов.СырьевойТовар
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
	|				КОНЕЦ
	|		ИНАЧЕ &ОснованиеНалогообложениеНДС
	|	КОНЕЦ КАК НалогообложениеНДС,
	|	ЛОЖЬ КАК Первичное
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|ГДЕ
	|	НЕ ТаблицаВидыЗапасов.НаДоходыРасходы
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыОрганизацийКПередаче(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыОрганизацийКПередаче";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовСписание", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовСписание(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "
	// Виды запасов к списанию
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки								  КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)						  КАК ВидДвижения,
	|	&Период														  КАК Период,
	|	ТаблицаВидыЗапасов.ВидЗапасовВладельца.Организация			  КАК ОрганизацияВладелец,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры				  КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.Номенклатура								  КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика							  КАК Характеристика,
	|	ТаблицаВидыЗапасов.ВидЗапасов								  КАК ВидЗапасовПродавца,
	|	ТаблицаВидыЗапасов.НомерГТД									  КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Количество								  КАК Количество
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|
	|ГДЕ
	|	ТаблицаВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|	И НЕ ТаблицаВидыЗапасов.НаДоходыРасходы
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтАгентскиеУслуги(ТекстыЗапроса)
	
	ИмяРегистра = "ВтАгентскиеУслуги";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР КОГДА НЕ Услуги.Ссылка ЕСТЬ NULL
	|			ТОГДА Услуги.Ссылка
	|		КОГДА НЕ Соглашение.Ссылка ЕСТЬ NULL
	|			ТОГДА Соглашение.Ссылка
	|		КОГДА НЕ УслугиСоглашениеЗакрытое.Ссылка ЕСТЬ NULL
	|			ТОГДА УслугиСоглашениеЗакрытое.Ссылка
	|		ИНАЧЕ ЕСТЬNULL(СоглашениеЗакрытое.Ссылка, НЕОПРЕДЕЛЕНО)
	|	КОНЕЦ КАК Соглашение,
	|	ВЫБОР КОГДА НЕ Услуги.Ссылка ЕСТЬ NULL
	|				ТОГДА Услуги.Ссылка.Валюта
	|		КОГДА НЕ Соглашение.Ссылка ЕСТЬ NULL
	|				ТОГДА Соглашение.Валюта
	|		КОГДА НЕ УслугиСоглашениеЗакрытое.Ссылка ЕСТЬ NULL
	|				ТОГДА УслугиСоглашениеЗакрытое.Ссылка.Валюта
	|		ИНАЧЕ ЕСТЬNULL(СоглашениеЗакрытое.Валюта, &Валюта)
	|	КОНЕЦ КАК Валюта,
	|	КОформлению.Номенклатура КАК Номенклатура,
	|	КОформлению.Характеристика КАК Характеристика,
	|	ЕСТЬNULL(КОформлению.Характеристика.Принципал, КОформлению.Номенклатура.Принципал) КАК Принципал
	|ПОМЕСТИТЬ ВтАгентскиеУслуги
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК КОформлению
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСПоставщиками КАК Соглашение
	|	ПО Соглашение.Партнер = ЕСТЬNULL(КОформлению.Характеристика.Принципал, КОформлению.Номенклатура.Принципал)
	|		И Соглашение.Организация = &Организация
	|		И Соглашение.ХозяйственнаяОперация
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОказаниеАгентскихУслуг)
	|		И Соглашение.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийСПоставщиками.Действует)
	|		И (&Период >= Соглашение.ДатаНачалаДействия
	|			И (&Период <= Соглашение.ДатаОкончанияДействия
	|			ИЛИ Соглашение.ДатаОкончанияДействия = ДАТАВРЕМЯ(1,1,1))
	|		)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСПоставщиками.АгентскиеУслуги КАК Услуги
	|	ПО Соглашение.Ссылка = Услуги.Ссылка
	|		И КОформлению.Номенклатура = Услуги.Номенклатура
	|		И КОформлению.Характеристика = Услуги.Характеристика
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСПоставщиками КАК СоглашениеЗакрытое
	|	ПО СоглашениеЗакрытое.Партнер = ЕСТЬNULL(КОформлению.Характеристика.Принципал, КОформлению.Номенклатура.Принципал)
	|		И СоглашениеЗакрытое.Организация = &Организация
	|		И СоглашениеЗакрытое.ХозяйственнаяОперация
	|				= ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОказаниеАгентскихУслуг)
	|		И СоглашениеЗакрытое.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийСПоставщиками.Закрыто)
	|		И (&Период >= СоглашениеЗакрытое.ДатаНачалаДействия
	|			И (&Период <= СоглашениеЗакрытое.ДатаОкончанияДействия
	|			ИЛИ СоглашениеЗакрытое.ДатаОкончанияДействия = ДАТАВРЕМЯ(1,1,1))
	|		)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСПоставщиками.АгентскиеУслуги КАК УслугиСоглашениеЗакрытое
	|	ПО СоглашениеЗакрытое.Ссылка = УслугиСоглашениеЗакрытое.Ссылка
	|		И КОформлению.Номенклатура = УслугиСоглашениеЗакрытое.Номенклатура
	|		И КОформлению.Характеристика = УслугиСоглашениеЗакрытое.Характеристика
	|
	|ГДЕ
	|	КОформлению.Ссылка = &Ссылка
	|	И КОформлению.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|	И ЕСТЬNULL(КОформлению.Характеристика.Принципал, КОформлению.Номенклатура.Принципал) <> &Организация
	|	И ЕСТЬNULL(КОформлению.Характеристика.Принципал, КОформлению.Номенклатура.Принципал) <> НЕОПРЕДЕЛЕНО
	|	И &ИспользоватьПродажуАгентскихУслуг";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтКурсыВалют(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "КурсыВалют";
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовСписание", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовСписание(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос,ТекстыЗапроса);
	КонецЕсли; 
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтАгентскиеУслуги", ТекстыЗапроса) Тогда
		ТекстЗапросаВтАгентскиеУслуги(ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтВидыЗапасовКорректировкаВыручки", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВидыЗапасовКорректировкаВыручки(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	КурсыВалют.Валюта КАК Валюта,
		|
		|	(КурсВалютыДокумента.Курс * КурсыВалют.Кратность)
		|   / (КурсВалютыДокумента.Кратность * КурсыВалют.Курс) КАК КоэффициентПересчета
		|
		|ПОМЕСТИТЬ КурсыВалют
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(&Период,
		|		Валюта В (
		|			ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВЫБОР
		|					КОГДА ТаблицаВидыЗапасов.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|						ТОГДА ТаблицаВидыЗапасов.ВидЗапасовВладельца.Валюта
		|					ИНАЧЕ ТаблицаВидыЗапасов.Валюта
		|				КОНЕЦ КАК Валюта
		|			ИЗ
		|				ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
		|			ГДЕ
		|				НЕ ТаблицаВидыЗапасов.НаДоходыРасходы
		|				И (ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|					ИЛИ ТаблицаВидыЗапасов.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар))
		|				И (ТаблицаВидыЗапасов.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|					ИЛИ (ТаблицаВидыЗапасов.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) И Не  &ВернутьМногооборотнуюТару)
		|					ИЛИ ТаблицаВидыЗапасов.ТипНоменклатуры ЕСТЬ NULL)
		|
		|			ОБЪЕДИНИТЬ
		|
		|			ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВЫБОР
		|					КОГДА ТаблицаВидыЗапасов.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|						ТОГДА ТаблицаВидыЗапасов.ВидЗапасовВладельца.Валюта
		|					ИНАЧЕ ТаблицаВидыЗапасов.Валюта
		|				КОНЕЦ КАК Валюта
		|			ИЗ
		|				ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
		|			ГДЕ
		|				НЕ ТаблицаВидыЗапасов.НаДоходыРасходы
		|				И (ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|					ИЛИ ТаблицаВидыЗапасов.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар))
		|				И (ТаблицаВидыЗапасов.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|					ИЛИ (ТаблицаВидыЗапасов.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) И Не  &ВернутьМногооборотнуюТару)
		|					ИЛИ ТаблицаВидыЗапасов.ТипНоменклатуры ЕСТЬ NULL)
		|
		|			ОБЪЕДИНИТЬ
		|
		|			ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВЫБОР
		|					КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|						ТОГДА ТаблицаВидыЗапасов.ВидЗапасовВладельца.Валюта
		|					ИНАЧЕ ТаблицаВидыЗапасов.Валюта
		|				КОНЕЦ КАК Валюта
		|			ИЗ
		|				ВтВидыЗапасовКорректировкаВыручки КАК ТаблицаВидыЗапасов
		|			ГДЕ
		|				(ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
		|					ИЛИ ТаблицаВидыЗапасов.ВидЗапасовВладельца.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар))
		|				И (ТаблицаВидыЗапасов.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|					ИЛИ (ТаблицаВидыЗапасов.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) И Не &ВернутьМногооборотнуюТару)
		|					ИЛИ ТаблицаВидыЗапасов.ТипНоменклатуры ЕСТЬ NULL)
		|
		|			ОБЪЕДИНИТЬ
		|
		|			ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				Услуги.Валюта КАК Валюта
		|			ИЗ
		|				ВтАгентскиеУслуги КАК Услуги
		|			)
		|	) КАК КурсыВалют
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		РегистрСведений.КурсыВалют.СрезПоследних(&ПериодПродажи, Валюта = &Валюта) КАК КурсВалютыДокумента
		|	ПО
		|		Истина
		|
		|ГДЕ
		|	КурсВалютыДокумента.Кратность <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаТоварыКОформлениюОтчетовКомитенту(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыКОформлениюОтчетовКомитенту";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовСписание", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовСписание(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтВидыЗапасовКорректировкаВыручки", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВидыЗапасовКорректировкаВыручки(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("КурсыВалют", ТекстыЗапроса) Тогда
		ТекстЗапросаВтКурсыВалют(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	// Виды запасов к списанию
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки								   КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)						   КАК ВидДвижения,
	|	&Период														   КАК Период,
	|	ТаблицаВидыЗапасов.Валюта									   КАК Валюта,
	|	ТаблицаВидыЗапасов.ВидЗапасов								   КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Номенклатура								   КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика							   КАК Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД								  	   КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту) КАК ХозяйственнаяОперация,
	|	&ДокументОснование											   КАК ДокументРеализации,
	|	ТаблицаВидыЗапасов.Количество								   КАК Количество,
	|	
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.Валюта = &Валюта ТОГДА
	|		ТаблицаВидыЗапасов.СуммаСНДС
	|	ИНАЧЕ
	|		ТаблицаВидыЗапасов.СуммаСНДС * КурсыВалют.КоэффициентПересчета
	|	КОНЕЦ КАК СуммаВыручки,
	|	
	|	0															   КАК КоличествоСписано,
	|
	|	ТаблицаВидыЗапасов.Количество								   КАК КоличествоКОформлению,
	|	
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.Валюта = &Валюта ТОГДА
	|		ТаблицаВидыЗапасов.СуммаСНДС
	|	ИНАЧЕ
	|		ТаблицаВидыЗапасов.СуммаСНДС * КурсыВалют.КоэффициентПересчета
	|	КОНЕЦ КАК СуммаВыручкиКОформлению,
	|	
	|	0															   КАК КоличествоСписаноКОформлению,
	|	НЕОПРЕДЕЛЕНО												   КАК КорВидЗапасов,
	|	ТаблицаВидыЗапасов.АналитикаКомитента						   КАК АналитикаУчетаНоменклатуры
	|
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсыВалют
	|	ПО
	|		ТаблицаВидыЗапасов.ВидЗапасов.Валюта = КурсыВалют.Валюта
	|
	|ГДЕ
	|	НЕ ТаблицаВидыЗапасов.НаДоходыРасходы
	|	И ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И (ТаблицаВидыЗапасов.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИЛИ (ТаблицаВидыЗапасов.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) И Не &ВернутьМногооборотнуюТару)
	|		ИЛИ ТаблицаВидыЗапасов.ТипНоменклатуры ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Виды запасов к оприходованию
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки										 КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)								 КАК ВидДвижения,
	|	&Период																 КАК Период,
	|	ТаблицаВидыЗапасов.Валюта											 КАК Валюта,
	|	ТаблицаВидыЗапасов.ВидЗапасов										 КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Номенклатура										 КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика									 КАК Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД											 КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента) КАК ХозяйственнаяОперация,
	|	&ДокументОснование													 КАК ДокументРеализации,
	|	
	|	-ТаблицаВидыЗапасов.Количество										КАК Количество,
	|	
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.Валюта = &Валюта ТОГДА
	|		-ТаблицаВидыЗапасов.СуммаСНДС
	|	ИНАЧЕ
	|		-ТаблицаВидыЗапасов.СуммаСНДС * КурсыВалют.КоэффициентПересчета
	|	КОНЕЦ																КАК СуммаВыручки,
	|	
	|	0 																	КАК КоличествоСписано,
	|	-ТаблицаВидыЗапасов.Количество										КАК КоличествоКОформлению,
	|	
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.Валюта = &Валюта ТОГДА
	|		-ТаблицаВидыЗапасов.СуммаСНДС
	|	ИНАЧЕ
	|		-ТаблицаВидыЗапасов.СуммаСНДС * КурсыВалют.КоэффициентПересчета
	|	КОНЕЦ																КАК СуммаВыручкиКОформлению,
	|	
	|	0																	КАК КоличествоСписаноКОформлению,
	|
	|	НЕОПРЕДЕЛЕНО														КАК КорВидЗапасов,
	|	ТаблицаВидыЗапасов.АналитикаКомитента								КАК АналитикаУчетаНоменклатуры
	|
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсыВалют
	|	ПО
	|		ТаблицаВидыЗапасов.ВидЗапасов.Валюта = КурсыВалют.Валюта
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ТаблицаВидыЗапасов.Номенклатура = Аналитика.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = Аналитика.Характеристика
	|		И ТаблицаВидыЗапасов.Серия = Аналитика.Серия
	|		И ТаблицаВидыЗапасов.Назначение = Аналитика.Назначение
	|		И ТаблицаВидыЗапасов.ВидЗапасов.ВладелецТовара = Аналитика.Склад
	|
	|ГДЕ
	|	НЕ ТаблицаВидыЗапасов.НаДоходыРасходы
	|	И ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И (ТаблицаВидыЗапасов.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИЛИ (ТаблицаВидыЗапасов.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) И Не &ВернутьМногооборотнуюТару)
	|		ИЛИ ТаблицаВидыЗапасов.ТипНоменклатуры ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Виды запасов к корректировке выручки
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки 									КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 							КАК ВидДвижения,
	|	&Период 														КАК Период,
	|	ТаблицаВидыЗапасов.Валюта 										КАК Валюта,
	|	ТаблицаВидыЗапасов.ВидЗапасов 									КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Номенклатура 								КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика 								КАК Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД										КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту) 	КАК ХозяйственнаяОперация,
	|	&ДокументОснование												КАК ДокументРеализации,
	|	0 																КАК Количество,
	|
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.Валюта = &Валюта ТОГДА
	|		ТаблицаВидыЗапасов.СуммаСНДС
	|	ИНАЧЕ
	|		ТаблицаВидыЗапасов.СуммаСНДС * КурсыВалют.КоэффициентПересчета
	|	КОНЕЦ 															КАК СуммаВыручки,
	|	0 КАК КоличествоСписано,
	|
	|	0 																КАК КоличествоКОформлению,
	|
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.Валюта = &Валюта ТОГДА
	|		ТаблицаВидыЗапасов.СуммаСНДС
	|	ИНАЧЕ
	|		ТаблицаВидыЗапасов.СуммаСНДС * КурсыВалют.КоэффициентПересчета
	|	КОНЕЦ 															КАК СуммаВыручкиКОформлению,
	|	0 КАК КоличествоСписаноКОформлению,
	|
	|	НЕОПРЕДЕЛЕНО													КАК КорВидЗапасов,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры					КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ВтВидыЗапасовКорректировкаВыручки КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсыВалют
	|	ПО
	|		ТаблицаВидыЗапасов.ВидЗапасов.Валюта = КурсыВалют.Валюта
	|
	|ГДЕ
	|	ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И (ТаблицаВидыЗапасов.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	ИЛИ (ТаблицаВидыЗапасов.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) И Не  &ВернутьМногооборотнуюТару)
	|	ИЛИ ТаблицаВидыЗапасов.ТипНоменклатуры ЕСТЬ NULL)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтРасхожденияРаботыУслуги(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтРасхожденияРаботыУслуги";
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтАгентскиеУслуги", ТекстыЗапроса) Тогда
		ТекстЗапросаВтАгентскиеУслуги(ТекстыЗапроса);
	КонецЕсли;
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Запрос);
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтроки,
	|	ТаблицаРасхождения.Номенклатура КАК Номенклатура,
	|	ТаблицаРасхождения.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаРасхождения.Характеристика КАК Характеристика,
	|	ТаблицаРасхождения.Количество КАК Количество,
	|	ТаблицаРасхождения.СуммаСНДС КАК СуммаСНДС,
	|
	|	ТаблицаРасхождения.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС <> 0 ТОГДА
	|		ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаВзаиморасчетов * ТаблицаРасхождения.СуммаНДС / ТаблицаРасхождения.СуммаСНДС КАК ЧИСЛО(15, 2))
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаНДСВзаиморасчетов,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ТаблицаРасхождения.СуммаВзаиморасчетов
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуУпр) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаСНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ТаблицаРасхождения.СуммаВзаиморасчетов - 
	|				ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС <> 0 ТОГДА
	|					ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаВзаиморасчетов * ТаблицаРасхождения.СуммаНДС / ТаблицаРасхождения.СуммаСНДС КАК ЧИСЛО(15, 2))
	|				ИНАЧЕ
	|					0
	|				КОНЕЦ
	|	ИНАЧЕ
	|		ВЫРАЗИТЬ(((ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаБезНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ТаблицаРасхождения.СуммаВзаиморасчетов - 
	|				ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС <> 0 ТОГДА
	|					ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаВзаиморасчетов * ТаблицаРасхождения.СуммаНДС / ТаблицаРасхождения.СуммаСНДС КАК ЧИСЛО(15, 2))
	|				ИНАЧЕ
	|					0
	|				КОНЕЦ
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(((ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС <> 0 ТОГДА
	|				ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаВзаиморасчетов * ТаблицаРасхождения.СуммаНДС / ТаблицаРасхождения.СуммаСНДС КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаНДС * &КоэффициентПересчетаВВалютуРегл) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК НДСРегл,
	|
	|	ТаблицаРасхождения.ЗаказКлиента                     КАК ЗаказКлиента,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры       КАК АналитикаУчетаНоменклатуры,
	|	АналитикаБезНазначения.КлючАналитики                КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ТаблицаРасхождения.АналитикаУчетаНаборов            КАК АналитикаУчетаНаборов,
	|	ТаблицаРасхождения.СуммаНДС                         КАК СуммаНДС,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Склад КАК Склад,
	|	ТаблицаРасхождения.Назначение                       КАК Назначение,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)       КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВариантОтражения В (ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.ОтразитьНаПрочихДоходах),
	|													ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.СписатьНаРасходы))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                               КАК НаДоходыРасходы,
	|
	|	АгентскиеУслуги.Соглашение КАК Соглашение,
	|	АгентскиеУслуги.Валюта КАК Валюта,
	|	ЕСТЬNULL(КлючиПринципала.КлючАналитики, НЕОПРЕДЕЛЕНО) КАК АналитикаНоменклатурыПринципала,
	|	ЕСТЬNULL(АгентскиеУслуги.Принципал, НЕОПРЕДЕЛЕНО) КАК Принципал,
	|
	|	(ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоСделкам И &ОбособленныйУчетТоваровПоСделке
	|			ТОГДА &Сделка
	|		КОГДА &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|		 И &ВариантОбособленногоУчетаТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоМенеджерамПодразделения)
	|			ТОГДА &Менеджер
	|		КОГДА &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|		 И &ВариантОбособленногоУчетаТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоПодразделению)
	|			ТОГДА &Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ) КАК АналитикаФинансовогоУчета,
	|	(ВЫБОР
	|		КОГДА ТаблицаРасхождения.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|		 И &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		ИНАЧЕ &НалогообложениеНДС
	|	КОНЕЦ) КАК ВидДеятельностиНДС
	|
	|ПОМЕСТИТЬ ВтРасхожденияРаботыУслуги
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтАгентскиеУслуги КАК АгентскиеУслуги
	|	ПО ТаблицаРасхождения.Номенклатура = АгентскиеУслуги.Номенклатура
	|		И ТаблицаРасхождения.Характеристика = АгентскиеУслуги.Характеристика
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО ТаблицаРасхождения.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО Аналитика.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И Аналитика.Характеристика = АналитикаБезНазначения.Характеристика
	|		И Аналитика.Серия = АналитикаБезНазначения.Серия
	|		И Аналитика.Склад = АналитикаБезНазначения.Склад
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиПринципала
	|	ПО КлючиПринципала.Номенклатура = ТаблицаРасхождения.Номенклатура
	|		И КлючиПринципала.Характеристика = ТаблицаРасхождения.Характеристика
	|		И КлючиПринципала.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И КлючиПринципала.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И КлючиПринципала.Склад = АгентскиеУслуги.Принципал
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	ИмяРегистра = "СебестоимостьТоваров";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовСписание", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовСписание(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтРасхожденияРаботыУслуги", ТекстыЗапроса) Тогда
		ТекстЗапросаВтРасхожденияРаботыУслуги(Запрос, ТекстыЗапроса);
	КонецЕсли;
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	УстановитьПараметрыЗапросаАналитикаУчетаПоПартнерам(Запрос);
	
	ТекстЗапроса = "
	// Виды запасов к списанию
	|ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ВидыЗапасов.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	&Организация КАК Организация,
	|	ВЫБОР КОГДА ВидыЗапасов.ЦеховаяКладовая
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ВидыЗапасов.Количество КАК Количество,
	|	0 КАК Стоимость,
	|	0 КАК КорСтоимость,
	|	0 КАК СтоимостьРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)  КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
	|
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	&Подразделение КАК Подразделение,
	|
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ВидыЗапасов.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяДоходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
	|
	|	НЕОПРЕДЕЛЕНО КАК ПериодПродажи,
	|	ВидыЗапасов.АналитикаФинансовогоУчета КАК КорАналитикаФинансовогоУчета,
	|	ВидыЗапасов.ВидДеятельностиНДС КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление) КАК ТипЗаписи
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ВидыЗапасов
	|ГДЕ
	|	НЕ ВидыЗапасов.НаДоходыРасходы
	|	И ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	И (ВидыЗапасов.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИЛИ (ВидыЗапасов.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) И Не &ВернутьМногооборотнуюТару)
	|		ИЛИ ВидыЗапасов.ТипНоменклатуры ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Списание многооборотной тары к возврату
	|ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ВидыЗапасов.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	&Организация КАК Организация,
	|	ВЫБОР КОГДА ВидыЗапасов.ЦеховаяКладовая
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ВЫБОР КОГДА ВидыЗапасов.ЦеховаяКладовая
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		КОНЕЦ
	|	КОНЕЦ КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ВидыЗапасов.Количество КАК Количество,
	|	ВидыЗапасов.СуммаСНДСУпр КАК Стоимость,
	|	ВидыЗапасов.СуммаСНДСУпр КАК КорСтоимость,
	|	ВидыЗапасов.СуммаБезНДСРегл КАК СтоимостьРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПереданнойВозвратнойТары) КАК ХозяйственнаяОперация,
	|	Неопределено КАК КорРазделУчета,
	|
	|	Неопределено КорВидЗапасов,
	|
	|	Неопределено КАК КорАналитикаУчетаНоменклатуры,
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	&Подразделение КАК Подразделение,
	|
	|	ВЫБОР КОГДА &РеализацияПоЗаказам
	|		ТОГДА ВидыЗапасов.ЗаказКлиента
	|		ИНАЧЕ &Ссылка
	|	КОНЕЦ КАК ЗаказКлиента,
	|
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РазницыСтоимостиВозвратаИФактическойСтоимостиТоваров) КАК СтатьяРасходовСписания,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.РазницыСтоимостиВозвратаИФактическойСтоимостиТоваров) КАК СтатьяДоходов,
	|	&Партнер КАК АналитикаРасходов,
	|	&Партнер КАК АналитикаДоходов,
	|
	|	Неопределено КАК ПериодПродажи,
	|	ВидыЗапасов.АналитикаФинансовогоУчета КАК КорАналитикаФинансовогоУчета,
	|	ВидыЗапасов.ВидДеятельностиНДС КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление) КАК ТипЗаписи
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ВидыЗапасов
	|ГДЕ
	|	НЕ ВидыЗапасов.НаДоходыРасходы
	|	И ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	И ВидыЗапасов.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	И &ВернутьМногооборотнуюТару
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ВидыЗапасов.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию) КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ВидыЗапасов.Количество КАК Количество,
	|	0 КАК Стоимость,
	|	0 КАК КорСтоимость,
	|	0 КАК СтоимостьРегл,
	|	ВЫБОР КОГДА &КорректировкаОтгрузкиБезПереходаПраваСобственности
	|		ТОГДА 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту) 
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|	ВЫБОР КОГДА &КорректировкаОтгрузкиБезПереходаПраваСобственности 
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути)
	|		ИНАЧЕ
	|			Неопределено
	|	КОНЕЦ КАК КорРазделУчета,
	|
	|	ВЫБОР КОГДА &КорректировкаОтгрузкиБезПереходаПраваСобственности 
	|		ТОГДА
	|			ВидыЗапасов.ВидЗапасов
	|		ИНАЧЕ
	|			Неопределено
	|	КОНЕЦ КорВидЗапасов,
	|
	|	ВЫБОР КОГДА &КорректировкаОтгрузкиБезПереходаПраваСобственности 
	|		ТОГДА
	|			ВЫБОР КОГДА &ИспользуетсяНазначение
	|				ТОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры
	|				ИНАЧЕ ВидыЗапасов.АналитикаУчетаНоменклатурыБезНазначения
	|			КОНЕЦ
	|		ИНАЧЕ
	|			Неопределено
	|	КОНЕЦ КАК КорАналитикаУчетаНоменклатуры,
	|
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	&Подразделение КАК Подразделение,
	|
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ВидыЗапасов.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|
	|	Неопределено КАК СтатьяРасходовСписания,
	|	Неопределено КАК СтатьяДоходов,
	|	Неопределено КАК АналитикаРасходов,
	|	Неопределено КАК АналитикаДоходов,
	|
	|	Неопределено КАК ПериодПродажи,
	|	ВидыЗапасов.АналитикаФинансовогоУчета КАК КорАналитикаФинансовогоУчета,
	|	ВидыЗапасов.ВидДеятельностиНДС КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление) КАК ТипЗаписи
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ВидыЗапасов
	|ГДЕ
	|	НЕ ВидыЗапасов.НаДоходыРасходы
	|	И ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Виды запасов к оприходованию
	//
	// Корректировка прошлого периода.
	|ВЫБРАТЬ
	|	2 КАК Порядок,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ВидыЗапасов.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ВидыЗапасов.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		ИНАЧЕ ВЫБОР КОГДА ВидыЗапасов.ЦеховаяКладовая
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		КОНЕЦ
	|	КОНЕЦ КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ВидыЗапасов.Количество КАК Количество,
	|	0 КАК Стоимость,
	|	0 КАК КорСтоимость,
	|	0 КАК СтоимостьРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ВЫБОР КОГДА ВидыЗапасов.ВидЗапасовОтгрузки <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|   		ВидыЗапасов.ВидЗапасовОтгрузки
	|		ИНАЧЕ
	|   		ВидыЗапасов.ВидЗапасов
	|		КОНЕЦ
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК КорВидЗапасов,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ВидыЗапасов.АналитикаРеализации
	|		ИНАЧЕ ВидыЗапасов.АналитикаРеализацииБезНазначения
	|	КОНЕЦ КАК КорАналитикаУчетаНоменклатуры,
	|
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	&Подразделение КАК Подразделение,
	|
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ВидыЗапасов.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|
	|	Неопределено КАК СтатьяРасходовСписания,
	|	Неопределено КАК СтатьяДоходов,
	|	Неопределено КАК АналитикаРасходов,
	|	Неопределено КАК АналитикаДоходов,
	|
	|	&ПериодПродажи КАК ПериодПродажи,
	|	ВидыЗапасов.АналитикаФинансовогоУчета КАК КорАналитикаФинансовогоУчета,
	|	ВидыЗапасов.ВидДеятельностиНДС КАК КорВидДеятельностиНДС,
	|	&ДокументОснование КАК ДокументИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВозвратПрошлыхПериодов) КАК ТипЗаписи
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ВидыЗапасов
	|
	|ГДЕ
	|	НЕ ВидыЗапасов.НаДоходыРасходы
	|	И &КорректировкаПрошлогоПериода
	|	И ВидыЗапасов.ТипЗапасов В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Корректировка реализации текущего периода. Используется аналитика продажи.
	//
	|ВЫБРАТЬ
	|	3 КАК Порядок,
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ТаблицаВидыЗапасов.АналитикаРеализации
	|		ИНАЧЕ ТаблицаВидыЗапасов.АналитикаРеализацииБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		ИНАЧЕ ВЫБОР КОГДА ТаблицаВидыЗапасов.ЦеховаяКладовая
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		КОНЕЦ
	|	КОНЕЦ КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасовОтгрузки <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|   		ТаблицаВидыЗапасов.ВидЗапасовОтгрузки
	|		ИНАЧЕ
	|   		ТаблицаВидыЗапасов.ВидЗапасов
	|		КОНЕЦ
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	-ТаблицаВидыЗапасов.Количество КАК Количество,
	|	0 КАК Стоимость,
	|	0 КАК КорСтоимость,
	|	0 КАК СтоимостьРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноРеализации) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ТаблицаВидыЗапасов.ВидЗапасовОтгрузки
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК КорВидЗапасов,
	|	ТаблицаВидыЗапасов.АналитикаРеализации КАК КорАналитикаУчетаНоменклатуры,
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	&Подразделение КАК Подразделение,
	|
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ТаблицаВидыЗапасов.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|
	|	Неопределено КАК СтатьяРасходовСписания,
	|	Неопределено КАК СтатьяДоходов,
	|	Неопределено КАК АналитикаРасходов,
	|	Неопределено КАК АналитикаДоходов,
	|
	|	&ПериодПродажи КАК ПериодПродажи,
	|	ТаблицаВидыЗапасов.АналитикаФинансовогоУчета КАК КорАналитикаФинансовогоУчета,
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС КАК КорВидДеятельностиНДС,
	|	&ДокументОснование КАК ДокументИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Сторно) КАК ТипЗаписи
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	НЕ ТаблицаВидыЗапасов.НаДоходыРасходы
	|	И &КорректировкаТекущегоПериода
	|	И ТаблицаВидыЗапасов.ТипЗапасов В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Перемещение с реализации на корректировку - списание
	|ВЫБРАТЬ
	|	4 КАК Порядок,
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ТаблицаВидыЗапасов.АналитикаРеализации
	|		ИНАЧЕ ТаблицаВидыЗапасов.АналитикаРеализацииБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	&Организация КАК Организация,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ЦеховаяКладовая
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасовОтгрузки <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|			ТаблицаВидыЗапасов.ВидЗапасовОтгрузки
	|		ИНАЧЕ
	|			ТаблицаВидыЗапасов.ВидЗапасов
	|		КОНЕЦ
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	0 КАК Стоимость,
	|	0 КАК КорСтоимость,
	|	0 КАК СтоимостьРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента) КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ЦеховаяКладовая
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ КАК КорРазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ТаблицаВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК КорВидЗапасов,
	|
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаВидыЗапасов.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ КАК КорАналитикаУчетаНоменклатуры,
	|
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
	|
	|	Неопределено КАК СтатьяРасходовСписания,
	|	Неопределено КАК СтатьяДоходов,
	|	Неопределено КАК АналитикаРасходов,
	|	Неопределено КАК АналитикаДоходов,
	|
	|	ДатаВремя(1,1,1) КАК ПериодПродажи,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление) КАК ТипЗаписи
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|ГДЕ
	|	НЕ ТаблицаВидыЗапасов.НаДоходыРасходы
	|	И &КорректировкаТекущегоПериода
	|	И ТаблицаВидыЗапасов.ВидЗапасовОтгрузки <> ТаблицаВидыЗапасов.ВидЗапасов
	|	И &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Перемещение с реализации на корректировку - оприходование
	|ВЫБРАТЬ
	|	5 КАК Порядок,
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаВидыЗапасов.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|
	|	&Организация КАК Организация,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ЦеховаяКладовая
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ТаблицаВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	0 КАК Стоимость,
	|	0 КАК КорСтоимость,
	|	0 КАК СтоимостьРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПоПартнерам,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
	|
	|	Неопределено КАК СтатьяРасходовСписания,
	|	Неопределено КАК СтатьяДоходов,
	|	Неопределено КАК АналитикаРасходов,
	|	Неопределено КАК АналитикаДоходов,
	|
	|	ДатаВремя(1,1,1) КАК ПериодПродажи,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Перемещение) КАК ТипЗаписи
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|ГДЕ
	|	НЕ ТаблицаВидыЗапасов.НаДоходыРасходы
	|	И &КорректировкаТекущегоПериода
	|	И ТаблицаВидыЗапасов.ВидЗапасовОтгрузки <> ТаблицаВидыЗапасов.ВидЗапасов
	|	И &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Работы. Продажа.
	|ВЫБРАТЬ
	|	6 КАК Порядок,
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ТаблицаРасхождения.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаРасхождения.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ТаблицаРасхождения.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ТаблицаРасхождения.Количество КАК Количество,
	|	0 КАК Стоимость,
	|	0 КАК КорСтоимость,
	|	0 КАК СтоимостьРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаУчетаНоменклатуры,
	|
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	&Подразделение КАК Подразделение,
	|
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ТаблицаРасхождения.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|
	|	Неопределено КАК СтатьяРасходовСписания,
	|	Неопределено КАК СтатьяДоходов,
	|	Неопределено КАК АналитикаРасходов,
	|	Неопределено КАК АналитикаДоходов,
	|
	|	ДатаВремя(1,1,1) КАК ПериодПродажи,
	|	ТаблицаРасхождения.АналитикаФинансовогоУчета КАК КорАналитикаФинансовогоУчета,
	|	ТаблицаРасхождения.ВидДеятельностиНДС КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление) КАК ТипЗаписи
	|ИЗ
	|	ВтРасхожденияРаботыУслуги КАК ТаблицаРасхождения
	|ГДЕ
	|	НЕ ТаблицаРасхождения.НаДоходыРасходы
	|	И ТаблицаРасхождения.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И ТаблицаРасхождения.Количество > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Работы. Корректировка в минус
	|ВЫБРАТЬ
	|	7 КАК Порядок,
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ТаблицаРасхождения.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаРасхождения.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ТаблицаРасхождения.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ТаблицаРасхождения.Количество КАК Количество,
	|	0 КАК Стоимость,
	|	0 КАК КорСтоимость,
	|	0 КАК СтоимостьРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноРеализации) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ТаблицаРасхождения.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК КорВидЗапасов,
	|
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	&Подразделение КАК Подразделение,
	|
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ТаблицаРасхождения.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|
	|	Неопределено КАК СтатьяРасходовСписания,
	|	Неопределено КАК СтатьяДоходов,
	|	Неопределено КАК АналитикаРасходов,
	|	Неопределено КАК АналитикаДоходов,
	|
	|	&ПериодПродажи КАК ПериодПродажи,
	|	ТаблицаРасхождения.АналитикаФинансовогоУчета КАК КорАналитикаФинансовогоУчета,
	|	ТаблицаРасхождения.ВидДеятельностиНДС КАК КорВидДеятельностиНДС,
	|	&ДокументОснование КАК ДокументИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Сторно) КАК ТипЗаписи
	|ИЗ
	|	ВтРасхожденияРаботыУслуги КАК ТаблицаРасхождения
	|ГДЕ
	|	НЕ ТаблицаРасхождения.НаДоходыРасходы
	|	И ТаблицаРасхождения.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И &КорректировкаТекущегоПериода
	|	И ТаблицаРасхождения.Количество < 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Работы. Корректировка в минус прошлого периода.
	|ВЫБРАТЬ
	|	8 КАК Порядок,
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтрокиДокумента,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ТаблицаРасхождения.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	-ТаблицаРасхождения.Количество КАК Количество,
	|	0 КАК Стоимость,
	|	0 КАК КорСтоимость,
	|	0 КАК СтоимостьРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК КорРазделУчета,
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ТаблицаРасхождения.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК КорВидЗапасов,
	|
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	&Подразделение КАК Подразделение,
	|
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ТаблицаРасхождения.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|
	|	Неопределено КАК СтатьяРасходовСписания,
	|	Неопределено КАК СтатьяДоходов,
	|	Неопределено КАК АналитикаРасходов,
	|	Неопределено КАК АналитикаДоходов,
	|
	|	&ПериодПродажи КАК ПериодПродажи,
	|	ТаблицаРасхождения.АналитикаФинансовогоУчета КАК КорАналитикаФинансовогоУчета,
	|	ТаблицаРасхождения.ВидДеятельностиНДС КАК КорВидДеятельностиНДС,
	|	&ДокументОснование КАК ДокументИсточник,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВозвратПрошлыхПериодов) КАК ТипЗаписи
	|ИЗ
	|	ВтРасхожденияРаботыУслуги КАК ТаблицаРасхождения
	|ГДЕ
	|	НЕ ТаблицаРасхождения.НаДоходыРасходы
	|	И ТаблицаРасхождения.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И ТаблицаРасхождения.Количество < 0
	|	И &КорректировкаПрошлогоПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтрокиДокумента";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВыручкаИСебестоимостьПродаж(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ВыручкаИСебестоимостьПродаж";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовСписание", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовСписание(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтВидыЗапасовКорректировкаВыручки", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВидыЗапасовКорректировкаВыручки(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтРасхожденияРаботыУслуги", ТекстыЗапроса) Тогда
		ТекстЗапросаВтРасхожденияРаботыУслуги(Запрос, ТекстыЗапроса);
	КонецЕсли;
	УстановитьПараметрыЗапросаАналитикаУчетаПоПартнерам(Запрос);
	ТекстЗапроса = "
	// Виды запасов к списанию
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	&Подразделение КАК Подразделение,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаВидыЗапасов.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНаборов КАК АналитикаУчетаНаборов,
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ТаблицаВидыЗапасов.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|	ТаблицаВидыЗапасов.ТипЗапасов КАК ТипЗапасов,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ТаблицаВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		КОГДА ТаблицаВидыЗапасов.ЦеховаяКладовая 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ КАК РазделУчета,
	|	ТаблицаВидыЗапасов.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|			И &ФИФОСкользящаяОценка
	|			ТОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВидДеятельностиНДС,
	|
	|	&Менеджер КАК Менеджер,
	|
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельностиНоменклатуры,
	|
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) Тогда
	|		ТаблицаВидыЗапасов.СуммаСНДСУпр
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) Тогда
	|		ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СтоимостьБезНДС,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) Тогда
	|		ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СтоимостьУпр,
	|
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) Тогда
	|		ТаблицаВидыЗапасов.СуммаБезНДСРегл
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ТаблицаВидыЗапасов.СуммаСНДСУпр КАК СуммаВыручки,
	|	ТаблицаВидыЗапасов.СуммаБезНДСУпр КАК СуммаВыручкиБезНДС,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл КАК СуммаВыручкиРегл,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.НДСРегл КАК СуммаВыручкиСНДСРегл,
	|	0 КАК СуммаРучнойСкидки,
	|	0 КАК СуммаАвтоматическойСкидки,
	|	ТаблицаВидыЗапасов.Склад КАК Склад,
	|	&Договор КАК Договор,
	|	&Соглашение КАК Соглашение,
	|
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаВидыЗапасов.СуммаВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	ТаблицаВидыЗапасов.СуммаВзаиморасчетов - ТаблицаВидыЗапасов.СуммаНДСВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	&Валюта КАК ВалютаДокумента,
	|	ТаблицаВидыЗапасов.СуммаСНДС КАК СуммаВВалютеДокумента,
	|	ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС КАК СуммаБезНДСВВалютеДокумента,
	|
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС КАК НалогообложениеНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту) КАК ХозяйственнаяОперация,
	|
	|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		ТаблицаВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		ТаблицаВидыЗапасов.Номенклатура
	|	КОНЕЦ КАК ИсточникГФУНоменклатуры,
	|	ВЫБОР
	|		КОГДА &РасчетыПоДоговорам
	|			ТОГДА &Договор
	|		КОГДА &ПродажаПоЗаказам И НЕ &РасчетыПоНакладным
	|			ТОГДА ТаблицаВидыЗапасов.ЗаказКлиента
	|		ИНАЧЕ
	|			&ДокументОснование
	|	КОНЕЦ КАК ИсточникГФУРасчетов
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	НЕ ТаблицаВидыЗапасов.НаДоходыРасходы
	|	И (ТаблицаВидыЗапасов.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИЛИ (ТаблицаВидыЗапасов.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) И Не &ВернутьМногооборотнуюТару)
	|		ИЛИ ТаблицаВидыЗапасов.ТипНоменклатуры ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Расхождения по услугам 
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	&Подразделение КАК Подразделение,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ТаблицаРасхождения.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаРасхождения.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаРасхождения.АналитикаУчетаНаборов КАК АналитикаУчетаНаборов,
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ТаблицаРасхождения.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Принципал <> НЕОПРЕДЕЛЕНО
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.АгентскаяУслуга)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|	КОНЕЦ КАК ТипЗапасов,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ТаблицаРасхождения.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	(ВЫБОР
	|		КОГДА ТаблицаРасхождения.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка) КОНЕЦ) КАК РазделУчета,
	|	ТаблицаРасхождения.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	ТаблицаРасхождения.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|
	|	&Менеджер КАК Менеджер,
	|
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельностиНоменклатуры,
	|
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Принципал <> НЕОПРЕДЕЛЕНО
	|		ТОГДА ТаблицаРасхождения.СуммаСНДСУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Принципал <> НЕОПРЕДЕЛЕНО
	|		ТОГДА ТаблицаРасхождения.СуммаБезНДСУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьБезНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Принципал <> НЕОПРЕДЕЛЕНО
	|		ТОГДА ТаблицаРасхождения.СуммаБезНДСУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьУпр,
	|
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Принципал <> НЕОПРЕДЕЛЕНО
	|		ТОГДА ТаблицаРасхождения.СуммаБезНДСРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|	ТаблицаРасхождения.Количество КАК Количество,
	|	ТаблицаРасхождения.СуммаСНДСУпр КАК СуммаВыручки,
	|	ТаблицаРасхождения.СуммаБезНДСУпр КАК СуммаВыручкиБезНДС,
	|	ТаблицаРасхождения.СуммаБезНДСРегл КАК СуммаВыручкиРегл,
	|	ТаблицаРасхождения.СуммаБезНДСРегл + ТаблицаРасхождения.НДСРегл КАК СуммаВыручкиСНДСРегл,
	|	0 КАК СуммаРучнойСкидки,
	|	0 КАК СуммаАвтоматическойСкидки,
	|	ТаблицаРасхождения.Склад КАК Склад,
	|	&Договор КАК Договор,
	|	&Соглашение КАК Соглашение,
	|
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаРасхождения.СуммаВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	ТаблицаРасхождения.СуммаВзаиморасчетов - ТаблицаРасхождения.СуммаНДСВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	&Валюта КАК ВалютаДокумента,
	|	ТаблицаРасхождения.СуммаСНДС КАК СуммаВВалютеДокумента,
	|	ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС КАК СуммаБезНДСВВалютеДокумента,
	|
	|	ТаблицаРасхождения.ВидДеятельностиНДС КАК НалогообложениеНДС,
	|
	|	ВЫБОР
	|		КОГДА &КорректировкаПрошлогоПериода И ТаблицаРасхождения.Количество < 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов)
	|		КОГДА ТаблицаРасхождения.Количество < 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноРеализации)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|	ТаблицаРасхождения.Номенклатура КАК ИсточникГФУНоменклатуры,
	|	ВЫБОР
	|		КОГДА &РасчетыПоДоговорам
	|			ТОГДА &Договор
	|		КОГДА &ПродажаПоЗаказам И НЕ &РасчетыПоНакладным
	|			ТОГДА ТаблицаРасхождения.ЗаказКлиента
	|		ИНАЧЕ
	|			&ДокументОснование
	|	КОНЕЦ КАК ИсточникГФУРасчетов
	|ИЗ
	|	ВтРасхожденияРаботыУслуги КАК ТаблицаРасхождения
	|ГДЕ
	|	НЕ ТаблицаРасхождения.НаДоходыРасходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Виды запасов к оприходованию
	|ВЫБРАТЬ
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	&Подразделение КАК Подразделение,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ВидыЗапасов.АналитикаРеализации
	|		ИНАЧЕ ВидыЗапасов.АналитикаРеализацииБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.АналитикаУчетаНаборов КАК АналитикаУчетаНаборов,
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ВидыЗапасов.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|
	|	ВидыЗапасов.ТипЗапасов КАК ТипЗапасов,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|		ВЫБОР КОГДА ВидыЗапасов.ВидЗапасовОтгрузки <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|			ВидыЗапасов.ВидЗапасовОтгрузки
	|		ИНАЧЕ
	|			ВидыЗапасов.ВидЗапасов
	|		КОНЕЦ
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|	(ВЫБОР
	|		КОГДА ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		КОГДА ВидыЗапасов.ЦеховаяКладовая 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КОНЕЦ) КАК РазделУчета,
	|	ВидыЗапасов.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	ВидыЗапасов.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|
	|	&Менеджер КАК Менеджер,
	|
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ВидыЗапасов.АналитикаРеализации.Назначение.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельностиНоменклатуры,
	|
	|	ВЫБОР КОГДА ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) ТОГДА
	|		- ВидыЗапасов.СуммаСНДСУпр
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР КОГДА ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) ТОГДА
	|		- ВидыЗапасов.СуммаБезНДСУпр
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СтоимостьБезНДС,
	|	ВЫБОР КОГДА ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) ТОГДА
	|		- ВидыЗапасов.СуммаБезНДСУпр
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СтоимостьУпр,
	|
	|	ВЫБОР КОГДА ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) ТОГДА
	|		- ВидыЗапасов.СуммаБезНДСРегл
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	- ВидыЗапасов.Количество      КАК Количество,
	|	- ВидыЗапасов.СуммаСНДСУпр    КАК СуммаВыручки,
	|	- ВидыЗапасов.СуммаБезНДСУпр  КАК СуммаВыручкиБезНДС,
	|	- ВидыЗапасов.СуммаБезНДСРегл КАК СуммаВыручкиРегл,
	|	- (ВидыЗапасов.СуммаБезНДСРегл + ВидыЗапасов.НДСРегл) КАК СуммаВыручкиСНДСРегл,
	|
	|	0 КАК СуммаРучнойСкидки,
	|	0 КАК СуммаАвтоматическойСкидки,
	|	ВидыЗапасов.Склад КАК Склад,
	|	&Договор КАК Договор,
	|	&Соглашение КАК Соглашение,
	|
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ВидыЗапасов.СуммаВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	ВидыЗапасов.СуммаВзаиморасчетов - ВидыЗапасов.СуммаНДСВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	&Валюта КАК ВалютаДокумента,
	|	ВидыЗапасов.СуммаСНДС КАК СуммаВВалютеДокумента,
	|	ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС КАК СуммаБезНДСВВалютеДокумента,
	|
	|	ВидыЗапасов.ВидДеятельностиНДС КАК НалогообложениеНДС,
	|	ВЫБОР КОГДА &КорректировкаПрошлогоПериода ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноРеализации)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		ВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		ВидыЗапасов.Номенклатура
	|	КОНЕЦ КАК ИсточникГФУНоменклатуры,
	|	ВЫБОР
	|		КОГДА &РасчетыПоДоговорам
	|			ТОГДА &Договор
	|		КОГДА &ПродажаПоЗаказам И НЕ &РасчетыПоНакладным
	|			ТОГДА ВидыЗапасов.ЗаказКлиента
	|		ИНАЧЕ
	|			&ДокументОснование
	|	КОНЕЦ КАК ИсточникГФУРасчетов
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ВидыЗапасов
	|
	|ГДЕ
	|	НЕ ВидыЗапасов.НаДоходыРасходы
	|	И (ВидыЗапасов.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИЛИ (ВидыЗапасов.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) И Не &ВернутьМногооборотнуюТару)
	|		ИЛИ ВидыЗапасов.ТипНоменклатуры ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Виды запасов к корректировке выручки
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	&Подразделение КАК Подразделение,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаВидыЗапасов.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНаборов КАК АналитикаУчетаНаборов,
	|	&АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &ПродажаПоЗаказам ТОГДА
	|		ТаблицаВидыЗапасов.ЗаказКлиента
	|	ИНАЧЕ
	|		&ДокументОснование
	|	КОНЕЦ КАК ЗаказКлиента,
	|	ТаблицаВидыЗапасов.ТипЗапасов КАК ТипЗапасов,
	|
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов ТОГДА
	|   	ТаблицаВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ВидЗапасов,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|		КОГДА ТаблицаВидыЗапасов.ЦеховаяКладовая 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах) КОНЕЦ) КАК РазделУчета,
	|	ТаблицаВидыЗапасов.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|
	|	&Менеджер КАК Менеджер,
	|
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|	ВЫБОР КОГДА &ИспользуетсяНазначение
	|		ТОГДА ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельностиНоменклатуры,
	|
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) Тогда
	|		ТаблицаВидыЗапасов.СуммаСНДСУпр
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) Тогда
	|		ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СтоимостьБезНДС,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) Тогда
	|		ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СтоимостьУпр,
	|
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) Тогда
	|		ТаблицаВидыЗапасов.СуммаБезНДСРегл
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СтоимостьРегл,
	|	0	КАК Количество,
	|	ТаблицаВидыЗапасов.СуммаСНДСУпр    КАК СуммаВыручки,
	|	ТаблицаВидыЗапасов.СуммаБезНДСУпр  КАК СуммаВыручкиБезНДС,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл КАК СуммаВыручкиРегл,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.НДСРегл КАК СуммаВыручкиСНДСРегл,
	|
	|	0 КАК СуммаРучнойСкидки,
	|	0 КАК СуммаАвтоматическойСкидки,
	|	ТаблицаВидыЗапасов.Склад КАК Склад,
	|	&Договор КАК Договор,
	|	&Соглашение КАК Соглашение,
	|
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаВидыЗапасов.СуммаВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	ТаблицаВидыЗапасов.СуммаВзаиморасчетов - ТаблицаВидыЗапасов.СуммаНДСВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	&Валюта КАК ВалютаДокумента,
	|	ТаблицаВидыЗапасов.СуммаСНДС КАК СуммаВВалютеДокумента,
	|	ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС КАК СуммаБезНДСВВалютеДокумента,
	|
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС КАК НалогообложениеНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту) КАК ХозяйственнаяОперация,
	|
	|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		ТаблицаВидыЗапасов.ВидЗапасов
	|	ИНАЧЕ
	|		ТаблицаВидыЗапасов.Номенклатура
	|	КОНЕЦ КАК ИсточникГФУНоменклатуры,
	|	ВЫБОР
	|		КОГДА &РасчетыПоДоговорам
	|			ТОГДА &Договор
	|		КОГДА &ПродажаПоЗаказам И НЕ &РасчетыПоНакладным
	|			ТОГДА ТаблицаВидыЗапасов.ЗаказКлиента
	|		ИНАЧЕ
	|			&ДокументОснование
	|	КОНЕЦ КАК ИсточникГФУРасчетов
	|ИЗ
	|	ВтВидыЗапасовКорректировкаВыручки КАК ТаблицаВидыЗапасов
	|
	|ГДЕ
	|	(ТаблицаВидыЗапасов.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	ИЛИ (ТаблицаВидыЗапасов.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) И Не &ВернутьМногооборотнуюТару)
	|	ИЛИ ТаблицаВидыЗапасов.ТипНоменклатуры ЕСТЬ NULL)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСКлиентами(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыСКлиентами";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	УстановитьПараметрыЗапросаАналитикаУчетаПоПартнерам(Запрос);
	ТекстЗапроса = "
	//1. Расхождения по ресурсу "Сумма". Без тары.
	|ВЫБРАТЬ
	|	&Период																			 КАК Период,
	|	&Период																			 КАК ДатаРегистратора,
	|	&ДатаПлатежа																	 КАК ДатаПлатежа,
	|
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ																			 КАК ВидДвижения,
	|
	|	&АналитикаУчетаПоПартнерам														 КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам ТОГДА
	|		&Договор
	|	ИНАЧЕ
	|		ВЫБОР КОГДА &ПродажаПоЗаказам И Не &РасчетыПоНакладным ТОГДА
	|			ТаблицаРасхождения.ЗаказКлиента
	|		ИНАЧЕ
	|			&ДокументОснование
	|		КОНЕЦ
	|	КОНЕЦ																			 КАК ЗаказКлиента,
	|
	|	Неопределено																	 КАК ПродажаПоЗаказу,
	|	&ВалютаВзаиморасчетов															 КАК Валюта,
	|	Истина																			 КАК ИсключатьПриКонтроле,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)			 КАК ХозяйственнаяОперация,
	|	Неопределено																	 КАК ФормаОплаты,
	|
	|	СУММА(ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		-ТаблицаРасхождения.СуммаВзаиморасчетов
	|	ИНАЧЕ
	|		ТаблицаРасхождения.СуммаВзаиморасчетов
	|	КОНЕЦ)																			 КАК Сумма,
	|
	|	0																				 КАК КОплате,
	|	0																				 КАК КОтгрузке,
	|	0																				 КАК Отгружается,
	|	ВЫРАЗИТЬ(СУММА(ВЫБОР
	|						КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|							ВЫБОР 
	|								КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|									-ТаблицаРасхождения.СуммаВзаиморасчетов
	|								ИНАЧЕ
	|									ТаблицаРасхождения.СуммаВзаиморасчетов
	|							КОНЕЦ 
	|						ИНАЧЕ
	|							ВЫБОР 
	|								КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|									-ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл
	|								ИНАЧЕ
	|									ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл
	|							КОНЕЦ
	|						КОНЕЦ) КАК ЧИСЛО(15, 2))		 КАК СуммаРегл,
	|	ВЫРАЗИТЬ(СУММА(ВЫБОР
	|						КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|							ВЫБОР 
	|								КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|									-ТаблицаРасхождения.СуммаВзаиморасчетов
	|								ИНАЧЕ
	|									ТаблицаРасхождения.СуммаВзаиморасчетов
	|							КОНЕЦ 
	|						ИНАЧЕ
	|							ВЫБОР 
	|								КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|									-ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуУпр
	|								ИНАЧЕ
	|									ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуУпр
	|							КОНЕЦ
	|						КОНЕЦ) КАК ЧИСЛО(15, 2))		 КАК СуммаУпр,
	|	
	|	0																				 КАК ЗалогЗаТару,
	|	&Организация																	 КАК Организация
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И (ТаблицаРасхождения.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИЛИ НЕ &ВернутьМногооборотнуюТару)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	//2. Расхождения по ресурсу "Сумма". МногооборотнаяТара.
	|ВЫБРАТЬ
	|	&Период																			 КАК Период,
	|	&Период																			 КАК ДатаРегистратора,
	|	&ДатаПлатежа																	 КАК ДатаПлатежа,
	|
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ																			 КАК ВидДвижения,
	|
	|	&АналитикаУчетаПоПартнерам														 КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам ТОГДА
	|		&Договор
	|	ИНАЧЕ
	|		ВЫБОР КОГДА &ПродажаПоЗаказам И Не &РасчетыПоНакладным ТОГДА
	|			ТаблицаРасхождения.ЗаказКлиента
	|		ИНАЧЕ
	|			&ДокументОснование
	|		КОНЕЦ
	|	КОНЕЦ																			 КАК ЗаказКлиента,
	|
	|	Неопределено																	 КАК ПродажаПоЗаказу,
	|	&ВалютаВзаиморасчетов															 КАК Валюта,
	|	Истина																			 КАК ИсключатьПриКонтроле,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)			 КАК ХозяйственнаяОперация,
	|	Неопределено																	 КАК ФормаОплаты,
	|
	|	СУММА(ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		-ТаблицаРасхождения.СуммаВзаиморасчетов
	|	ИНАЧЕ
	|		ТаблицаРасхождения.СуммаВзаиморасчетов
	|	КОНЕЦ)																			 КАК Сумма,
	|
	|	0																				 КАК КОплате,
	|	0																				 КАК КОтгрузке,
	|	0																				 КАК Отгружается,
	|	ВЫРАЗИТЬ(СУММА(ВЫБОР
	|						КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|							ВЫБОР 
	|								КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|									-ТаблицаРасхождения.СуммаВзаиморасчетов
	|								ИНАЧЕ
	|									ТаблицаРасхождения.СуммаВзаиморасчетов
	|							КОНЕЦ 
	|						ИНАЧЕ
	|							ВЫБОР 
	|								КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|									-ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл
	|								ИНАЧЕ
	|									ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуРегл
	|							КОНЕЦ
	|						КОНЕЦ) КАК ЧИСЛО(15, 2))									 КАК СуммаРегл,
	|	ВЫРАЗИТЬ(СУММА(ВЫБОР
	|						КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|							ВЫБОР 
	|								КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|									-ТаблицаРасхождения.СуммаВзаиморасчетов
	|								ИНАЧЕ
	|									ТаблицаРасхождения.СуммаВзаиморасчетов
	|							КОНЕЦ 
	|						ИНАЧЕ
	|							ВЫБОР 
	|								КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|									-ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуУпр
	|								ИНАЧЕ
	|									ТаблицаРасхождения.СуммаСНДС * &КоэффициентПересчетаВВалютуУпр
	|							КОНЕЦ
	|						КОНЕЦ) КАК ЧИСЛО(15, 2))									 КАК СуммаУпр,
	|	СУММА(ВЫБОР 
	|						КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|							-ТаблицаРасхождения.СуммаВзаиморасчетов
	|						ИНАЧЕ
	|							ТаблицаРасхождения.СуммаВзаиморасчетов
	|		КОНЕЦ)		 																 КАК ЗалогЗаТару,
	|	
	|	&Организация																	 КАК Организация
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ТаблицаРасхождения.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	И &ТребуетсяЗалогЗаТару
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ
	|
	//3. Изменения ресурса "КОплате".
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		&Период
	|	ИНАЧЕ
	|		КОНЕЦПЕРИОДА(&ДатаПлатежа, День)
	|	КОНЕЦ																			 КАК Период,
	|	&Период																			 КАК ДатаРегистратора,
	|	&ДатаПлатежа																	 КАК ДатаПлатежа,
	|
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ																			 КАК ВидДвижения,
	|
	|	&АналитикаУчетаПоПартнерам														 КАК АналитикаУчетаПоПартнерам,
	|
	|	ВЫБОР КОГДА &РасчетыПоДоговорам ТОГДА
	|		&Договор
	|	ИНАЧЕ
	|		ВЫБОР КОГДА &ПродажаПоЗаказам И Не &РасчетыПоНакладным ТОГДА
	|			ТаблицаРасхождения.ЗаказКлиента
	|		ИНАЧЕ
	|			&ДокументОснование
	|		КОНЕЦ
	|	КОНЕЦ																			 КАК ЗаказКлиента,
	|
	|	Неопределено																	 КАК ПродажаПоЗаказу,
	|	&ВалютаВзаиморасчетов															 КАК Валюта,
	|	Истина																			 КАК ИсключатьПриКонтроле,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) 		 КАК ХозяйственнаяОперация,
	|	&ФормаОплаты																	 КАК ФормаОплаты,
	|	0																				 КАК Сумма,
	|
	|	СУММА(ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		-ТаблицаРасхождения.СуммаВзаиморасчетов
	|	ИНАЧЕ
	|		ТаблицаРасхождения.СуммаВзаиморасчетов
	|	КОНЕЦ)																			 КАК КОплате,
	|
	|	0																				 КАК КОтгрузке,
	|	0																				 КАК Отгружается,
	|	0																				 КАК СуммаРегл,
	|	0																				 КАК СуммаУпр,
	|	0																				 КАК ЗалогЗаТару,
	|	&Организация																	 КАК Организация
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И ((ТаблицаРасхождения.КодСтроки <> 0 И ТаблицаРасхождения.СуммаСНДС > 0)
	|		ИЛИ ТаблицаРасхождения.КодСтроки = 0 
	|		ИЛИ &РасчетыПоНакладным)
	|	И (ТаблицаРасхождения.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИЛИ &ТребуетсяЗалогЗаТару
	|		ИЛИ ТаблицаРасхождения.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
	|	И НЕ &ГрафикИсполненияВДоговоре
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СуммаСНДС < 0 ТОГДА
	|		&Период
	|	ИНАЧЕ
	|		КОНЕЦПЕРИОДА(&ДатаПлатежа, День)
	|	КОНЕЦ";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеДоходы";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаРасхождения", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхождения(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период                                КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация                           КАК Организация,
	|	&ПодразделениеДоходы                   КАК Подразделение,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаВидыЗапасов.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ТОГДА
	|			ТаблицаВидыЗапасов.НаправлениеДеятельности
	|		ИНАЧЕ
	|			&НаправлениеДеятельности
	|	КОНЕЦ                                  КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА
	|			ТИПЗНАЧЕНИЯ(ДанныеДокумента.ДокументОснование) = ТИП(Документ.РеализацияУслугПрочихАктивов)
	|			И НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, ГОД) = НАЧАЛОПЕРИОДА(ДанныеДокумента.ДокументОснование.Дата, ГОД)
	|		ТОГДА 
	|			ТаблицаВидыЗапасов.СтатьяДоходов
	|		ИНАЧЕ
	|			&СтатьяДоходов
	|	КОНЕЦ                                  КАК СтатьяДоходов,
	|	ВЫБОР
	|		КОГДА
	|			ТИПЗНАЧЕНИЯ(ДанныеДокумента.ДокументОснование) = ТИП(Документ.РеализацияУслугПрочихАктивов)
	|			И НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, ГОД) = НАЧАЛОПЕРИОДА(ДанныеДокумента.ДокументОснование.Дата, ГОД)
	|		ТОГДА 
	|			ТаблицаВидыЗапасов.АналитикаДоходов
	|		ИНАЧЕ
	|			&АналитикаДоходов
	|	КОНЕЦ КАК АналитикаДоходов,
	|	ТаблицаВидыЗапасов.СуммаСНДСУпр        КАК Сумма,
	|	(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаУпр,
	|	(ВЫБОР
	|		КОГДА &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСРегл
	|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) КАК ХозяйственнаяОперация
	|ИЗ
	|	ВтТаблицаРасхождения КАК ТаблицаВидыЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаВидыЗапасов.Ссылка
	|ГДЕ
	|	ТаблицаВидыЗапасов.НаДоходы
	|	И &ИспользоватьУчетПрочихДоходовРасходов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаВидыЗапасов.НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;

	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МатериалыИРаботыВПроизводстве";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтРасхожденияРаботыУслуги", ТекстыЗапроса) Тогда
		ТекстЗапросаВтРасхожденияРаботыУслуги(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Работы.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Период КАК ДатаРегистратора,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	Работы.Номенклатура,
	|	Работы.Характеристика,
	|	Работы.АналитикаУчетаНоменклатуры,
	|	Работы.Количество,
	|	Работы.Назначение,
	|	ВЫБОР 
	|		КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг)
	|			ИНАЧЕ &НалогообложениеНДС 
	|	КОНЕЦ КАК НалогообложениеНДС
	|ИЗ
	|	ВтРасхожденияРаботыУслуги КАК Работы
	|ГДЕ
	|	НЕ Работы.НаДоходыРасходы
	|	И Работы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецФункции

Функция ТекстЗапросаТаблицаСуммыДокументовВВалютеРегл(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СуммыДокументовВВалютеРегл";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтВидыЗапасовКорректировкаВыручки", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВидыЗапасовКорректировкаВыручки(Запрос, ТекстыЗапроса);
	КонецЕсли;
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
	КонецЕсли;
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовСписание", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовСписание(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	&ПериодПродажи КАК Период,
	|	&Валюта КАК Валюта,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА &Валюта = &ВалютаРегламентированногоУчета
	|		ТОГДА ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И ТаблицаДокумента.СуммаВзаиморасчетов <> 0
	|		ТОГДА ТаблицаДокумента.СуммаВзаиморасчетов - ВЫБОР КОГДА ТаблицаДокумента.СуммаСНДС = 0 ТОГДА 0 ИНАЧЕ
	|			ВЫРАЗИТЬ(ТаблицаДокумента.СуммаВзаиморасчетов * ТаблицаДокумента.СуммаНДС / ТаблицаДокумента.СуммаСНДС КАК ЧИСЛО(15, 2)) КОНЕЦ
	|		ИНАЧЕ (ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|	ВЫБОР КОГДА &Валюта = &ВалютаРегламентированногоУчета
	|		ТОГДА ТаблицаДокумента.СуммаНДС
	|		ИНАЧЕ ТаблицаДокумента.СуммаНДС * &КоэффициентПересчетаВВалютуРегл
	|	КОНЕЦ КАК СуммаНДСРегл,
	|	ВЫБОР
	|		КОГДА &Валюта = &ВалютаРегламентированногоУчета
	|			ТОГДА ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И ТаблицаДокумента.СуммаВзаиморасчетов <> 0
	|			ТОГДА ТаблицаДокумента.СуммаВзаиморасчетов - 
	|				ВЫБОР КОГДА ТаблицаДокумента.СуммаСНДС = 0 ТОГДА 0 
	|					ИНАЧЕ ВЫРАЗИТЬ(ТаблицаДокумента.СуммаВзаиморасчетов * ТаблицаДокумента.СуммаНДС / ТаблицаДокумента.СуммаСНДС КАК ЧИСЛО(15, 2)) 
	|				КОНЕЦ
	|		ИНАЧЕ (ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл
	|	КОНЕЦ КАК БазаНДСРегл,
	|	ВЫБОР
	|		КОГДА &Валюта = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И ТаблицаДокумента.СуммаВзаиморасчетов <> 0
	|			ТОГДА ТаблицаДокумента.СуммаВзаиморасчетов - 
	|				ВЫБОР КОГДА ТаблицаДокумента.СуммаСНДС = 0 ТОГДА 0 
	|					ИНАЧЕ ВЫРАЗИТЬ(ТаблицаДокумента.СуммаВзаиморасчетов * ТаблицаДокумента.СуммаНДС / ТаблицаДокумента.СуммаСНДС КАК ЧИСЛО(15, 2)) 
	|				КОНЕЦ
	|		ИНАЧЕ (ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр
	|	КОНЕЦ КАК БазаНДСУпр,
	|	ВЫБОР
	|		КОГДА &Валюта = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И ТаблицаДокумента.СуммаВзаиморасчетов <> 0
	|			ТОГДА ТаблицаДокумента.СуммаВзаиморасчетов - ВЫБОР КОГДА ТаблицаДокумента.СуммаСНДС = 0 ТОГДА 0 ИНАЧЕ
	|				ВЫРАЗИТЬ(ТаблицаДокумента.СуммаВзаиморасчетов * ТаблицаДокумента.СуммаНДС / ТаблицаДокумента.СуммаСНДС КАК ЧИСЛО(15, 2)) КОНЕЦ
	|		ИНАЧЕ (ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр
	|	КОНЕЦ КАК СуммаБезНДСУпр,
	|	ВЫБОР КОГДА &Валюта = &ВалютаУправленческогоУчета
	|		ТОГДА ТаблицаДокумента.СуммаНДС
	|		ИНАЧЕ ТаблицаДокумента.СуммаНДС * &КоэффициентПересчетаВВалютуУпр
	|	КОНЕЦ КАК СуммаНДСУпр,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов
	|
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И (Номенклатура.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|		ИЛИ Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2 КАК Порядок,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	&ПериодПродажи КАК Период,
	|	&Валюта КАК Валюта,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаБезНДСРегл КАК СуммаБезНДСРегл,
	|	ТаблицаДокумента.НДСРегл КАК СуммаНДСРегл,
	|	ТаблицаДокумента.СуммаБезНДСРегл КАК БазаНДСРегл,
	|	ТаблицаДокумента.СуммаБезНДСУпр КАК БазаНДСУпр,
	|	ТаблицаДокумента.СуммаБезНДСУпр КАК СуммаБезНДСУпр,
	|	ТаблицаДокумента.СуммаСНДСУпр - ТаблицаДокумента.СуммаБезНДСУпр КАК СуммаНДСУпр,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов
	|
	|ИЗ
	|	ВтВидыЗапасовКорректировкаВыручки КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3 КАК Порядок,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	&ПериодПродажи КАК Период,
	|	&Валюта КАК Валюта,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	-(ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	-(ТаблицаДокумента.СуммаНДС) КАК СуммаНДС,
	|	-(ТаблицаДокумента.СуммаБезНДСРегл) КАК СуммаБезНДСРегл,
	|	-(ТаблицаДокумента.НДСРегл) КАК СуммаНДСРегл,
	|	-(ТаблицаДокумента.СуммаБезНДСРегл) КАК БазаНДСРегл,
	|	-(ТаблицаДокумента.СуммаБезНДСУпр) КАК БазаНДСУпр,
	|	-(ТаблицаДокумента.СуммаБезНДСУпр) КАК СуммаБезНДСУпр,
	|	-(ТаблицаДокумента.СуммаСНДСУпр - ТаблицаДокумента.СуммаБезНДСУпр) КАК СуммаНДСУпр,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов
	|
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4 КАК Порядок,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	&ПериодПродажи КАК Период,
	|	&Валюта КАК Валюта,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаБезНДСРегл КАК СуммаБезНДСРегл,
	|	ТаблицаДокумента.НДСРегл КАК СуммаНДСРегл,
	|	ТаблицаДокумента.СуммаБезНДСРегл КАК БазаНДСРегл,
	|	ТаблицаДокумента.СуммаБезНДСУпр КАК БазаНДСУпр,
	|	ТаблицаДокумента.СуммаБезНДСУпр КАК СуммаБезНДСУпр,
	|	ТаблицаДокумента.СуммаСНДСУпр - ТаблицаДокумента.СуммаБезНДСУпр КАК СуммаНДСУпр,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов
	|
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияКонтрагентДоходыРасходы(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияКонтрагентДоходыРасходы";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаРасхождения", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхождения(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
#Область КорректировкаРеализацииСоСписаниемНаРасходы
	КорректировкаРеализацииСоСписаниемНаРасходы = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаРеализацииСоСписаниемНаРасходы) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	ВЫБОР 
	|		КОГДА &ВидКорректировки В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара))
	|			ТОГДА &ПодразделениеРасходы 
	|		ИНАЧЕ &Подразделение
	|	КОНЕЦ КАК Подразделение,
	|
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА &ПродажаПоЗаказам
	|			ТОГДА ТаблицаВидыЗапасов.ЗаказКлиента
	|		ИНАЧЕ &ДокументОснование
	|	КОНЕЦ КАК ОбъектРасчетов,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаВидыЗапасов.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ТОГДА
	|			ТаблицаВидыЗапасов.НаправлениеДеятельности
	|		ИНАЧЕ
	|			&НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиСтатьи,
	|	&СтатьяРасходов КАК СтатьяДоходовРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	
	|	-ТаблицаВидыЗапасов.СуммаСНДСУпр КАК Сумма,
	|	-ТаблицаВидыЗапасов.СуммаБезНДСУпр КАК СуммаБезНДС,
	|	-ТаблицаВидыЗапасов.СуммаБезНДСРегл - ТаблицаВидыЗапасов.НДСРегл КАК СуммаРегл,
	|	-ТаблицаВидыЗапасов.СуммаБезНДСРегл КАК СуммаРеглБезНДС,
	|
	|	&Валюта КАК Валюта,
	|	-ТаблицаВидыЗапасов.СуммаСНДС КАК СуммаВВалюте,
	|	-ТаблицаВидыЗапасов.СуммаСНДС + ТаблицаВидыЗапасов.СуммаНДС КАК СуммаБезНДСВВалюте,
	|		
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	-ТаблицаВидыЗапасов.СуммаСНДС КАК СуммаВВалютеВзаиморасчетов,
	|	-ТаблицаВидыЗапасов.СуммаСНДС + ТаблицаВидыЗапасов.СуммаНДС КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	ВЫБОР
	|		КОГДА &РасчетыПоДоговорам
	|			ТОГДА &Договор
	|		КОГДА &ПродажаПоЗаказам И НЕ &РасчетыПоНакладным
	|			ТОГДА ТаблицаВидыЗапасов.ЗаказКлиента
	|		ИНАЧЕ &ДокументОснование
	|   КОНЕЦ КАК ИсточникГФУРасчетов
	|ИЗ
	|	ВтТаблицаРасхождения КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	ТаблицаВидыЗапасов.НаРасходы
	|";
#КонецОбласти

#Область КорректировкаРеализацииСОтражениемНаПрочихДоходах
	КорректировкаРеализацииСОтражениемНаПрочихДоходах = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаРеализацииСОтражениемНаПрочихДоходах) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	ВЫБОР 
	|		КОГДА &ВидКорректировки В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара))
	|			ТОГДА &ПодразделениеДоходы
	|		ИНАЧЕ &Подразделение
	|	КОНЕЦ КАК Подразделение,
	|
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА &ПродажаПоЗаказам
	|			ТОГДА ТаблицаВидыЗапасов.ЗаказКлиента
	|		ИНАЧЕ &ДокументОснование
	|	КОНЕЦ КАК ОбъектРасчетов,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаВидыЗапасов.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ТОГДА
	|			ТаблицаВидыЗапасов.НаправлениеДеятельности
	|		ИНАЧЕ
	|			&НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиСтатьи,
	|	ВЫБОР
	|		КОГДА
	|			ТИПЗНАЧЕНИЯ(ДанныеДокумента.ДокументОснование) = ТИП(Документ.РеализацияУслугПрочихАктивов)
	|			И НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, ГОД) = НАЧАЛОПЕРИОДА(ДанныеДокумента.ДокументОснование.Дата, ГОД)
	|		ТОГДА 
	|			ТаблицаВидыЗапасов.СтатьяДоходов
	|		ИНАЧЕ
	|			&СтатьяДоходов
	|	КОНЕЦ КАК СтатьяДоходовРасходов,
	|	ВЫБОР
	|		КОГДА
	|			ТИПЗНАЧЕНИЯ(ДанныеДокумента.ДокументОснование) = ТИП(Документ.РеализацияУслугПрочихАктивов)
	|			И НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, ГОД) = НАЧАЛОПЕРИОДА(ДанныеДокумента.ДокументОснование.Дата, ГОД)
	|		ТОГДА 
	|			ТаблицаВидыЗапасов.АналитикаДоходов
	|		ИНАЧЕ
	|			&АналитикаДоходов
	|	КОНЕЦ КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	
	|	ТаблицаВидыЗапасов.СуммаСНДСУпр КАК Сумма,
	|	ТаблицаВидыЗапасов.СуммаБезНДСУпр КАК СуммаБезНДС,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.НДСРегл КАК СуммаРегл,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл КАК СуммаРеглБезНДС,
	|
	|	&Валюта КАК Валюта,
	|	ТаблицаВидыЗапасов.СуммаСНДС КАК СуммаВВалюте,
	|	ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС КАК СуммаБезНДСВВалюте,
	|		
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаВидыЗапасов.СуммаСНДС КАК СуммаВВалютеВзаиморасчетов,
	|	ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	ВЫБОР
	|		КОГДА &РасчетыПоДоговорам
	|			ТОГДА &Договор
	|		КОГДА &ПродажаПоЗаказам И НЕ &РасчетыПоНакладным
	|			ТОГДА ТаблицаВидыЗапасов.ЗаказКлиента
	|		ИНАЧЕ &ДокументОснование
	|   КОНЕЦ КАК ИсточникГФУРасчетов
	|ИЗ
	|	ВтТаблицаРасхождения КАК ТаблицаВидыЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаВидыЗапасов.Ссылка
	|ГДЕ
	|	ТаблицаВидыЗапасов.НаДоходы";
#КонецОбласти

	ТекстЗапроса = КорректировкаРеализацииСоСписаниемНаРасходы
		+ " ОБЪЕДИНИТЬ ВСЕ " + КорректировкаРеализацииСОтражениемНаПрочихДоходах;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаКорректировкаРеализацииБезНДС(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаКорректировкаРеализацииБезНДС";
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	НЕ МАКСИМУМ(ВЫБОР
	|			КОГДА ДанныеДокумента.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|				ТОГДА ИСТИНА
	|			КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|				ТОГДА ИСТИНА
	|			КОГДА &ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара)
	|				ТОГДА ИСТИНА
	|			КОГДА &ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара)
	|				ТОГДА ИСТИНА
	|			КОГДА тчТовары.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ТОГДА ИСТИНА
	|			КОГДА тчРасхождения.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ТребуетсяЗаписьКнигиПродаж
	|ПОМЕСТИТЬ ВтТаблицаКорректировкаРеализацииБезНДС
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Товары КАК тчТовары
	|		ПО ДанныеДокумента.Ссылка = тчТовары.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Расхождения КАК тчРасхождения
	|		ПО ДанныеДокумента.Ссылка = тчРасхождения.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка";              
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаНДСЗаписиКнигиПродаж(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НДСЗаписиКнигиПродаж";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовСписание", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовСписание(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаКорректировкаРеализацииБезНДС", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаКорректировкаРеализацииБезНДС(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);

#Область ТекстЗапросаРеализацияПерепоставленногоТовара
	ТекстЗапросаРеализацияПерепоставленногоТовара =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Покупатель,
	|	&Ссылка КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) КАК ВидЦенности,
	|	ТаблицаРасхождения.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НалогИсчисляетПокупатель)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация)
	|	КОНЕЦ КАК Событие,
	|	&Период КАК ДатаСобытия,
	|	СУММА(ЕСТЬNULL(СуммыДокументовВВалютеРегл.БазаНДСРегл,
	|		ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)))
	|	) КАК СуммаБезНДС,
	|	СУММА(ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаНДСРегл,
	|		ВЫРАЗИТЬ(ТаблицаРасхождения.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)))
	|	) КАК НДС
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|		ПО ТаблицаРасхождения.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|			И ТаблицаРасхождения.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|			И СуммыДокументовВВалютеРегл.БазаНДСРегл <> 0
	|			И (ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС) = СуммыДокументовВВалютеРегл.СуммаБезНДС
	|			И ТаблицаРасхождения.СуммаНДС = СуммыДокументовВВалютеРегл.СуммаНДС
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И &ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара)
	|	И (ТаблицаРасхождения.Номенклатура.ТипНоменклатуры НЕ В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|	И &НалогообложениеНДС В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС))
	|	И &Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НалогИсчисляетПокупатель)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация)
	|	КОНЕЦ,
	|	ТаблицаРасхождения.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Покупатель,
	|	&Ссылка КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) КАК ВидЦенности,
	|	ТаблицаСписание.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НалогИсчисляетПокупатель)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация)
	|	КОНЕЦ КАК Событие,
	|	&Период КАК ДатаСобытия,
	|	СУММА(
	|		ЕСТЬNULL(СуммыДокументовВВалютеРегл.БазаНДСРегл,
	|			ВЫРАЗИТЬ((ТаблицаСписание.СуммаСНДС - ТаблицаСписание.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)))
	|	) КАК СуммаБезНДС,
	|	СУММА(
	|		ЕСТЬNULL(СуммыДокументовВВалютеРегл.СуммаНДСРегл,
	|			ВЫРАЗИТЬ(ТаблицаСписание.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)))
	|	) КАК НДС
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК ТаблицаСписание
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|		ПО ТаблицаСписание.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|			И ТаблицаСписание.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|			И СуммыДокументовВВалютеРегл.БазаНДСРегл <> 0
	|			И (ТаблицаСписание.СуммаСНДС - ТаблицаСписание.СуммаНДС) = СуммыДокументовВВалютеРегл.СуммаБезНДС
	|			И ТаблицаСписание.СуммаНДС = СуммыДокументовВВалютеРегл.СуммаНДС
	|ГДЕ
	|	ТаблицаСписание.Ссылка = &Ссылка
	|	И &ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара)
	|	И ТаблицаСписание.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	И &НалогообложениеНДС В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС))
	|	И &Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА &НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.НалогИсчисляетПокупатель)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация)
	|	КОНЕЦ,
	|	ТаблицаСписание.СтавкаНДС";
#КонецОбласти
	
#Область ТекстЗапросаКорректировкаРеализацииБезНДС
	ТекстЗапросаКорректировкаРеализацииБезНДС =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Покупатель,
	|	&Ссылка КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) КАК ВидЦенности,
	|	ТаблицаРасхождения.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация) КАК Событие,
	|	&Период КАК ДатаСобытия,
	|	СУММА(ЕСТЬNULL(СуммыДокументовВВалютеРегл.БазаНДСРегл,
	|		ВЫРАЗИТЬ((ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)))
	|	) КАК СуммаБезНДС,
	|	0 КАК НДС
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаРасхождения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаКорректировкаРеализацииБезНДС КАК РеализацииБезНДС
	|		ПО ТаблицаРасхождения.Ссылка = РеализацииБезНДС.Ссылка 
	|			И РеализацииБезНДС.ТребуетсяЗаписьКнигиПродаж
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|		ПО ТаблицаРасхождения.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|			И ТаблицаРасхождения.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|ГДЕ
	|	(ТаблицаРасхождения.Номенклатура.ТипНоменклатуры НЕ В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|	И НЕ &ПрименяетсяОсвобождениеОтУплатыНДС
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасхождения.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Покупатель,
	|	&Ссылка КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) КАК ВидЦенности,
	|	ТаблицаКорректировкаВыручки.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация) КАК Событие,
	|	&Период КАК ДатаСобытия,
	|	СУММА(ЕСТЬNULL(СуммыДокументовВВалютеРегл.БазаНДСРегл,
	|		ВЫРАЗИТЬ((ТаблицаКорректировкаВыручки.СуммаСНДС - ТаблицаКорректировкаВыручки.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)))
	|	) КАК СуммаБезНДС,
	|	0 КАК НДС
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК ТаблицаКорректировкаВыручки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаКорректировкаРеализацииБезНДС КАК РеализацииБезНДС
	|		ПО ТаблицаКорректировкаВыручки.Ссылка = РеализацииБезНДС.Ссылка 
	|			И РеализацииБезНДС.ТребуетсяЗаписьКнигиПродаж
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|		ПО ТаблицаКорректировкаВыручки.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|			И ТаблицаКорректировкаВыручки.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|
	|ГДЕ
	|	НЕ ТаблицаКорректировкаВыручки.ВидЗапасов.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар),
	|															ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.АгентскаяУслуга))
	|	И НЕ &ПрименяетсяОсвобождениеОтУплатыНДС
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаКорректировкаВыручки.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Покупатель,
	|	&Ссылка КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) КАК ВидЦенности,
	|	ТаблицаСписание.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация) КАК Событие,
	|	&Период КАК ДатаСобытия,
	|	СУММА(
	|		ЕСТЬNULL(СуммыДокументовВВалютеРегл.БазаНДСРегл,
	|			ВЫРАЗИТЬ((ТаблицаСписание.СуммаСНДС - ТаблицаСписание.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)))
	|	) КАК СуммаБезНДС,
	|	0 КАК НДС
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК ТаблицаСписание
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаКорректировкаРеализацииБезНДС КАК РеализацииБезНДС
	|		ПО ТаблицаСписание.Ссылка = РеализацииБезНДС.Ссылка 
	|			И РеализацииБезНДС.ТребуетсяЗаписьКнигиПродаж
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|		ПО ТаблицаСписание.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|			И ТаблицаСписание.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|
	|ГДЕ
	|	НЕ ТаблицаСписание.ВидЗапасов.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар),
	|															ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.АгентскаяУслуга))
	|	И НЕ &ПрименяетсяОсвобождениеОтУплатыНДС
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСписание.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Покупатель,
	|	&Ссылка КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) КАК ВидЦенности,
	|	ТаблицаОприходование.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация) КАК Событие,
	|	&Период КАК ДатаСобытия,
	|	СУММА(
	|		ЕСТЬNULL(СуммыДокументовВВалютеРегл.БазаНДСРегл,
	|			ВЫРАЗИТЬ((-ТаблицаОприходование.СуммаСНДС - ТаблицаОприходование.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)))
	|	) КАК СуммаБезНДС,
	|	0 КАК НДС
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК ТаблицаОприходование
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаКорректировкаРеализацииБезНДС КАК РеализацииБезНДС
	|		ПО ТаблицаОприходование.Ссылка = РеализацииБезНДС.Ссылка 
	|			И РеализацииБезНДС.ТребуетсяЗаписьКнигиПродаж
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыДокументовВВалютеРегл
	|		ПО ТаблицаОприходование.Ссылка = СуммыДокументовВВалютеРегл.Регистратор
	|			И ТаблицаОприходование.ИдентификаторСтроки = СуммыДокументовВВалютеРегл.ИдентификаторСтроки
	|
	|ГДЕ
	|	НЕ ТаблицаОприходование.ВидЗапасов.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар),
	|															ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.АгентскаяУслуга))
	|	И НЕ &ПрименяетсяОсвобождениеОтУплатыНДС
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОприходование.СтавкаНДС";
#КонецОбласти
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ТекстЗапросаРеализацияПерепоставленногоТовара);
	ТекстыОтражения.Добавить(ТекстЗапросаКорректировкаРеализацииБезНДС);
	ТекстЗапроса = СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаНДСПредъявленный(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НДСПредъявленный";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период		                           КАК Период,
	|	&Организация		                   КАК Организация,
	|	&Ссылка                         	   КАК СчетФактура,
	|	&Контрагент                            КАК Поставщик,
	|	ТаблицаВидыЗапасов.ВидЦенности         КАК ВидЦенности,
	|	ТаблицаВидыЗапасов.СтавкаНДС           КАК СтавкаНДС,
	|	&НалогообложениеНДС			           КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО                           КАК ИсправленныйСчетФактура,
	|	НЕОПРЕДЕЛЕНО                           КАК РеализацияЭкспорт,
	|	-(ТаблицаВидыЗапасов.СуммаБезНДСРегл)  КАК СуммаБезНДС,
	|	-(ТаблицаВидыЗапасов.НДСРегл)		   КАК НДС,
	|	(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА -(ТаблицаВидыЗапасов.НДСУпр)
	|		ИНАЧЕ 0 КОНЕЦ)                     КАК НДСУпр,
	|	НЕОПРЕДЕЛЕНО                           КАК Событие,
	|	ЛОЖЬ                                   КАК РегламентнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|ГДЕ
	|	&Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|	И &НалогообложениеНДС В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров))
	|	И ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	И (ТаблицаВидыЗапасов.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) ИЛИ Не &ВернутьМногооборотнуюТару)
	|	И &ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаУслугиКОформлениюОтчетовПринципалу(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "УслугиКОформлениюОтчетовПринципалу";

	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("КурсыВалют", ТекстыЗапроса) Тогда
		ТекстЗапросаВтКурсыВалют(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтРасхожденияРаботыУслуги", ТекстыЗапроса) Тогда
		ТекстЗапросаВтРасхожденияРаботыУслуги(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	КОформлению.НомерСтроки                     КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	&Организация                                КАК Организация,
	|	КОформлению.АналитикаНоменклатурыПринципала КАК АналитикаУчетаНоменклатуры,
	|	КОформлению.Соглашение                      КАК Соглашение,
	|	КОформлению.Валюта                          КАК Валюта,
	|	КОформлению.Количество                      КАК Количество,
	|	КОформлению.СуммаСНДС 
	|		* КурсыВалют.КоэффициентПересчета       КАК СуммаВыручки,
	|	КОформлению.СуммаНДС 
	|		* КурсыВалют.КоэффициентПересчета       КАК СуммаНДС,
	|	&Контрагент                                 КАК Покупатель,
	|	&Ссылка                                     КАК ДокументРеализации
	|ИЗ
	|	ВтРасхожденияРаботыУслуги КАК КОформлению
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|	ПО КОформлению.Валюта = КурсыВалют.Валюта
	|ГДЕ
	|	НЕ КОформлению.НаДоходыРасходы
	|	И КОформлению.АналитикаНоменклатурыПринципала <> НЕОПРЕДЕЛЕНО
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеАктивыПассивы.ТекстЗапросаТаблицаПрочиеАктивыПассивы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";

	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаРасхождения", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхождения(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы();
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ" + "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&ПодразделениеРасходы                  КАК Подразделение,
	|	&СтатьяРасходов                        КАК СтатьяРасходов,
	|	&АналитикаРасходов                     КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ТОГДА
	|			ТаблицаРасхождения.НаправлениеДеятельности
	|		ИНАЧЕ
	|			&НаправлениеДеятельности
	|	КОНЕЦ                                  КАК НаправлениеДеятельности,
	|	&НалогообложениеНДС КАК ВидДеятельностиНДС,
	|
	|	(ВЫБОР
	|		КОГДА ТаблицаРасхождения.СуммаСНДСУпр < 0
	|			ТОГДА -ТаблицаРасхождения.СуммаСНДСУпр
	|		ИНАЧЕ ТаблицаРасхождения.СуммаСНДСУпр КОНЕЦ) КАК СуммаСНДС,
	|	(ВЫБОР
	|		КОГДА ТаблицаРасхождения.СуммаБезНДСУпр < 0
	|			ТОГДА -ТаблицаРасхождения.СуммаБезНДСУпр
	|		ИНАЧЕ ТаблицаРасхождения.СуммаБезНДСУпр КОНЕЦ) КАК СуммаБезНДС,
	|	(ВЫБОР
	|		КОГДА ТаблицаРасхождения.СуммаБезНДСУпр < 0
	|			ТОГДА -ТаблицаРасхождения.СуммаБезНДСУпр
	|		ИНАЧЕ ТаблицаРасхождения.СуммаБезНДСУпр КОНЕЦ) КАК СуммаБезНДСУпр,
	|
	|	(ВЫБОР
	|		КОГДА (ТаблицаРасхождения.СуммаБезНДСРегл + ТаблицаРасхождения.НДСРегл) < 0
	|			ТОГДА -(ТаблицаРасхождения.СуммаБезНДСРегл + ТаблицаРасхождения.НДСРегл)
	|		ИНАЧЕ ТаблицаРасхождения.СуммаБезНДСРегл + ТаблицаРасхождения.НДСРегл КОНЕЦ) КАК СуммаСНДСРегл,
	|	(ВЫБОР
	|		КОГДА ТаблицаРасхождения.СуммаБезНДСРегл < 0
	|			ТОГДА -ТаблицаРасхождения.СуммаБезНДСРегл
	|		ИНАЧЕ ТаблицаРасхождения.СуммаБезНДСРегл КОНЕЦ) КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ВтТаблицаРасхождения КАК ТаблицаРасхождения
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО СтатьиРасходов.Ссылка = &СтатьяРасходов
	|ГДЕ
	|	ТаблицаРасхождения.НаРасходы
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";

	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&ПодразделениеРасходы                   КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	ТаблицаВидыЗапасов.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаВидыЗапасов.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаВидыЗапасов.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ТОГДА
	|			ТаблицаВидыЗапасов.НаправлениеДеятельности
	|		ИНАЧЕ
	|			&НаправлениеДеятельности
	|	КОНЕЦ                                   КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	&НалогообложениеНДС КАК ВидДеятельностиНДС,
	|
	|	ТаблицаВидыЗапасов.СуммаСНДСУпр КАК Стоимость,
	|	ТаблицаВидыЗапасов.СуммаБезНДСУпр КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл КАК СтоимостьРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	0 КАК НДСРегл,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация
	|
	|ПОМЕСТИТЬ ВтИсходныеПартииПрочихРасходов
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	ТаблицаВидыЗапасов.НаДоходыРасходы
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти



#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Реализация товаров
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Реализация товаров'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

	// Реестр номеров ГТД
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "РеестрНомеровГТД";
	КомандаПечати.Представление = НСтр("ru = 'Реестр номеров ГТД'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ФункциональныеОпции = "ИспользоватьИмпортныеТовары";

	Если НЕ ПраваПользователяПовтИсп.ЭтоПартнер() Тогда
		// Акт об оказании услуг
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьАктОбОказанииУслуг";
		КомандаПечати.Идентификатор = "Акт";
		КомандаПечати.Представление = НСтр("ru = 'Акт об оказании услуг'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 1;	
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьАктыНаПередачуПрав") Тогда
			// Акт на передачу прав
			КомандаПечати = КомандыПечати.Добавить();
			КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
			КомандаПечати.Идентификатор = "АктНаПередачуПрав";
			КомандаПечати.Представление = НСтр("ru = 'Акт на передачу прав'");
			КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
			КомандаПечати.Порядок = 99;
		КонецЕсли;
	
		// Товарная накладная (ТОРГ-12)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "ТОРГ12";
		КомандаПечати.Представление = НСтр("ru = 'Товарная накладная (ТОРГ-12)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Истина);
		КомандаПечати.Порядок = 2;	
		// Товарная накладная без услуг (ТОРГ-12)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "ТОРГ12";
		КомандаПечати.Представление = НСтр("ru = 'Товарная накладная без услуг (ТОРГ-12)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Ложь);
		КомандаПечати.Порядок = 3;	
		// Товарная накладная с номерами ГТД (ТОРГ-12)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "ТОРГ12";
		КомандаПечати.Представление = НСтр("ru = 'Товарная накладная с номерами ГТД (ТОРГ-12)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Ложь);
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьГТД",    Истина);
		КомандаПечати.Порядок = 4;	
	КонецЕсли;

	// Счет-фактура
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "СчетФактура";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Ложь);
	КомандаПечати.Порядок = 5;	

	Если НЕ ПраваПользователяПовтИсп.ЭтоПартнер() Тогда
		// Счет-фактура (в валюте)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "СчетФактураВВалюте";
		КомандаПечати.Представление = НСтр("ru = 'Счет-фактура (в валюте)'");
		КомандаПечати.ФункциональныеОпции = "ИспользоватьНесколькоВалют";
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Истина);
		КомандаПечати.Порядок = 6;	
	КонецЕсли;

	Документы.ТранспортнаяНакладная.ДобавитьКомандыПечати(КомандыПечати);

	Если ПравоДоступа("Чтение", Метаданные.Справочники.СертификатыНоменклатуры)
		И ПолучитьФункциональнуюОпцию("ИспользоватьСертификатыНоменклатуры") Тогда
		// Реестр сертификатов номенклатуры
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "СертификатыНоменклатурыРеестр";
		КомандаПечати.Представление = НСтр("ru = 'Сертификаты (реестр)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 8;
		
		// Изображения сертификатов номенклатуры
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "СертификатыНоменклатурыИзображенияИзДокументов";
		КомандаПечати.Представление = НСтр("ru = 'Сертификаты (по каждой позиции документа)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 9;
		
		// Изображения сертификатов номенклатуры
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "СертификатыНоменклатурыИзображенияИзДокументовБезДублей";
		КомандаПечати.Представление = НСтр("ru = 'Сертификаты (по одному на сертификат)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 10;	
	КонецЕсли;

	Если НЕ ПраваПользователяПовтИсп.ЭтоПартнер() Тогда
		
		// Универсальный передаточный документ (УПД)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "УПД";
		КомандаПечати.Представление = НСтр("ru = 'Универсальный передаточный документ (УПД)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 11;	
		
		// Универсальный корректировочный документ (УКД)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "УКД";
		КомандаПечати.Представление = НСтр("ru = 'Универсальный корректировочный документ (УКД)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 12;	
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранение") Тогда
		// МХ-1 
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "МХ1";
		КомандаПечати.Представление = НСтр("ru='Акт о приеме-передаче ТМЦ на хранение (МХ-1)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранение") Тогда
		// МХ-3 
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "МХ3";
		КомандаПечати.Представление = НСтр("ru = 'Акт о возврате ТМЦ, сданных на хранение (МХ-3)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Функция получает данные для формирования печатной формы Акт на передачу прав
//
Функция ПолучитьДанныеДляПечатнойФормыАктНаПередачуПрав(ПараметрыПечати, МассивОбъектов) Экспорт 
	Если ПараметрыПечати.Свойство("ВыводитьГТД") Тогда
		ПараметрыПечати.Удалить("ВыводитьГТД");
	КонецЕсли;
	Если ПараметрыПечати.Свойство("ВыводитьУслуги") Тогда
		ПараметрыПечати.ВыводитьУслуги = Истина;
	Иначе
		ПараметрыПечати.Вставить("ВыводитьУслуги", Истина);
	КонецЕсли;
	Возврат ПолучитьДанныеДляПечатнойФормыТОРГ12(ПараметрыПечати, МассивОбъектов);
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Накладная", НСтр("ru = 'Реализация товаров'"), СформироватьПечатнуюФормуНакладная(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РеестрНомеровГТД") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "РеестрНомеровГТД", НСтр("ru = 'Реестр номеров ГТД'"), СформироватьПечатнуюФормуРеестрНомеровГТД(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СертификатыНоменклатурыРеестр") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СертификатыНоменклатурыРеестр",
			НСтр("ru = 'Сертификаты (реестр)'"),
			Справочники.СертификатыНоменклатуры.СформироватьПечатнуюФормуРеестрСертификатовНоменклатуры(МассивОбъектов, ОбъектыПечати),
			,
			"Справочник.СертификатыНоменклатуры.ПФ_MXL_РеестрСертификатов");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СертификатыНоменклатурыИзображенияИзДокументов") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СертификатыНоменклатурыИзображенияИзДокументов",
			НСтр("ru = 'Сертификаты (по каждой позиции документа)'"),
			Справочники.СертификатыНоменклатуры.СформироватьПечатнуюФормуИзображенияСертификатовИзДокументов(МассивОбъектов, ОбъектыПечати),
			,
			"Справочник.СертификатыНоменклатуры.ПФ_MXL_ИзображенияСертификатов");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СертификатыНоменклатурыИзображенияИзДокументовБезДублей") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СертификатыНоменклатурыИзображенияИзДокументовБезДублей",
			НСтр("ru = 'Сертификаты (по одному на сертификат)'"),
			Справочники.СертификатыНоменклатуры.СформироватьПечатнуюФормуИзображенияСертификатовИзДокументовБезДублей(МассивОбъектов, ОбъектыПечати),
			,
			"Справочник.СертификатыНоменклатуры.ПФ_MXL_ИзображенияСертификатов");
	КонецЕсли;
		
	ФормированиеПечатныхФорм.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);
	
КонецПроцедуры

Функция ТекстЗапросаВтВидыЗапасов() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Основной,
	|	ДанныеДокументов.Ссылка КАК Подчиненный,
	|	ДанныеДокументов.Ссылка.МоментВремени КАК МоментВремени
	|ПОМЕСТИТЬ ВтСвязанныеДокументы
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущийДокумент.Ссылка КАК Основной,
	|	ТекущийДокумент.ДокументОснование КАК Подчиненный,
	|	ТекущийДокумент.ДокументОснование.МоментВремени КАК МоментВремени
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ТекущийДокумент
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ТекущийДокумент.Ссылка = ДанныеДокументов.Ссылка
	|ГДЕ
	|	НЕ ДанныеДокументов.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущийДокумент.Ссылка КАК Основной,
	|	ПредыдущиеДокументы.Ссылка КАК Подчиненный,
	|	ПредыдущиеДокументы.МоментВремени КАК МоментВремени
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ТекущийДокумент
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ТекущийДокумент.Ссылка = ДанныеДокументов.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ПредыдущиеДокументы
	|		ПО ТекущийДокумент.ДокументОснование = ПредыдущиеДокументы.ДокументОснование
	|			И (ТекущийДокумент.Дата >= ПредыдущиеДокументы.Дата
	|			ИЛИ (ТекущийДокумент.Дата = ПредыдущиеДокументы.Дата
	|			И ТекущийДокумент.Ссылка >= ПредыдущиеДокументы.Ссылка))
	|ГДЕ
	|	ПредыдущиеДокументы.Проведен
	|	И НЕ ДанныеДокументов.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО КАК Основной,
	|	Корректировки.Ссылка КАК Подчиненный,
	|	Корректировки.МоментВремени КАК МоментВремени
	|ИЗ
	|	Документ.КорректировкаРеализации КАК Корректировки
	|ГДЕ
	|	Корректировки.ДокументОснование = &ОснованиеНеЗаписанногоДокумента
	|	И Корректировки.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО КАК Основной,
	|	ДанныеОснования.Ссылка КАК Подчиненный,
	|	ДанныеОснования.МоментВремени КАК МоментВремени
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеОснования
	|ГДЕ
	|	ДанныеОснования.Ссылка = &ОснованиеНеЗаписанногоДокумента
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ДоКорректировки,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.АналитикаУчетаНаборов КАК АналитикаУчетаНаборов,
	|	ВидыЗапасов.Упаковка КАК Упаковка,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ВидыЗапасов.КодТНВЭД КАК КодТНВЭД,
	|	ВидыЗапасов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВидыЗапасов.Количество КАК Количество,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	) КАК СуммаБезНДС,
	|	
	|	ВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	) КАК СуммаНДС,
	|	
	|	ВидыЗапасов.ЗаказКлиента КАК ЗаказКлиента
	|ПОМЕСТИТЬ ВсеВидыЗапасов
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	ИСТИНА КАК ДоКорректировки,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.АналитикаУчетаНаборов КАК АналитикаУчетаНаборов,
	|	ВидыЗапасов.Упаковка КАК Упаковка,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ВидыЗапасов.КодТНВЭД КАК КодТНВЭД,
	|	ВидыЗапасов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВидыЗапасов.Количество КАК Количество,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	) КАК СуммаБезНДС,
	|	
	|	ВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	) КАК СуммаНДС,
	|	
	|	ВидыЗапасов.ЗаказКлиента КАК ЗаказКлиента
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной,
	|	ЛОЖЬ,
	|	ВидыЗапасов.НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.АналитикаУчетаНаборов,
	|	ВидыЗапасов.Упаковка,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.НомерГТД,
	|	ВидыЗапасов.КодТНВЭД,
	|	0,
	|	0,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.СтавкаНДС,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.ЗаказКлиента
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной,
	|	ИСТИНА,
	|	ВидыЗапасов.НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.АналитикаУчетаНаборов,
	|	ВидыЗапасов.Упаковка,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.НомерГТД,
	|	ВидыЗапасов.КодТНВЭД,
	|	0,
	|	0,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.СтавкаНДС,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.ЗаказКлиента
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|			И ВидыЗапасов.Ссылка <> СвязанныеДокументы.Основной
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной,
	|	ЛОЖЬ,
	|	ВидыЗапасов.НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.АналитикаУчетаНаборов,
	|	ВидыЗапасов.Упаковка,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.НомерГТД,
	|	ВидыЗапасов.КодТНВЭД,
	|	-ВидыЗапасов.КоличествоУпаковок,
	|	-ВидыЗапасов.Количество,
	|	
	|	-ЕСТЬNULL(
	|		-СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.СтавкаНДС,
	|	
	|	-ЕСТЬNULL(
	|		-СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.ЗаказКлиента
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной,
	|	ИСТИНА,
	|	ВидыЗапасов.НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.АналитикаУчетаНаборов,
	|	ВидыЗапасов.Упаковка,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.НомерГТД,
	|	ВидыЗапасов.КодТНВЭД,
	|	-ВидыЗапасов.КоличествоУпаковок,
	|	-ВидыЗапасов.Количество,
	|	
	|	-ЕСТЬNULL(
	|		-СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.СтавкаНДС,
	|	
	|	-ЕСТЬNULL(
	|		-СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.ЗаказКлиента
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|			И ВидыЗапасов.Ссылка <> СвязанныеДокументы.Основной
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной,
	|	ЛОЖЬ,
	|	ВидыЗапасов.НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.АналитикаУчетаНаборов,
	|	ВидыЗапасов.Упаковка,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.НомерГТД,
	|	ВидыЗапасов.КодТНВЭД,
	|	ВидыЗапасов.КоличествоУпаковок,
	|	ВидыЗапасов.Количество,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.СтавкаНДС,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.ЗаказКлиента
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной,
	|	ИСТИНА,
	|	ВидыЗапасов.НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.АналитикаУчетаНаборов,
	|	ВидыЗапасов.Упаковка,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.НомерГТД,
	|	ВидыЗапасов.КодТНВЭД,
	|	ВидыЗапасов.КоличествоУпаковок,
	|	ВидыЗапасов.Количество,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаБезНДСРегл,
	|		ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.СтавкаНДС,
	|	
	|	ЕСТЬNULL(
	|		СуммыРегл.СуммаНДСРегл,
	|		ВидыЗапасов.СуммаНДС
	|	),
	|	
	|	ВидыЗапасов.ЗаказКлиента
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ВидыЗапасов.Ссылка = СвязанныеДокументы.Подчиненный
	|			И ВидыЗапасов.Ссылка <> СвязанныеДокументы.Основной
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеВидыЗапасов.Ссылка                                        КАК Ссылка,
	|	ВсеВидыЗапасов.Ссылка.ДокументОснование                      КАК ДокументОснование,
	|	ЕСТЬNULL(ВсеВидыЗапасов.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару, Ложь) КАК ВернутьМногооборотнуюТару,
	|	ВсеВидыЗапасов.ДоКорректировки                               КАК ДоКорректировки,
	|	МАКСИМУМ(ВсеВидыЗапасов.НомерСтроки)                         КАК НомерСтроки,
	|	ВсеВидыЗапасов.АналитикаУчетаНоменклатуры                    КАК АналитикаУчетаНоменклатуры,
	|	ВсеВидыЗапасов.АналитикаУчетаНаборов                         КАК АналитикаУчетаНаборов,
	|	ВсеВидыЗапасов.Упаковка                                      КАК Упаковка,
	|	ВсеВидыЗапасов.НомерГТД                                      КАК НомерГТД,
	|	ВсеВидыЗапасов.КодТНВЭД                                      КАК КодТНВЭД,
	|	ВсеВидыЗапасов.ВидЗапасов                                    КАК ВидЗапасов,
	|	СУММА(ВсеВидыЗапасов.КоличествоУпаковок)                     КАК КоличествоУпаковок,
	|	СУММА(ВсеВидыЗапасов.Количество)                             КАК Количество,
	|	СУММА(ВсеВидыЗапасов.СуммаБезНДС)                            КАК СуммаБезНДС,
	|	ВсеВидыЗапасов.СтавкаНДС                                     КАК СтавкаНДС,
	|	СУММА(ВсеВидыЗапасов.СуммаНДС)                               КАК СуммаНДС,
	|	СУММА(ВсеВидыЗапасов.СуммаБезНДС + ВсеВидыЗапасов.СуммаНДС)  КАК СуммаСНДС
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	ВсеВидыЗапасов КАК ВсеВидыЗапасов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеВидыЗапасов.Ссылка,
	|	ВсеВидыЗапасов.Ссылка.ДокументОснование,
	|	ЕСТЬNULL(ВсеВидыЗапасов.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару, Ложь),
	|	ВсеВидыЗапасов.ДоКорректировки,
	|	ВсеВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	ВсеВидыЗапасов.АналитикаУчетаНаборов,
	|	ВсеВидыЗапасов.Упаковка,
	|	ВсеВидыЗапасов.НомерГТД,
	|	ВсеВидыЗапасов.КодТНВЭД,
	|	ВсеВидыЗапасов.ВидЗапасов,
	|	ВсеВидыЗапасов.СтавкаНДС
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВсеВидыЗапасов
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьПараметрыВтВидыЗапасов(Запрос, ПересчитыватьВВалютуРегл, ВключаяДоКорректировки, ОснованиеНеЗаписанногоДокумента = Неопределено) Экспорт
	
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл",        ПересчитыватьВВалютуРегл);
	Запрос.УстановитьПараметр("ВключаяДоКорректировки",          ВключаяДоКорректировки);
	Запрос.УстановитьПараметр("ОснованиеНеЗаписанногоДокумента", ОснованиеНеЗаписанногоДокумента);
	
КонецПроцедуры

// Функция получает данные для формирования печатной формы ТОРГ - 12
//
Функция ПолучитьДанныеДляПечатнойФормыТОРГ12(ПараметрыПечати, МассивОбъектов) Экспорт 

	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Валюта КАК Валюта,
	|	ДанныеДокументов.ВидКорректировки КАК ХозяйственнаяОперация
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК ДокументОснование,
	|	ЛОЖЬ КАК Корректировочный
	|
	|ПОМЕСТИТЬ СчетаФактурыОснования
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	Если ПараметрыПечати.Свойство("ВыводитьГТД")Тогда
		ПараметрыЗаполнения.ВключаяНомераГТД = ПараметрыПечати.Свойство("ВыводитьГТД");
	КонецЕсли;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	ПродажиСервер.ПоместитьВременнуюТаблицуКоэффициентыУпаковок(МенеджерВременныхТаблиц, "ТаблицаТоваровКорректировкаРеализации");
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);	
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка КАК Ссылка,
	|	КорректировкаРеализации.ДокументОснование.Номер КАК Номер,
	|	КорректировкаРеализации.ДокументОснование.Дата КАК Дата,
	|	НЕОПРЕДЕЛЕНО КАК Статус,
	|	КорректировкаРеализации.Партнер КАК Партнер,
	|	КорректировкаРеализации.Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Организация.ОбособленноеПодразделение
	|			ТОГДА КорректировкаРеализации.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ КорректировкаРеализации.Организация
	|	КОНЕЦ КАК Организация,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	КорректировкаРеализации.Отпустил КАК Кладовщик,
	|	КорректировкаРеализации.ОтпустилДолжность КАК ДолжностьКладовщика,
	|	КорректировкаРеализации.Организация.Префикс КАК Префикс,
	|	КорректировкаРеализации.Основание КАК Основание,
	|	КорректировкаРеализации.ОснованиеДата КАК ОснованиеДата,
	|	КорректировкаРеализации.ОснованиеНомер КАК ОснованиеНомер,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Контрагент
	|		ИНАЧЕ КорректировкаРеализации.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА КорректировкаРеализации.Организация
	|		ИНАЧЕ КорректировкаРеализации.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	КорректировкаРеализации.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	КорректировкаРеализации.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	КорректировкаРеализации.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	КорректировкаРеализации.БанковскийСчетГрузополучателя КАК БанковскийСчетГрузополучателя,
	|	КорректировкаРеализации.АдресДоставки КАК АдресДоставки,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	КорректировкаРеализации.НалогообложениеНДС КАК НалогообложениеНДС,
	|	КорректировкаРеализации.Валюта КАК Валюта,
	|	КорректировкаРеализации.ДоверенностьНомер КАК ДоверенностьНомер,
	|	КорректировкаРеализации.ДоверенностьДата КАК ДоверенностьДата,
	|	КорректировкаРеализации.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	КорректировкаРеализации.ДоверенностьЛицо КАК ДоверенностьЛицо,
	|	&ЕдиницаИзмеренияВеса КАК ЕдиницаИзмеренияВеса
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО КорректировкаРеализации.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО КорректировкаРеализации.Ссылка = ДанныеДокументов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(ТаблицаТоваров.Номенклатура.НаименованиеПолное, ТаблицаТоваров.Содержание) КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаТоваров.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаТоваров.Номенклатура.Код
	|	КОНЕЦ КАК Артикул,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаТоваров.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаТоваров.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
	|			ТОГДА """"
	|		ИНАЧЕ ТаблицаТоваров.Упаковка.Наименование
	|	КОНЕЦ КАК УпаковкаНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			&ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|		ИНАЧЕ
	|			&ТекстЗапросаНаименованиеЕдиницыИзмерения2
	|	КОНЕЦ КАК ВидУпаковки,
	|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТоваров.НомерГТД КАК НомерГТД,
	|	ТаблицаТоваров.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ ТаблицаТоваров.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ КоэффициентыУпаковок.Количество / КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ВЫБОР
	|					КОГДА КоэффициентыУпаковок.Количество < КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|						ТОГДА КоэффициентыУпаковок.Количество
	|					ИНАЧЕ КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|					ТОГДА 1
	|				ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоВОдномМесте,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.Количество
	|	КОНЕЦ КАК Цена,
	|	ТаблицаТоваров.СуммаБезНДС КАК СуммаБезНДС,
	|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС КАК СуммаСНДС,
	|	ТаблицаТоваров.Количество * &ТекстЗапросаВес2 КАК МассаНетто,
	|	ВЫБОР
	|		КОГДА &ЗаполненаЕдиницаИзмеренияВеса
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL ТОГДА
	|						ТаблицаТоваров.Количество * &ТекстЗапросаВес1
	|					ИНАЧЕ
	|						ТаблицаТоваров.КоличествоУпаковок * &ТекстЗапросаВес1
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК МассаБрутто,
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаТоваров.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару
	|			И ТаблицаТоваров.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	ТаблицаТоваровКорректировкаРеализации КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыУпаковок КАК КоэффициентыУпаковок
	|		ПО ТаблицаТоваров.Ссылка = КоэффициентыУпаковок.Ссылка
	|			И ТаблицаТоваров.КоличествоУпаковок = КоэффициентыУпаковок.КоличествоУпаковок
	|			И ТаблицаТоваров.НомерСтроки = КоэффициентыУпаковок.НомерСтроки
	|			И НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|ГДЕ
	|	(ТаблицаТоваров.Ссылка.ДокументОснование ССЫЛКА Документ.РеализацияТоваровУслуг
	|	ИЛИ ТаблицаТоваров.Ссылка.ДокументОснование ССЫЛКА Документ.РеализацияУслугПрочихАктивов)
	|	И (ТаблицаТоваров.ЭтоТовар
	|			ИЛИ &ВыводитьУслуги)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ТаблицаТоваров.Упаковка",
		"ТаблицаТоваров.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаВес1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаВес2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"КоэффициентыУпаковок.ВидУпаковки",
			"ТаблицаТоваров.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.УстановитьПараметр("ВыводитьУслуги",                  ПараметрыПечати.ВыводитьУслуги);
	Запрос.УстановитьПараметр("КолонкаКодов",                    КолонкаКодов);
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияВеса",            Константы.ЕдиницаИзмеренияВеса.Получить());
	Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса",   ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[1];
	
	СтруктураДанныхДляПечати = Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",
	                                               РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Формирует текст запроса для получения данных основания при печати Счет-фактуры.
//
Функция ТекстЗапросаДанныхОснованияДляПечатнойФормыСчетФактура() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                                   КАК Ссылка,
	|	ДанныеДокументов.ВидКорректировки                         КАК ХозяйственнаяОперация,
	|	ДанныеДокументов.Валюта                                   КАК Валюта,
	|	ДанныеДокументов.Организация                              КАК Организация,
	|	ДанныеДокументов.НалогообложениеНДС                       КАК НалогообложениеНДС,
	|	ДанныеДокументов.Подразделение                            КАК Подразделение,
	|	ДанныеДокументов.Склад                                    КАК Склад,
	|	ДанныеДокументов.Грузоотправитель                         КАК Грузоотправитель,
	|	ДанныеДокументов.Грузополучатель                          КАК Грузополучатель,
	|	ЛОЖЬ                                                      КАК РасчетыЧерезОтдельногоКонтрагента,
	|	НЕОПРЕДЕЛЕНО                                              КАК Номенклатура,
	|	""""                                                      КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)             КАК Комиссионер,
	|	ДанныеДокументов.Основание                                КАК Основание,
	|	ДанныеДокументов.ОснованиеДата                            КАК ОснованиеДата,
	|	ДанныеДокументов.ОснованиеНомер                           КАК ОснованиеНомер,
	|	ДанныеДокументов.ДоверенностьНомер                        КАК ДоверенностьНомер,
	|	ДанныеДокументов.ДоверенностьДата                         КАК ДоверенностьДата,
	|	ДанныеДокументов.ДоверенностьВыдана                       КАК ДоверенностьВыдана,
	|	ДанныеДокументов.ДоверенностьЛицо                         КАК ДоверенностьЛицо,
	|	ДанныеДокументов.Отпустил.Наименование                    КАК Кладовщик,
	|	ДанныеДокументов.ОтпустилДолжность                        КАК ДолжностьКладовщика
	|
	|//ОператорПОМЕСТИТЬ
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&ДокументОснование_КорректировкаРеализации)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция получения данных печатной формы Акт документа
//
Функция ПолучитьДанныеДляПечатнойФормыАктОбОказанииУслуг(ПараметрыПечати, МассивОбъектов) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА ПрочаяРеализация.Ссылка ЕСТЬ NULL ТОГДА
	|		ЛОЖЬ
	|	ИНАЧЕ
	|		ИСТИНА
	|	КОНЕЦ КАК КорректировкаПрочейРеализации
	|ПОМЕСТИТЬ ДанныеКорректировки
	|
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.РеализацияУслугПрочихАктивов КАК ПрочаяРеализация
	|	ПО
	|		КорректировкаРеализации.ДокументОснование = ПрочаяРеализация.Ссылка
	|
	|ГДЕ
	|	КорректировкаРеализации.Ссылка В (&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ШАПКЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка              	КАК Ссылка,
	|	КорректировкаРеализации.ДокументОснование.Номер КАК Номер,
	|	КорректировкаРеализации.ДокументОснование.Дата	КАК Дата,
	|	КорректировкаРеализации.Партнер             	КАК Партнер,
	|	КорректировкаРеализации.Контрагент          	КАК Контрагент,
	|	КорректировкаРеализации.Организация         	КАК Организация,
	|	КорректировкаРеализации.Организация.Префикс 	КАК Префикс,
	|	КорректировкаРеализации.Валюта              	КАК Валюта,
	|	КорректировкаРеализации.ЦенаВключаетНДС     	КАК ЦенаВключаетНДС,
	|	ВЫБОР
	|		КОГДА
	|			КорректировкаРеализации.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|			ИЛИ КорректировкаРеализации.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|		ТОГДА
	|			ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ                                                            КАК УчитыватьНДС,
	|	КорректировкаРеализации.Склад.ТекущийОтветственный.Наименование   КАК ОтпускПроизвел
	|
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|
	|ГДЕ
	|	КорректировкаРеализации.Ссылка В (&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ТАБЛИЧНЫМ ЧАСТЯМ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка                                                     КАК Ссылка,
	|	ВложенныйЗапрос.Номенклатура                                               КАК Номенклатура,
	|	ВложенныйЗапрос.Номенклатура.НаименованиеПолное                            КАК УслугаНаименованиеПолное,
	|	ВложенныйЗапрос.Номенклатура.Код                                           КАК Код,
	|	ВложенныйЗапрос.Номенклатура.Артикул                                       КАК Артикул,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Наименование                              КАК ЕдиницаЦены,
	|	ВложенныйЗапрос.ЕдиницаИзмерения                                           КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Наименование                              КАК ЕдиницаИзмеренияНаименование,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Код                                       КАК ЕдиницаИзмеренияКод,
	|	ВложенныйЗапрос.Характеристика                                             КАК Характеристика,
	|	ВложенныйЗапрос.Характеристика.НаименованиеПолное                          КАК ХарактеристикаНаименованиеПолное,
	|	ВЫБОР КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) = 1
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВложенныйЗапрос.Упаковка.Наименование
	|	КОНЕЦ                                                                      КАК УпаковкаНаименование,
	|	ВложенныйЗапрос.СтавкаНДС                                                  КАК СтавкаНДС,
	|	ВложенныйЗапрос.Цена                                                       КАК Цена,
	|	ВложенныйЗапрос.Количество                                                 КАК Количество,
	|	ВложенныйЗапрос.Сумма                                                      КАК Сумма,
	|	ВложенныйЗапрос.СуммаНДС                                                   КАК СуммаНДС,
	|	ВложенныйЗапрос.СуммаСкидки                                                КАК СуммаСкидки,
	|	ВложенныйЗапрос.СуммаБезСкидки                                             КАК СуммаБезСкидки,
	|	ВложенныйЗапрос.НомерСтроки                                                КАК НомерСтроки,
	|	Ложь                                                                       КАК ЭтоВозвратнаяТара
	|ИЗ (
	|	ВЫБРАТЬ
	|		КорректировкаРеализации.Ссылка КАК Ссылка,
	|		КорректировкаРеализации.Номенклатура КАК Номенклатура,
	|
	|		ВЫБОР КОГДА КорректировкаРеализации.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) ТОГДА
	|			1
	|		ИНАЧЕ
	|			&ТекстЗапросаКоэффициентУпаковки2
	|		КОНЕЦ КАК Коэффициент,
	|		&ТекстЗапросаЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		0 КАК ПроцентСкидки,
	|		КорректировкаРеализации.Характеристика              КАК Характеристика,
	|		КорректировкаРеализации.Упаковка                    КАК Упаковка,
	|		КорректировкаРеализации.СтавкаНДС                   КАК СтавкаНДС,
	|		КорректировкаРеализации.Цена                        КАК Цена,
	|		КорректировкаРеализации.Количество                  КАК Количество,
	|		КорректировкаРеализации.Сумма                       КАК Сумма,
	|		0												    КАК СуммаСкидки,
	|		КорректировкаРеализации.Сумма 						КАК СуммаБезСкидки,
	|		КорректировкаРеализации.СуммаНДС                    КАК СуммаНДС,
	|		КорректировкаРеализации.НомерСтроки                 КАК НомерСтроки
	|	ИЗ
	|		Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализации
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеКорректировки КАК ДанныеКорректировки
	|		ПО КорректировкаРеализации.Ссылка = ДанныеКорректировки.Ссылка
	|
	|	ГДЕ
	|		КорректировкаРеализации.Ссылка В (&МассивДокументов)
	|		И КорректировкаРеализации.Номенклатура.ТипНоменклатуры НЕ В
	|			(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		И НЕ ДанныеКорректировки.КорректировкаПрочейРеализации
	|	) КАК ВложенныйЗапрос
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
	|	КорректировкаРеализации.Содержание КАК УслугаНаименованиеПолное,
	|	"""" КАК Код,
	|	"""" КАК Артикул,
	|	"""" КАК ЕдиницаЦены,
	|	НЕОПРЕДЕЛЕНО КАК ЕдиницаИзмерения,
	|	"""" КАК ЕдиницаИзмеренияНаименование,
	|	"""" КАК ЕдиницаИзмеренияКод,
	|	НЕОПРЕДЕЛЕНО КАК Характеристика,
	|	"""" КАК ХарактеристикаНаименованиеПолное,
	|	"""" КАК УпаковкаНаименование,
	|	КорректировкаРеализации.СтавкаНДС КАК СтавкаНДС,
	|	КорректировкаРеализации.Цена КАК Цена,
	|	КорректировкаРеализации.Количество КАК Количество,
	|	КорректировкаРеализации.Сумма КАК Сумма,
	|	КорректировкаРеализации.СуммаНДС КАК СуммаНДС,
	|	0 КАК СуммаСкидки,
	|	КорректировкаРеализации.Сумма КАК СуммаБезСкидки,
	|	КорректировкаРеализации.НомерСтроки КАК НомерСтроки,
	|	Ложь КАК ЭтоВозвратнаяТара
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализации
	|
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеКорректировки КАК ДанныеКорректировки
	|	ПО КорректировкаРеализации.Ссылка = ДанныеКорректировки.Ссылка
	|
	|ГДЕ
	|	КорректировкаРеализации.Ссылка В (&МассивДокументов)
	|	И ДанныеКорректировки.КорректировкаПрочейРеализации
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|
	|ИТОГИ
	|	СУММА(СуммаСкидки)
	|ПО
	|	Ссылка
	|");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ВложенныйЗапрос.Упаковка",
			"ВложенныйЗапрос.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"КорректировкаРеализации.Упаковка",
			"КорректировкаРеализации.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"КорректировкаРеализации.Упаковка",
			"КорректировкаРеализации.Номенклатура"));
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Возврат Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		МассивРезультатов[1],
		МассивРезультатов[2]);
	
КонецФункции

// Функция получает данные для формирования ЭД "Соглашение об изменении стоимости"
//
Функция ПолучитьДанныеДляПечатнойФормыСоглашениеОбИзмененииСтоимости(ПараметрыПечати, МассивОбъектов) Экспорт 

	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Валюта КАК Валюта,
	|	ДанныеДокументов.ВидКорректировки КАК ХозяйственнаяОперация
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК ДокументОснование,
	|	ЛОЖЬ КАК Корректировочный
	|
	|ПОМЕСТИТЬ СчетаФактурыОснования
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ВключаяДоКорректировки = Истина;
	Если ПараметрыПечати.Свойство("ВыводитьГТД") Тогда
		ПараметрыЗаполнения.ВключаяНомераГТД = ПараметрыПечати.ВыводитьГТД;
	КонецЕсли;
	ПоместитьВременнуюТаблицуТоваровДляСоглашенийОбИзмененииСтоимости(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА УпаковкиНоменклатуры.Ссылка ЕСТЬ NULL 
	|				ТОГДА 1
	|			ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки1
	|		КОНЕЦ) КАК Коэффициент
	|ПОМЕСТИТЬ Упаковки
	|ИЗ
	|	ТаблицаТоваровКорректировкаРеализации КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиНоменклатуры
	|		ПО (УпаковкиНоменклатуры.Родитель = ТаблицаТоваров.Упаковка)
	|			И (УпаковкиНоменклатуры.Владелец = ТаблицаТоваров.Номенклатура)
	|ГДЕ
	|	НЕ УпаковкиНоменклатуры.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Упаковки.Номенклатура,
	|	Упаковки.Упаковка КАК Упаковка,
	|	&ТекстЗапросаКоэффициентУпаковки2 КАК КоэффициентУпаковки,
	|	МИНИМУМ(УпаковкиНоменклатуры.Ссылка) КАК ВложеннаяУпаковка,
	|	МИНИМУМ(&ТекстЗапросаКоэффициентУпаковки3) КАК КоэффициентВложеннойУпаковки
	|ПОМЕСТИТЬ ВложенныеУпаковки
	|ИЗ
	|	Упаковки КАК Упаковки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиНоменклатуры
	|		ПО Упаковки.Номенклатура = УпаковкиНоменклатуры.Владелец
	|			И Упаковки.Коэффициент = &ТекстЗапросаКоэффициентУпаковки3
	|ГДЕ
	|	УпаковкиНоменклатуры.Родитель = Упаковки.Упаковка
	|
	|СГРУППИРОВАТЬ ПО
	|	Упаковки.Номенклатура,
	|	Упаковки.Упаковка,
	|	&ТекстЗапросаКоэффициентУпаковки2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.НомерСтроки,
	|	ТаблицаТоваров.Содержание,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Упаковка,
	|	ТаблицаТоваров.НомерГТД,
	|	ТаблицаТоваров.Количество,
	|	ТаблицаТоваров.КоличествоУпаковок,
	|	ТаблицаТоваров.СуммаБезНДС,
	|	ТаблицаТоваров.СтавкаНДС,
	|	ТаблицаТоваров.СуммаНДС,
	|	ТаблицаТоваров.ЭтоТовар,
	|	ТаблицаТоваров.ДоКорректировки,
	|	ВЫБОР
	|		КОГДА ВложенныеУпаковки.ВложеннаяУпаковка ЕСТЬ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|						ТОГДА 1
	|					ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки4
	|				КОНЕЦ
	|		ИНАЧЕ ВложенныеУпаковки.КоэффициентУпаковки
	|	КОНЕЦ КАК КоэффициентУпаковки,
	|	ВЫБОР
	|		КОГДА ВложенныеУпаковки.ВложеннаяУпаковка ЕСТЬ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|						ТОГДА 1
	|					ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки4
	|				КОНЕЦ
	|		ИНАЧЕ ВложенныеУпаковки.КоэффициентВложеннойУпаковки
	|	КОНЕЦ КАК КоэффициентВложеннойУпаковки,
	|	ВЫБОР
	|		КОГДА ВложенныеУпаковки.ВложеннаяУпаковка ЕСТЬ NULL 
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|						ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|					ИНАЧЕ ТаблицаТоваров.Упаковка
	|				КОНЕЦ
	|		ИНАЧЕ ВложенныеУпаковки.ВложеннаяУпаковка
	|	КОНЕЦ КАК ВидУпаковки,
	|	ТаблицаТоваров.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ТаблицаТоваровСКоэффициентамиУпаковок
	|ИЗ
	|	ТаблицаТоваровКорректировкаРеализации КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВложенныеУпаковки КАК ВложенныеУпаковки
	|		ПО ТаблицаТоваров.Номенклатура = ВложенныеУпаковки.Номенклатура
	|			И ТаблицаТоваров.Упаковка = ВложенныеУпаковки.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Упаковки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВложенныеУпаковки
	|;";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"УпаковкиНоменклатуры",
		"ТаблицаТоваров.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"Упаковки.Упаковка",
		"Упаковки.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки3",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"УпаковкиНоменклатуры",
		"Упаковки.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки4",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ТаблицаТоваров.Упаковка",
		"ТаблицаТоваров.Номенклатура"));
				
	Запрос.Выполнить();
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст = "
	|////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ШАПКЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка                                                 КАК Ссылка,
	|	КорректировкаРеализации.ДокументОснование                                      КАК ДокументОснование,
	|	КорректировкаРеализации.Номер                                                  КАК Номер,
	|	КорректировкаРеализации.Дата                                                   КАК Дата,
	|	КорректировкаРеализации.Партнер                                                КАК Партнер,
	|	КорректировкаРеализации.Контрагент                                             КАК Контрагент,
	|	КорректировкаРеализации.Организация                                            КАК Организация,
	|	ТаблицаОтветственныеЛица.РуководительНаименование                              КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность                                 КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование                          КАК ГлавныйБухгалтер,
	|	КорректировкаРеализации.Отпустил.Наименование                                  КАК Кладовщик,
	|	КорректировкаРеализации.ОтпустилДолжность                                      КАК ДолжностьКладовщика,
	|	КорректировкаРеализации.Организация.Префикс                                    КАК Префикс,
	|	КорректировкаРеализации.Основание                                              КАК Основание,
	|
	|	ВЫБОР КОГДА КорректировкаРеализации.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) ТОГДА
	|		КорректировкаРеализации.Контрагент
	|	ИНАЧЕ
	|		КорректировкаРеализации.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|
	|	ВЫБОР КОГДА КорректировкаРеализации.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) ТОГДА
	|		КорректировкаРеализации.Организация
	|	ИНАЧЕ
	|		КорректировкаРеализации.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|
	|	КорректировкаРеализации.БанковскийСчетОрганизации                             КАК БанковскийСчетОрганизации,
	|	КорректировкаРеализации.БанковскийСчетКонтрагента                             КАК БанковскийСчетКонтрагента,
	|	КорректировкаРеализации.БанковскийСчетГрузоотправителя                        КАК БанковскийСчетГрузоотправителя,
	|	КорректировкаРеализации.БанковскийСчетГрузополучателя                         КАК БанковскийСчетГрузополучателя,
	|	КорректировкаРеализации.АдресДоставки                                         КАК АдресДоставки,
	|	Неопределено                                                                  КАК Подразделение,
	|	КорректировкаРеализации.Валюта                                                КАК Валюта,
	|	КорректировкаРеализации.ДоверенностьНомер                                     КАК ДоверенностьНомер,
	|	КорректировкаРеализации.ДоверенностьДата                                      КАК ДоверенностьДата,
	|	КорректировкаРеализации.ДоверенностьВыдана                                    КАК ДоверенностьВыдана,
	|	КорректировкаРеализации.ДоверенностьЛицо                                      КАК ДоверенностьЛицо,
	|	&ЕдиницаИзмеренияВеса                                                         КАК ЕдиницаИзмеренияВеса
	|
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО
	|			КорректировкаРеализации.Ссылка = ДанныеДокументов.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО 
	|			КорректировкаРеализации.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|	
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ТАБЛИЧНЫМ ЧАСТЯМ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.НаименованиеНоменклатуры,
	|	ВложенныйЗапрос.НоменклатураКод,
	|	ВложенныйЗапрос.ЕдиницаИзмерения,
	|	ВложенныйЗапрос.ЕдиницаИзмеренияНаименование,
	|	ВложенныйЗапрос.ЕдиницаИзмеренияКод,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.НаименованиеХарактеристики,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.УпаковкаНаименование,
	|	ВложенныйЗапрос.ВидУпаковки,
	|	ВложенныйЗапрос.СтавкаНДС,
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.СтранаПроисхождения,
	|	СУММА(ВложенныйЗапрос.Количество)                           КАК Количество,
	|	СУММА(ВложенныйЗапрос.КоличествоДоКорректировки)            КАК КоличествоДоКорректировки,
	|	СУММА(ВложенныйЗапрос.КоличествоМест)                       КАК КоличествоМест,
	|	СУММА(ВложенныйЗапрос.КоличествоМестДоКорректировки)        КАК КоличествоМестДоКорректировки,
	|	СУММА(ВложенныйЗапрос.КоличествоВОдномМесте)                КАК КоличествоВОдномМесте,
	|	СУММА(ВложенныйЗапрос.КоличествоВОдномМестеДоКорректировки) КАК КоличествоВОдномМестеДоКорректировки,
	|	СУММА(ВложенныйЗапрос.Цена)                                 КАК Цена,
	|	СУММА(ВложенныйЗапрос.ЦенаДоКорректировки)                  КАК ЦенаДоКорректировки,
	|	СУММА(ВложенныйЗапрос.СуммаБезНДС)                          КАК СуммаБезНДС,
	|	СУММА(ВложенныйЗапрос.СуммаБезНДСДоКорректировки)           КАК СуммаБезНДСДоКорректировки,
	|	СУММА(ВложенныйЗапрос.СуммаСНДС)                            КАК СуммаСНДС,
	|	СУММА(ВложенныйЗапрос.СуммаСНДСДоКорректировки)             КАК СуммаСНДСДоКорректировки,
	|	СУММА(ВложенныйЗапрос.СуммаНДС)                             КАК СуммаНДС,
	|	СУММА(ВложенныйЗапрос.СуммаНДСДоКорректировки)              КАК СуммаНДСДоКорректировки,
	|	СУММА(ВложенныйЗапрос.МассаНетто)                           КАК МассаНетто,
	|	СУММА(ВложенныйЗапрос.МассаНеттоДоКорректировки)            КАК МассаНеттоДоКорректировки,
	|	СУММА(ВложенныйЗапрос.МассаБрутто)                          КАК МассаБрутто,
	|	СУММА(ВложенныйЗапрос.МассаБруттоДоКорректировки)           КАК МассаБруттоДоКорректировки,
	|	МАКСИМУМ(ВложенныйЗапрос.НомерСтроки)                       КАК НомерСтроки,
	|	ВложенныйЗапрос.ЭтоВозвратнаяТара
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТоваров.Ссылка                                       КАК Ссылка,
	|		ТаблицаТоваров.Номенклатура                                 КАК Номенклатура,
	|		ТаблицаТоваров.Номенклатура.НаименованиеПолное              КАК НаименованиеНоменклатуры,
	|		
	|		ВЫБОР КОГДА &КолонкаКодов = ""Артикул"" ТОГДА
	|			ТаблицаТоваров.Номенклатура.Артикул
	|		ИНАЧЕ
	|			ТаблицаТоваров.Номенклатура.Код
	|		КОНЕЦ                                                       КАК НоменклатураКод,
	|		
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ
	|			&ТекстЗапросаЕдиницаИзмерения
	|		КОНЕЦ                                                       КАК ЕдиницаИзмерения,
	|		
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Наименование
	|		ИНАЧЕ
	|			&ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|		КОНЕЦ                                                       КАК ЕдиницаИзмеренияНаименование,
	|		
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ
	|			&ТекстЗапросаКодЕдиницыИзмерения
	|		КОНЕЦ                                                       КАК ЕдиницаИзмеренияКод,
	|		
	|		ТаблицаТоваров.Характеристика                               КАК Характеристика,
	|		ТаблицаТоваров.Характеристика.НаименованиеПолное            КАК НаименованиеХарактеристики,
	|		ТаблицаТоваров.Упаковка                                     КАК Упаковка,
	|		
	|		ВЫБОР КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1 ТОГДА
	|			""""
	|		ИНАЧЕ
	|			ТаблицаТоваров.Упаковка.Наименование
	|		КОНЕЦ                                                       КАК УпаковкаНаименование,
	|		
	|		ВЫБОР КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Наименование
	|		ИНАЧЕ
	|			ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|				&ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|			ИНАЧЕ
	|				&ТекстЗапросаНаименованиеЕдиницыИзмерения2
	|			КОНЕЦ
	|		КОНЕЦ                                                       КАК ВидУпаковки,
	|		
	|		ТаблицаТоваров.СтавкаНДС                                    КАК СтавкаНДС,
	|		ТаблицаТоваров.НомерГТД                                     КАК НомерГТД,
	|		ТаблицаТоваров.НомерГТД.СтранаПроисхождения                 КАК СтранаПроисхождения,
	|		
	|		ВЫБОР КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ
	|			ТаблицаТоваров.Количество
	|		КОНЕЦ                                                       КАК Количество,
	|		0                                                           КАК КоличествоДоКорректировки,
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ
	|			ТаблицаТоваров.Количество / ТаблицаТоваров.КоэффициентВложеннойУпаковки
	|		КОНЕЦ                                                       КАК КоличествоМест,
	|		0                                                           КАК КоличествоМестДоКорректировки,
	|		ВЫБОР КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ВЫБОР КОГДА ТаблицаТоваров.Количество < ТаблицаТоваров.КоэффициентВложеннойУпаковки ТОГДА
	|				ТаблицаТоваров.Количество
	|			ИНАЧЕ
	|				ТаблицаТоваров.КоэффициентВложеннойУпаковки
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) ТОГДА
	|				1
	|			ИНАЧЕ
	|				&ТекстЗапросаКоэффициентУпаковки
	|			КОНЕЦ
	|		КОНЕЦ                                                       КАК КоличествоВОдномМесте,
	|		0                                                           КАК КоличествоВОдномМестеДоКорректировки,
	|		
	|		ВЫБОР КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ
	|			ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.Количество
	|		КОНЕЦ                                                       КАК Цена,
	|		0                                                           КАК ЦенаДоКорректировки,
	|		
	|		ТаблицаТоваров.СуммаБезНДС                                  КАК СуммаБезНДС,
	|		0                                                           КАК СуммаБезНДСДоКорректировки,
	|		
	|		ТаблицаТоваров.СуммаНДС                                     КАК СуммаНДС,
	|		0                                                           КАК СуммаНДСДоКорректировки,
	|		
	|		ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС        КАК СуммаСНДС,
	|		0                                                           КАК СуммаСНДСДоКорректировки,
	|		
	|		ТаблицаТоваров.Количество * &ТекстЗапросаВес КАК МассаНетто,
	|		0                                                           КАК МассаНеттоДоКорректировки,
	|		
	|		ВЫБОР КОГДА &ЗаполненаЕдиницаИзмеренияВеса ТОГДА
	|			ВЫБОР КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL ТОГДА
	|				ТаблицаТоваров.Количество * &ТекстЗапросаВес1
	|			ИНАЧЕ
	|				ТаблицаТоваров.КоличествоУпаковок * &ТекстЗапросаВес1
	|			КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ                                                       КАК МассаБрутто,
	|		0                                                           КАК МассаБруттоДоКорректировки,
	|		ТаблицаТоваров.НомерСтроки                                  КАК НомерСтроки,
	|		ЛОЖЬ                                                        КАК ЭтоВозвратнаяТара
	|	
	|	ИЗ
	|		ТаблицаТоваровСКоэффициентамиУпаковок КАК ТаблицаТоваров
	|		
	|	ГДЕ
	|		НЕ ТаблицаТоваров.ДоКорректировки
	|		И (ТаблицаТоваров.ЭтоТовар ИЛИ &ВыводитьУслуги)
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|		
	|	ВЫБРАТЬ
	|		ТаблицаТоваров.Ссылка,
	|		ТаблицаТоваров.Номенклатура,
	|		ТаблицаТоваров.Номенклатура.НаименованиеПолное,
	|		
	|		ВЫБОР КОГДА &КолонкаКодов = ""Артикул"" ТОГДА
	|			ТаблицаТоваров.Номенклатура.Артикул
	|		ИНАЧЕ
	|			ТаблицаТоваров.Номенклатура.Код
	|		КОНЕЦ,
	|		
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ
	|			&ТекстЗапросаЕдиницаИзмерения
	|		КОНЕЦ,
	|		
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Наименование
	|		ИНАЧЕ
	|			&ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|		КОНЕЦ,
	|		
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ
	|			&ТекстЗапросаКодЕдиницыИзмерения
	|		КОНЕЦ,
	|		
	|		ТаблицаТоваров.Характеристика,
	|		ТаблицаТоваров.Характеристика.НаименованиеПолное,
	|		ТаблицаТоваров.Упаковка,
	|		
	|		ВЫБОР КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1 ТОГДА
	|			""""
	|		ИНАЧЕ
	|			ТаблицаТоваров.Упаковка.Наименование
	|		КОНЕЦ,
	|		
	|		ВЫБОР КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) ТОГДА
	|			ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Наименование
	|		ИНАЧЕ
	|			ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|				&ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|			ИНАЧЕ
	|				&ТекстЗапросаНаименованиеЕдиницыИзмерения2
	|			КОНЕЦ
	|		КОНЕЦ,
	|		
	|		ТаблицаТоваров.СтавкаНДС,
	|		ТаблицаТоваров.НомерГТД,
	|		ТаблицаТоваров.НомерГТД.СтранаПроисхождения,
	|		
	|		0,
	|		ВЫБОР КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ
	|			ТаблицаТоваров.Количество
	|		КОНЕЦ,
	|		
	|		0,
	|		ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ
	|			ТаблицаТоваров.Количество / ТаблицаТоваров.КоэффициентВложеннойУпаковки
	|		КОНЕЦ,
	|		
	|		0,
	|		ВЫБОР КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ВЫБОР КОГДА ТаблицаТоваров.Количество < ТаблицаТоваров.КоэффициентВложеннойУпаковки ТОГДА
	|				ТаблицаТоваров.Количество
	|			ИНАЧЕ
	|				ТаблицаТоваров.КоэффициентВложеннойУпаковки
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) ТОГДА
	|				1
	|			ИНАЧЕ
	|				&ТекстЗапросаКоэффициентУпаковки
	|			КОНЕЦ
	|		КОНЕЦ,
	|		
	|		0,
	|		ВЫБОР КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ
	|			ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.Количество
	|		КОНЕЦ,
	|		
	|		0,
	|		ТаблицаТоваров.СуммаБезНДС,
	|		
	|		0,
	|		ТаблицаТоваров.СуммаНДС,
	|		
	|		0,
	|		ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС,
	|		
	|		0,
	|		ТаблицаТоваров.Количество * &ТекстЗапросаВес КАК МассаНеттоДоКорректировки,
	|		
	|		0,
	|		ВЫБОР КОГДА &ЗаполненаЕдиницаИзмеренияВеса ТОГДА
	|			ВЫБОР КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL ТОГДА
	|				ТаблицаТоваров.Количество * &ТекстЗапросаВес1
	|			ИНАЧЕ
	|				ТаблицаТоваров.КоличествоУпаковок * &ТекстЗапросаВес1
	|			КОНЕЦ
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ,
	|		NULL,
	|		ЛОЖЬ
	|	
	|	ИЗ
	|		ТаблицаТоваровСКоэффициентамиУпаковок КАК ТаблицаТоваров
	|		
	|	ГДЕ
	|		ТаблицаТоваров.ДоКорректировки
	|		И (ТаблицаТоваров.ЭтоТовар ИЛИ &ВыводитьУслуги)) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Ссылка,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.НаименованиеНоменклатуры,
	|	ВложенныйЗапрос.НоменклатураКод,
	|	ВложенныйЗапрос.ЕдиницаИзмерения,
	|	ВложенныйЗапрос.ЕдиницаИзмеренияНаименование,
	|	ВложенныйЗапрос.ЕдиницаИзмеренияКод,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.НаименованиеХарактеристики,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.УпаковкаНаименование,
	|	ВложенныйЗапрос.ВидУпаковки,
	|	ВложенныйЗапрос.СтавкаНДС,
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.СтранаПроисхождения,
	|	ВложенныйЗапрос.ЭтоВозвратнаяТара
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка
	|;";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаВес1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаВес",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения",
			"ТаблицаТоваров.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваров.ВидУпаковки",
			"ТаблицаТоваров.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
		
	Запрос.УстановитьПараметр("ВыводитьУслуги",                  ПараметрыПечати.ВыводитьУслуги);
	Запрос.УстановитьПараметр("КолонкаКодов",                    КолонкаКодов);
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияВеса",            Константы.ЕдиницаИзмеренияВеса.Получить());
	Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса",   ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
		
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[1];
	
	СтруктураДанныхДляПечати 	= Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",
	                                               РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

Функция ПолучитьДанныеДляПечатнойФормыУПД(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                КАК Ссылка,
	|	ДанныеДокументов.Валюта                КАК Валюта,
	|	ДанныеДокументов.Организация           КАК Организация,
	|	ДанныеДокументов.Подразделение         КАК Подразделение,
	|	ДанныеДокументов.Склад                 КАК Склад,
	|	ДанныеДокументов.ВидКорректировки      КАК ХозяйственнаяОперация
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК ДокументОснование,
	|	ЛОЖЬ КАК Корректировочный
	|
	|ПОМЕСТИТЬ СчетаФактурыОснования
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПродажиСервер.ПоместитьВременнуюТаблицуДанныхПоставщика(МенеджерВременныхТаблиц);
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ВключаяНомераГТД = Истина;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НЕ ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьУслуги,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьТовары
	|ПОМЕСТИТЬ
	|	НоменклатураДокументов
	|ИЗ
	|	ТаблицаТоваровКорректировкаРеализации КАК ТаблицаТоваров
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	&ПредставлениеСчетФактура КАК ПредставлениеДокумента,
	|	2 КАК СтатусУПД,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Дата,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	ЛОЖЬ КАК Исправление,
	|	НЕОПРЕДЕЛЕНО КАК НомерСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияСчетаФактуры,
	|	ЛОЖЬ КАК КорректировочныйСчетФактура,
	|	"""" КАК СтрокаПоДокументу,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаСчетаФактуры,
	|	ДанныеДокумента.Партнер КАК Партнер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Контрагент.ОбособленноеПодразделение
	|			ТОГДА ДанныеДокумента.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ ДанныеДокумента.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ДанныеДокумента.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ДанныеПоставщика.ГоловнаяОрганизация КАК Организация,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	0 КАК ИндексПодразделения,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.Контрагент
	|		ИНАЧЕ ДанныеДокумента.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.Организация
	|		ИНАЧЕ ДанныеДокумента.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	ДанныеПоставщика.КПППоставщика КАК КПППоставщика,
	|	""""                           КАК КПППокупателя,
	|	ДанныеДокумента.Контрагент.ИНН КАК ИННПокупателя,
	|	ДанныеДокумента.АдресДоставки КАК АдресДоставки,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Валюта.НаименованиеПолное КАК ВалютаНаименованиеПолное,
	|	ДанныеДокумента.Валюта.Код КАК ВалютаКод,
	|	ВЫБОР
	|		КОГДА НоменклатураДокументов.ЕстьУслуги
	|				И НЕ НоменклатураДокументов.ЕстьТовары
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТолькоУслуги,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПередачаНаКомиссию,
	|	ДанныеДокумента.Основание КАК Основание,
	|	ДанныеДокумента.ОснованиеДата КАК ОснованиеДата,
	|	ДанныеДокумента.ОснованиеНомер КАК ОснованиеНомер,
	|	ДанныеДокумента.ДоверенностьНомер КАК ДоверенностьНомер,
	|	ДанныеДокумента.ДоверенностьДата КАК ДоверенностьДата,
	|	ДанныеДокумента.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	ДанныеДокумента.ДоверенностьЛицо КАК ДоверенностьЛицо,
	|	ДанныеДокумента.Отпустил КАК Кладовщик,
	|	ДанныеДокумента.ОтпустилДолжность КАК ДолжностьКладовщика
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ДанныеДокумента.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоставщика КАК ДанныеПоставщика
	|		ПО ДанныеДокумента.Ссылка = ДанныеПоставщика.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураДокументов КАК НоменклатураДокументов
	|		ПО ДанныеДокумента.Ссылка = НоменклатураДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(ТаблицаДокумента.Номенклатура.НаименованиеПолное, ТаблицаДокумента.Содержание) КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаДокумента.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.НомерГТД КАК НомерГТД,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Количество
	|		ИНАЧЕ ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество
	|		ИНАЧЕ ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ КАК Цена,
	|	ТаблицаДокумента.СуммаБезНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС КАК СуммаСНДС,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	ТаблицаТоваровКорректировкаРеализации КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	ИЛИ (ТаблицаДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	И Не ТаблицаДокумента.ВернутьМногооборотнуюТару)
	|	ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	Запрос.УстановитьПараметр("ПредставлениеСчетФактура", НСтр("ru='счет-фактура'"));
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[1];
	РезультатПоТабличнойЧасти = МассивРезультатов[2];
	
	СтруктураДанныхДляПечати 	= Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",
	                                               РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

Функция ПолучитьДанныеДляПечатнойФормыУКД(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                КАК Ссылка,
	|	ДанныеДокументов.Валюта                КАК Валюта,
	|	ДанныеДокументов.Организация           КАК Организация,
	|	ДанныеДокументов.Подразделение         КАК Подразделение,
	|	ДанныеДокументов.Склад                 КАК Склад,
	|	ДанныеДокументов.ДокументОснование     КАК ДокументОснование,
	|	ДанныеДокументов.ВидКорректировки      КАК ХозяйственнаяОперация
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК ДокументОснование,
	|	ИСТИНА КАК Корректировочный
	|
	|ПОМЕСТИТЬ СчетаФактурыОснования
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка");
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПродажиСервер.ПоместитьВременнуюТаблицуДанныхПоставщика(МенеджерВременныхТаблиц);
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ВключаяНомераГТД       = Истина;
	ПараметрыЗаполнения.ВключаяДоКорректировки = Истина;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка КАК Ссылка,
	|	ДанныеОснования.Номер КАК НомерСчетаФактуры,
	|	ДанныеОснования.Дата КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияСчетаФактуры
	|
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК ДанныеОснования
	|		ПО ТаблицаДанныхДокументов.ДокументОснование = ДанныеОснования.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка,
	|	ДанныеОснования.Номер,
	|	ДанныеОснования.Дата,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияУслугПрочихАктивов КАК ДанныеОснования
	|		ПО ТаблицаДанныхДокументов.ДокументОснование = ДанныеОснования.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка,
	|	ДанныеОснования.Номер,
	|	ДанныеОснования.Дата,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктВыполненныхРабот КАК ДанныеОснования
	|		ПО ТаблицаДанныхДокументов.ДокументОснование = ДанныеОснования.Ссылка
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	2 КАК СтатусУПД,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Дата,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	ЛОЖЬ КАК Исправление,
	|	НЕОПРЕДЕЛЕНО КАК НомерСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаСчетаФактуры,
	|	ДанныеДокумента.Партнер КАК Партнер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Контрагент.ОбособленноеПодразделение
	|			ТОГДА ДанныеДокумента.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ ДанныеДокумента.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ДанныеПоставщика.ГоловнаяОрганизация КАК Организация,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	0 КАК ИндексПодразделения,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ДанныеПоставщика.КПППоставщика КАК КПППоставщика,
	|	ДанныеДокумента.Контрагент.КПП КАК КПППокупателя,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Валюта.НаименованиеПолное КАК ВалютаНаименованиеПолное,
	|	ДанныеДокумента.Валюта.Код КАК ВалютаКод,
	|	ДанныеДокумента.Основание КАК Основание,
	|	ДанныеДокумента.ОснованиеДата КАК ОснованиеДата,
	|	ДанныеДокумента.ОснованиеНомер КАК ОснованиеНомер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПечатьНеТребуется,
	|	ДанныеДокумента.НалогообложениеНДС КАК НалогообложениеНДС
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ДанныеДокумента.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоставщика КАК ДанныеПоставщика
	|		ПО ДанныеДокумента.Ссылка = ДанныеПоставщика.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(ТаблицаДокумента.Номенклатура.НаименованиеПолное, ТаблицаДокумента.Содержание) КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаДокумента.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения1
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения1
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.НомерГТД КАК НомерГТД,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Количество
	|		ИНАЧЕ ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0)
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0)
	|	КОНЕЦ КАК КоличествоДо,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ВЫБОР КОГДА ТаблицаДокумента.Количество <> 0 ТОГДА
	|				ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ТаблицаДокумента.КоличествоУпаковок <> 0 ТОГДА
	|				ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ВЫБОР КОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0) <> 0 ТОГДА
	|				ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.Количество
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0) <> 0 ТОГДА
	|				ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.КоличествоУпаковок
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|	КОНЕЦ КАК ЦенаДо,
	|	ТаблицаДокумента.СуммаБезНДС КАК СуммаБезНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) КАК СуммаБезНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаБезНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаБезНДСУменьшение,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0) КАК СуммаНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУменьшение,
	|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС КАК СуммаСНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0) КАК СуммаСНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС >=
	|				ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС -
	|				(ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаСНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС <
	|				ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС -
	|				(ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаСНДСУменьшение,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара,
	|	1 КАК Порядок
	|ИЗ
	|	ТаблицаТоваровКорректировкаРеализации КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоваровКорректировкаРеализации КАК ТаблицаТоваровПредыдущие
	|		ПО (ТаблицаТоваровПредыдущие.ДоКорректировки)
	|			И ТаблицаДокумента.Ссылка = ТаблицаТоваровПредыдущие.Ссылка
	|			И ТаблицаДокумента.Номенклатура = ТаблицаТоваровПредыдущие.Номенклатура
	|			И ТаблицаДокумента.Характеристика = ТаблицаТоваровПредыдущие.Характеристика
	|			И ТаблицаДокумента.Упаковка = ТаблицаТоваровПредыдущие.Упаковка
	|			И ТаблицаДокумента.НомерГТД = ТаблицаТоваровПредыдущие.НомерГТД
	|			И ТаблицаДокумента.СтавкаНДС = ТаблицаТоваровПредыдущие.СтавкаНДС
	|			И ТаблицаДокумента.Содержание = ТаблицаТоваровПредыдущие.Содержание
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактурыОснования КАК СчетаФактуры
	|		ПО ТаблицаДокумента.Ссылка = СчетаФактуры.ДокументОснование
	|ГДЕ
	|	НЕ ТаблицаДокумента.ДоКорректировки
	|	И (НЕ ЕСТЬNULL(СчетаФактуры.Корректировочный, ИСТИНА)
	|			ИЛИ ТаблицаДокумента.СуммаНДС <> ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ИЛИ ТаблицаДокумента.СуммаБезНДС <> ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0))
	|	И (ТаблицаДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	ИЛИ (ТаблицаДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	И Не ТаблицаДокумента.ВернутьМногооборотнуюТару)
	|	ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваровПредыдущие.Ссылка,
	|	ТаблицаТоваровПредыдущие.Номенклатура,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.Номенклатура.НаименованиеПолное, ТаблицаТоваровПредыдущие.Содержание),
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаТоваровПредыдущие.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаТоваровПредыдущие.Номенклатура.Код
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваровПредыдущие.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения2
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваровПредыдущие.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения2
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваровПредыдущие.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения2
	|	КОНЕЦ,
	|	ТаблицаТоваровПредыдущие.Характеристика,
	|	ТаблицаТоваровПредыдущие.Характеристика.НаименованиеПолное,
	|	ТаблицаТоваровПредыдущие.СтавкаНДС,
	|	ТаблицаТоваровПредыдущие.НомерГТД,
	|	ТаблицаТоваровПредыдущие.НомерГТД.СтранаПроисхождения,
	|	ТаблицаТоваровПредыдущие.НомерГТД.СтранаПроисхождения.Код,
	|	0,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваровПредыдущие.Количество
	|		ИНАЧЕ ТаблицаТоваровПредыдущие.КоличествоУпаковок
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			ВЫБОР КОГДА ТаблицаТоваровПредыдущие.Количество <> 0 ТОГДА
	|				ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.Количество
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ТаблицаТоваровПредыдущие.КоличествоУпаковок <> 0 ТОГДА
	|				ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.КоличествоУпаковок
	|			ИНАЧЕ
	|				0
	|			КОНЕЦ
	|	КОНЕЦ,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС + ТаблицаТоваровПредыдущие.СуммаНДС,
	|	0,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС + ТаблицаТоваровПредыдущие.СуммаНДС,
	|	ТаблицаТоваровПредыдущие.НомерСтроки,
	|	ЛОЖЬ,
	|	2
	|ИЗ
	|	ТаблицаТоваровКорректировкаРеализации КАК ТаблицаТоваровПредыдущие
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоваровКорректировкаРеализации КАК ТаблицаТоваровПослеКорректировки
	|		ПО (НЕ ТаблицаТоваровПослеКорректировки.ДоКорректировки)
	|			И ТаблицаТоваровПредыдущие.Ссылка = ТаблицаТоваровПослеКорректировки.Ссылка
	|			И ТаблицаТоваровПредыдущие.Номенклатура = ТаблицаТоваровПослеКорректировки.Номенклатура
	|			И ТаблицаТоваровПредыдущие.Характеристика = ТаблицаТоваровПослеКорректировки.Характеристика
	|			И ТаблицаТоваровПредыдущие.Упаковка = ТаблицаТоваровПослеКорректировки.Упаковка
	|			И ТаблицаТоваровПредыдущие.НомерГТД = ТаблицаТоваровПослеКорректировки.НомерГТД
	|			И ТаблицаТоваровПредыдущие.СтавкаНДС = ТаблицаТоваровПослеКорректировки.СтавкаНДС
	|			И ТаблицаТоваровПредыдущие.Содержание = ТаблицаТоваровПослеКорректировки.Содержание
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактурыОснования КАК СчетаФактуры
	|		ПО ТаблицаТоваровПредыдущие.Ссылка = СчетаФактуры.ДокументОснование
	|ГДЕ
	|	ТаблицаТоваровПредыдущие.ДоКорректировки
	|	И ТаблицаТоваровПослеКорректировки.Ссылка ЕСТЬ NULL 
	|	И ЕСТЬNULL(СчетаФактуры.Корректировочный, ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаТоваровПредыдущие.Упаковка",
			"ТаблицаТоваровПредыдущие.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваровПредыдущие.Упаковка",
			"ТаблицаТоваровПредыдущие.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТоваровПредыдущие.Упаковка",
			"ТаблицаТоваровПредыдущие.Номенклатура"));
	
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	Запрос.УстановитьПараметр("ПустаяУпаковкаНоменклатуры", Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоИсходнымДанным = МассивРезультатов[0];
	РезультатПоШапке          = МассивРезультатов[1];
	РезультатПоТабличнойЧасти = МассивРезультатов[2];
	
	СтруктураДанныхДляПечати  = Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти, РезультатПоИсходнымДанным",
		РезультатПоШапке, 
		РезультатПоТабличнойЧасти,
		РезультатПоИсходнымДанным);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Функция получает данные для формирования печатной формы МХ - 1
//
Функция ПолучитьДанныеДляПечатнойФормыМХ1(ПараметрыПечати, МассивОбъектов) Экспорт 
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет КАК ПредварительныйРасчет,
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Ссылка.Номер КАК Номер,
	|	Документ.Ссылка.Дата КАК Дата,
	|	Документ.Ссылка.Дата КАК ДатаДокумента,
	|	Документ.Ссылка.Организация КАК Организация,
	|	Документ.Ссылка.Организация.Префикс КАК Префикс,
	|	Документ.Склад КАК Склад,
	|	Склады.ИсточникИнформацииОЦенахДляПечати КАК ИсточникИнформацииОЦенахДляПечати,
	|	Склады.УчетныйВидЦены КАК ВидЦены,
	|	Склады.УчетныйВидЦены.ВалютаЦены КАК ВалютаЦены
	|ПОМЕСТИТЬ ВтШапка
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО (РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(Документ.Ссылка.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(Документ.Ссылка.Дата, МЕСЯЦ))
	|			И (РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен)
	|			И (Документ.Ссылка.Организация = РасчетСебестоимостиТоваровОрганизации.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО (Документ.Склад = Склады.Ссылка)
	|ГДЕ
	|	Документ.Ссылка В(&МассивДокументов)
	|	И Документ.Ссылка.Проведен
	|	И Склады.СкладОтветственногоХранения
	|	И Документ.Ссылка.Организация <> Склады.Поклажедержатель
	|;

	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Склад КАК Склад,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения1 КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ВидУпаковки,
	|	-Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	-Товары.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	Шапка.ВидЦены КАК ВидЦены,
	|	Шапка.ВалютаЦены КАК ВалютаЦены
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО Товары.Склад = Шапка.Склад
	|			И Товары.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен)
	|		ИЛИ (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|			И Шапка.ПредварительныйРасчет ЕСТЬ NULL))
	|	И Товары.ВариантОтражения В(
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУвеличитьСкладскиеОстатки))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК Ссылка,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	Шапка.Организация КАК Организация,
	|	Аналитика.Склад КАК Склад,
	|	ВидыЗапасов.Упаковка КАК Упаковка,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения2 КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения2 КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения2 КАК ВидУпаковки,
	|	ВидыЗапасов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВидыЗапасов.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(ВидыЗапасов.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	Аналитика.Склад.УчетныйВидЦены КАК ВидЦены,
	|	Аналитика.Склад.УчетныйВидЦены.ВалютаЦены КАК ВалютаЦены,
	|	Шапка.ПредварительныйРасчет КАК ПредварительныйРасчет
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК ВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО ВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО ВидыЗапасов.АналитикаУчетаНоменклатуры.Склад = Шапка.Склад
	|			И ВидыЗапасов.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Аналитика.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И Аналитика.Склад.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|;
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "Товары.Упаковка", "Товары.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "Товары.Упаковка", "Товары.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "ВидыЗапасов.Упаковка", "Аналитика.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "ВидыЗапасов.Упаковка", "Аналитика.Номенклатура"));
	
	СкладыСервер.ДополнитьТекстЗапросаДляПечатныхФормМХ1Х3(Запрос);
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоВидамЗапасов",
		ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьТоваровПоВидамЗапасов"));
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	УстановитьПараметрыВтВидыЗапасов(
		Запрос,
		Ложь, // ПересчитыватьВВалютуРегл
		Ложь,); // ВключаяДоКорректировки
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоШапке = МассивРезультатов[6];
	РезультатПоСкладам = МассивРезультатов[7];
	РезультатПоТабличнойЧасти = МассивРезультатов[8];
	
	СтруктураДанныхДляПечати = Новый Структура(
		"РезультатПоШапке, РезультатПоСкладам, РезультатПоТабличнойЧасти",
		РезультатПоШапке,
		РезультатПоСкладам,
		РезультатПоТабличнойЧасти);
		
	Возврат СтруктураДанныхДляПечати
	
КонецФункции

// Функция получает данные для формирования печатной формы МХ - 3
//
Функция ПолучитьДанныеДляПечатнойФормыМХ3(ПараметрыПечати, МассивОбъектов) Экспорт 
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет КАК ПредварительныйРасчет,
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Ссылка.Номер КАК Номер,
	|	Документ.Ссылка.Дата КАК Дата,
	|	Документ.Ссылка.Дата КАК ДатаДокумента,
	|	Документ.Ссылка.Организация КАК Организация,
	|	Документ.Ссылка.Организация.Префикс КАК Префикс,
	|	Документ.Склад КАК Склад,
	|	Склады.ИсточникИнформацииОЦенахДляПечати КАК ИсточникИнформацииОЦенахДляПечати,
	|	Склады.УчетныйВидЦены КАК ВидЦены,
	|	Склады.УчетныйВидЦены.ВалютаЦены КАК ВалютаЦены
	|ПОМЕСТИТЬ ВтШапка
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО (РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(Документ.Ссылка.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(Документ.Ссылка.Дата, МЕСЯЦ))
	|			И (РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен)
	|			И (Документ.Ссылка.Организация = РасчетСебестоимостиТоваровОрганизации.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО (Документ.Склад = Склады.Ссылка)
	|ГДЕ
	|	Документ.Ссылка В(&МассивДокументов)
	|	И Документ.Ссылка.Проведен
	|	И Склады.СкладОтветственногоХранения
	|	И Документ.Ссылка.Организация <> Склады.Поклажедержатель
	|;

	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Склад КАК Склад,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения1 КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ВидУпаковки,
	|	Товары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Товары.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	Шапка.ВидЦены КАК ВидЦены,
	|	Шапка.ВалютаЦены КАК ВалютаЦены
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО Товары.Склад = Шапка.Склад
	|			И Товары.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен)
	|		ИЛИ (Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|			И Шапка.ПредварительныйРасчет ЕСТЬ NULL))
	|	И Товары.ВариантОтражения В(
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УвеличитьРеализациюУменьшитьСкладскиеОстатки))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК Ссылка,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	Шапка.Организация КАК Организация,
	|	Аналитика.Склад КАК Склад,
	|	ВидыЗапасов.Упаковка КАК Упаковка,
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения2 КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения2 КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения2 КАК ВидУпаковки,
	|	ВидыЗапасов.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ВидыЗапасов.Количество КАК Количество,
	|	КОНЕЦПЕРИОДА(ВидыЗапасов.Ссылка.Дата, ДЕНЬ) КАК ДатаПолученияЦены,
	|	Аналитика.Склад.УчетныйВидЦены КАК ВидЦены,
	|	Аналитика.Склад.УчетныйВидЦены.ВалютаЦены КАК ВалютаЦены,
	|	Шапка.ПредварительныйРасчет КАК ПредварительныйРасчет
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК ВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО ВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО ВидыЗапасов.АналитикаУчетаНоменклатуры.Склад = Шапка.Склад
	|			И ВидыЗапасов.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Аналитика.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И Аналитика.Склад.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|;
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "Товары.Упаковка", "Товары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "Товары.Упаковка", "Товары.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "ВидыЗапасов.Упаковка", "Аналитика.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "ВидыЗапасов.Упаковка", "Аналитика.Номенклатура"));
	
	СкладыСервер.ДополнитьТекстЗапросаДляПечатныхФормМХ1Х3(Запрос);
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоВидамЗапасов",
		ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьТоваровПоВидамЗапасов"));
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	УстановитьПараметрыВтВидыЗапасов(
		Запрос,
		Ложь, // ПересчитыватьВВалютуРегл
		Ложь,); // ВключаяДоКорректировки
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоШапке = МассивРезультатов[6];
	РезультатПоСкладам = МассивРезультатов[7];
	РезультатПоТабличнойЧасти = МассивРезультатов[8];
	
	СтруктураДанныхДляПечати = Новый Структура(
		"РезультатПоШапке, РезультатПоСкладам, РезультатПоТабличнойЧасти",
		РезультатПоШапке,
		РезультатПоСкладам,
		РезультатПоТабличнойЧасти);
		
	Возврат СтруктураДанныхДляПечати
	
КонецФункции

Функция СформироватьПечатнуюФормуНакладная(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	ВыводитьКоды = ЗначениеЗаполнено(КолонкаКодов);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка КАК Ссылка,
	|	КорректировкаРеализации.ДокументОснование.Номер КАК Номер,
	|	КорректировкаРеализации.ДокументОснование.Дата КАК Дата,
	|	КорректировкаРеализации.Партнер КАК Партнер,
	|	КорректировкаРеализации.Контрагент КАК Получатель,
	|	КорректировкаРеализации.Организация КАК Организация,
	|	КорректировкаРеализации.Организация.Префикс КАК Префикс,
	|	КорректировкаРеализации.Валюта КАК Валюта,
	|	КорректировкаРеализации.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ВЫБОР
	|		КОГДА КорректировкаРеализации.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИЛИ КорректировкаРеализации.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК УчитыватьНДС,
	|	КорректировкаРеализации.Отпустил КАК ОтпускПроизвел
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	КорректировкаРеализации.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Ссылка КАК Ссылка,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Номенклатура.НаименованиеПолное КАК ТоварНаименованиеПолное,
	|	ВложенныйЗапрос.Номенклатура.Код КАК Код,
	|	ВложенныйЗапрос.Номенклатура.Артикул КАК Артикул,
	|	ВложенныйЗапрос.ЕдиницаИзмерения.Наименование КАК ЕдиницаЦены,
	|	ВложенныйЗапрос.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.Характеристика.НаименованиеПолное КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВложенныйЗапрос.Упаковка.Наименование
	|	КОНЕЦ КАК Упаковка,
	|	ВложенныйЗапрос.СтавкаНДС КАК СтавкаНДС,
	|	ВложенныйЗапрос.Цена КАК Цена,
	|	ВложенныйЗапрос.Количество КАК Количество,
	|	ВложенныйЗапрос.Сумма КАК Сумма,
	|	ВложенныйЗапрос.СуммаНДС КАК СуммаНДС,
	|	ВложенныйЗапрос.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА
	|			ВложенныйЗапрос.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару
	|			И ВложенныйЗапрос.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	(ВЫБРАТЬ
	|		КорректировкаРеализации.Ссылка КАК Ссылка,
	|		КорректировкаРеализации.Номенклатура КАК Номенклатура,
	|		ВЫБОР
	|			КОГДА КорректировкаРеализации.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки2
	|		КОНЕЦ КАК Коэффициент,
	|		&ТекстЗапросаЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		КорректировкаРеализации.Характеристика КАК Характеристика,
	|		КорректировкаРеализации.Упаковка КАК Упаковка,
	|		КорректировкаРеализации.СтавкаНДС КАК СтавкаНДС,
	|		КорректировкаРеализации.Цена КАК Цена,
	|		КорректировкаРеализации.КоличествоУпаковок КАК Количество,
	|		КорректировкаРеализации.Сумма КАК Сумма,
	|		КорректировкаРеализации.СуммаНДС КАК СуммаНДС,
	|		КорректировкаРеализации.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализации
	|	ГДЕ
	|		КорректировкаРеализации.Ссылка В(&МассивДокументов)
	|		И КорректировкаРеализации.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВложенныйЗапрос.Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ВложенныйЗапрос.Упаковка",
			"ВложенныйЗапрос.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"КорректировкаРеализации.Упаковка",
			"КорректировкаРеализации.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"КорректировкаРеализации.Упаковка",
			"КорректировкаРеализации.Номенклатура"));
			
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КорректировкаРеализации_Накладная";
	
	МассивРезультатов 		= Запрос.ВыполнитьПакет();
	ДанныеПечати			= МассивРезультатов[0].Выбрать();
	ВыборкаПоДокументам 	= МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПоказыватьНДС = Константы.ВыводитьДопКолонкиНДС.Получить();
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
	
		// Найдем в выборке товары по текущему документу
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
		НайденСледующий = ВыборкаПоДокументам.НайтиСледующий(СтруктураПоиска);
		
		// Если в накладной только услуги - перейдем к следующему документу
		
		Если НайденСледующий Тогда
			ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
			ЕстьНДС = ДанныеПечати.УчитыватьНДС;
			ВыборкаПоТоварам.Сбросить();
		Иначе
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В документе %1 отсутствуют товары. Печать накладной не требуется'"),
				ДанныеПечати.Ссылка);
				
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				ДанныеПечати.Ссылка);
			Продолжить;
		КонецЕсли;
		
		// Макет необходимо получать для каждого документа, т.к. размеры колонок изменяются динамически
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.КорректировкаРеализации.ПФ_MXL_РеализацияТоваров");
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент    = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Выводим шапку накладной
		
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьМакета, ДанныеПечати.Ссылка);
		
		ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(ДанныеПечати, НСтр("ru='Реализация товаров'"));
		
		ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета                                   = Макет.ПолучитьОбласть("Поставщик");
		ПредставлениеПоставщика                         = ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДанныеПечати.Дата), "ПолноеНаименование");
		ОбластьМакета.Параметры.ПредставлениеПоставщика = ПредставлениеПоставщика;
		ОбластьМакета.Параметры.Поставщик               = ДанныеПечати.Организация;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета                                   = Макет.ПолучитьОбласть("Покупатель");
		ПредставлениеПолучателя                         = ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Получатель, ДанныеПечати.Дата), "ПолноеНаименование");
		ОбластьМакета.Параметры.ПредставлениеПолучателя = ПредставлениеПолучателя;
		ОбластьМакета.Параметры.Получатель              = ДанныеПечати.Получатель;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим заголовок таблицы Товары
		
		СуффиксОбластиСтроки = ?(ЕстьНДС, "СНДС", "");
		СуффиксОбластиКолонки = ?(ЕстьНДС, "СНДС", "");
		
		ОбластьКолонкаТовар = Макет.Область("ПерваяКолонкаТовара");
		ОбластьКолонкаТовар.ШиринаКолонки = ОбластьКолонкаТовар.ШиринаКолонки
			+ ?(ВыводитьКоды, 0, Макет.Область("КолонкаКодов").ШиринаКолонки)
		;
		
		ОбластьНомера  = Макет.ПолучитьОбласть("ШапкаТаблицы" + СуффиксОбластиСтроки + "|НомерСтроки");
		ОбластьКодов   = Макет.ПолучитьОбласть("ШапкаТаблицы" + СуффиксОбластиСтроки + "|КолонкаКодов");
		ОбластьТовар   = Макет.ПолучитьОбласть("ШапкаТаблицы" + СуффиксОбластиСтроки + "|Товар" + СуффиксОбластиКолонки);
		ОбластьДанных  = Макет.ПолучитьОбласть("ШапкаТаблицы" + СуффиксОбластиСтроки + "|Данные" + СуффиксОбластиКолонки);
		
		ТабличныйДокумент.Вывести(ОбластьНомера);
			
		Если ВыводитьКоды Тогда
			ОбластьКодов.Параметры.ИмяКолонкиКодов = КолонкаКодов;
			ТабличныйДокумент.Присоединить(ОбластьКодов);
		КонецЕсли;
			
		ТабличныйДокумент.Присоединить(ОбластьТовар);
		ТабличныйДокумент.Присоединить(ОбластьДанных);
		
		ОбластьНомера  = Макет.ПолучитьОбласть("СтрокаТаблицы" + СуффиксОбластиСтроки + "|НомерСтроки");
		ОбластьКодов   = Макет.ПолучитьОбласть("СтрокаТаблицы" + СуффиксОбластиСтроки + "|КолонкаКодов");
		ОбластьТовар   = Макет.ПолучитьОбласть("СтрокаТаблицы" + СуффиксОбластиСтроки + "|Товар" + СуффиксОбластиКолонки);
		ОбластьДанных  = Макет.ПолучитьОбласть("СтрокаТаблицы" + СуффиксОбластиСтроки + "|Данные" + СуффиксОбластиКолонки);
		
		Сумма          = 0;
		СуммаНДС       = 0;
		НомерСтроки    = 0;
		
		// Выводим строки таблицы Товары
			
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			НомерСтроки = НомерСтроки + 1;
			
			ОбластьНомера.Параметры.НомерСтроки = НомерСтроки;
			ТабличныйДокумент.Вывести(ОбластьНомера);
			
			Если ВыводитьКоды Тогда
				
				ОбластьКодов.Параметры.Артикул = ВыборкаПоТоварам[КолонкаКодов];
				ТабличныйДокумент.Присоединить(ОбластьКодов);
				
			КонецЕсли;
			
			ОбластьТовар.Параметры.Заполнить(ВыборкаПоТоварам);
			
			ДополнительныеПараметрыПолученияНаименованияДляПечати = НоменклатураКлиентСервер.ДополнительныеПараметрыПредставлениеНоменклатурыДляПечати();
			ДополнительныеПараметрыПолученияНаименованияДляПечати.ВозвратнаяТара = ВыборкаПоТоварам.ЭтоВозвратнаяТара;
			
			ОбластьТовар.Параметры.Товар = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				ВыборкаПоТоварам.ТоварНаименованиеПолное,
				ВыборкаПоТоварам.Характеристика,
				,
				,
				ДополнительныеПараметрыПолученияНаименованияДляПечати);
			
			ТабличныйДокумент.Присоединить(ОбластьТовар);
			
			ОбластьДанных.Параметры.Заполнить(ВыборкаПоТоварам);
			
			ТабличныйДокумент.Присоединить(ОбластьДанных);
			
			Сумма          = Сумма          + ВыборкаПоТоварам.Сумма;
			СуммаНДС       = СуммаНДС       + ВыборкаПоТоварам.СуммаНДС;
			
		КонецЦикла;
		
		// Выводим подвал
		
		ОбластьНомера  = Макет.ПолучитьОбласть("ПодвалТаблицы" + СуффиксОбластиСтроки + "|НомерСтроки");
		ОбластьКодов   = Макет.ПолучитьОбласть("ПодвалТаблицы" + СуффиксОбластиСтроки + "|КолонкаКодов");
		ОбластьТовар   = Макет.ПолучитьОбласть("ПодвалТаблицы" + СуффиксОбластиСтроки + "|Товар" + СуффиксОбластиКолонки);
		ОбластьДанных  = Макет.ПолучитьОбласть("ПодвалТаблицы" + СуффиксОбластиСтроки + "|Данные" + СуффиксОбластиКолонки);
		
		ТабличныйДокумент.Вывести(ОбластьНомера);
		
		Если ВыводитьКоды Тогда
			ТабличныйДокумент.Присоединить(ОбластьКодов);
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьТовар);
		
		ОбластьДанных.Параметры.Всего = ФормированиеПечатныхФорм.ФорматСумм(Сумма);
		ТабличныйДокумент.Присоединить(ОбластьДанных);
		
		// Выводим ИтогоНДС
		
		Область = Макет.ПолучитьОбласть("ПодвалНДС");
		
		Область.Параметры.ВсегоНДС = СуммаНДС;
		Если ЕстьНДС Тогда
			Область.Параметры.НДС = ?(ДанныеПечати.ЦенаВключаетНДС, НСтр("ru='В том числе НДС:'"), НСтр("ru='Сумма НДС:'"));
		Иначе
			Область.Параметры.НДС = НСтр("ru='Без налога (НДС)'");
		КонецЕсли;
		ТабличныйДокумент.Присоединить(Область);
		
		// Выводим Сумму прописью
		
		ОбластьМакета = Макет.ПолучитьОбласть("СуммаПрописью");
		СуммаКПрописи = Сумма + ?(ДанныеПечати.ЦенаВключаетНДС, 0, СуммаНДС);
		
		ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Всего наименований %1, на сумму %2'"),
			ВыборкаПоТоварам.Количество(),
			ФормированиеПечатныхФорм.ФорматСумм(СуммаКПрописи, ДанныеПечати.Валюта));

		ОбластьМакета.Параметры.ИтоговаяСтрока = ИтоговаяСтрока;
		ОбластьМакета.Параметры.СуммаПрописью  = РаботаСКурсамиВалют.СформироватьСуммуПрописью(СуммаКПрописи, ДанныеПечати.Валюта);
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим подписи
		ОбластьМакета = Макет.ПолучитьОбласть("Подписи");
		
		Если ЗначениеЗаполнено(ДанныеПечати.ОтпускПроизвел) Тогда
			ОбластьМакета.Параметры.ОтпускПроизвел = ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(ДанныеПечати.ОтпускПроизвел, ДанныеПечати.Дата);
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;

	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;

КонецФункции

Функция СформироватьПечатнуюФормуРеестрНомеровГТД(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КорректировкаРеализации_РеестрНомеровГТД";
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	ВыводитьКоды = ЗначениеЗаполнено(КолонкаКодов);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.ВидКорректировки КАК ХозяйственнаяОперация
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Выполнить();
	
	Запрос.Текст = ТекстЗапросаВтВидыЗапасов() + "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаВидыЗапасов.Ссылка КАК Ссылка
	|
	|ПОМЕСТИТЬ ВтТаблицаДокументов
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	ТаблицаВидыЗапасов.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Контрагент КАК Контрагент,
	|	ДанныеДокумента.ДокументОснование.Номер КАК Номер,
	|	ДанныеДокумента.ДокументОснование.Дата КАК Дата,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ДанныеДокумента.Менеджер.ФизическоеЛицо КАК МенеджерФизическоеЛицо
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтТаблицаДокументов КАК ТаблицаДокументов
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаДокументов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(Справочник.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
	|	Справочник.Код КАК Код,
	|	Справочник.Артикул КАК Артикул,
	|	Справочник.НаименованиеПолное КАК Номенклатура,
	|	Аналитика.Характеристика.НаименованиеПолное КАК Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаВидыЗапасов.ВернутьМногооборотнуюТару
	|			И Справочник.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратнаяТара,
	|	СУММА(ТаблицаВидыЗапасов.Количество) КАК Количество
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.Номенклатура КАК Справочник
	|	ПО
	|		Аналитика.Номенклатура = Справочник.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтТаблицаДокументов КАК ТаблицаДокументов
	|	ПО
	|		ТаблицаВидыЗапасов.Ссылка = ТаблицаДокументов.Ссылка
	|ГДЕ
	|	ТаблицаВидыЗапасов.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаВидыЗапасов.Ссылка,
	|	ТаблицаВидыЗапасов.НомерСтроки,
	|	Справочник.Ссылка,
	|	Аналитика.Характеристика,
	|	ТаблицаВидыЗапасов.НомерГТД,
	|	ТаблицаВидыЗапасов.НомерГТД.СтранаПроисхождения,
	|	ТаблицаВидыЗапасов.ВернутьМногооборотнуюТару
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаВидыЗапасов.Количество) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ТаблицаВидыЗапасов.НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтТаблицаДокументов КАК ТаблицаДокументов
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаДокументов.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивОбъектов)
	|	И ТаблицаДокументов.Ссылка ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|";
	
	УстановитьПараметрыВтВидыЗапасов(
		Запрос,
		Ложь, // ПересчитыватьВВалютуРегл
		Ложь,); // ВключаяДоКорректировки
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.КорректировкаРеализации.ПФ_MXL_РеестрНомеровГТД");
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	// МассивРезультатов[0] - ВтСвязанныеДокументы
	// МассивРезультатов[1] - ВсеВидыЗапасов
	// МассивРезультатов[2] - ВтВидыЗапасов
	// МассивРезультатов[3] - УничтожитьВсеВидыЗапасов
	// МассивРезультатов[4] - ВтТаблицаДокументов
	ДанныеПечати = МассивРезультатов[5].Выбрать();
	ВыборкаПоДокументам = МассивРезультатов[6].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаПоДокументамБезГТД = МассивРезультатов[7].Выбрать();
	
	ПервыйДокумент = Истина;
	Пока ДанныеПечати.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Область = Макет.ПолучитьОбласть("Шапка");
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, Область, ДанныеПечати.Ссылка);
		Область.Параметры.ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(ДанныеПечати, НСтр("ru='Реестр номеров ГТД к накладной'"));
		Область.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(
			ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДанныеПечати.Дата),
			"ПолноеНаименование");
		Область.Параметры.ПредставлениеПартнера = ФормированиеПечатныхФорм.ОписаниеОрганизации(
			ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Контрагент, ДанныеПечати.Дата),
			"ПолноеНаименование");
		ТабличныйДокумент.Вывести(Область);
				
		Область = Макет.ПолучитьОбласть("ШапкаТаблицы|НачалоСтроки");
		ТабличныйДокумент.Вывести(Область);
		Если ВыводитьКоды Тогда
			Область = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");
			Область.Параметры.ИмяКолонкиКодов = КолонкаКодов;
			ТабличныйДокумент.Присоединить(Область);
		КонецЕсли;
		Область = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаТоваров");
		ТабличныйДокумент.Присоединить(Область);
				
		НомерСтроки = 1;
		
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
		ВыборкаПоДокументам.НайтиСледующий(СтруктураПоиска);
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		Пока ВыборкаПоТоварам.Следующий() Цикл
					
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы|НачалоСтроки");
			Область.Параметры.НомерСтроки = НомерСтроки;
			НомерСтроки = НомерСтроки + 1;
			ТабличныйДокумент.Вывести(Область);
			
			Если ВыводитьКоды Тогда
				Область = Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаКодов");
				Область.Параметры.ЗначениеКода = ВыборкаПоТоварам[КолонкаКодов];
				ТабличныйДокумент.Присоединить(Область);
			КонецЕсли;
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаТоваров");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			Область.Параметры.Товар = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				ВыборкаПоТоварам.Номенклатура,
				ВыборкаПоТоварам.Характеристика);
			
			ТабличныйДокумент.Присоединить(Область);
			
		КонецЦикла;
				
		Область = Макет.ПолучитьОбласть("ПодвалТаблицы|НачалоСтроки");
		ТабличныйДокумент.Вывести(Область);
		Если ВыводитьКоды Тогда
			Область = Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаКодов");
			ТабличныйДокумент.Присоединить(Область);
		КонецЕсли;
		Область = Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаТоваров");
		ТабличныйДокумент.Присоединить(Область);
		
		Область = Макет.ПолучитьОбласть("Подписи");
		Область.Параметры.ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Всего наименований %1'"), Строка(НомерСтроки - 1));
		Если ЗначениеЗаполнено(ДанныеПечати.МенеджерФизическоеЛицо) Тогда
			Область.Параметры.Менеджер = ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(ДанныеПечати.МенеджерФизическоеЛицо, ДанныеПечати.Дата);
		КонецЕсли;
		ТабличныйДокумент.Вывести(Область);
	
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	// Выведем сообщения о документах, для которых не требуется печатать реестр номеров ГТД.
	Пока ВыборкаПоДокументамБезГТД.Следующий() Цикл
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В документе %1 отсутствуют товары с номерами ГТД. Печать реестра номеров ГТД не требуется.'"),
			Строка(ВыборкаПоДокументамБезГТД.Ссылка));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			ВыборкаПоДокументамБезГТД.Ссылка);
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ЗаполнитьСтруктуруПолучателейПечатныхФорм(СтруктураДанныхОбъектаПечати) Экспорт
	
	СтруктураДанныхОбъектаПечати.ОсновнойПолучатель = "Партнер";
	
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Партнер");
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов") Тогда 
		СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Контрагент");
	КонецЕсли;
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Грузоотправитель");
	СтруктураДанныхОбъектаПечати.МассивРеквизитовПолучателей.Добавить("Грузополучатель");
	
КонецПроцедуры

#Конецобласти

#Область ШаблоныСообщений

// Вызывается при подготовке шаблонов сообщений и позволяет переопределить список реквизитов и вложений.
//
// Параметры:
//  Реквизиты               - ДеревоЗначений - список реквизитов шаблона.
//         ** Имя            - Строка - Уникальное имя общего реквизита.
//         ** Представление  - Строка - Представление общего реквизита.
//         ** Тип            - Тип    - Тип реквизита. По умолчанию строка.
//         ** Формат         - Строка - Формат вывода значения для чисел, дат, строк и булевых значений.
//  Вложения                - ТаблицаЗначений - печатные формы и вложения
//         ** Имя            - Строка - Уникальное имя вложения.
//         ** Представление  - Строка - Представление варианта.
//         ** ТипФайла       - Строка - Тип вложения, который соответствует расширению файла: "pdf", "png", "jpg", mxl" и др.
//  ДополнительныеПараметры - Структура - дополнительные сведения о шаблоне сообщений.
//
Процедура ПриПодготовкеШаблонаСообщения(Реквизиты, Вложения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Вызывается в момент создания сообщений по шаблону для заполнения значений реквизитов и вложений.
//
// Параметры:
//  Сообщение - Структура - структура с ключами:
//    * ЗначенияРеквизитов - Соответствие - список используемых в шаблоне реквизитов.
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * ЗначенияОбщихРеквизитов - Соответствие - список используемых в шаблоне общих реквизитов.
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * Вложения - Соответствие - значения реквизитов 
//      ** Ключ     - Строка - имя вложения в шаблоне;
//      ** Значение - ДвоичныеДанные, Строка - двоичные данные или адрес во временном хранилище вложения.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//  ДополнительныеПараметры - Структура -  Дополнительная информация о шаблоне сообщения.
//
Процедура ПриФормированииСообщения(Сообщение, ПредметСообщения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Заполняет список получателей SMS при отправке сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиSMS - ТаблицаЗначений - список получается SMS.
//     * НомерТелефона - Строка - номер телефона, куда будет отправлено сообщение SMS.
//     * Представление - Строка - представление получателя сообщения SMS.
//     * Контакт       - СправочникСсылка - контакт, которому принадлежит номер телефона.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//
Процедура ПриЗаполненииТелефоновПолучателейВСообщении(ПолучателиSMS, ПредметСообщения) Экспорт
	
КонецПроцедуры

// Заполняет список получателей письма при отправки сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиПисьма - ТаблицаЗначений - список получается письма.
//     * Адрес           - Строка - адрес электронной почты получателя.
//     * Представление   - Строка - представление получается письма.
//     * ВариантОтправки - Строка - Варианты отправки письма: "Кому", "Копия", "СкрытаяКопия", "ОбратныйАдреса";
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//
Процедура ПриЗаполненииПочтыПолучателейВСообщении(ПолучателиПисьма, ПредметСообщения) Экспорт
	
КонецПроцедуры

#КонецОбласти 

#Область Прочее

Процедура ЗаполнитьИменаРеквизитовПоОснованию(ДокументОснование, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("Склад");
	МассивВсехРеквизитов.Добавить("ПодразделениеДоходы");
	МассивВсехРеквизитов.Добавить("СтатьяДоходов");
	МассивВсехРеквизитов.Добавить("АналитикаДоходов");
	МассивВсехРеквизитов.Добавить("ПодразделениеРасходы");
	МассивВсехРеквизитов.Добавить("СтатьяРасходов");
	МассивВсехРеквизитов.Добавить("АналитикаРасходов");
	МассивВсехРеквизитов.Добавить("Отпустил");
	МассивВсехРеквизитов.Добавить("ОтпустилДолжность");
	МассивВсехРеквизитов.Добавить("Грузополучатель");
	МассивВсехРеквизитов.Добавить("Грузоотправитель");
	МассивВсехРеквизитов.Добавить("БанковскийСчетОрганизации");
	МассивВсехРеквизитов.Добавить("БанковскийСчетКонтрагента");
	МассивВсехРеквизитов.Добавить("БанковскийСчетГрузоотправителя");
	МассивВсехРеквизитов.Добавить("БанковскийСчетГрузополучателя");
	МассивВсехРеквизитов.Добавить("АдресДоставки");
	МассивВсехРеквизитов.Добавить("ДоверенностьНомер");
	МассивВсехРеквизитов.Добавить("ДоверенностьДата");
	МассивВсехРеквизитов.Добавить("ДоверенностьВыдана");
	МассивВсехРеквизитов.Добавить("ДоверенностьЛицо");
	МассивВсехРеквизитов.Добавить("Товары.Номенклатура");
	МассивВсехРеквизитов.Добавить("Товары.Характеристика");
	МассивВсехРеквизитов.Добавить("Товары.Назначение");
	МассивВсехРеквизитов.Добавить("Товары.Содержание");
	МассивВсехРеквизитов.Добавить("Товары.Упаковка");
	МассивВсехРеквизитов.Добавить("Товары.КоличествоУпаковок");
	МассивВсехРеквизитов.Добавить("Товары.ВидЦены");
	МассивВсехРеквизитов.Добавить("Товары.ЗаказКлиента");
	МассивВсехРеквизитов.Добавить("Товары.КодСтроки");
	МассивВсехРеквизитов.Добавить("Товары.Склад");
	МассивВсехРеквизитов.Добавить("Товары.Серия");
	МассивВсехРеквизитов.Добавить("Товары.СтатусУказанияСерий");
	МассивВсехРеквизитов.Добавить("Товары.СтатьяДоходов");
	МассивВсехРеквизитов.Добавить("Товары.АналитикаДоходов");
	МассивВсехРеквизитов.Добавить("Расхождения.Серия");
	МассивВсехРеквизитов.Добавить("Расхождения.Номенклатура");
	МассивВсехРеквизитов.Добавить("Расхождения.Характеристика");
	МассивВсехРеквизитов.Добавить("Расхождения.Назначение");
	МассивВсехРеквизитов.Добавить("Расхождения.Содержание");
	МассивВсехРеквизитов.Добавить("Расхождения.Упаковка");
	МассивВсехРеквизитов.Добавить("Расхождения.КоличествоУпаковок");
	МассивВсехРеквизитов.Добавить("Расхождения.ЗаказКлиента");
	МассивВсехРеквизитов.Добавить("Расхождения.КодСтроки");
	МассивВсехРеквизитов.Добавить("Расхождения.Склад");
	МассивВсехРеквизитов.Добавить("Расхождения.СтатьяДоходов");
	МассивВсехРеквизитов.Добавить("Расхождения.АналитикаДоходов");
	
	МассивРеквизитовОперации = Новый Массив;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		МассивРеквизитовОперации.Добавить("Склад");
		МассивРеквизитовОперации.Добавить("ПодразделениеДоходы");
		МассивРеквизитовОперации.Добавить("СтатьяДоходов");
		МассивРеквизитовОперации.Добавить("АналитикаДоходов");
		МассивРеквизитовОперации.Добавить("ПодразделениеРасходы");
		МассивРеквизитовОперации.Добавить("СтатьяРасходов");
		МассивРеквизитовОперации.Добавить("АналитикаРасходов");
		МассивРеквизитовОперации.Добавить("Отпустил");
		МассивРеквизитовОперации.Добавить("ОтпустилДолжность");
		МассивРеквизитовОперации.Добавить("Грузополучатель");
		МассивРеквизитовОперации.Добавить("Грузоотправитель");
		МассивРеквизитовОперации.Добавить("БанковскийСчетОрганизации");
		МассивРеквизитовОперации.Добавить("БанковскийСчетКонтрагента");
		МассивРеквизитовОперации.Добавить("БанковскийСчетГрузоотправителя");
		МассивРеквизитовОперации.Добавить("БанковскийСчетГрузополучателя");
		МассивРеквизитовОперации.Добавить("АдресДоставки");
		МассивРеквизитовОперации.Добавить("ДоверенностьНомер");
		МассивРеквизитовОперации.Добавить("ДоверенностьДата");
		МассивРеквизитовОперации.Добавить("ДоверенностьВыдана");
		МассивРеквизитовОперации.Добавить("ДоверенностьЛицо");
		МассивРеквизитовОперации.Добавить("Товары.Номенклатура");
		МассивРеквизитовОперации.Добавить("Товары.Характеристика");
		МассивРеквизитовОперации.Добавить("Товары.Назначение");
		МассивРеквизитовОперации.Добавить("Товары.Упаковка");
		МассивРеквизитовОперации.Добавить("Товары.КоличествоУпаковок");
		МассивРеквизитовОперации.Добавить("Товары.ВидЦены");
		МассивРеквизитовОперации.Добавить("Товары.ЗаказКлиента");
		МассивРеквизитовОперации.Добавить("Товары.КодСтроки");
		МассивРеквизитовОперации.Добавить("Товары.Склад");
		МассивРеквизитовОперации.Добавить("Товары.Серия");
		МассивРеквизитовОперации.Добавить("Товары.СтатусУказанияСерий");
		МассивРеквизитовОперации.Добавить("Расхождения.Серия");
		МассивРеквизитовОперации.Добавить("Расхождения.Номенклатура");
		МассивРеквизитовОперации.Добавить("Расхождения.Характеристика");
		МассивРеквизитовОперации.Добавить("Расхождения.Назначение");
		МассивРеквизитовОперации.Добавить("Расхождения.Упаковка");
		МассивРеквизитовОперации.Добавить("Расхождения.КоличествоУпаковок");
		МассивРеквизитовОперации.Добавить("Расхождения.ЗаказКлиента");
		МассивРеквизитовОперации.Добавить("Расхождения.КодСтроки");
		МассивРеквизитовОперации.Добавить("Расхождения.Склад");
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
		МассивРеквизитовОперации.Добавить("ПодразделениеДоходы");
		МассивРеквизитовОперации.Добавить("СтатьяДоходов");
		МассивРеквизитовОперации.Добавить("АналитикаДоходов");
		МассивРеквизитовОперации.Добавить("ПодразделениеРасходы");
		МассивРеквизитовОперации.Добавить("СтатьяРасходов");
		МассивРеквизитовОперации.Добавить("АналитикаРасходов");
		МассивРеквизитовОперации.Добавить("Товары.Номенклатура");
		МассивРеквизитовОперации.Добавить("Товары.Характеристика");
		МассивРеквизитовОперации.Добавить("Товары.Содержание");
		МассивРеквизитовОперации.Добавить("Товары.ВидЦены");
		МассивРеквизитовОперации.Добавить("Товары.ЗаказКлиента");
		МассивРеквизитовОперации.Добавить("Товары.КодСтроки");
		МассивРеквизитовОперации.Добавить("Расхождения.Номенклатура");
		МассивРеквизитовОперации.Добавить("Расхождения.Характеристика");
		МассивРеквизитовОперации.Добавить("Расхождения.Назначение");
		МассивРеквизитовОперации.Добавить("Расхождения.Содержание");
		МассивРеквизитовОперации.Добавить("Расхождения.ЗаказКлиента");
		МассивРеквизитовОперации.Добавить("Расхождения.КодСтроки");
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
		МассивРеквизитовОперации.Добавить("ПодразделениеДоходы");
		МассивРеквизитовОперации.Добавить("СтатьяДоходов");
		МассивРеквизитовОперации.Добавить("АналитикаДоходов");
		МассивРеквизитовОперации.Добавить("ПодразделениеРасходы");
		МассивРеквизитовОперации.Добавить("СтатьяРасходов");
		МассивРеквизитовОперации.Добавить("АналитикаРасходов");
		МассивРеквизитовОперации.Добавить("Товары.Содержание");
		МассивРеквизитовОперации.Добавить("Расхождения.Содержание");
		МассивРеквизитовОперации.Добавить("Товары.СтатьяДоходов");
		МассивРеквизитовОперации.Добавить("Товары.АналитикаДоходов");
		МассивРеквизитовОперации.Добавить("Расхождения.СтатьяДоходов");
		МассивРеквизитовОперации.Добавить("Расхождения.АналитикаДоходов");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТоварыПоИсходнымДанным(ДокументОснование, Товары) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	СформироватьВременнуюТаблицуИсходныхДанных(МенеджерВременныхТаблиц, ДокументОснование);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	*
	|ИЗ ИсходныеДанные КАК ИсходныеДанные
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИсходныеДанные.НомерСтроки
	|";
	
	Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьТоварыПоАктуПриемки(Объект) Экспорт
	
	Объект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	АктОРасхожденияхПослеОтгрузкиТовары.НомерСтроки КАК НомерСтроки,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Номенклатура,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Характеристика,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Назначение,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Упаковка,
	|	
	|	ВЫБОР КОГДА &РеализацияПерепоставленногоТовара ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковок - АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковокПоДокументу
	|	КОГДА &ВозвратНедопоставленногоТовара ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковокПоДокументу - АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковок
	|	КОГДА АктОРасхожденияхПослеОтгрузкиТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится)
	|			ИЛИ АктОРасхожденияхПослеОтгрузкиТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана) ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковокПоДокументу
	|	КОГДА АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковок > АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковокПоДокументу
	|			И НЕ АктОРасхожденияхПослеОтгрузкиТовары.ЗаказКлиента = НЕОПРЕДЕЛЕНО
	|			И НЕ АктОРасхожденияхПослеОтгрузкиТовары.КодСтроки = 0 ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковокПоДокументу
	|	ИНАЧЕ
	|		АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковок
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	
	|	ВЫБОР КОГДА &РеализацияПерепоставленногоТовара ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.Количество - АктОРасхожденияхПослеОтгрузкиТовары.КоличествоПоДокументу
	|	КОГДА &ВозвратНедопоставленногоТовара ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.КоличествоПоДокументу - АктОРасхожденияхПослеОтгрузкиТовары.Количество
	|	КОГДА АктОРасхожденияхПослеОтгрузкиТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится)
	|			ИЛИ АктОРасхожденияхПослеОтгрузкиТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана) ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.КоличествоПоДокументу
	|	КОГДА АктОРасхожденияхПослеОтгрузкиТовары.Количество > АктОРасхожденияхПослеОтгрузкиТовары.КоличествоПоДокументу
	|			И НЕ АктОРасхожденияхПослеОтгрузкиТовары.ЗаказКлиента = НЕОПРЕДЕЛЕНО
	|			И НЕ АктОРасхожденияхПослеОтгрузкиТовары.КодСтроки = 0 ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.КоличествоПоДокументу
	|	ИНАЧЕ
	|		АктОРасхожденияхПослеОтгрузкиТовары.Количество
	|	КОНЕЦ КАК Количество,
	|	
	|	АктОРасхожденияхПослеОтгрузкиТовары.ВидЦены,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Цена,
	|	
	|	ВЫБОР КОГДА &РеализацияПерепоставленногоТовара ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.Сумма - АктОРасхожденияхПослеОтгрузкиТовары.СуммаПоДокументу
	|	КОГДА &ВозвратНедопоставленногоТовара ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.СуммаПоДокументу - АктОРасхожденияхПослеОтгрузкиТовары.Сумма
	|	КОГДА АктОРасхожденияхПослеОтгрузкиТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится)
	|			ИЛИ АктОРасхожденияхПослеОтгрузкиТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана) ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.СуммаПоДокументу
	|	ИНАЧЕ
	|		АктОРасхожденияхПослеОтгрузкиТовары.Сумма
	|	КОНЕЦ КАК Сумма,
	|	
	|	АктОРасхожденияхПослеОтгрузкиТовары.СтавкаНДС,
	|	
	|	ВЫБОР КОГДА &РеализацияПерепоставленногоТовара ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.СуммаНДС - АктОРасхожденияхПослеОтгрузкиТовары.СуммаНДСПоДокументу
	|	КОГДА &ВозвратНедопоставленногоТовара ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.СуммаНДСПоДокументу - АктОРасхожденияхПослеОтгрузкиТовары.СуммаНДС
	|	КОГДА АктОРасхожденияхПослеОтгрузкиТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится)
	|			ИЛИ АктОРасхожденияхПослеОтгрузкиТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана) ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.СуммаНДСПоДокументу
	|	ИНАЧЕ
	|		АктОРасхожденияхПослеОтгрузкиТовары.СуммаНДС
	|	КОНЕЦ КАК СуммаНДС,
	|	
	|	ВЫБОР КОГДА &РеализацияПерепоставленногоТовара ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.СуммаСНДС - АктОРасхожденияхПослеОтгрузкиТовары.СуммаСНДСПоДокументу
	|	КОГДА &ВозвратНедопоставленногоТовара ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.СуммаСНДСПоДокументу - АктОРасхожденияхПослеОтгрузкиТовары.СуммаСНДС
	|	КОГДА АктОРасхожденияхПослеОтгрузкиТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится)
	|			ИЛИ АктОРасхожденияхПослеОтгрузкиТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана) ТОГДА
	|		АктОРасхожденияхПослеОтгрузкиТовары.СуммаСНДСПоДокументу
	|	ИНАЧЕ
	|		АктОРасхожденияхПослеОтгрузкиТовары.СуммаСНДС
	|	КОНЕЦ КАК СуммаСНДС,
	|	
	|	АктОРасхожденияхПослеОтгрузкиТовары.ЗаказКлиента,
	|	
	|	ВЫБОР КОГДА &РеализацияПерепоставленногоТовара ТОГДА
	|		0
	|	ИНАЧЕ
	|		АктОРасхожденияхПослеОтгрузкиТовары.КодСтроки
	|	КОНЕЦ КАК КодСтроки,
	|	
	|	АктОРасхожденияхПослеОтгрузкиТовары.Склад,
	|	АктОРасхожденияхПослеОтгрузкиТовары.Серия,
	|	АктОРасхожденияхПослеОтгрузкиТовары.СтатусУказанияСерий,
	|	
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковок > АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковокПоДокументу
	|				И НЕ АктОРасхожденияхПослеОтгрузкиТовары.ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|				И НЕ АктОРасхожденияхПослеОтгрузкиТовары.КодСтроки = 0
	|				И НЕ &РеализацияПерепоставленногоТовара
	|				И НЕ &ВозвратНедопоставленногоТовара
	|			ТОГДА АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковок - АктОРасхожденияхПослеОтгрузкиТовары.КоличествоУпаковокПоДокументу
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоУпаковокСверхЗаказа,
	|	
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхПослеОтгрузкиТовары.Количество > АктОРасхожденияхПослеОтгрузкиТовары.КоличествоПоДокументу
	|				И НЕ АктОРасхожденияхПослеОтгрузкиТовары.ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|				И НЕ АктОРасхожденияхПослеОтгрузкиТовары.КодСтроки = 0
	|				И НЕ &РеализацияПерепоставленногоТовара
	|				И НЕ &ВозвратНедопоставленногоТовара
	|			ТОГДА АктОРасхожденияхПослеОтгрузкиТовары.Количество - АктОРасхожденияхПослеОтгрузкиТовары.КоличествоПоДокументу
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоСверхЗаказа,
	|	
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхПослеОтгрузкиТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
	|																				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
	|			ТОГДА ВЫБОР
	|					КОГДА АктОРасхожденияхПослеОтгрузкиТовары.Количество > АктОРасхожденияхПослеОтгрузкиТовары.КоличествоПоДокументу
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УвеличитьВыручку)
	|					КОГДА  АктОРасхожденияхПослеОтгрузкиТовары.Количество < АктОРасхожденияхПослеОтгрузкиТовары.КоличествоПоДокументу
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.УменьшитьВыручку)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.ПустаяСсылка)
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокРеализаций.ПустаяСсылка)
	|	КОНЕЦ КАК ВариантОтражения
	|
	|ИЗ
	|	Документ.АктОРасхожденияхПослеОтгрузки.Товары КАК АктОРасхожденияхПослеОтгрузкиТовары
	|ГДЕ
	|	АктОРасхожденияхПослеОтгрузкиТовары.Ссылка = &АктОРасхожденияхПослеОтгрузкиОснование
	|	И АктОРасхожденияхПослеОтгрузкиТовары.Реализация = &РеализацияОснование
	|	И (АктОРасхожденияхПослеОтгрузкиТовары.Действие В (&ВариантыДействий)
	|		ИЛИ (НЕ (&РеализацияПерепоставленногоТовара ИЛИ &ВозвратНедопоставленногоТовара)))
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ВариантыДействий = Новый Массив;
	Если Объект.ВидКорректировки = Перечисления.ХозяйственныеОперации.РеализацияПерепоставленногоТовара Тогда
		ВариантыДействий.Добавить(Перечисления.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного);
		ВариантыДействий.Добавить(Перечисления.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного);
		РеализацияПерепоставленногоТовара = Истина;
		ВозвратНедопоставленногоТовара = Ложь;
	ИначеЕсли Объект.ВидКорректировки = Перечисления.ХозяйственныеОперации.ВозвратНедопоставленногоТовара Тогда
		ВариантыДействий.Добавить(Перечисления.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется);
		ВариантыДействий.Добавить(Перечисления.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка);
		РеализацияПерепоставленногоТовара = Ложь;
		ВозвратНедопоставленногоТовара = Истина;
	Иначе
		РеализацияПерепоставленногоТовара = Ложь;
		ВозвратНедопоставленногоТовара = Ложь;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("АктОРасхожденияхПослеОтгрузкиОснование", Объект.АктОРасхожденияхПослеОтгрузкиОснование);
	Запрос.УстановитьПараметр("РеализацияОснование", Объект.ДокументОснование);
	Запрос.УстановитьПараметр("ВариантыДействий", ВариантыДействий);
	Запрос.УстановитьПараметр("РеализацияПерепоставленногоТовара", РеализацияПерепоставленногоТовара);
	Запрос.УстановитьПараметр("ВозвратНедопоставленногоТовара", ВозвратНедопоставленногоТовара);
	
	СтруктураДействий = Новый Структура;
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект);
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Количество > 0 Тогда
			
			НоваяСтрокаТовары = Объект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, Выборка);
			Если ЗначениеЗаполнено(Выборка.КоличествоУпаковокСверхЗаказа) Тогда
				ДополнительнаяСтрокаТовары = Объект.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(ДополнительнаяСтрокаТовары, Выборка);
				ДополнительнаяСтрокаТовары.КоличествоУпаковок = Выборка.КоличествоУпаковокСверхЗаказа;
				ДополнительнаяСтрокаТовары.Количество = Выборка.КоличествоСверхЗаказа;
				ДополнительнаяСтрокаТовары.КодСтроки  = 0;
				
				ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрокаТовары, СтруктураДействий, Неопределено);
				ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ДополнительнаяСтрокаТовары, СтруктураДействий, Неопределено);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьРасхождения(Объект);
	
КонецПроцедуры

Процедура ЗаполнитьРасхождения(Объект) Экспорт
	
	Объект.Расхождения.Очистить();
	
	Если Объект.ВидКорректировки <> Перечисления.ХозяйственныеОперации.РеализацияПерепоставленногоТовара
		И Объект.ВидКорректировки <> Перечисления.ХозяйственныеОперации.ВозвратНедопоставленногоТовара Тогда
		
		ЗаполнитьРасхожденияПоРазнице(Объект);
		
	Иначе
		
		ЗаполнитьРасхожденияПоТоварам(Объект);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРасхожденияПоТоварам(Объект)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ТаблицаТоваров", Объект.Товары.Выгрузить());
	Запрос.УстановитьПараметр("ЭтоВозврат", Объект.ВидКорректировки = Перечисления.ХозяйственныеОперации.ВозвратНедопоставленногоТовара);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС", Объект.ЦенаВключаетНДС);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.НоменклатураНабора КАК НоменклатураНабора,
	|	ТаблицаТоваров.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Назначение КАК Назначение,
	|	ТаблицаТоваров.Серия КАК Серия,
	|	ТаблицаТоваров.Содержание КАК Содержание,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,
	|	ТаблицаТоваров.ЗаказКлиента КАК ЗаказКлиента,
	|	ТаблицаТоваров.КодСтроки КАК КодСтроки,
	|	ТаблицаТоваров.Склад КАК Склад,
	|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТоваров.ВариантОтражения КАК ВариантОтражения,
	|	
	|	ВЫБОР КОГДА &ЭтоВозврат ТОГДА
	|		-ТаблицаТоваров.КоличествоУпаковок
	|	ИНАЧЕ
	|		ТаблицаТоваров.КоличествоУпаковок
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	
	|	ВЫБОР КОГДА &ЭтоВозврат ТОГДА
	|		-ТаблицаТоваров.Количество
	|	ИНАЧЕ
	|		ТаблицаТоваров.Количество
	|	КОНЕЦ КАК Количество,
	|	
	|	ВЫБОР КОГДА &ЭтоВозврат ТОГДА
	|		-ТаблицаТоваров.Сумма
	|	ИНАЧЕ
	|		ТаблицаТоваров.Сумма
	|	КОНЕЦ КАК Сумма,
	|	
	|	ВЫБОР КОГДА &ЭтоВозврат ТОГДА
	|		-ТаблицаТоваров.СуммаНДС
	|	ИНАЧЕ
	|		ТаблицаТоваров.СуммаНДС
	|	КОНЕЦ КАК СуммаНДС,
	|	
	|	ВЫБОР КОГДА &ЭтоВозврат ТОГДА
	|		-ТаблицаТоваров.СуммаСНДС
	|	ИНАЧЕ
	|		ТаблицаТоваров.СуммаСНДС
	|	КОНЕЦ КАК СуммаСНДС
	|
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРасхождений.НоменклатураНабора,
	|	ДанныеРасхождений.ХарактеристикаНабора,
	|	ДанныеРасхождений.Номенклатура,
	|	ДанныеРасхождений.Характеристика,
	|	ДанныеРасхождений.Назначение,
	|	ДанныеРасхождений.Серия,
	|	ДанныеРасхождений.Содержание,
	|	ДанныеРасхождений.Упаковка,
	|	ДанныеРасхождений.ЗаказКлиента,
	|	ДанныеРасхождений.КодСтроки,
	|	ДанныеРасхождений.Склад,
	|	ДанныеРасхождений.СтавкаНДС,
	|	ДанныеРасхождений.ВариантОтражения,
	|	СУММА(ДанныеРасхождений.КоличествоУпаковок),
	|	СУММА(ДанныеРасхождений.Количество),
	|	СУММА(ДанныеРасхождений.СуммаНДС),
	|	СУММА(ДанныеРасхождений.СуммаСНДС),
	|	
	|	ВЫБОР КОГДА &ЦенаВключаетНДС ТОГДА
	|		СУММА(ДанныеРасхождений.СуммаСНДС)
	|	ИНАЧЕ
	|		СУММА(ДанныеРасхождений.СуммаСНДС) - СУММА(ДанныеРасхождений.СуммаНДС)
	|	КОНЕЦ КАК Сумма,
	|	
	|	ВЫРАЗИТЬ(ДанныеРасхождений.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры КАК ТипНоменклатуры
	|ИЗ
	|	ТаблицаТоваров КАК ДанныеРасхождений
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРасхождений.НоменклатураНабора,
	|	ДанныеРасхождений.ХарактеристикаНабора,
	|	ДанныеРасхождений.Номенклатура,
	|	ДанныеРасхождений.Характеристика,
	|	ДанныеРасхождений.Назначение,
	|	ДанныеРасхождений.Серия,
	|	ДанныеРасхождений.Содержание,
	|	ДанныеРасхождений.Упаковка,
	|	ДанныеРасхождений.ЗаказКлиента,
	|	ДанныеРасхождений.КодСтроки,
	|	ДанныеРасхождений.Склад,
	|	ДанныеРасхождений.СтавкаНДС,
	|	ДанныеРасхождений.ВариантОтражения
	|";
	
	ДанныеРасхождения = Запрос.Выполнить().Выгрузить();
	Объект.Расхождения.Загрузить(ДанныеРасхождения);
	
КонецПроцедуры

Процедура ЗаполнитьРасхожденияПоРазнице(Объект)
	
	КлючевыеПоля = "
	|	НоменклатураНабора,
	|	ХарактеристикаНабора,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Серия,
	|	Содержание,
	|	Упаковка,
	|	ЗаказКлиента,
	|	КодСтроки,
	|	Склад,
	|	СтавкаНДС,
	|	КодТНВЭД,
	|	СтатьяДоходов,
	|	АналитикаДоходов";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ДатаОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "Дата");
	ДатаДокумента = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса());
	КорректировкаПрошлогоПериода = НачалоГода(ДатаДокумента) > НачалоГода(ДатаОснования);
	КорректировкаУслугПрочихАктивов = ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов");
	
	СформироватьВременнуюТаблицуИсходныхДанных(МенеджерВременныхТаблиц, Объект.ДокументОснование);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НовыеДанные", Объект.Товары.Выгрузить());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НовыеДанные.НоменклатураНабора КАК НоменклатураНабора,
	|	НовыеДанные.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	НовыеДанные.Номенклатура КАК Номенклатура,
	|	НовыеДанные.Характеристика КАК Характеристика,
	|	НовыеДанные.Назначение КАК Назначение,
	|	НовыеДанные.Серия КАК Серия,
	|	НовыеДанные.Содержание КАК Содержание,
	|	НовыеДанные.Упаковка КАК Упаковка,
	|	НовыеДанные.ЗаказКлиента КАК ЗаказКлиента,
	|	НовыеДанные.КодСтроки КАК КодСтроки,
	|	НовыеДанные.Склад КАК Склад,
	|	НовыеДанные.СтавкаНДС КАК СтавкаНДС,
	|	НовыеДанные.КодТНВЭД КАК КодТНВЭД,
	|	НовыеДанные.СтатьяДоходов КАК СтатьяДоходов,
	|	НовыеДанные.АналитикаДоходов КАК АналитикаДоходов,
	|	НовыеДанные.КоличествоУпаковок КАК КоличествоУпаковок,
	|	НовыеДанные.Количество КАК Количество,
	|	НовыеДанные.Сумма КАК Сумма,
	|	НовыеДанные.СуммаНДС КАК СуммаНДС,
	|	НовыеДанные.СуммаСНДС КАК СуммаСНДС
	|ПОМЕСТИТЬ НовыеДанныеИзТаблицы
	|ИЗ
	|	&НовыеДанные КАК НовыеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДанныеИзТаблицы.НоменклатураНабора,
	|	НовыеДанныеИзТаблицы.ХарактеристикаНабора,
	|	НовыеДанныеИзТаблицы.Номенклатура,
	|	НовыеДанныеИзТаблицы.Характеристика,
	|	НовыеДанныеИзТаблицы.Назначение,
	|	НовыеДанныеИзТаблицы.Серия,
	|	НовыеДанныеИзТаблицы.Содержание,
	|	НовыеДанныеИзТаблицы.Упаковка,
	|	НовыеДанныеИзТаблицы.ЗаказКлиента,
	|	НовыеДанныеИзТаблицы.КодСтроки,
	|	НовыеДанныеИзТаблицы.Склад,
	|	НовыеДанныеИзТаблицы.СтавкаНДС,
	|	НовыеДанныеИзТаблицы.КодТНВЭД,
	|	НовыеДанныеИзТаблицы.СтатьяДоходов,
	|	НовыеДанныеИзТаблицы.АналитикаДоходов,
	|	СУММА(НовыеДанныеИзТаблицы.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(НовыеДанныеИзТаблицы.Количество) КАК Количество,
	|	СУММА(НовыеДанныеИзТаблицы.Сумма) КАК Сумма,
	|	СУММА(НовыеДанныеИзТаблицы.СуммаНДС) КАК СуммаНДС,
	|	СУММА(НовыеДанныеИзТаблицы.СуммаСНДС) КАК СуммаСНДС
	|ПОМЕСТИТЬ НовыеДанные
	|ИЗ
	|	НовыеДанныеИзТаблицы КАК НовыеДанныеИзТаблицы
	|
	|СГРУППИРОВАТЬ ПО
	|	НовыеДанныеИзТаблицы.НоменклатураНабора,
	|	НовыеДанныеИзТаблицы.ХарактеристикаНабора,
	|	НовыеДанныеИзТаблицы.Номенклатура,
	|	НовыеДанныеИзТаблицы.Характеристика,
	|	НовыеДанныеИзТаблицы.Назначение,
	|	НовыеДанныеИзТаблицы.Серия,
	|	НовыеДанныеИзТаблицы.Содержание,
	|	НовыеДанныеИзТаблицы.Упаковка,
	|	НовыеДанныеИзТаблицы.ЗаказКлиента,
	|	НовыеДанныеИзТаблицы.КодСтроки,
	|	НовыеДанныеИзТаблицы.Склад,
	|	НовыеДанныеИзТаблицы.СтавкаНДС,
	|	НовыеДанныеИзТаблицы.КодТНВЭД,
	|	НовыеДанныеИзТаблицы.СтатьяДоходов,
	|	НовыеДанныеИзТаблицы.АналитикаДоходов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НовыеДанныеИзТаблицы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ИсходныеДанные.НоменклатураНабора КАК Справочник.Номенклатура) КАК НоменклатураНабора,
	|	ВЫРАЗИТЬ(ИсходныеДанные.ХарактеристикаНабора КАК Справочник.ХарактеристикиНоменклатуры) КАК ХарактеристикаНабора,
	|	ВЫРАЗИТЬ(ИсходныеДанные.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(ИсходныеДанные.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	ВЫРАЗИТЬ(ИсходныеДанные.Назначение КАК Справочник.Назначения) КАК Назначение,
	|	ВЫРАЗИТЬ(ИсходныеДанные.Серия КАК Справочник.СерииНоменклатуры) КАК Серия,
	|	ВЫБОР
	|		КОГДА ИсходныеДанные.Содержание <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ИсходныеДанные.Содержание
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Содержание,
	|	ВЫРАЗИТЬ(ИсходныеДанные.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения) КАК Упаковка,
	|	ИсходныеДанные.ЗаказКлиента,
	|	ВЫБОР
	|		КОГДА ИсходныеДанные.КодСтроки <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ВЫРАЗИТЬ(ИсходныеДанные.КодСтроки КАК ЧИСЛО(10, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КодСтроки,
	|	ВЫРАЗИТЬ(ИсходныеДанные.Склад КАК Справочник.Склады) КАК Склад,
	|	ВЫРАЗИТЬ(ИсходныеДанные.КодТНВЭД КАК Справочник.КлассификаторТНВЭД) КАК КодТНВЭД,
	|	ВЫРАЗИТЬ(ИсходныеДанные.СтавкаНДС КАК Перечисление.СтавкиНДС) КАК СтавкаНДС,
	|	ВЫРАЗИТЬ(ИсходныеДанные.СтатьяДоходов КАК ПланВидовХарактеристик.СтатьиДоходов) КАК СтатьяДоходов,
	|	ИсходныеДанные.АналитикаДоходов КАК АналитикаДоходов,
	|	СУММА(ВЫБОР
	|			КОГДА ИсходныеДанные.КоличествоУпаковок <> НЕОПРЕДЕЛЕНО
	|				ТОГДА ВЫРАЗИТЬ(ИсходныеДанные.КоличествоУпаковок КАК ЧИСЛО(15, 3))
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоУпаковок,
	|	СУММА(ВЫБОР
	|			КОГДА ИсходныеДанные.Количество <> НЕОПРЕДЕЛЕНО
	|				ТОГДА ВЫРАЗИТЬ(ИсходныеДанные.Количество КАК ЧИСЛО(15, 3))
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА ИсходныеДанные.Сумма <> НЕОПРЕДЕЛЕНО
	|				ТОГДА ВЫРАЗИТЬ(ИсходныеДанные.Сумма КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ИсходныеДанные.СуммаНДС <> НЕОПРЕДЕЛЕНО
	|				ТОГДА ВЫРАЗИТЬ(ИсходныеДанные.СуммаНДС КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаНДС,
	|	СУММА(ВЫБОР
	|			КОГДА ИсходныеДанные.СуммаСНДС <> НЕОПРЕДЕЛЕНО
	|				ТОГДА ВЫРАЗИТЬ(ИсходныеДанные.СуммаСНДС КАК ЧИСЛО(15, 2))
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаСНДС
	|ПОМЕСТИТЬ ИсходныеДанныеДляРасхождений
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	ИсходныеДанные.НоменклатураНабора,
	|	ИсходныеДанные.ХарактеристикаНабора,
	|	ИсходныеДанные.Номенклатура,
	|	ИсходныеДанные.Характеристика,
	|	ИсходныеДанные.Назначение,
	|	ИсходныеДанные.Серия,
	|	ИсходныеДанные.Содержание,
	|	ИсходныеДанные.Упаковка,
	|	ИсходныеДанные.ЗаказКлиента,
	|	ИсходныеДанные.КодСтроки,
	|	ИсходныеДанные.Склад,
	|	ИсходныеДанные.КодТНВЭД,
	|	ИсходныеДанные.СтавкаНДС,
	|	ИсходныеДанные.СтатьяДоходов,
	|	ИсходныеДанные.АналитикаДоходов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ИсходныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДанные.НоменклатураНабора КАК НоменклатураНабора,
	|	НовыеДанные.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	НовыеДанные.Номенклатура КАК Номенклатура,
	|	НовыеДанные.Характеристика КАК Характеристика,
	|	НовыеДанные.Назначение КАК Назначение,
	|	НовыеДанные.Серия КАК Серия,
	|	НовыеДанные.Содержание КАК Содержание,
	|	НовыеДанные.Упаковка КАК Упаковка,
	|	НовыеДанные.ЗаказКлиента КАК ЗаказКлиента,
	|	НовыеДанные.КодСтроки КАК КодСтроки,
	|	НовыеДанные.Склад КАК Склад,
	|	НовыеДанные.СтавкаНДС КАК СтавкаНДС,
	|	НовыеДанные.КодТНВЭД КАК КодТНВЭД,
	|	НовыеДанные.СтатьяДоходов КАК СтатьяДоходов,
	|	НовыеДанные.АналитикаДоходов КАК АналитикаДоходов,
	|	НовыеДанные.Сумма КАК Сумма,
	|	НовыеДанные.СуммаНДС КАК СуммаНДС,
	|	НовыеДанные.СуммаСНДС КАК СуммаСНДС
	|ПОМЕСТИТЬ ДанныеРасхождений
	|ИЗ
	|	НовыеДанные КАК НовыеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИсходныеДанныеДляРасхождений КАК ИсходныеДанные
	|		ПО    НовыеДанные.Номенклатура = ИсходныеДанные.Номенклатура
	|			И НовыеДанные.Характеристика = ИсходныеДанные.Характеристика
	|			И НовыеДанные.Назначение = ИсходныеДанные.Назначение
	|			И НовыеДанные.НоменклатураНабора = ИсходныеДанные.НоменклатураНабора
	|			И НовыеДанные.ХарактеристикаНабора = ИсходныеДанные.ХарактеристикаНабора
	|			И НовыеДанные.Серия = ИсходныеДанные.Серия
	|			И НовыеДанные.Содержание = ИсходныеДанные.Содержание
	|			И НовыеДанные.Упаковка = ИсходныеДанные.Упаковка
	|			И НовыеДанные.ЗаказКлиента = ИсходныеДанные.ЗаказКлиента
	|			И НовыеДанные.КодСтроки = ИсходныеДанные.КодСтроки
	|			И НовыеДанные.Склад = ИсходныеДанные.Склад
	|			И НовыеДанные.СтавкаНДС = ИсходныеДанные.СтавкаНДС
	|			И НовыеДанные.КодТНВЭД = ИсходныеДанные.КодТНВЭД
	|			И НовыеДанные.СтатьяДоходов = ИсходныеДанные.СтатьяДоходов
	|			И НовыеДанные.АналитикаДоходов = ИсходныеДанные.АналитикаДоходов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИсходныеДанные.НоменклатураНабора,
	|	ИсходныеДанные.ХарактеристикаНабора,
	|	ИсходныеДанные.Номенклатура,
	|	ИсходныеДанные.Характеристика,
	|	ИсходныеДанные.Назначение,
	|	ИсходныеДанные.Серия,
	|	ИсходныеДанные.Содержание,
	|	ИсходныеДанные.Упаковка,
	|	ИсходныеДанные.ЗаказКлиента,
	|	ИсходныеДанные.КодСтроки,
	|	ИсходныеДанные.Склад,
	|	ИсходныеДанные.СтавкаНДС,
	|	ИсходныеДанные.КодТНВЭД,
	|	ИсходныеДанные.СтатьяДоходов,
	|	ИсходныеДанные.АналитикаДоходов,
	|	-ИсходныеДанные.Сумма,
	|	-ИсходныеДанные.СуммаНДС,
	|	-ИсходныеДанные.СуммаСНДС
	|ИЗ
	|	ИсходныеДанныеДляРасхождений КАК ИсходныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НовыеДанные КАК НовыеДанные
	|		ПО ИсходныеДанные.Номенклатура = НовыеДанные.Номенклатура
	|			И ИсходныеДанные.Характеристика = НовыеДанные.Характеристика
	|			И ИсходныеДанные.Назначение = НовыеДанные.Назначение
	|			И ИсходныеДанные.НоменклатураНабора = НовыеДанные.НоменклатураНабора
	|			И ИсходныеДанные.ХарактеристикаНабора = НовыеДанные.ХарактеристикаНабора
	|			И ИсходныеДанные.Серия = НовыеДанные.Серия
	|			И ИсходныеДанные.Содержание = НовыеДанные.Содержание
	|			И ИсходныеДанные.Упаковка = НовыеДанные.Упаковка
	|			И ИсходныеДанные.ЗаказКлиента = НовыеДанные.ЗаказКлиента
	|			И ИсходныеДанные.КодСтроки = НовыеДанные.КодСтроки
	|			И ИсходныеДанные.Склад = НовыеДанные.Склад
	|			И ИсходныеДанные.СтавкаНДС = НовыеДанные.СтавкаНДС
	|			И ИсходныеДанные.КодТНВЭД = НовыеДанные.КодТНВЭД
	|			И ИсходныеДанные.СтатьяДоходов = НовыеДанные.СтатьяДоходов
	|			И ИсходныеДанные.АналитикаДоходов = НовыеДанные.АналитикаДоходов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРасхождений.НоменклатураНабора,
	|	ДанныеРасхождений.ХарактеристикаНабора,
	|	ДанныеРасхождений.Номенклатура,
	|	ДанныеРасхождений.Характеристика,
	|	ДанныеРасхождений.Назначение,
	|	ДанныеРасхождений.Серия,
	|	ДанныеРасхождений.Содержание,
	|	ДанныеРасхождений.Упаковка,
	|	ДанныеРасхождений.ЗаказКлиента,
	|	ДанныеРасхождений.КодСтроки,
	|	ДанныеРасхождений.Склад,
	|	ДанныеРасхождений.СтавкаНДС,
	|	ДанныеРасхождений.КодТНВЭД,
	|	ДанныеРасхождений.СтатьяДоходов,
	|	ДанныеРасхождений.АналитикаДоходов,
	|	СУММА(ДанныеРасхождений.Сумма) КАК Сумма,
	|	СУММА(ДанныеРасхождений.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ДанныеРасхождений.СуммаСНДС) КАК СуммаСНДС,
	|	ВЫРАЗИТЬ(ДанныеРасхождений.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры КАК ТипНоменклатуры
	|ИЗ
	|	ДанныеРасхождений КАК ДанныеРасхождений
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРасхождений.НоменклатураНабора,
	|	ДанныеРасхождений.ХарактеристикаНабора,
	|	ДанныеРасхождений.Номенклатура,
	|	ДанныеРасхождений.Характеристика,
	|	ДанныеРасхождений.Назначение,
	|	ДанныеРасхождений.Серия,
	|	ДанныеРасхождений.Содержание,
	|	ДанныеРасхождений.Упаковка,
	|	ДанныеРасхождений.ЗаказКлиента,
	|	ДанныеРасхождений.КодСтроки,
	|	ДанныеРасхождений.Склад,
	|	ДанныеРасхождений.СтавкаНДС,
	|	ДанныеРасхождений.КодТНВЭД,
	|	ДанныеРасхождений.СтатьяДоходов,
	|	ДанныеРасхождений.АналитикаДоходов
	|
	|ИМЕЮЩИЕ
	|	(ВЫРАЗИТЬ(СУММА(ДанныеРасхождений.СуммаСНДС) КАК ЧИСЛО(15, 2))) <> 0";
	ТаблицаРасхожденийВСуммах = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ДанныеРасхождения Из ТаблицаРасхожденийВСуммах Цикл
		
		НовоеРасхождение = Объект.Расхождения.Добавить();
		ЗаполнитьЗначенияСвойств(НовоеРасхождение, ДанныеРасхождения);
		НовоеРасхождение.ВариантОтражения = ВариантОтраженияПоСтроке(
			НовоеРасхождение,
			ДанныеРасхождения.ТипНоменклатуры,
			КорректировкаУслугПрочихАктивов,
			КорректировкаПрошлогоПериода);
		
	КонецЦикла;
	
	Запрос.Текст = "
	|УНИЧТОЖИТЬ ДанныеРасхождений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчтенныеРасхождения.НоменклатураНабора КАК НоменклатураНабора,
	|	УчтенныеРасхождения.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	УчтенныеРасхождения.Номенклатура КАК Номенклатура,
	|	УчтенныеРасхождения.Характеристика КАК Характеристика,
	|	УчтенныеРасхождения.Назначение КАК Назначение,
	|	УчтенныеРасхождения.Серия КАК Серия,
	|	УчтенныеРасхождения.Содержание КАК Содержание,
	|	УчтенныеРасхождения.Упаковка КАК Упаковка,
	|	УчтенныеРасхождения.ЗаказКлиента КАК ЗаказКлиента,
	|	УчтенныеРасхождения.КодСтроки КАК КодСтроки,
	|	УчтенныеРасхождения.Склад КАК Склад,
	|	УчтенныеРасхождения.СтавкаНДС КАК СтавкаНДС,
	|	УчтенныеРасхождения.КодТНВЭД КАК КодТНВЭД,
	|	УчтенныеРасхождения.СтатьяДоходов КАК СтатьяДоходов,
	|	УчтенныеРасхождения.АналитикаДоходов КАК АналитикаДоходов,
	|	УчтенныеРасхождения.Сумма КАК Сумма,
	|	УчтенныеРасхождения.СуммаНДС КАК СуммаНДС,
	|	УчтенныеРасхождения.СуммаСНДС КАК СуммаСНДС
	|ПОМЕСТИТЬ УчтенныеРасхождения
	|ИЗ
	|	&УчтенныеРасхождения КАК УчтенныеРасхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДанные.НоменклатураНабора КАК НоменклатураНабора,
	|	НовыеДанные.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	НовыеДанные.Номенклатура КАК Номенклатура,
	|	НовыеДанные.Характеристика КАК Характеристика,
	|	НовыеДанные.Назначение КАК Назначение,
	|	НовыеДанные.Серия КАК Серия,
	|	НовыеДанные.Содержание КАК Содержание,
	|	НовыеДанные.Упаковка КАК Упаковка,
	|	НовыеДанные.ЗаказКлиента КАК ЗаказКлиента,
	|	НовыеДанные.КодСтроки КАК КодСтроки,
	|	НовыеДанные.Склад КАК Склад,
	|	НовыеДанные.СтавкаНДС КАК СтавкаНДС,
	|	НовыеДанные.КодТНВЭД КАК КодТНВЭД,
	|	НовыеДанные.СтатьяДоходов КАК СтатьяДоходов,
	|	НовыеДанные.АналитикаДоходов КАК АналитикаДоходов,
	|	НовыеДанные.КоличествоУпаковок КАК КоличествоУпаковок,
	|	НовыеДанные.Количество КАК Количество,
	|	НовыеДанные.Сумма КАК Сумма,
	|	НовыеДанные.СуммаНДС КАК СуммаНДС,
	|	НовыеДанные.СуммаСНДС КАК СуммаСНДС
	|ПОМЕСТИТЬ ДанныеРасхождений
	|ИЗ
	|	НовыеДанные КАК НовыеДанные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИсходныеДанные.НоменклатураНабора,
	|	ИсходныеДанные.ХарактеристикаНабора,
	|	ИсходныеДанные.Номенклатура,
	|	ИсходныеДанные.Характеристика,
	|	ИсходныеДанные.Назначение,
	|	ИсходныеДанные.Серия,
	|	ИсходныеДанные.Содержание,
	|	ИсходныеДанные.Упаковка,
	|	ИсходныеДанные.ЗаказКлиента,
	|	ИсходныеДанные.КодСтроки,
	|	ИсходныеДанные.Склад,
	|	ИсходныеДанные.СтавкаНДС,
	|	ИсходныеДанные.КодТНВЭД,
	|	ИсходныеДанные.СтатьяДоходов,
	|	ИсходныеДанные.АналитикаДоходов,
	|	-ИсходныеДанные.КоличествоУпаковок,
	|	-ИсходныеДанные.Количество,
	|	-ИсходныеДанные.Сумма,
	|	-ИсходныеДанные.СуммаНДС,
	|	-ИсходныеДанные.СуммаСНДС
	|ИЗ
	|	ИсходныеДанныеДляРасхождений КАК ИсходныеДанные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УчтенныеРасхождения.НоменклатураНабора,
	|	УчтенныеРасхождения.ХарактеристикаНабора,
	|	УчтенныеРасхождения.Номенклатура,
	|	УчтенныеРасхождения.Характеристика,
	|	УчтенныеРасхождения.Назначение,
	|	УчтенныеРасхождения.Серия,
	|	УчтенныеРасхождения.Содержание,
	|	УчтенныеРасхождения.Упаковка,
	|	УчтенныеРасхождения.ЗаказКлиента,
	|	УчтенныеРасхождения.КодСтроки,
	|	УчтенныеРасхождения.Склад,
	|	УчтенныеРасхождения.СтавкаНДС,
	|	УчтенныеРасхождения.КодТНВЭД,
	|	УчтенныеРасхождения.СтатьяДоходов,
	|	УчтенныеРасхождения.АналитикаДоходов,
	|	0,
	|	0,
	|	-УчтенныеРасхождения.Сумма,
	|	-УчтенныеРасхождения.СуммаНДС,
	|	-УчтенныеРасхождения.СуммаСНДС
	|ИЗ
	|	УчтенныеРасхождения КАК УчтенныеРасхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРасхождений.НоменклатураНабора,
	|	ДанныеРасхождений.ХарактеристикаНабора,
	|	ДанныеРасхождений.Номенклатура,
	|	ДанныеРасхождений.Характеристика,
	|	ДанныеРасхождений.Назначение,
	|	ДанныеРасхождений.Серия,
	|	ДанныеРасхождений.Содержание,
	|	ДанныеРасхождений.Упаковка,
	|	ДанныеРасхождений.ЗаказКлиента,
	|	ДанныеРасхождений.КодСтроки,
	|	ДанныеРасхождений.Склад,
	|	ДанныеРасхождений.СтавкаНДС,
	|	ДанныеРасхождений.КодТНВЭД,
	|	ДанныеРасхождений.СтатьяДоходов,
	|	ДанныеРасхождений.АналитикаДоходов,
	|	СУММА(ДанныеРасхождений.КоличествоУпаковок),
	|	СУММА(ДанныеРасхождений.Количество),
	|	СУММА(ДанныеРасхождений.Сумма),
	|	СУММА(ДанныеРасхождений.СуммаНДС),
	|	СУММА(ДанныеРасхождений.СуммаСНДС),
	|	ВЫРАЗИТЬ(ДанныеРасхождений.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры КАК ТипНоменклатуры
	|ИЗ
	|	ДанныеРасхождений КАК ДанныеРасхождений
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРасхождений.НоменклатураНабора,
	|	ДанныеРасхождений.ХарактеристикаНабора,
	|	ДанныеРасхождений.Номенклатура,
	|	ДанныеРасхождений.Характеристика,
	|	ДанныеРасхождений.Назначение,
	|	ДанныеРасхождений.Серия,
	|	ДанныеРасхождений.Содержание,
	|	ДанныеРасхождений.Упаковка,
	|	ДанныеРасхождений.ЗаказКлиента,
	|	ДанныеРасхождений.КодСтроки,
	|	ДанныеРасхождений.Склад,
	|	ДанныеРасхождений.СтавкаНДС,
	|	ДанныеРасхождений.КодТНВЭД,
	|	ДанныеРасхождений.СтатьяДоходов,
	|	ДанныеРасхождений.АналитикаДоходов
	|
	|ИМЕЮЩИЕ
	|	ВЫРАЗИТЬ(СУММА(ДанныеРасхождений.КоличествоУпаковок) КАК ЧИСЛО(15,3)) <> 0
	|	ИЛИ ВЫРАЗИТЬ(СУММА(ДанныеРасхождений.Количество) КАК ЧИСЛО(15,3)) <> 0
	|";
	Запрос.УстановитьПараметр("УчтенныеРасхождения", Объект.Расхождения.Выгрузить());
	
	ДанныеРасхождения = Запрос.Выполнить().Выбрать();
	Пока ДанныеРасхождения.Следующий() Цикл
		
		ПараметрыПоиска = Новый Структура(КлючевыеПоля);
		ЗаполнитьЗначенияСвойств(ПараметрыПоиска, ДанныеРасхождения);
		
		НайденныеСтроки = Объект.Расхождения.НайтиСтроки(ПараметрыПоиска);
		Если НайденныеСтроки.Количество() = 0 Тогда
			НовоеРасхождение = Объект.Расхождения.Добавить();
			ЗаполнитьЗначенияСвойств(НовоеРасхождение, ДанныеРасхождения);
		Иначе
			НовоеРасхождение = НайденныеСтроки.Получить(0);
			НовоеРасхождение.Количество = НовоеРасхождение.Количество + ДанныеРасхождения.Количество;
			НовоеРасхождение.КоличествоУпаковок = НовоеРасхождение.КоличествоУпаковок + ДанныеРасхождения.КоличествоУпаковок;
		КонецЕсли;
		
		НовоеРасхождение.Сумма = НовоеРасхождение.СуммаСНДС - ?(Объект.ЦенаВключаетНДС, 0, НовоеРасхождение.СуммаНДС);
		НовоеРасхождение.ВариантОтражения = ВариантОтраженияПоСтроке(
			НовоеРасхождение,
			ДанныеРасхождения.ТипНоменклатуры,
			КорректировкаУслугПрочихАктивов,
			КорректировкаПрошлогоПериода);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВариантОтраженияПоСтроке(СтрокаРасхождений, ТипНоменклатуры, КорректировкаУслугПрочихАктивов, КорректировкаПрошлогоПериода)
	
	ВариантОтражения = Неопределено;
	
	Если КорректировкаУслугПрочихАктивов Тогда
		
		Если КорректировкаПрошлогоПериода Тогда
		
			Если СтрокаРасхождений.СуммаСНДС < 0 Тогда
				ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.СписатьНаРасходы;
			Иначе
				ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.ОтразитьНаПрочихДоходах;
			КонецЕсли;
			
		Иначе
			
			ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.ОтразитьНаПрочихДоходах;
			
		КонецЕсли;
	
	ИначеЕсли (ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Товар
		И ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.МногооборотнаяТара
		И ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Работа)
		ИЛИ СтрокаРасхождений.КоличествоУпаковок = 0 Тогда
		
		Если СтрокаРасхождений.СуммаСНДС < 0 Тогда
			ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.УменьшитьВыручку;
		Иначе
			ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.УвеличитьВыручку;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВариантОтражения;
	
КонецФункции

Процедура СформироватьВременнуюТаблицуИсходныхДанных(МенеджерВременныхТаблиц, ДокументОснование)
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ТекстЗапросаПоОснованию = ТекстЗапросаПоИсходнымДаннымРеализацияТоваровУслуг();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
		ТекстЗапросаПоОснованию = ТекстЗапросаПоИсходнымДаннымАктВыполненныхРабот();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
		ТекстЗапросаПоОснованию = ТекстЗапросаПоИсходнымДаннымРеализацияУслугПрочихАктивов();
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	КорректировкаРеализации.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДанныеПоследнейКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	КорректировкаРеализации.Проведен
	|	И КорректировкаРеализации.ВидКорректировки <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара)
	|	И КорректировкаРеализации.ВидКорректировки <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара)
	|	И КорректировкаРеализации.ДокументОснование = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	КорректировкаРеализации.МоментВремени УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки				   КАК НомерСтроки,
	|	ТаблицаТовары.НоменклатураНабора		   КАК НоменклатураНабора,
	|	ТаблицаТовары.ХарактеристикаНабора		   КАК ХарактеристикаНабора,
	|	ТаблицаТовары.Номенклатура		 		   КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаТовары.Характеристика			   КАК Характеристика,
	|	ТаблицаТовары.Назначение				   КАК Назначение,
	|	ТаблицаТовары.Серия						   КАК Серия,
	|	ТаблицаТовары.СтатусУказанияСерий		   КАК СтатусУказанияСерий,
	|	ТаблицаТовары.Содержание				   КАК Содержание,
	|	ТаблицаТовары.Упаковка					   КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок		   КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество				   КАК Количество,
	|	ТаблицаТовары.Сумма						   КАК Сумма,
	|	ТаблицаТовары.СтавкаНДС					   КАК СтавкаНДС,
	|	ТаблицаТовары.СтатьяДоходов				   КАК СтатьяДоходов,
	|	ТаблицаТовары.АналитикаДоходов			   КАК АналитикаДоходов,
	|	ТаблицаТовары.СуммаНДС					   КАК СуммаНДС,
	|	ТаблицаТовары.СуммаСНДС					   КАК СуммаСНДС,
	|	ТаблицаТовары.КодСтроки					   КАК КодСтроки,
	|	ТаблицаТовары.Склад						   КАК Склад,
	|	ТаблицаТовары.ЗаказКлиента				   КАК ЗаказКлиента,
	|	ТаблицаТовары.КодТНВЭД					   КАК КодТНВЭД,
	|	ТаблицаТовары.Цена						   КАК Цена,
	|	ТаблицаТовары.ВидЦены					   КАК ВидЦены
	|
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ТаблицаТовары.Ссылка = ДанныеПоследнейКорректировки.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|" + ТекстЗапросаПоОснованию + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеПоследнейКорректировки
	|";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ТекстЗапросаПоИсходнымДаннымРеализацияТоваровУслуг()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки				   КАК НомерСтроки,
	|	ТаблицаТовары.НоменклатураНабора		   КАК НоменклатураНабора,
	|	ТаблицаТовары.ХарактеристикаНабора		   КАК ХарактеристикаНабора,
	|	ТаблицаТовары.Номенклатура				   КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаТовары.Характеристика			   КАК Характеристика,
	|	ТаблицаТовары.Назначение				   КАК Назначение,
	|	ТаблицаТовары.Серия						   КАК Серия,
	|	ТаблицаТовары.СтатусУказанияСерий		   КАК СтатусУказанияСерий,
	|	НЕОПРЕДЕЛЕНО							   КАК Содержание,
	|	ТаблицаТовары.Упаковка					   КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок		   КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество				   КАК Количество,
	|	ТаблицаТовары.Сумма						   КАК Сумма,
	|	ТаблицаТовары.СтавкаНДС					   КАК СтавкаНДС,
	|	Неопределено							   КАК СтатьяДоходов,
	|	Неопределено							   КАК АналитикаДоходов,
	|	ТаблицаТовары.СуммаНДС					   КАК СуммаНДС,
	|	ТаблицаТовары.СуммаСНДС					   КАК СуммаСНДС,
	|	ТаблицаТовары.КодСтроки					   КАК КодСтроки,
	|	ТаблицаТовары.Склад						   КАК Склад,
	|	ТаблицаТовары.ЗаказКлиента				   КАК ЗаказКлиента,
	|	ТаблицаТовары.КодТНВЭД					   КАК КодТНВЭД,
	|	
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ТаблицаТовары.Цена * ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(15,2)) = ТаблицаТовары.Сумма
	|			ТОГДА ТаблицаТовары.Цена
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТовары.Сумма / ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК Цена,
	|	
	|	ВЫБОР КОГДА ВЫРАЗИТЬ(ТаблицаТовары.Цена * ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(15,2)) = ТаблицаТовары.Сумма ТОГДА
	|		ТаблицаТовары.ВидЦены
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)
	|	КОНЕЦ КАК ВидЦены
	|
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ИСТИНА
	|ГДЕ
	|	ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
	|	И ТаблицаТовары.Ссылка = &ДокументОснование
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПоИсходнымДаннымАктВыполненныхРабот()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаУслуги.НомерСтроки				   КАК НомерСтроки,
	|	ТаблицаУслуги.НоменклатураНабора		   КАК НоменклатураНабора,
	|	ТаблицаУслуги.ХарактеристикаНабора		   КАК ХарактеристикаНабора,
	|	ТаблицаУслуги.Номенклатура	 			   КАК Номенклатура,
	|	ТаблицаУслуги.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаУслуги.Характеристика			   КАК Характеристика,
	|	ЕСТЬNULL(ТоварыЗаказа.Ссылка.Назначение, Неопределено)	КАК Назначение,
	|	Неопределено							   КАК Серия,
	|	Неопределено							   КАК СтатусУказанияСерий,
	|	ТаблицаУслуги.Содержание				   КАК Содержание,
	|	Неопределено							   КАК Упаковка,
	|	ТаблицаУслуги.Количество				   КАК КоличествоУпаковок,
	|	ТаблицаУслуги.Количество				   КАК Количество,
	|	ТаблицаУслуги.Сумма						   КАК Сумма,
	|	ТаблицаУслуги.СтавкаНДС					   КАК СтавкаНДС,
	|	Неопределено							   КАК СтатьяДоходов,
	|	Неопределено							   КАК АналитикаДоходов,
	|	ТаблицаУслуги.СуммаНДС					   КАК СуммаНДС,
	|	ТаблицаУслуги.СуммаСНДС					   КАК СуммаСНДС,
	|	ТаблицаУслуги.КодСтроки					   КАК КодСтроки,
	|	Неопределено							   КАК Склад,
	|	ТаблицаУслуги.ЗаказКлиента				   КАК ЗаказКлиента,
	|	НЕОПРЕДЕЛЕНО							   КАК КодТНВЭД,
	|	
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Цена * ТаблицаУслуги.Количество КАК ЧИСЛО(15,2)) = ТаблицаУслуги.Сумма
	|			ТОГДА ТаблицаУслуги.Цена
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаУслуги.Сумма / ТаблицаУслуги.Количество КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК Цена,
	|	
	|	ВЫБОР КОГДА ВЫРАЗИТЬ(ТаблицаУслуги.Цена * ТаблицаУслуги.Количество КАК ЧИСЛО(15,2)) = ТаблицаУслуги.Сумма ТОГДА
	|		ТаблицаУслуги.ВидЦены
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)
	|	КОНЕЦ КАК ВидЦены
	|
	|ИЗ
	|	Документ.АктВыполненныхРабот.Услуги КАК ТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ИСТИНА
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ЗаказКлиента.Товары КАК ТоварыЗаказа
	|	ПО
	|		ТаблицаУслуги.ЗаказКлиента = ТоварыЗаказа.Ссылка
	|		И ТаблицаУслуги.КодСтроки = ТоварыЗаказа.КодСтроки
	|		И ТоварыЗаказа.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно)
	|
	|ГДЕ
	|	ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
	|	И ТаблицаУслуги.Ссылка = &ДокументОснование
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПоИсходнымДаннымРеализацияУслугПрочихАктивов()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаДоходы.НомерСтроки					 КАК НомерСтроки,
	|	Неопределено								 КАК НоменклатураНабора,
	|	Неопределено								 КАК ХарактеристикаНабора,
	|	Неопределено								 КАК Номенклатура,
	|	Неопределено								 КАК ТипНоменклатуры,
	|	Неопределено								 КАК Характеристика,
	|	Неопределено								 КАК Назначение,
	|	Неопределено								 КАК Серия,
	|	Неопределено								 КАК СтатусУказанияСерий,
	|	ТаблицаДоходы.Содержание					 КАК Содержание,
	|	Неопределено								 КАК Упаковка,
	|	Неопределено								 КАК КоличествоУпаковок,
	|	ТаблицаДоходы.Количество					 КАК Количество,
	|	ТаблицаДоходы.Сумма							 КАК Сумма,
	|	ТаблицаДоходы.СтавкаНДС						 КАК СтавкаНДС,
	|	ТаблицаДоходы.СтатьяДоходов					КАК СтатьяДоходов,
	|	ТаблицаДоходы.АналитикаДоходов				КАК АналитикаДоходов,
	|	ТаблицаДоходы.СуммаНДС						 КАК СуммаНДС,
	|	ТаблицаДоходы.СуммаСНДС						 КАК СуммаСНДС,
	|	Неопределено								 КАК КодСтроки,
	|	Неопределено				 				 КАК Склад,
	|	ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка) КАК ЗаказКлиента,
	|	НЕОПРЕДЕЛЕНО								 КАК КодТНВЭД,
	|
	|	ВЫРАЗИТЬ(ТаблицаДоходы.Сумма / ТаблицаДоходы.Количество КАК ЧИСЛО(15,2)) КАК Цена,
	|
	|	Неопределено								 КАК ВидЦены
	|
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТаблицаДоходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ИСТИНА
	|ГДЕ
	|	ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
	|	И ТаблицаДоходы.Ссылка = &ДокументОснование
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Формирует временную таблицу, содержащую табличную часть по таблице данных документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаДанныхДокументов с полями:
//		Ссылка,
//		Валюта.
//
//	ПараметрыЗаполнения - Структура - структура, возвращаемая функцией ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров
//
Процедура ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения = Неопределено) Экспорт
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл",       ПараметрыЗаполнения.ПересчитыватьВВалютуРегл);
	Запрос.УстановитьПараметр("ВключаяНомераГТД",               ПараметрыЗаполнения.ВключаяНомераГТД);
	Запрос.УстановитьПараметр("ПустаяГТД",                      Справочники.НомераГТД.ПустаяСсылка());
	
	// Актуализировать расчеты для получения сумм по товарам документа-основания (например, реализации товаров и услуг)
	Если ПараметрыЗаполнения.ПересчитыватьВВалютуРегл И ПараметрыЗаполнения.АктуализироватьРасчеты Тогда
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ТаблицаДанныхДокументов КАК ДанныеДокументов
		|	ПО
		|		РасчетыСКлиентами.Регистратор = ДанныеДокументов.Ссылка
		|
		|ГДЕ
		|	ДанныеДокументов.Валюта <> &ВалютаРегламентированногоУчета
		|	И РасчетыСКлиентами.Активность
		|";
		ТаблицаАналитик = Запрос.Выполнить().Выгрузить();
		МассивАналитикУчетаПоПартнерам = ТаблицаАналитик.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
		
		Если МассивАналитикУчетаПоПартнерам.Количество() > 0 Тогда 
			ОкончаниеПериодаРасчета = КонецМесяца(ВзаиморасчетыСервер.ПолучитьМаксимальнуюДатуВКоллекцииДокументов(МенеджерВременныхТаблиц)) + 1;
			АналитикиРасчета = РаспределениеВзаиморасчетовВызовСервера.АналитикиРасчета();
			АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикУчетаПоПартнерам;
			Попытка
				РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСКлиентами(ОкончаниеПериодаРасчета, АналитикиРасчета);
			Исключение
				ТекстСообщения = НСтр("ru ='Печатная форма сформирована по неактуальным данным.
				|Необходимо актуализировать взаиморасчеты вручную и переформировать печатную форму.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПараметрыВтВидыЗапасов(
		Запрос,
		ПараметрыЗаполнения.ПересчитыватьВВалютуРегл,
		ПараметрыЗаполнения.ВключаяДоКорректировки);
	
	Запрос.Текст = ТекстЗапросаВтВидыЗапасов() + "
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Основной,
	|	СвязанныеДокументы.Подчиненный КАК Предыдущий
	|ИЗ
	|	ВтСвязанныеДокументы КАК СвязанныеДокументы
	|
	|ГДЕ
	|	СвязанныеДокументы.Подчиненный <> СвязанныеДокументы.Основной
	|
	|УПОРЯДОЧИТЬ ПО
	|	СвязанныеДокументы.МоментВремени УБЫВ
	|
	|ИТОГИ ПО
	|	СвязанныеДокументы.Основной
	|";
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаПредыдущихДокументов = Новый ТаблицаЗначений;
	ТаблицаПредыдущихДокументов.Колонки.Добавить("Основной",   Новый ОписаниеТипов("ДокументСсылка.КорректировкаРеализации"));
	ТаблицаПредыдущихДокументов.Колонки.Добавить("Предыдущий", Новый ОписаниеТипов("
		|ДокументСсылка.КорректировкаРеализации,
		|ДокументСсылка.РеализацияТоваровУслуг,
		|ДокументСсылка.АктВыполненныхРабот,
		|ДокументСсылка.РеализацияУслугПрочихАктивов"));
	
	ВыборкаПоСсылкам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоСсылкам.Следующий() Цикл
		Выборка = ВыборкаПоСсылкам.Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаПредыдущихДокументов.Добавить(), Выборка);
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ПредыдущиеДокументы", ТаблицаПредыдущихДокументов);
	
	Запрос.Текст = "
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредыдущиеДокументы.Основной   КАК Основной,
	|	ПредыдущиеДокументы.Предыдущий КАК Предыдущий
	|
	|ПОМЕСТИТЬ ПредыдущиеДокументы
	|ИЗ
	|	&ПредыдущиеДокументы КАК ПредыдущиеДокументы
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка                КАК Ссылка,
	|	ТаблицаТоваров.Содержание            КАК Содержание,
	|	ТаблицаТоваров.Номенклатура          КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика        КАК Характеристика,
	|	ТаблицаТоваров.Упаковка              КАК Упаковка,
	|	МАКСИМУМ(ТаблицаТоваров.НомерСтроки) КАК НомерСтроки
	|
	|ПОМЕСТИТЬ СтрокиТоваров
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТаблицаТоваров
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаТоваров.Ссылка = ДанныеДокументов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Содержание,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Упаковка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Упаковка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	"""" КАК Содержание,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ КАК ДоКорректировки
	|ПОМЕСТИТЬ ВсеУслуги
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	ИЛИ Товары.Номенклатура.ТипНоменклатуры ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Товары.Содержание КАК Содержание,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ КАК ДоКорректировки
	|ИЗ
	|	Документ.АктВыполненныхРабот.Услуги КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Товары.Содержание КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ КАК ДоКорректировки
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|//предыдущие корректировки реализации
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Товары.Содержание КАК Содержание,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	ВЫБОР КОГДА Товары.Количество < 0 И ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС) > 0
	|		ТОГДА -ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС)
	|		ИНАЧЕ ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС)
	|	КОНЕЦ КАК СуммаБезНДС,
	|	
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ВЫБОР КОГДА Товары.Количество < 0 И ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС) > 0
	|		ТОГДА -ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС)
	|		ИНАЧЕ ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС)
	|	КОНЕЦ КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ КАК ДоКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Подчиненный
	|			И СвязанныеДокументы.Основной <> СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	ИЛИ Товары.Номенклатура.ТипНоменклатуры ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|//текущая корректировка реализации
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Товары.Содержание КАК Содержание,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	ВЫБОР КОГДА Товары.Количество < 0 И ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС) > 0
	|		ТОГДА -ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС)
	|		ИНАЧЕ ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС)
	|	КОНЕЦ КАК СуммаБезНДС,
	|	
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ВЫБОР КОГДА Товары.Количество < 0 И ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС) > 0
	|		ТОГДА -ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС)
	|		ИНАЧЕ ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС)
	|	КОНЕЦ КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ КАК ДоКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Основной
	|			И СвязанныеДокументы.Основной = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	ИЛИ Товары.Номенклатура.ТипНоменклатуры ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Исходная продажа по документу реализации.
	|// Выводится если &ВключаяДоКорректировки
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	"""" КАК Содержание,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Товары.Количество КАК Количество,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Товары.СуммаСНДС - Товары.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Товары.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ИСТИНА КАК ДоКорректировки
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Товары.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО Товары.Ссылка = СуммыРегл.Регистратор
	|			И Товары.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|	И (Товары.Номенклатура.ТипНоменклатуры НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	) ИЛИ Товары.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Исходная продажа по акту выполненных работ.
	|// Выводится если &ВключаяДоКорректировки
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Услуги.Содержание КАК Содержание,
	|	Услуги.Номенклатура КАК Номенклатура,
	|	Услуги.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Услуги.Количество КАК Количество,
	|	Услуги.Количество КАК КоличествоУпаковок,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Услуги.СуммаСНДС - Услуги.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Услуги.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Услуги.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ИСТИНА КАК ДоКорректировки
	|ИЗ
	|	Документ.АктВыполненныхРабот.Услуги КАК Услуги
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Услуги.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО Услуги.Ссылка = СуммыРегл.Регистратор
	|			И Услуги.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|	И (Услуги.Номенклатура.ТипНоменклатуры НЕ В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	) ИЛИ Услуги.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Исходная продажа по реализации услуг и прочих активов.
	|// Выводится если &ВключаяДоКорректировки
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	Доходы.Содержание КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	Доходы.Количество КАК Количество,
	|	Доходы.Количество КАК КоличествоУпаковок,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, Доходы.СуммаСНДС - Доходы.СуммаНДС) КАК СуммаБезНДС,
	|	
	|	Доходы.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, Доходы.СуммаНДС) КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ИСТИНА КАК ДоКорректировки
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК Доходы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязанныеДокументы КАК СвязанныеДокументы
	|		ПО Доходы.Ссылка = СвязанныеДокументы.Подчиненный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыРегл
	|		ПО Доходы.Ссылка = СуммыРегл.Регистратор
	|			И Доходы.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|			И СуммыРегл.Активность
	|			И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                 КАК Ссылка,
	|	ТаблицаДокумента.ВернутьМногооборотнуюТару              КАК ВернутьМногооборотнуюТару,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999)              КАК НомерСтроки,
	|	""""                                                    КАК Содержание,
	|	Аналитика.Номенклатура                                  КАК Номенклатура,
	|	Аналитика.Характеристика                                КАК Характеристика,
	|	ТаблицаДокумента.Упаковка                               КАК Упаковка,
	|	
	|	ВЫБОР КОГДА &ВключаяНомераГТД И НЕ СчетаФактуры.Корректировочный ТОГДА
	|		ТаблицаДокумента.НомерГТД
	|	ИНАЧЕ
	|		&ПустаяГТД
	|	КОНЕЦ КАК НомерГТД,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.Количество) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.Количество)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.Количество)
	|	КОНЕЦ КАК Количество,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.КоличествоУпаковок) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.КоличествоУпаковок)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.КоличествоУпаковок)
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.СуммаБезНДС) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.СуммаБезНДС)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.СуммаБезНДС)
	|	КОНЕЦ КАК СуммаБезНДС,
	|	
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.КодТНВЭД КАК КодТНВЭД,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.СуммаНДС) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.СуммаНДС)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.СуммаНДС)
	|	КОНЕЦ КАК СуммаНДС,
	|	
	|	ИСТИНА КАК ЭтоТовар,
	|	ВЫБОР
	|		КОГДА
	|			Аналитика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			И ТаблицаДокумента.ВернутьМногооборотнуюТару
	|		ТОГДА
	|			ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ КАК ЭтоНеВозвратнаяТара,
	|	ТаблицаДокумента.ДоКорректировки КАК ДоКорректировки
	|
	|ПОМЕСТИТЬ ТаблицаТоваровКорректировкаРеализации
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ТаблицаДокумента.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СтрокиТоваров КАК СтрокиТоваров
	|	ПО
	|		ТаблицаДокумента.Ссылка           = СтрокиТоваров.Ссылка
	|		И Аналитика.Номенклатура          = СтрокиТоваров.Номенклатура
	|		И СтрокиТоваров.Содержание        = """"
	|		И Аналитика.Характеристика        = СтрокиТоваров.Характеристика
	|		И ТаблицаДокумента.Упаковка       = СтрокиТоваров.Упаковка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СчетаФактурыОснования КАК СчетаФактуры
	|	ПО
	|		ТаблицаДокумента.Ссылка = СчетаФактуры.ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.ВернутьМногооборотнуюТару,
	|	СтрокиТоваров.НомерСтроки,
	|	Аналитика.Номенклатура,
	|	Аналитика.Номенклатура.ТипНоменклатуры,
	|	Аналитика.Характеристика,
	|	ТаблицаДокумента.Упаковка,
	|	
	|	ВЫБОР КОГДА &ВключаяНомераГТД И НЕ СчетаФактуры.Корректировочный ТОГДА
	|		ТаблицаДокумента.НомерГТД
	|	ИНАЧЕ
	|		&ПустаяГТД
	|	КОНЕЦ,
	|	
	|	ТаблицаДокумента.СтавкаНДС,
	|	ТаблицаДокумента.КодТНВЭД,
	|	ТаблицаДокумента.ДоКорректировки,
	|	ДанныеДокументов.ХозяйственнаяОперация
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаДокумента.Количество) <> 0
	|	И (СУММА(ТаблицаДокумента.Количество) > 0 
	|		ИЛИ ДанныеДокументов.ХозяйственнаяОперация В(
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара))
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	| 
	|ВЫБРАТЬ
	|	Услуги.Ссылка КАК Ссылка,
	|	Услуги.ВернутьМногооборотнуюТару КАК ВернутьМногооборотнуюТару,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999) КАК НомерСтроки,
	|	Услуги.Содержание КАК Содержание,
	|	Услуги.Номенклатура КАК Номенклатура,
	|	Услуги.Характеристика КАК Характеристика,
	|	Услуги.Упаковка КАК Упаковка,
	|	Услуги.НомерГТД КАК НомерГТД,
	|	
	|	ВЫБОР КОГДА СУММА(Услуги.Количество) > 0 ТОГДА
	|		СУММА(Услуги.Количество)
	|	ИНАЧЕ
	|		-СУММА(Услуги.Количество)
	|	КОНЕЦ КАК Количество,
	|	
	|	ВЫБОР КОГДА СУММА(Услуги.КоличествоУпаковок) > 0 ТОГДА
	|		СУММА(Услуги.КоличествоУпаковок)
	|	ИНАЧЕ
	|		-СУММА(Услуги.КоличествоУпаковок)
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	
	|	ВЫБОР КОГДА СУММА(Услуги.СуммаБезНДС) > 0 ТОГДА
	|		СУММА(Услуги.СуммаБезНДС)
	|	ИНАЧЕ
	|		-СУММА(Услуги.СуммаБезНДС)
	|	КОНЕЦ КАК СуммаБезНДС,
	|
	|	Услуги.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка) КАК КодТНВЭД,
	|	
	|	ВЫБОР КОГДА СУММА(Услуги.СуммаНДС) > 0 ТОГДА
	|		СУММА(Услуги.СуммаНДС)
	|	ИНАЧЕ
	|		-СУММА(Услуги.СуммаНДС)
	|	КОНЕЦ КАК СуммаНДС,
	|
	|	Услуги.ЭтоТовар КАК ЭтоТовар,
	|	Услуги.ЭтоНеВозвратнаяТара КАК ЭтоНеВозвратнаяТара,
	|	Услуги.ДоКорректировки КАК ДоКорректировки
	|ИЗ 
	|	ВсеУслуги КАК Услуги
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СтрокиТоваров КАК СтрокиТоваров
	|	ПО
	|		Услуги.Ссылка           = СтрокиТоваров.Ссылка
	|		И Услуги.Номенклатура   = СтрокиТоваров.Номенклатура
	|		И Услуги.Содержание     = СтрокиТоваров.Содержание
	|		И Услуги.Характеристика = СтрокиТоваров.Характеристика
	|		И Услуги.Упаковка       = СтрокиТоваров.Упаковка
	|СГРУППИРОВАТЬ ПО
	|	Услуги.Ссылка,
	|	Услуги.ВернутьМногооборотнуюТару,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	|	Услуги.Содержание,
	|	Услуги.Номенклатура,
	|	Услуги.Характеристика,
	|	Услуги.Упаковка,
	|	Услуги.НомерГТД,
	|	Услуги.СтавкаНДС,
	|	Услуги.ЭтоТовар,
	|	Услуги.ЭтоНеВозвратнаяТара,
	|	Услуги.ДоКорректировки
	|ИМЕЮЩИЕ
	|	СУММА(Услуги.Количество) <> 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВтСвязанныеДокументы
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПредыдущиеДокументы
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СтрокиТоваров
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВтВидыЗапасов
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Формирует временную таблицу для печати Соглашения об изменении стоимости, содержащую табличную часть по таблице данных документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаДанныхДокументов с полями:
//		Ссылка,
//		Валюта.
//
//	ПараметрыЗаполнения - Структура - структура, возвращаемая функцией ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров
// 
Процедура ПоместитьВременнуюТаблицуТоваровДляСоглашенийОбИзмененииСтоимости(МенеджерВременныхТаблиц, ПараметрыЗаполнения = Неопределено)
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл",       ПараметрыЗаполнения.ПересчитыватьВВалютуРегл);
	Запрос.УстановитьПараметр("ВключаяНомераГТД",               ПараметрыЗаполнения.ВключаяНомераГТД);
	Запрос.УстановитьПараметр("ПустаяГТД",                      Справочники.НомераГТД.ПустаяСсылка());
	
	// Актуализировать расчеты для получения сумм по товарам документа-основания (например, реализации товаров и услуг)
	Если ПараметрыЗаполнения.ПересчитыватьВВалютуРегл И ПараметрыЗаполнения.АктуализироватьРасчеты Тогда
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ТаблицаДанныхДокументов КАК ДанныеДокументов
		|	ПО
		|		РасчетыСКлиентами.Регистратор = ДанныеДокументов.Ссылка
		|
		|ГДЕ
		|	ДанныеДокументов.Валюта <> &ВалютаРегламентированногоУчета
		|	И РасчетыСКлиентами.Активность
		|";
		ТаблицаАналитик = Запрос.Выполнить().Выгрузить();
		МассивАналитикУчетаПоПартнерам = ТаблицаАналитик.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
		
		Если МассивАналитикУчетаПоПартнерам.Количество() > 0 Тогда 
			ОкончаниеПериодаРасчета = КонецМесяца(ВзаиморасчетыСервер.ПолучитьМаксимальнуюДатуВКоллекцииДокументов(МенеджерВременныхТаблиц)) + 1;
			АналитикиРасчета = РаспределениеВзаиморасчетовВызовСервера.АналитикиРасчета();
			АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикУчетаПоПартнерам;
			Попытка
				РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСКлиентами(ОкончаниеПериодаРасчета, АналитикиРасчета);
			Исключение
				ТекстСообщения = НСтр("ru ='Печатная форма сформирована по неактуальным данным.
				|Необходимо актуализировать взаиморасчеты вручную и переформировать печатную форму.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПараметрыВтВидыЗапасов(
		Запрос,
		ПараметрыЗаполнения.ПересчитыватьВВалютуРегл,
		ПараметрыЗаполнения.ВключаяДоКорректировки);
	
	Запрос.Текст = ТекстЗапросаВтВидыЗапасов() + "
	|ВЫБРАТЬ
	|	СвязанныеДокументы.Основной КАК Основной,
	|	СвязанныеДокументы.Подчиненный КАК Предыдущий
	|ИЗ
	|	ВтСвязанныеДокументы КАК СвязанныеДокументы
	|
	|ГДЕ
	|	СвязанныеДокументы.Подчиненный <> СвязанныеДокументы.Основной
	|
	|УПОРЯДОЧИТЬ ПО
	|	СвязанныеДокументы.МоментВремени УБЫВ
	|
	|ИТОГИ ПО
	|	СвязанныеДокументы.Основной
	|";
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаПредыдущихДокументов = Новый ТаблицаЗначений;
	ТаблицаПредыдущихДокументов.Колонки.Добавить("Основной",   Новый ОписаниеТипов("ДокументСсылка.КорректировкаРеализации"));
	ТаблицаПредыдущихДокументов.Колонки.Добавить("Предыдущий", Новый ОписаниеТипов("
		|ДокументСсылка.КорректировкаРеализации,
		|ДокументСсылка.РеализацияТоваровУслуг,
		|ДокументСсылка.АктВыполненныхРабот,
		|ДокументСсылка.РеализацияУслугПрочихАктивов"));
	
	ВыборкаПоСсылкам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоСсылкам.Следующий() Цикл
		Выборка = ВыборкаПоСсылкам.Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаПредыдущихДокументов.Добавить(), Выборка);
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ПредыдущиеДокументы", ТаблицаПредыдущихДокументов);
	
	Запрос.Текст = "
	|////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредыдущиеДокументы.Основной   КАК Основной,
	|	ПредыдущиеДокументы.Предыдущий КАК Предыдущий
	|
	|ПОМЕСТИТЬ ПредыдущиеДокументы
	|ИЗ
	|	&ПредыдущиеДокументы КАК ПредыдущиеДокументы
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка                КАК Ссылка,
	|	ТаблицаТоваров.Содержание            КАК Содержание,
	|	ТаблицаТоваров.Номенклатура          КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика        КАК Характеристика,
	|	ТаблицаТоваров.Упаковка              КАК Упаковка,
	|	МАКСИМУМ(ТаблицаТоваров.НомерСтроки) КАК НомерСтроки
	|
	|ПОМЕСТИТЬ СтрокиТоваров
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТаблицаТоваров
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаТоваров.Ссылка = ДанныеДокументов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Содержание,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Упаковка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Упаковка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                 КАК Ссылка,
	|	ТаблицаДокумента.ВернутьМногооборотнуюТару              КАК ВернутьМногооборотнуюТару,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999)              КАК НомерСтроки,
	|	""""                                                    КАК Содержание,
	|	Аналитика.Номенклатура                                  КАК Номенклатура,
	|	Аналитика.Характеристика                                КАК Характеристика,
	|	ТаблицаДокумента.Упаковка                               КАК Упаковка,
	|	
	|	ВЫБОР КОГДА &ВключаяНомераГТД И НЕ СчетаФактуры.Корректировочный ТОГДА
	|		ТаблицаДокумента.НомерГТД
	|	ИНАЧЕ
	|		&ПустаяГТД
	|	КОНЕЦ КАК НомерГТД,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.Количество) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.Количество)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.Количество)
	|	КОНЕЦ КАК Количество,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.КоличествоУпаковок) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.КоличествоУпаковок)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.КоличествоУпаковок)
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.СуммаБезНДС) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.СуммаБезНДС)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.СуммаБезНДС)
	|	КОНЕЦ КАК СуммаБезНДС,
	|	
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.СуммаНДС) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.СуммаНДС)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.СуммаНДС)
	|	КОНЕЦ КАК СуммаНДС,
	|	
	|	ИСТИНА КАК ЭтоТовар,
	|	ВЫБОР
	|		КОГДА
	|			Аналитика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			И ТаблицаДокумента.ВернутьМногооборотнуюТару
	|		ТОГДА
	|			ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ КАК ЭтоНеВозвратнаяТара,
	|	ТаблицаДокумента.ДоКорректировки КАК ДоКорректировки
	|
	|ПОМЕСТИТЬ ТаблицаТоваровКорректировкаРеализации
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО
	|		ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ТаблицаДокумента.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СтрокиТоваров КАК СтрокиТоваров
	|	ПО
	|		ТаблицаДокумента.Ссылка           = СтрокиТоваров.Ссылка
	|		И Аналитика.Номенклатура          = СтрокиТоваров.Номенклатура
	|		И СтрокиТоваров.Содержание        = """"
	|		И Аналитика.Характеристика        = СтрокиТоваров.Характеристика
	|		И ТаблицаДокумента.Упаковка       = СтрокиТоваров.Упаковка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СчетаФактурыОснования КАК СчетаФактуры
	|	ПО
	|		ТаблицаДокумента.Ссылка = СчетаФактуры.ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.ВернутьМногооборотнуюТару,
	|	СтрокиТоваров.НомерСтроки,
	|	Аналитика.Номенклатура,
	|	Аналитика.Номенклатура.ТипНоменклатуры,
	|	Аналитика.Характеристика,
	|	ТаблицаДокумента.Упаковка,
	|	
	|	ВЫБОР КОГДА &ВключаяНомераГТД И НЕ СчетаФактуры.Корректировочный ТОГДА
	|		ТаблицаДокумента.НомерГТД
	|	ИНАЧЕ
	|		&ПустаяГТД
	|	КОНЕЦ,
	|	
	|	ТаблицаДокумента.СтавкаНДС,
	|	ТаблицаДокумента.ДоКорректировки,
	|	ДанныеДокументов.ХозяйственнаяОперация
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаДокумента.Количество) <> 0
	|	И (СУММА(ТаблицаДокумента.Количество) > 0 
	|		ИЛИ ДанныеДокументов.ХозяйственнаяОперация В(
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара))
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПредыдущиеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999)             КАК НомерСтроки,
	|	ТаблицаДокумента.Содержание КАК Содержание,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД                                             КАК НомерГТД,
	|	СУММА(ТаблицаДокумента.Количество) КАК Количество,
	|	СУММА(ТаблицаДокумента.Количество) КАК КоличествоУпаковок,
	|	СУММА(ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ТаблицаДокумента.СуммаНДС) КАК СуммаНДС,
	|	ЛОЖЬ                                                  КАК ЭтоТовар,
	|	ЛОЖЬ                                                  КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ                        КАК ДоКорректировки
	|
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТаблицаДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПредыдущиеДокументы КАК ПредыдущиеДокументы
	|	ПО
	|		ТаблицаДокумента.Ссылка = ПредыдущиеДокументы.Основной
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаРеализации КАК ДанныеДокументов
	|	ПО
	|		ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СтрокиТоваров КАК СтрокиТоваров
	|	ПО
	|		ТаблицаДокумента.Ссылка           = СтрокиТоваров.Ссылка
	|		И ТаблицаДокумента.Номенклатура   = СтрокиТоваров.Номенклатура
	|		И ТаблицаДокумента.Содержание     = СтрокиТоваров.Содержание
	|		И ТаблицаДокумента.Характеристика = СтрокиТоваров.Характеристика
	|		И ТаблицаДокумента.Упаковка       = СтрокиТоваров.Упаковка
	|
	|ГДЕ
	|	ДанныеДокументов.ВидКорректировки <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара)
	|	И ДанныеДокументов.ВидКорректировки <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара)
	|	И (ТаблицаДокумента.Номенклатура.ТипНоменклатуры НЕ В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредыдущиеДокументы.Основной,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	|	ТаблицаДокумента.Содержание,
	|	ТаблицаДокумента.Номенклатура,
	|	ТаблицаДокумента.Характеристика,
	|	ТаблицаДокумента.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999) КАК НомерСтроки,
	|	ТаблицаДокумента.Содержание КАК Содержание,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД КАК НомерГТД,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.Количество) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.Количество)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.Количество)
	|	КОНЕЦ КАК Количество,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.Количество) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.Количество)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.Количество)
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС)
	|	ИНАЧЕ
	|		-(СУММА(ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС))
	|	КОНЕЦ КАК СуммаБезНДС,
	|	
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	
	|	ВЫБОР КОГДА СУММА(ТаблицаДокумента.СуммаНДС) > 0 ТОГДА
	|		СУММА(ТаблицаДокумента.СуммаНДС)
	|	ИНАЧЕ
	|		-СУММА(ТаблицаДокумента.СуммаНДС)
	|	КОНЕЦ КАК СуммаНДС,
	|	
	|	ЛОЖЬ КАК ЭтоТовар,
	|	ЛОЖЬ КАК ЭтоНеВозвратнаяТара,
	|	ЛОЖЬ КАК ДоКорректировки
	|
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК ТаблицаДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаРеализации КАК ДанныеДокументов
	|	ПО
	|		ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СтрокиТоваров КАК СтрокиТоваров
	|	ПО
	|		ТаблицаДокумента.Ссылка           = СтрокиТоваров.Ссылка
	|		И ТаблицаДокумента.Номенклатура   = СтрокиТоваров.Номенклатура
	|		И ТаблицаДокумента.Содержание     = СтрокиТоваров.Содержание
	|		И ТаблицаДокумента.Характеристика = СтрокиТоваров.Характеристика
	|		И ТаблицаДокумента.Упаковка       = СтрокиТоваров.Упаковка
	|
	|ГДЕ
	|	ДанныеДокументов.ВидКорректировки В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара))
	|	И (ТаблицаДокумента.Номенклатура.ТипНоменклатуры НЕ В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокументов.Ссылка,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	|	ТаблицаДокумента.Содержание,
	|	ТаблицаДокумента.Номенклатура,
	|	ТаблицаДокумента.Характеристика,
	|	ТаблицаДокумента.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПредыдущиеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999)              КАК НомерСтроки,
	|	ТаблицаДокумента.Содержание КАК Содержание,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД                                             КАК НомерГТД,
	|	СУММА(ТаблицаДокумента.Количество) КАК Количество,
	|	СУММА(ТаблицаДокумента.Количество) КАК КоличествоУпаковок,
	|	СУММА(ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ТаблицаДокумента.СуммаНДС) КАК СуммаНДС,
	|	ЛОЖЬ                                                  КАК ЭтоТовар,
	|	ЛОЖЬ                                                  КАК ЭтоНеВозвратнаяТара,
	|	ИСТИНА                        КАК ДоКорректировки
	|
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТаблицаДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПредыдущиеДокументы КАК ПредыдущиеДокументы
	|	ПО
	|		ТаблицаДокумента.Ссылка = ПредыдущиеДокументы.Предыдущий
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СтрокиТоваров КАК СтрокиТоваров
	|	ПО
	|		ТаблицаДокумента.Ссылка           = СтрокиТоваров.Ссылка
	|		И ТаблицаДокумента.Номенклатура   = СтрокиТоваров.Номенклатура
	|		И ТаблицаДокумента.Содержание     = СтрокиТоваров.Содержание
	|		И ТаблицаДокумента.Характеристика = СтрокиТоваров.Характеристика
	|		И ТаблицаДокумента.Упаковка       = СтрокиТоваров.Упаковка
	|
	|ГДЕ
	|	&ВключаяДоКорректировки
	|	И (ТаблицаДокумента.Номенклатура.ТипНоменклатуры НЕ В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредыдущиеДокументы.Основной,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	|	ТаблицаДокумента.Содержание,
	|	ТаблицаДокумента.Номенклатура,
	|	ТаблицаДокумента.Характеристика,
	|	ТаблицаДокумента.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПредыдущиеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999)              КАК НомерСтроки,
	|	"""" КАК Содержание,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД                                             КАК НомерГТД,
	|	СУММА(ТаблицаДокумента.Количество) КАК Количество,
	|	СУММА(ТаблицаДокумента.Количество) КАК КоличествоУпаковок,
	|	СУММА(ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ТаблицаДокумента.СуммаНДС) КАК СуммаНДС,
	|	ЛОЖЬ                                                  КАК ЭтоТовар,
	|	ЛОЖЬ                                                  КАК ЭтоНеВозвратнаяТара,
	|	ИСТИНА                        КАК ДоКорректировки
	|
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПредыдущиеДокументы КАК ПредыдущиеДокументы
	|	ПО
	|		ТаблицаДокумента.Ссылка = ПредыдущиеДокументы.Предыдущий
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СтрокиТоваров КАК СтрокиТоваров
	|	ПО
	|		ТаблицаДокумента.Ссылка           = СтрокиТоваров.Ссылка
	|		И ТаблицаДокумента.Номенклатура   = СтрокиТоваров.Номенклатура
	|		И СтрокиТоваров.Содержание = """"
	|		И ТаблицаДокумента.Характеристика = СтрокиТоваров.Характеристика
	|		И ТаблицаДокумента.Упаковка       = СтрокиТоваров.Упаковка
	|
	|ГДЕ
	|	&ВключаяДоКорректировки
	|	И ТаблицаДокумента.Номенклатура.ТипНоменклатуры НЕ В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредыдущиеДокументы.Основной,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	|	ТаблицаДокумента.Номенклатура,
	|	ТаблицаДокумента.Характеристика,
	|	ТаблицаДокумента.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПредыдущиеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999)              КАК НомерСтроки,
	|	ТаблицаДокумента.Содержание КАК Содержание,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД                                             КАК НомерГТД,
	|	СУММА(ТаблицаДокумента.Количество) КАК Количество,
	|	СУММА(ТаблицаДокумента.Количество) КАК КоличествоУпаковок,
	|	СУММА(ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ТаблицаДокумента.СуммаНДС) КАК СуммаНДС,
	|	ЛОЖЬ                                                  КАК ЭтоТовар,
	|	ЛОЖЬ                                                  КАК ЭтоНеВозвратнаяТара,
	|	ИСТИНА                        КАК ДоКорректировки
	|
	|ИЗ
	|	Документ.АктВыполненныхРабот.Услуги КАК ТаблицаДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПредыдущиеДокументы КАК ПредыдущиеДокументы
	|	ПО
	|		ТаблицаДокумента.Ссылка = ПредыдущиеДокументы.Предыдущий
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СтрокиТоваров КАК СтрокиТоваров
	|	ПО
	|		ТаблицаДокумента.Ссылка           = СтрокиТоваров.Ссылка
	|		И ТаблицаДокумента.Номенклатура   = СтрокиТоваров.Номенклатура
	|		И ТаблицаДокумента.Содержание     = СтрокиТоваров.Содержание
	|		И ТаблицаДокумента.Характеристика = СтрокиТоваров.Характеристика
	|		И СтрокиТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|
	|ГДЕ
	|	&ВключаяДоКорректировки
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредыдущиеДокументы.Основной,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	|	ТаблицаДокумента.Содержание,
	|	ТаблицаДокумента.Номенклатура,
	|	ТаблицаДокумента.Характеристика,
	|	ТаблицаДокумента.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПредыдущиеДокументы.Основной КАК Ссылка,
	|	ЛОЖЬ КАК ВернутьМногооборотнуюТару,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999)              КАК НомерСтроки,
	|	ТаблицаДокумента.Содержание КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	&ПустаяГТД                                             КАК НомерГТД,
	|	СУММА(ТаблицаДокумента.Количество) КАК Количество,
	|	СУММА(ТаблицаДокумента.Количество) КАК КоличествоУпаковок,
	|	СУММА(ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ТаблицаДокумента.СуммаНДС) КАК СуммаНДС,
	|	ЛОЖЬ                                                  КАК ЭтоТовар,
	|	ЛОЖЬ                                                  КАК ЭтоНеВозвратнаяТара,
	|	ИСТИНА                        КАК ДоКорректировки
	|
	|ИЗ
	|	Документ.РеализацияУслугПрочихАктивов.Доходы КАК ТаблицаДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПредыдущиеДокументы КАК ПредыдущиеДокументы
	|	ПО
	|		ТаблицаДокумента.Ссылка = ПредыдущиеДокументы.Предыдущий
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СтрокиТоваров КАК СтрокиТоваров
	|	ПО
	|		ТаблицаДокумента.Ссылка           = СтрокиТоваров.Ссылка
	|		И СтрокиТоваров.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		И ТаблицаДокумента.Содержание     = СтрокиТоваров.Содержание
	|		И СтрокиТоваров.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		И СтрокиТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|
	|ГДЕ
	|	&ВключаяДоКорректировки
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредыдущиеДокументы.Основной,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	|	ТаблицаДокумента.Содержание,
	|	ТаблицаДокумента.СтавкаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВтСвязанныеДокументы
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПредыдущиеДокументы
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СтрокиТоваров
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВтВидыЗапасов
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Преобразует временную таблицу товаров, созданную функцией ПоместитьВременнуюТаблицуТоваров()
// к виду, используемому при печати счетов-фактуры.
// После преобразования временная таблица уничтожается.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаТоваровКорректировкаРеализации
//
Процедура ПреобразоватьВременнуюТаблицуТоваровДляСчетаФактуры(МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                  КАК Ссылка,
	|	ТаблицаДокумента.Номенклатура                            КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика                          КАК Характеристика,
	|	ТаблицаДокумента.Упаковка                                КАК Упаковка,
	|	ТаблицаДокумента.НомерГТД                                КАК НомерГТД,
	|	ТаблицаДокумента.Содержание                              КАК Содержание,
	|	ТаблицаДокумента.Количество                              КАК Количество,
	|	ТаблицаДокумента.КоличествоУпаковок                      КАК КоличествоУпаковок,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0)         КАК КоличествоДо,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0) КАК КоличествоУпаковокДо,
	|	ТаблицаДокумента.СтавкаНДС                               КАК СтавкаНДС,
	|	ТаблицаДокумента.КодТНВЭД                                КАК КодТНВЭД,
	|	ТаблицаДокумента.СуммаБезНДС                             КАК СуммаБезНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)        КАК СуммаБезНДСДо,
	|	ТаблицаДокумента.СуммаНДС                                КАК СуммаНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)           КАК СуммаНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                    КАК РазницаБезНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                    КАК РазницаБезНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ИНАЧЕ 0
	|	КОНЕЦ                                                    КАК РазницаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                    КАК РазницаНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС >=
	|				ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС -
	|				(ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                    КАК РазницаСНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС <
	|				ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС -
	|				(ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)))
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                    КАК РазницаСНДСУменьшение,
	|	ТаблицаДокумента.НомерСтроки                             КАК НомерСтроки,
	|	ТаблицаДокумента.ЭтоТовар                                КАК ЭтоТовар,
	|	ЛОЖЬ                                                     КАК ВернутьМногооборотнуюТару
	|ПОМЕСТИТЬ КорректировкаРеализацииТаблицаТоваров
	|ИЗ
	|	ТаблицаТоваровКорректировкаРеализации КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоваровКорректировкаРеализации КАК ТаблицаТоваровПредыдущие
	|		ПО (ТаблицаТоваровПредыдущие.ДоКорректировки)
	|			И ТаблицаДокумента.Ссылка = ТаблицаТоваровПредыдущие.Ссылка
	|			И ТаблицаДокумента.Номенклатура = ТаблицаТоваровПредыдущие.Номенклатура
	|			И ТаблицаДокумента.Характеристика = ТаблицаТоваровПредыдущие.Характеристика
	|			И ТаблицаДокумента.Упаковка = ТаблицаТоваровПредыдущие.Упаковка
	|			И ТаблицаДокумента.НомерГТД = ТаблицаТоваровПредыдущие.НомерГТД
	|			И ТаблицаДокумента.СтавкаНДС = ТаблицаТоваровПредыдущие.СтавкаНДС
	|			И ТаблицаДокумента.Содержание = ТаблицаТоваровПредыдущие.Содержание
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактурыОснования КАК СчетаФактуры
	|		ПО ТаблицаДокумента.Ссылка = СчетаФактуры.ДокументОснование
	|ГДЕ
	|	НЕ ТаблицаДокумента.ДоКорректировки
	|	И (НЕ СчетаФактуры.Корректировочный
	|			ИЛИ ТаблицаДокумента.СуммаНДС <> ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ИЛИ ТаблицаДокумента.СуммаБезНДС <> ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0))
	|	И (ТаблицаДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	ИЛИ (ТаблицаДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	И Не ТаблицаДокумента.ВернутьМногооборотнуюТару)
	|	ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваровПредыдущие.Ссылка                                          КАК Ссылка,
	|	ТаблицаТоваровПредыдущие.Номенклатура                                    КАК Номенклатура,
	|	ТаблицаТоваровПредыдущие.Характеристика                                  КАК Характеристика,
	|	ТаблицаТоваровПредыдущие.Упаковка                                        КАК Упаковка,
	|	ТаблицаТоваровПредыдущие.НомерГТД                                        КАК НомерГТД,
	|	ТаблицаТоваровПредыдущие.Содержание                                      КАК Содержание,
	|	0                                                                        КАК Количество,
	|	0                                                                        КАК КоличествоУпаковок,
	|	ТаблицаТоваровПредыдущие.Количество                                      КАК КоличествоДо,
	|	ТаблицаТоваровПредыдущие.КоличествоУпаковок                              КАК КоличествоУпаковокДо,
	|	ТаблицаТоваровПредыдущие.СтавкаНДС                                       КАК СтавкаНДС,
	|	ТаблицаТоваровПредыдущие.КодТНВЭД                                        КАК КодТНВЭД,
	|	0                                                                        КАК СуммаБезНДС,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС                                     КАК СуммаБезНДСДо,
	|	0                                                                        КАК СуммаНДС,
	|	ТаблицаТоваровПредыдущие.СуммаНДС                                        КАК СуммаНДСДо,
	|	0                                                                        КАК РазницаБезНДСУвеличение,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС                                     КАК РазницаБезНДСУменьшение,
	|	0                                                                        КАК РазницаНДСУвеличение,
	|	ТаблицаТоваровПредыдущие.СуммаНДС                                        КАК РазницаНДСУменьшение,
	|	0                                                                        КАК РазницаСНДСУвеличение,
	|	ТаблицаТоваровПредыдущие.СуммаБезНДС + ТаблицаТоваровПредыдущие.СуммаНДС КАК РазницаСНДСУменьшение,
	|	ТаблицаТоваровПредыдущие.НомерСтроки                                     КАК НомерСтроки,
	|	ТаблицаТоваровПредыдущие.ЭтоТовар                                        КАК ЭтоТовар,
	|	ЛОЖЬ                                                                     КАК ВернутьМногооборотнуюТару
	|ИЗ
	|	ТаблицаТоваровКорректировкаРеализации КАК ТаблицаТоваровПредыдущие
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаТоваровКорректировкаРеализации КАК ТаблицаТоваровПослеКорректировки
	|		ПО (НЕ ТаблицаТоваровПослеКорректировки.ДоКорректировки)
	|			И ТаблицаТоваровПредыдущие.Ссылка = ТаблицаТоваровПослеКорректировки.Ссылка
	|			И ТаблицаТоваровПредыдущие.Номенклатура = ТаблицаТоваровПослеКорректировки.Номенклатура
	|			И ТаблицаТоваровПредыдущие.Характеристика = ТаблицаТоваровПослеКорректировки.Характеристика
	|			И ТаблицаТоваровПредыдущие.Упаковка = ТаблицаТоваровПослеКорректировки.Упаковка
	|			И ТаблицаТоваровПредыдущие.НомерГТД = ТаблицаТоваровПослеКорректировки.НомерГТД
	|			И ТаблицаТоваровПредыдущие.СтавкаНДС = ТаблицаТоваровПослеКорректировки.СтавкаНДС
	|			И ТаблицаТоваровПредыдущие.Содержание = ТаблицаТоваровПослеКорректировки.Содержание
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаФактурыОснования КАК СчетаФактуры
	|		ПО ТаблицаТоваровПредыдущие.Ссылка = СчетаФактуры.ДокументОснование
	|ГДЕ
	|	ТаблицаТоваровПредыдущие.ДоКорректировки
	|	И ТаблицаТоваровПослеКорректировки.Ссылка ЕСТЬ NULL 
	|	И СчетаФактуры.Корректировочный
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаТоваровКорректировкаРеализации
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Возвращает текст основания по данным документа и указанному порядку расчетов
//
// Параметры:
//	Объект - ДанныеФормыСтруктура, ДокументОбъект.КорректировкаРеализации - Объект документа, по которму необходимо получить текст основания
//	ПорядокРасчетов - ПеречислениеСсылка.ПорядокРасчетов - Порядок расчетов
//
// Возвращаемое значение:
//	СтруктураОснование - Структура с наименованием, датой и номером основания
//
Функция СтруктураОснования(Объект, ПорядокРасчетов)
	
	СтруктураОснование = Новый Структура;
	СтруктураОснование.Вставить("Основание");
	СтруктураОснование.Вставить("ОснованиеНомер");
	СтруктураОснование.Вставить("ОснованиеДата");
	
	Если ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов
		И ЗначениеЗаполнено(Объект.Договор) Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ДоговорыКонтрагентов.НаименованиеДляПечати КАК Основание,
			|	ДоговорыКонтрагентов.Дата КАК ОснованиеДата,
			|	ДоговорыКонтрагентов.Номер КАК ОснованиеНомер
			|ИЗ
			|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
			|ГДЕ
			|	ДоговорыКонтрагентов.Ссылка = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка", Объект.Договор);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СтруктураОснование.Основание = СокрЛП(Выборка.Основание);
			СтруктураОснование.ОснованиеДата = Выборка.ОснованиеДата;
			СтруктураОснование.ОснованиеНомер = СокрЛП(Выборка.ОснованиеНомер);
		КонецЕсли;
		
	ИначеЕсли ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным
		И Объект.ПродажаПоЗаказам Тогда
		
		Если Объект.Товары.Количество() <> 0 Тогда
		
			Запрос = Новый Запрос(
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	ВЫБОР
				|		КОГДА
				|			ЗаказыКлиентов.НомерПоДаннымКлиента <> """" И ЗаказыКлиентов.ДатаПоДаннымКлиента <> ДАТАВРЕМЯ(1,1,1)
				|		ТОГДА
				|			ЗаказыКлиентов.НомерПоДаннымКлиента
				|		ИНАЧЕ
				|			ЗаказыКлиентов.Номер
				|	КОНЕЦ КАК Номер,
				|	ВЫБОР
				|		КОГДА
				|			ЗаказыКлиентов.НомерПоДаннымКлиента <> """" И ЗаказыКлиентов.ДатаПоДаннымКлиента <> ДАТАВРЕМЯ(1,1,1)
				|		ТОГДА
				|			ЗаказыКлиентов.ДатаПоДаннымКлиента
				|		ИНАЧЕ
				|			ЗаказыКлиентов.Дата
				|	КОНЕЦ КАК Дата,
				|	&СинонимЗаказа КАК Синоним
				|ИЗ
				|	Документ.ЗаказКлиента КАК ЗаказыКлиентов
				|ГДЕ
				|	ЗаказыКлиентов.Ссылка В(&МассивЗаказов)
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	ЗаявкиНаВозврат.Номер,
				|	ЗаявкиНаВозврат.Дата,
				|	&СинонимЗаявки
				|ИЗ
				|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК ЗаявкиНаВозврат
				|ГДЕ
				|	ЗаявкиНаВозврат.Ссылка В(&МассивЗаказов)");
			
			Если ТипЗнч(Объект) = Тип("Структура") Тогда
				МассивЗаказов = Объект.Товары.ВыгрузитьКолонку("ЗаказКлиента");
			Иначе
				МассивЗаказов = Объект.Товары.Выгрузить(,"ЗаказКлиента").ВыгрузитьКолонку("ЗаказКлиента");
			КонецЕсли;
			
			Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
			Запрос.УстановитьПараметр("СинонимЗаказа", НСтр("ru='Заказ клиента'"));
			Запрос.УстановитьПараметр("СинонимЗаявки", НСтр("ru='Заявка на замену'"));
			Выборка = Запрос.Выполнить().Выбрать();
			
			СтруктураОснование.Основание = "";
			ОдноОснование = Выборка.Количество() = 1;
			Пока Выборка.Следующий() Цикл
				СтруктураОснование.Основание = СтруктураОснование.Основание + ", " + ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(Выборка, Выборка.Синоним);
				ДатаПоЗаказам  = Выборка.Дата;
				НомерПоЗаказам = Выборка.Номер;
			КонецЦикла;
			СтруктураОснование.Основание = СокрЛП(Сред(СтруктураОснование.Основание, 3));
			СтруктураОснование.ОснованиеДата = ?(ОдноОснование, ДатаПоЗаказам, "");
			СтруктураОснование.ОснованиеНомер = ?(ОдноОснование,ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(НомерПоЗаказам),"");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураОснование; // Возврат значения по умолчанию
	
КонецФункции

#Конецобласти

#Область ОбновлениеИнформационнойБазы

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КорректировкаРеализации.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	КорректировкаРеализации.Проведен
	|	И КорректировкаРеализации.ПодразделениеРасходы = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|	И НЕ КорректировкаРеализации.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КорректировкаРеализации.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	КорректировкаРеализации.Проведен
	|	И КорректировкаРеализации.ПодразделениеДоходы = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|	И НЕ КорректировкаРеализации.СтатьяДоходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК Товары
	|ГДЕ
	|	Товары.Ссылка.Проведен
	|	И Товары.Ссылка.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|	И Товары.Ссылка.Дата >= ДАТАВРЕМЯ(2016, 7, 1)
	|	И Товары.КодТНВЭД = ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Если ОбновлениеИнформационнойБазы.ЕстьЗаблокированныеПредыдущимиОчередямиДанные(Параметры.Очередь, "Справочник.КлассификаторТНВЭД") Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	МетаданныеДокумента = Метаданные.Документы.КорректировкаРеализации;
	ПолноеИмяОбъекта = МетаданныеДокумента.ПолноеИмя();
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц);
	
	Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугТовары.Номенклатура КАК ПолеНоменклатуры,
	|	РеализацияТоваровУслугТовары.Номенклатура.УдалитьСырьевойТовар КАК СырьевойТовар,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслугТовары.Номенклатура.КодТНВЭД <> ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|				И РеализацияТоваровУслугТовары.Номенклатура.КодТНВЭД.СырьевойТовар = РеализацияТоваровУслугТовары.Номенклатура.УдалитьСырьевойТовар
	|			ТОГДА РеализацияТоваровУслугТовары.Номенклатура.КодТНВЭД
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|	КОНЕЦ КАК КодТНВЭД
	|ПОМЕСТИТЬ КодыТНВЭДНоменклатуры
	|ИЗ
	|	ВТОбъектыДляОбработки КАК ТаблицаСсылок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Товары КАК РеализацияТоваровУслугТовары
	|		ПО ТаблицаСсылок.Ссылка = РеализацияТоваровУслугТовары.Ссылка
	|ГДЕ
	|		РеализацияТоваровУслугТовары.Ссылка.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугТовары.Номенклатура КАК ПолеНоменклатуры,
	|	РеализацияТоваровУслугТовары.Номенклатура.УдалитьСырьевойТовар КАК СырьевойТовар,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслугТовары.Номенклатура.КодТНВЭД <> ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|				И РеализацияТоваровУслугТовары.Номенклатура.КодТНВЭД.СырьевойТовар = РеализацияТоваровУслугТовары.Номенклатура.УдалитьСырьевойТовар
	|			ТОГДА РеализацияТоваровУслугТовары.Номенклатура.КодТНВЭД
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|	КОНЕЦ КАК КодТНВЭД
	|ИЗ
	|	ВТОбъектыДляОбработки КАК ТаблицаСсылок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Расхождения КАК РеализацияТоваровУслугТовары
	|		ПО ТаблицаСсылок.Ссылка = РеализацияТоваровУслугТовары.Ссылка
	|ГДЕ
	|		РеализацияТоваровУслугТовары.Ссылка.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.УдалитьСырьевойТовар,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД <> ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|				И РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД.СырьевойТовар 
	|					= РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.УдалитьСырьевойТовар
	|			ТОГДА РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|	КОНЕЦ
	|ИЗ
	|	ВТОбъектыДляОбработки КАК ТаблицаСсылок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК РеализацияТоваровУслугВидыЗапасов
	|		ПО ТаблицаСсылок.Ссылка = РеализацияТоваровУслугВидыЗапасов.Ссылка
	|ГДЕ
	|		РеализацияТоваровУслугВидыЗапасов.Ссылка.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.УдалитьСырьевойТовар,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД <> ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|				И РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД.СырьевойТовар 
	|					= РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.УдалитьСырьевойТовар
	|			ТОГДА РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|	КОНЕЦ
	|ИЗ
	|	ВТОбъектыДляОбработки КАК ТаблицаСсылок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК РеализацияТоваровУслугВидыЗапасов
	|		ПО ТаблицаСсылок.Ссылка = РеализацияТоваровУслугВидыЗапасов.Ссылка
	|ГДЕ
	|		РеализацияТоваровУслугВидыЗапасов.Ссылка.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.УдалитьСырьевойТовар,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД <> ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|				И РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД.СырьевойТовар 
	|					= РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.УдалитьСырьевойТовар
	|			ТОГДА РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)
	|	КОНЕЦ
	|ИЗ
	|	ВТОбъектыДляОбработки КАК ТаблицаСсылок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК РеализацияТоваровУслугВидыЗапасов
	|		ПО ТаблицаСсылок.Ссылка = РеализацияТоваровУслугВидыЗапасов.Ссылка
	|ГДЕ
	|		РеализацияТоваровУслугВидыЗапасов.Ссылка.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбъектыДляОбработки.Ссылка						КАК Ссылка,
	|	ОбъектыДляОбработки.Ссылка.ВерсияДанных			КАК ВерсияДанных,
	|	ОбъектыДляОбработки.Ссылка.НалогообложениеНДС	КАК НалогообложениеНДС,
	|	МАКСИМУМ(ВЫБОР
	|				КОГДА Реквизиты.ПодразделениеРасходы = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|					И НЕ Реквизиты.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)				КАК ПодразделениеРасходы,
	|	МАКСИМУМ(ВЫБОР
	|				КОГДА Реквизиты.ПодразделениеДоходы = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|					И НЕ Реквизиты.СтатьяДоходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)				КАК ПодразделениеДоходы
	|ИЗ
	|	ВТОбъектыДляОбработки КАК ОбъектыДляОбработки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК Реквизиты
	|		ПО Реквизиты.Ссылка = ОбъектыДляОбработки.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбъектыДляОбработки.Ссылка,
	|	ОбъектыДляОбработки.Ссылка.НалогообложениеНДС,
	|	ОбъектыДляОбработки.Ссылка.ВерсияДанных";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТОбъектыДляОбработки", Результат.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Результат = Запрос.Выполнить();
	КодыТНВЭДНоменклатуры = МенеджерВременныхТаблиц.Таблицы["КодыТНВЭДНоменклатуры"].ПолучитьДанные().Выгрузить();
	КодыТНВЭДНоменклатуры.Индексы.Добавить("ПолеНоменклатуры");
	СоответствиеКодов = Справочники.КлассификаторТНВЭД.ПолучитьСлужебныеЭлементы();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(Выборка.Ссылка, Выборка.ВерсияДанных, Параметры.Очередь);
			Если Не ДокументОбъект = Неопределено Тогда
				
				Если Выборка.ПодразделениеРасходы Тогда
					ДокументОбъект.ПодразделениеРасходы = ДокументОбъект.Подразделение;
				КонецЕсли;
				
				Если Выборка.ПодразделениеДоходы Тогда
					ДокументОбъект.ПодразделениеДоходы = ДокументОбъект.Подразделение;
				КонецЕсли;
				
				Если Выборка.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНаЭкспорт Тогда
					Отказ = Ложь;
					Справочники.КлассификаторТНВЭД.ЗаполнитьКодыТНВЭДВКоллекции(ДокументОбъект.Товары, "Номенклатура", КодыТНВЭДНоменклатуры, СоответствиеКодов, Отказ);
					Справочники.КлассификаторТНВЭД.ЗаполнитьКодыТНВЭДВКоллекции(ДокументОбъект.Расхождения, "Номенклатура", КодыТНВЭДНоменклатуры, СоответствиеКодов, Отказ);
					Справочники.КлассификаторТНВЭД.ЗаполнитьКодыТНВЭДВКоллекции(ДокументОбъект.ВидыЗапасовКорректировкаВыручки, "АналитикаУчетаНоменклатуры", КодыТНВЭДНоменклатуры, СоответствиеКодов, Отказ);
					Справочники.КлассификаторТНВЭД.ЗаполнитьКодыТНВЭДВКоллекции(ДокументОбъект.ВидыЗапасовОприходование, "АналитикаУчетаНоменклатуры", КодыТНВЭДНоменклатуры, СоответствиеКодов, Отказ);
					Справочники.КлассификаторТНВЭД.ЗаполнитьКодыТНВЭДВКоллекции(ДокументОбъект.ВидыЗапасовСписание, "АналитикаУчетаНоменклатуры", КодыТНВЭДНоменклатуры, СоответствиеКодов, Отказ);
					Если Отказ Тогда
						ОтменитьТранзакцию();
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект, , , РежимЗаписиДокумента.Запись);
				
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru = 'Не удалось обработать документ: %Ссылка% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеДокумента,
				Выборка.Ссылка,
				ТекстСообщения);
			ВызватьИсключение;
			
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#Область УТ_11_2_2_ЗакрытьОтрицательныеОстаткиПоРасчетамСКлиентамиВКорректировках
	
Функция ДокументыДляПереносаДвиженийРасчетыСКлиентами() Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСКлиентами.ЗаказКлиента КАК ЗаказКлиента
	|ПОМЕСТИТЬ ВтЗаказы
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|ГДЕ
	|	РасчетыСКлиентами.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	|	И РасчетыСКлиентами.Активность
	|	И РасчетыСКлиентами.КОтгрузке <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыСКлиентами.ЗаказКлиента КАК ЗаказКлиента,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыСКлиентами.КОтгрузке
	|			ИНАЧЕ -РасчетыСКлиентами.КОтгрузке
	|		КОНЕЦ) КАК КОтгрузке
	|ПОМЕСТИТЬ ВТДвижения
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтЗаказы КАК ВтЗаказы
	|		ПО РасчетыСКлиентами.ЗаказКлиента = ВтЗаказы.ЗаказКлиента
	|ГДЕ
	|	НЕ РасчетыСКлиентами.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	|	И РасчетыСКлиентами.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентами.ЗаказКлиента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДвижения.ЗаказКлиента КАК ЗаказКлиента
	|ПОМЕСТИТЬ ВтКорректируемыеЗаказы
	|ИЗ
	|	ВТДвижения КАК ВТДвижения
	|ГДЕ
	|	ВТДвижения.КОтгрузке > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыСКлиентами.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтКорректируемыеЗаказы КАК ВтКорректируемыеЗаказы
	|		ПО РасчетыСКлиентами.ЗаказКлиента = ВтКорректируемыеЗаказы.ЗаказКлиента
	|ГДЕ
	|	РасчетыСКлиентами.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	|	И РасчетыСКлиентами.Активность
	|	И РасчетыСКлиентами.КОтгрузке > 0;
	|");
	
	Результат = Запрос.Выполнить().Выгрузить();
	Возврат Результат.ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозОперацияОснования                   КАК ХозяйственнаяОперация,
	|	&Партнер                                КАК Партнер,
	|	&Контрагент                             КАК Контрагент,
	|	&Договор                                КАК Договор,
	|	&НаправлениеДеятельности                КАК НаправлениеДеятельности,
	|	&Склад                                  КАК МестоХранения,
	|	&Подразделение                          КАК Подразделение,
	|	&Менеджер                               КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Валюта                                 КАК Валюта,
	|	&СуммаДокумента                         КАК Сумма,
	|	НЕОПРЕДЕЛЕНО                            КАК Статус,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&ИнформацияПоДоговору                   КАК Дополнительно,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса
	
КонецФункции

#КонецОбласти

#Конецобласти

#КонецОбласти

#КонецЕсли
