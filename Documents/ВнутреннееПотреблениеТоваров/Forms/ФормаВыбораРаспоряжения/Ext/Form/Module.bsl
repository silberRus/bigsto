
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем ЗначениеОтбора;
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьТекстЗапросаСпискаРаспоряжений();
	
	МассивОтборов = Новый Массив;
	МассивОтборов.Добавить("Организация");
	МассивОтборов.Добавить("Подразделение");
	МассивОтборов.Добавить("Склад");
	МассивОтборов.Добавить("Сделка");
	МассивОтборов.Добавить("ХозяйственнаяОперация");
	МассивОтборов.Добавить("НаправлениеДеятельности");
	
	Для каждого ИмяОтбора Из МассивОтборов Цикл
		Если Параметры.Отбор.Свойство(ИмяОтбора, ЗначениеОтбора) Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				Список,
				ИмяОтбора,
				ЗначениеОтбора);
			
			Параметры.Отбор.Удалить(ИмяОтбора);
			
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"Регистратор",
		Параметры.Регистратор);

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(Элементы.Список.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ОбщегоНазначенияУТКлиент.ПроверитьНаличиеВыделенныхВСпискеСтрок(Элементы.Список) Тогда
		Закрыть(Элементы.Список.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьТекстЗапросаСпискаРаспоряжений()
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыНаВнутренееПотребление.Ссылка КАК Ссылка,
	|	ТИПЗНАЧЕНИЯ(ЗаказыНаВнутренееПотребление.Ссылка) КАК ТипРаспоряжения,
	|	ЗаказыНаВнутренееПотребление.Дата КАК Дата,
	|	ЗаказыНаВнутренееПотребление.Номер КАК Номер,
	|	ЗаказыНаВнутренееПотребление.Подразделение,
	|	ЗаказыНаВнутренееПотребление.Организация КАК Организация,
	|	ЗаказыНаВнутренееПотребление.Склад КАК Склад,
	|	ЗаказыНаВнутренееПотребление.Статус КАК Статус,
	|	ЗаказыНаВнутренееПотребление.Ответственный КАК Ответственный,
	|	ЗаказыНаВнутренееПотребление.Комментарий КАК Комментарий
	|ИЗ
	|	Документ.ЗаказНаВнутреннееПотребление КАК ЗаказыНаВнутренееПотребление
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТаблицаЗаказы.ЗаказНаВнутреннееПотребление КАК ЗаказНаВнутреннееПотребление,
	|			СУММА(ТаблицаЗаказы.КОформлению) КАК КОформлению
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ЗаказыОстатки.ЗаказНаВнутреннееПотребление КАК ЗаказНаВнутреннееПотребление,
	|				ЗаказыОстатки.КОформлениюОстаток КАК КОформлению
	|			ИЗ
	|				РегистрНакопления.ЗаказыНаВнутреннееПотребление.Остатки(
	|						,
	|						{(ЗаказНаВнутреннееПотребление.Подразделение = &Подразделение),
	|							(ЗаказНаВнутреннееПотребление.Организация = &Организация),
	|							(ЗаказНаВнутреннееПотребление.Склад = &Склад),
	|							(ЗаказНаВнутреннееПотребление.Сделка = &Сделка),
	|							(ЗаказНаВнутреннееПотребление.ХозяйственнаяОперация = &ХозяйственнаяОперация),
	|							(ЗаказНаВнутреннееПотребление.НаправлениеДеятельности = &НаправлениеДеятельности)}) КАК ЗаказыОстатки
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				ЗаказыДвижения.ЗаказНаВнутреннееПотребление,
	|				ВЫБОР
	|					КОГДА ЗаказыДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ТОГДА -ЗаказыДвижения.КОформлению
	|					ИНАЧЕ ЗаказыДвижения.КОформлению
	|				КОНЕЦ
	|			ИЗ
	|				РегистрНакопления.ЗаказыНаВнутреннееПотребление КАК ЗаказыДвижения
	|			ГДЕ
	|				ЗаказыДвижения.Регистратор = &Регистратор
	|				И ЗаказыДвижения.Активность
	|			{ГДЕ
	|				(ЗаказыДвижения.ЗаказНаВнутреннееПотребление.Подразделение = &Подразделение),
	|				(ЗаказыДвижения.ЗаказНаВнутреннееПотребление.Организация = &Организация),
	|				(ЗаказыДвижения.ЗаказНаВнутреннееПотребление.Склад = &Склад),
	|				(ЗаказыДвижения.ЗаказНаВнутреннееПотребление.Сделка = &Сделка),
	|				(ЗаказыДвижения.ЗаказНаВнутреннееПотребление.ХозяйственнаяОперация = &ХозяйственнаяОперация),
	|				(ЗаказыДвижения.ЗаказНаВнутреннееПотребление.НаправлениеДеятельности = &НаправлениеДеятельности)}) КАК ТаблицаЗаказы
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТаблицаЗаказы.ЗаказНаВнутреннееПотребление) КАК ТаблицаЗаказы
	|		ПО ЗаказыНаВнутренееПотребление.Ссылка = ТаблицаЗаказы.ЗаказНаВнутреннееПотребление
	|ГДЕ
	|	ТаблицаЗаказы.КОформлению > 0
	|";
	
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
КонецПроцедуры

#КонецОбласти
