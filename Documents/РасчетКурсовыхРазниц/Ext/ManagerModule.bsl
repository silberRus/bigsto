#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
	
КонецПроцедуры

// Добавляет команду создания документа "Расчет курсовых разниц".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.РасчетКурсовыхРазниц) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РасчетКурсовыхРазниц.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.РасчетКурсовыхРазниц);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьНесколькоВалют";
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
	
КонецПроцедуры

// Возвращает регистры переоценки для заданной хозяйственной операции.
// Если хозяйственная операция не задана, то возвращаются все оперативные регистры переоцениваемые документом.
//
// Параметры:
//  Операция - Перечисление.ХозяйственныеОперации - хозяйственная операция для которой необходимо получить список регистров.
//	ИменаРегистров - Структура - структура с ранее заполненными именами регистров (для добавления новых регистров)
//
// Возвращаемое значение:
//  Структура - в ключах структуры перечислены имена переоцениваемых регистров
//
Функция РегистрыРасчета(Операция = Неопределено, ИменаРегистров = Неопределено) Экспорт
	
	Если ИменаРегистров = Неопределено Тогда
		ИменаРегистров = Новый Структура;
	КонецЕсли;
	
	Если Операция = Перечисления.ХозяйственныеОперации.ПереоценкаРасчетовСПоставщиками ИЛИ Операция = Неопределено Тогда
		ИменаРегистров.Вставить("РасчетыСПоставщикамиПоДокументам");
	КонецЕсли;
	
	Если Операция = Перечисления.ХозяйственныеОперации.ПереоценкаРасчетовСКлиентами ИЛИ Операция = Неопределено Тогда
		ИменаРегистров.Вставить("РасчетыСКлиентамиПоДокументам");
	КонецЕсли;
		
	Если Операция = Перечисления.ХозяйственныеОперации.ПереоценкаДенежныхСредств ИЛИ Операция = Неопределено Тогда
		ИменаРегистров.Вставить("ДенежныеСредстваБезналичные");
		ИменаРегистров.Вставить("ДенежныеСредстваНаличные");
		ИменаРегистров.Вставить("ДенежныеСредстваВКассахККМ");
		ИменаРегистров.Вставить("ДенежныеСредстваВПути");
		ИменаРегистров.Вставить("ДенежныеСредстваУПодотчетныхЛиц");
	КонецЕсли;
		
	Если Операция = Перечисления.ХозяйственныеОперации.ПереоценкаКредитовИДепозитов ИЛИ Операция = Неопределено Тогда
		ИменаРегистров.Вставить("РасчетыПоДоговорамКредитовИДепозитов");
	КонецЕсли;
	
	Возврат ИменаРегистров;
	
КонецФункции

// Выполняет переоценку всего чего возможно в оперативном учете.
//	Переоцениваются:
//	- Остатки денежных средств.
//	- Взаиморасчеты с клиентами.
//	- Взаиморасчеты с поставщиками.
//	- Остатки по кредитам, депозитам, займам.
//
// Параметры:
//	Период - Дата - любая дата месяца за который неоходимо провести переоценку
//	Организация - СправочникСсылка.Организации, Массив, Неопределено - Организация (список организаций) по которой необходимо выполнить переоценку.
//                Если не задан, то для всех.
//
Процедура ВыполнитьПереоценку(Период, Организация = Неопределено) Экспорт
	
	ПровестиДокументПереоценки(Организация, Период, Перечисления.ХозяйственныеОперации.ПереоценкаДенежныхСредств);
	ПровестиДокументПереоценки(Организация, Период, Перечисления.ХозяйственныеОперации.ПереоценкаРасчетовСКлиентами);
	ПровестиДокументПереоценки(Организация, Период, Перечисления.ХозяйственныеОперации.ПереоценкаРасчетовСПоставщиками);
	ПровестиДокументПереоценки(Организация, Период, Перечисления.ХозяйственныеОперации.ПереоценкаКредитовИДепозитов);
	
КонецПроцедуры

// Выполняет переоценку остатков денежных средств.
//
// Параметры:
//	МассивОрганизаций - СправочникСсылка.Организации, Массив - Организация (список организаций) по которой необходимо выполнить переоценку.
//	Период - Дата - любая дата месяца за который неоходимо провести переоценку.
//
Процедура ПереоценитьДенежныеСредства(МассивОрганизаций, Период) Экспорт
	
	Для Каждого Организация Из ОбщегоНазначенияУТКлиентСервер.Массив(МассивОрганизаций) Цикл
		ПровестиДокументПереоценки(Организация, Период, Перечисления.ХозяйственныеОперации.ПереоценкаДенежныхСредств);
	КонецЦикла;
	
КонецПроцедуры

// Выполняет переоценку остатков наличных денежных средств в заданных кассах организации.
//
// Параметры:
//	Организация - СправочникСсылка.Организации, Массив, Неопределено - Организация (список организаций) по которой необходимо выполнить переоценку.
//                Если не задан, то для всех.
//	АналитикаРасчета - СправочникСсылка.Кассы, Массив, Неопределено - Касса (список касс) по которой необходимо выполнить переоценку.
//	                   Если не задана, то для всех.
//	Период - Дата - любая дата месяца за который неоходимо провести переоценку.
//
Процедура ПереоценитьКассыОрганизации(Организация, Период, АналитикаРасчета = Неопределено) Экспорт
	
	Операция = Перечисления.ХозяйственныеОперации.ПереоценкаДенежныхСредств;
	РегистрыДляРасчета = Новый Структура("ДенежныеСредстваНаличные");
	ПараметрыПереоценки = Новый Структура("АналитикаРасчета, РегистрыРасчета", АналитикаРасчета, РегистрыДляРасчета);
	
	ПровестиДокументПереоценки(Организация, Период, Операция, ПараметрыПереоценки);
	
КонецПроцедуры

// Выполняет переоценку остатков денежных средств у подотчетных лиц.
//
// Параметры:
//	Организация - СправочникСсылка.Организации, Массив, Неопределено - Организация (список организаций) по которой необходимо выполнить переоценку.
//                Если не задан, то для всех.
//	АналитикаРасчета - СправочникСсылка.ФизическиеЛица, Массив, Неопределено - Физическое лицо (список лиц) у которых необходимо выполнить переоценку.
//	                   Если не задано, то для всех.
//	Период - Дата - любая дата месяца за который неоходимо провести переоценку.
//
Процедура ПереоценитьДенежныеСредстваУПодотчетныхЛиц(Организация, Период, АналитикаРасчета = Неопределено) Экспорт
	
	Операция = Перечисления.ХозяйственныеОперации.ПереоценкаДенежныхСредств;
	РегистрыДляРасчета = Новый Структура("ДенежныеСредстваУПодотчетныхЛиц");
	ПараметрыПереоценки = Новый Структура("АналитикаРасчета, РегистрыРасчета", АналитикаРасчета, РегистрыДляРасчета);
	
	ПровестиДокументПереоценки(Организация, Период, Операция, ПараметрыПереоценки);
	
КонецПроцедуры

// Выполняет переоценку взаиморасчетов с клиентами.
//
// Параметры:
//	Организация - СправочникСсылка.Организации, Массив, Неопределено - Организация (список организаций) по которой необходимо выполнить переоценку.
//                Если не задан, то для всех.
//	АналитикаРасчета - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам, Массив, Неопределено - АналитикаУчетаПоПартнерам (список аналитик) 
//	                   по которой необходимо выполнить переоценку. Если не задана, то для всех.
//	Период - Дата - любая дата месяца за который неоходимо провести переоценку.
//	КоличествоОбработанныхЭлементов - Число - в переменную помещается количество записанных записей
//                                            в регистр РасчетыСКлиентамиПоДокументам при расчете курсовых разниц.
//
Процедура ПереоценитьРасчетыСКлиентами(Организация, Период, АналитикаРасчета = Неопределено, КоличествоОбработанныхЭлементов = 0) Экспорт
	
	Операция = Перечисления.ХозяйственныеОперации.ПереоценкаРасчетовСКлиентами;
	ПараметрыПереоценки = Новый Структура("АналитикаРасчета, КоличествоОбработанныхЭлементов", АналитикаРасчета, КоличествоОбработанныхЭлементов);
	Если ЗначениеЗаполнено(Организация) Тогда
		ПровестиДокументПереоценки(Организация, Период, Операция, ПараметрыПереоценки);
		
	Иначе
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Организации.Ссылка КАК Организация
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	НЕ Организации.ПометкаУдаления
		|	И (Организации.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
		|		ИЛИ &УчитыватьУпрОрганизацию)");
		Запрос.УстановитьПараметр("УчитыватьУпрОрганизацию", ПолучитьФункциональнуюОпцию("ИспользоватьУправленческуюОрганизацию"));
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ПровестиДокументПереоценки(Организация, Период, Операция, ПараметрыПереоценки);
		КонецЦикла;
		
	КонецЕсли;
	КоличествоОбработанныхЭлементов = ПараметрыПереоценки.КоличествоОбработанныхЭлементов;
	
КонецПроцедуры

// Выполняет переоценку взаиморасчетов с поставщиками.
//
// Параметры:
//	Организация - СправочникСсылка.Организации, Массив, Неопределено - Организация (список организаций) по которой необходимо выполнить переоценку.
//                Если не задан, то для всех.
//	АналитикаРасчета - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам, Массив, Неопределено - АналитикаУчетаПоПартнерам (список аналитик) 
//	                   по которой необходимо выполнить переоценку. Если не задана, то для всех.
//	Период - Дата - любая дата месяца за который неоходимо провести переоценку.
//	КоличествоОбработанныхЭлементов - Число - в переменную помещается количество записанных записей
//                                            в регистр РасчетыСПоставщикамиПоДокументам при расчете курсовых разниц.
//
Процедура ПереоценитьРасчетыСПоставщиками(Организация, Период, АналитикаРасчета = Неопределено, КоличествоОбработанныхЭлементов = 0) Экспорт
	
	Операция = Перечисления.ХозяйственныеОперации.ПереоценкаРасчетовСПоставщиками;
	ПараметрыПереоценки = Новый Структура("АналитикаРасчета, КоличествоОбработанныхЭлементов", АналитикаРасчета, КоличествоОбработанныхЭлементов);
	Если ЗначениеЗаполнено(Организация) Тогда
		ПровестиДокументПереоценки(Организация, Период, Операция, ПараметрыПереоценки);
		
	Иначе
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Организации.Ссылка КАК Организация
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	НЕ Организации.ПометкаУдаления
		|	И (Организации.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
		|		ИЛИ &УчитыватьУпрОрганизацию)");
		Запрос.УстановитьПараметр("УчитыватьУпрОрганизацию", ПолучитьФункциональнуюОпцию("ИспользоватьУправленческуюОрганизацию"));
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ПровестиДокументПереоценки(Организация, Период, Операция, ПараметрыПереоценки);
		КонецЦикла;
		
	КонецЕсли;
	КоличествоОбработанныхЭлементов = ПараметрыПереоценки.КоличествоОбработанныхЭлементов;
	
КонецПроцедуры

// Выполняет переоценку кредитов, депозитов, займов.
//
// Параметры:
//	Организация - СправочникСсылка.Организации, Массив, Неопределено - Организация (список организаций) по которой необходимо выполнить переоценку.
//                Если не задан, то для всех.
//	Период - Дата - любая дата месяца за который неоходимо провести переоценку.
//
Процедура ПереоценитьКредитыДепозитыЗаймы(Организация, Период) Экспорт
	
	ПровестиДокументПереоценки(Организация, Период, Перечисления.ХозяйственныеОперации.ПереоценкаКредитовИДепозитов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	Если Регистры = Неопределено И НЕ ДополнительныеСвойства.Свойство("ВыполненРасчет") Тогда
		РассчитатьКурсовыеРазницы(Запрос.Параметры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	// Временные таблицы
	ТекстЗапросаВтТаблицаРасчетыСПоставщикамиПоДокументам(ТекстыЗапроса);
	ТекстЗапросаВтТаблицаРасчетыСКлиентамиПоДокументам(ТекстыЗапроса);
	ТекстЗапросаВтТаблицаДенежныеСредстваБезналичные(ТекстыЗапроса);
	ТекстЗапросаВтТаблицаДенежныеСредстваНаличные(ТекстыЗапроса);
	ТекстЗапросаВтТаблицаДенежныеСредстваВКассахККМ(ТекстыЗапроса);
	ТекстЗапросаВтТаблицаДенежныеСредстваВПути(ТекстыЗапроса);
	ТекстЗапросаВтТаблицаДенежныеСредстваУПодотчетныхЛиц(ТекстыЗапроса);
	ТекстЗапросаВтТаблицаКредитыИДепозиты(ТекстыЗапроса);
	
	ТекстЗапросаВтРасчетыСКлиентами(ТекстыЗапроса);
	ТекстЗапросаВтРасчетыСКлиентамиПоОбъектамРасчета(ТекстыЗапроса);
	ТекстЗапросаВтРасчетыСПоставщиками(ТекстыЗапроса);
	ТекстЗапросаВтРасчетыСПоставщикамиПоОбъектамРасчета(ТекстыЗапроса);
	
	// Таблицы on-line движений
	ТекстЗапросаТаблицаПрочиеДоходы(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДвиженияКонтрагентДоходыРасходы(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДвиженияДенежныеСредстваДоходыРасходы(ТекстыЗапроса, Регистры);
	
	////////////////////////////////////////////////////////////////////////////
	// Выполним запрос и поместим результаты запроса в дополнительные свойства
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.ХозяйственнаяОперация,
	|	ДанныеДокумента.ПереоценкаПоДням,
	|	ВЫБОР КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаДенежныхСредств)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПереоценкаДС,
	|	ВЫБОР КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаРасчетовСПоставщиками)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПереоценкаРасчетовСПоставщиками,
	|	ВЫБОР КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаРасчетовСКлиентами)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПереоценкаРасчетовСКлиентами,
	|	ВЫБОР КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаКредитовИДепозитов)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПереоценкаКредитовИДепозитов
	|
	|ИЗ
	|	Документ.РасчетКурсовыхРазниц КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	НачалоПериода = НачалоМесяца(Реквизиты.Дата);
	КонецПериода = КонецМесяца(Реквизиты.Дата);
	Запрос.УстановитьПараметр("НачалоПериода",                         НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",                          КонецПериода);
	Запрос.УстановитьПараметр("ГраницаНачалоПериода",                  Новый Граница(НачалоПериода, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ГраницаКонецПериода",                   Новый Граница(КонецПериода,  ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",                           Реквизиты.Организация);
	Запрос.УстановитьПараметр("ЗаказПодразделение",                    Неопределено);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",                 Реквизиты.ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",            Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета",        Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ПереоценкаПоДням",                      Реквизиты.ПереоценкаПоДням);
	Запрос.УстановитьПараметр("ИспользоватьУчетПрочихДоходовРасходов", Константы.ИспользоватьУчетПрочихДоходовРасходов.Получить());
	Запрос.УстановитьПараметр("АналитикаРасчета",
		РегистрыСведений.АналитикаУчетаПоПартнерам.ПолучитьМассивКлючейАналитикиПоОрганизации(Реквизиты.Организация));
	Запрос.УстановитьПараметр("Кассы",                                 Неопределено);
	Запрос.УстановитьПараметр("ПоВсемКассам",                          Истина);
	Запрос.УстановитьПараметр("ПодотчетныеЛица",                       Неопределено);
	Запрос.УстановитьПараметр("ПоВсемПодотчетнымЛицам",                Истина);
		
	ОплатаКлиента = Новый Массив;
	ОплатаКлиента.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента);
	Запрос.УстановитьПараметр("ОплатаКлиента", ОплатаКлиента);
	
	ОплатаПоставщику = Новый Массив;
	ОплатаПоставщику.Добавить(Перечисления.ХозяйственныеОперации.ОплатаПоставщику);
	Запрос.УстановитьПараметр("ОплатаПоставщику", ОплатаПоставщику);
	
	Запрос.УстановитьПараметр("ИменаРегистров", РегистрыРасчета(Реквизиты.ХозяйственнаяОперация));
	
	УниверсальныеМеханизмыПартийИСебестоимости.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаРасчетыСПоставщикамиПоДокументам(ТекстыЗапроса)

	ИмяРегистра = "ТаблицаРасчетыСПоставщиками";

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РасчетыПоДокументам.ВидДвижения,
	|	РасчетыПоДокументам.Валюта КАК Валюта,
	|	РасчетыПоДокументам.Период КАК Период,
	|	РасчетыПоДокументам.АналитикаУчетаПоПартнерам,
	|	&ЗаказПодразделение КАК Подразделение,
	|	РасчетыПоДокументам.ЗаказПоставщику КАК Заказ,
	|	РасчетыПоДокументам.РасчетныйДокумент,
	|	РасчетыПоДокументам.ДолгУпр КАК СуммаУпр,
	|	РасчетыПоДокументам.ДолгРегл КАК СуммаРегл,
	|	РасчетыПоДокументам.ПредоплатаРегл КАК ПредоплатаРегл,
	|	РасчетыПоДокументам.ПредоплатаУпр КАК ПредоплатаУпр
	|
	|ПОМЕСТИТЬ ТаблицаРасчетыСПоставщиками
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК РасчетыПоДокументам
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаРасчетовСПоставщиками)
	|	И ТИПЗНАЧЕНИЯ(РасчетыПоДокументам.Регистратор) = ТИП(Документ.РасчетКурсовыхРазниц)
	|	И РасчетыПоДокументам.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И РасчетыПоДокументам.АналитикаУчетаПоПартнерам В (&АналитикаРасчета)";
	
	ТекстПоляПодразделения = ДенежныеСредстваПовтИсп.ТекстЗапросаРеквизитаОбъектаРасчетов(
								"РасчетыПоДокументам.ЗаказПоставщику", "Подразделение", "ОпределяемыеТипы.ОбъектРасчетовСПоставщиками");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЗаказПодразделение", ТекстПоляПодразделения);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаРасчетыСКлиентамиПоДокументам(ТекстыЗапроса)

	ИмяРегистра = "ТаблицаРасчетыСКлиентами";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РасчетыПоДокументам.ВидДвижения,
	|	РасчетыПоДокументам.Валюта КАК Валюта,
	|	РасчетыПоДокументам.Период КАК Период,
	|	РасчетыПоДокументам.АналитикаУчетаПоПартнерам,
	|	&ЗаказПодразделение КАК Подразделение,
	|	РасчетыПоДокументам.ЗаказКлиента КАК Заказ,
	|	РасчетыПоДокументам.РасчетныйДокумент,
	|	РасчетыПоДокументам.ДатаПлатежа,
	|	РасчетыПоДокументам.ДолгРегл КАК СуммаРегл,
	|	РасчетыПоДокументам.ДолгУпр КАК СуммаУпр,
	|	РасчетыПоДокументам.ПредоплатаРегл КАК ПредоплатаРегл,
	|	РасчетыПоДокументам.ПредоплатаУпр КАК ПредоплатаУпр
	|
	|ПОМЕСТИТЬ ТаблицаРасчетыСКлиентами
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РасчетыПоДокументам
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаРасчетовСКлиентами)
	|	И ТИПЗНАЧЕНИЯ(РасчетыПоДокументам.Регистратор) = ТИП(Документ.РасчетКурсовыхРазниц)
	|	И РасчетыПоДокументам.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И РасчетыПоДокументам.АналитикаУчетаПоПартнерам В (&АналитикаРасчета)";
	
	ТекстПоляПодразделения = ДенежныеСредстваПовтИсп.ТекстЗапросаРеквизитаОбъектаРасчетов(
								"РасчетыПоДокументам.ЗаказКлиента", "Подразделение", "ОпределяемыеТипы.ОбъектРасчетовСКлиентами");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЗаказПодразделение", ТекстПоляПодразделения);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаДенежныеСредстваБезналичные(ТекстыЗапроса)

	ИмяРегистра = "ТаблицаДенежныеСредстваБезналичные";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДенежныеСредства.ВидДвижения,
	|	ДенежныеСредства.Период КАК Период,
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.БанковскийСчет КАК БанковскийСчет,
	|	ДенежныеСредства.БанковскийСчет.Подразделение КАК Подразделение,
	|	ДенежныеСредства.БанковскийСчет.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДенежныеСредства.СуммаУпр КАК СуммаУпр,
	|	ДенежныеСредства.СуммаРегл КАК СуммаРегл,
	|	ДенежныеСредства.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДенежныеСредства.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ ТаблицаДенежныеСредстваБезналичные
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваБезналичные КАК ДенежныеСредства
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаДенежныхСредств)
	|	И ТИПЗНАЧЕНИЯ(ДенежныеСредства.Регистратор) = ТИП(Документ.РасчетКурсовыхРазниц)
	|	И ДенежныеСредства.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДенежныеСредства.Организация = &Организация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаДенежныеСредстваНаличные(ТекстыЗапроса)

	ИмяРегистра = "ТаблицаДенежныеСредстваНаличные";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДенежныеСредства.ВидДвижения,
	|	ДенежныеСредства.Период КАК Период,
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.Касса КАК Касса,
	|	ДенежныеСредства.Касса.Подразделение КАК Подразделение,
	|	ДенежныеСредства.Касса.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДенежныеСредства.СуммаУпр КАК СуммаУпр,
	|	ДенежныеСредства.СуммаРегл КАК СуммаРегл,
	|	ДенежныеСредства.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДенежныеСредства.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ ТаблицаДенежныеСредстваНаличные
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные КАК ДенежныеСредства
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаДенежныхСредств)
	|	И ТИПЗНАЧЕНИЯ(ДенежныеСредства.Регистратор) = ТИП(Документ.РасчетКурсовыхРазниц)
	|	И ДенежныеСредства.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДенежныеСредства.Организация = &Организация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаДенежныеСредстваВКассахККМ(ТекстыЗапроса)

	ИмяРегистра = "ТаблицаДенежныеСредстваВКассахККМ";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДенежныеСредства.ВидДвижения,
	|	ДенежныеСредства.Период КАК Период,
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.КассаККМ КАК КассаККМ,
	|	ДенежныеСредства.КассаККМ.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ДенежныеСредства.СуммаУпр КАК СуммаУпр,
	|	ДенежныеСредства.СуммаРегл КАК СуммаРегл
	|ПОМЕСТИТЬ ТаблицаДенежныеСредстваВКассахККМ
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВКассахККМ КАК ДенежныеСредства
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаДенежныхСредств)
	|	И ТИПЗНАЧЕНИЯ(ДенежныеСредства.Регистратор) = ТИП(Документ.РасчетКурсовыхРазниц)
	|	И ДенежныеСредства.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДенежныеСредства.Организация = &Организация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаДенежныеСредстваУПодотчетныхЛиц(ТекстыЗапроса)

	ИмяРегистра = "ТаблицаДенежныеСредстваУПодотчетныхЛиц";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДенежныеСредства.ВидДвижения,
	|	ДенежныеСредства.Период КАК Период,
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	ДенежныеСредства.Подразделение КАК Подразделение,
	|	ДенежныеСредства.ЦельВыдачи КАК ЦельВыдачи,
	|	ДенежныеСредства.Валюта КАК Валюта,
	|	ДенежныеСредства.СуммаУпр КАК СуммаУпр,
	|	ДенежныеСредства.СуммаРегл КАК СуммаРегл,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ДенежныеСредства.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДенежныеСредства.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ ТаблицаДенежныеСредстваУПодотчетныхЛиц
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц КАК ДенежныеСредства
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаДенежныхСредств)
	|	И ТИПЗНАЧЕНИЯ(ДенежныеСредства.Регистратор) = ТИП(Документ.РасчетКурсовыхРазниц)
	|	И ДенежныеСредства.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДенежныеСредства.Организация = &Организация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаДенежныеСредстваВПути(ТекстыЗапроса)

	ИмяРегистра = "ТаблицаДенежныеСредстваВПути";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДенежныеСредства.Период КАК Период,
	|	ДенежныеСредства.Организация,
	|	ЕСТЬNULL(ДенежныеСредства.Получатель.Подразделение,
	|			ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|	ЕСТЬNULL(ДенежныеСредства.Получатель.НаправлениеДеятельности,
	|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	ДенежныеСредства.ВидПереводаДенежныхСредств КАК ВидПереводаДенежныхСредств,
	|	ВЫБОР КОГДА ДенежныеСредства.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|		ТОГДА ДенежныеСредства.Получатель
	|		ИНАЧЕ ДенежныеСредства.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ДенежныеСредства.Валюта КАК Валюта,
	|	СУММА(ВЫБОР КОГДА ДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -ДенежныеСредства.СуммаУпр
	|			ИНАЧЕ ДенежныеСредства.СуммаУпр
	|	КОНЕЦ) КАК СуммаУпр,
	|	СУММА(
	|		ВЫБОР КОГДА ДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -ДенежныеСредства.СуммаРегл
	|			ИНАЧЕ ДенежныеСредства.СуммаРегл
	|		КОНЕЦ) КАК СуммаРегл
	|ПОМЕСТИТЬ втДенежныеСредстваВПути
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВПути КАК ДенежныеСредства
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаДенежныхСредств)
	|	И ТИПЗНАЧЕНИЯ(ДенежныеСредства.Регистратор) = ТИП(Документ.РасчетКурсовыхРазниц)
	|	И ДенежныеСредства.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДенежныеСредства.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ДенежныеСредства.Период,
	|	ДенежныеСредства.Организация,
	|	ЕСТЬNULL(ДенежныеСредства.Получатель.Подразделение,
	|			ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)),
	|	ЕСТЬNULL(ДенежныеСредства.Получатель.НаправлениеДеятельности,
	|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
	|	ДенежныеСредства.ВидПереводаДенежныхСредств,
	|	ВЫБОР КОГДА ДенежныеСредства.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|		ТОГДА ДенежныеСредства.Получатель
	|		ИНАЧЕ ДенежныеСредства.Контрагент
	|	КОНЕЦ,
	|	ДенежныеСредства.Валюта";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "втДенежныеСредстваВПути");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР КОГДА ДенежныеСредства.СуммаУпр > 0
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	ДенежныеСредства.Период КАК Период,
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.Подразделение КАК Подразделение,
	|	ДенежныеСредства.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК Получатель,
	|	НЕОПРЕДЕЛЕНО КАК Отправитель,
	|	ДенежныеСредства.ВидПереводаДенежныхСредств КАК ВидПереводаДенежныхСредств,
	|	ДенежныеСредства.Контрагент КАК Контрагент,
	|	ДенежныеСредства.Валюта КАК Валюта,
	|	ВЫБОР КОГДА ДенежныеСредства.СуммаУпр > 0
	|			ТОГДА ДенежныеСредства.СуммаУпр
	|			ИНАЧЕ -ДенежныеСредства.СуммаУпр
	|	КОНЕЦ КАК СуммаУпр,
	|	0 КАК СуммаРегл,
	|	ВЫБОР КОГДА ДенежныеСредства.СуммаУпр > 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток)
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ ТаблицаДенежныеСредстваВПути
	|ИЗ
	|	втДенежныеСредстваВПути КАК ДенежныеСредства
	|ГДЕ
	|	ДенежныеСредства.СуммаУпр <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА ДенежныеСредства.СуммаРегл > 0
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	ДенежныеСредства.Период КАК Период,
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.Подразделение КАК Подразделение,
	|	ДенежныеСредства.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК Получатель,
	|	НЕОПРЕДЕЛЕНО КАК Отправитель,
	|	ДенежныеСредства.ВидПереводаДенежныхСредств КАК ВидПереводаДенежныхСредств,
	|	ДенежныеСредства.Контрагент КАК Контрагент,
	|	ДенежныеСредства.Валюта КАК Валюта,
	|	0 КАК СуммаУпр,
	|	ВЫБОР КОГДА ДенежныеСредства.СуммаРегл > 0
	|			ТОГДА ДенежныеСредства.СуммаРегл
	|			ИНАЧЕ -ДенежныеСредства.СуммаРегл
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР КОГДА ДенежныеСредства.СуммаРегл > 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток)
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств
	|
	|ИЗ
	|	втДенежныеСредстваВПути КАК ДенежныеСредства
	|ГДЕ
	|	ДенежныеСредства.СуммаРегл <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаКредитыИДепозиты(ТекстыЗапроса)

	ИмяРегистра = "ТаблицаКредитыИДепозиты";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КредитыИДепозиты.ВидДвижения,
	|	КредитыИДепозиты.Период КАК Период,
	|	КредитыИДепозиты.АналитикаУчетаПоПартнерам,
	|	КредитыИДепозиты.Договор КАК Договор,
	|	КредитыИДепозиты.Договор.Подразделение КАК Подразделение,
	|	КредитыИДепозиты.Договор.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	КредитыИДепозиты.Договор.ХарактерДоговора КАК ХарактерДоговора,
	|	КредитыИДепозиты.ТипСуммы КАК ТипСуммы,
	|	КредитыИДепозиты.Договор.ВалютаВзаиморасчетов КАК Валюта,
	|	КредитыИДепозиты.СуммаВВалюте КАК СуммаВВалюте,
	|	КредитыИДепозиты.СуммаУпр КАК СуммаУпр,
	|	КредитыИДепозиты.СуммаРегл КАК СуммаРегл
	|ПОМЕСТИТЬ ТаблицаКредитыИДепозиты
	|ИЗ
	|	РегистрНакопления.РасчетыПоДоговорамКредитовИДепозитов КАК КредитыИДепозиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО КредитыИДепозиты.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаКредитовИДепозитов)
	|	И КредитыИДепозиты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Аналитика.Организация = &Организация
	|	И ТИПЗНАЧЕНИЯ(КредитыИДепозиты.Регистратор) = ТИП(Документ.РасчетКурсовыхРазниц)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтРасчетыСКлиентами(ТекстыЗапроса = Неопределено)

	ИмяРегистра = "РасчетыСКлиентами";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период,
	|	ДанныеРегистра.Валюта,
	|	ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ТОГДА -ДанныеРегистра.СуммаРегл
	|		ИНАЧЕ ДанныеРегистра.СуммаРегл
	|	КОНЕЦ ДолгРегл,
	|	ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ТОГДА -ДанныеРегистра.СуммаУпр
	|		ИНАЧЕ ДанныеРегистра.СуммаУпр
	|	КОНЕЦ ДолгУпр,
	|	ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ТОГДА -ДанныеРегистра.ПредоплатаРегл
	|		ИНАЧЕ ДанныеРегистра.ПредоплатаРегл
	|	КОНЕЦ ПредоплатаРегл,
	|	ВЫБОР КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ТОГДА -ДанныеРегистра.ПредоплатаУпр
	|		ИНАЧЕ ДанныеРегистра.ПредоплатаУпр
	|	КОНЕЦ ПредоплатаУпр,
	|	ДанныеРегистра.АналитикаУчетаПоПартнерам,
	|	ДанныеРегистра.Заказ,
	|	ДанныеРегистра.Подразделение
	|
	|ПОМЕСТИТЬ РасчетыСКлиентами
	|ИЗ
	|	ТаблицаРасчетыСКлиентами КАК ДанныеРегистра";
	
	Если ТекстыЗапроса <> Неопределено Тогда
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтРасчетыСКлиентамиПоОбъектамРасчета(ТекстыЗапроса = Неопределено)

	ИмяРегистра = "РасчетыСКлиентамиПоОбъектамРасчета";
	
	Если ТекстыЗапроса <> Неопределено Тогда
		Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ТаблицаРасчетыСКлиентами", ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаРасчетыСКлиентамиПоДокументам(ТекстыЗапроса);
		КонецЕсли;

		Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("РасчетыСКлиентами", ТекстыЗапроса) Тогда
			ТекстЗапросаВтРасчетыСКлиентами(ТекстыЗапроса);
		КонецЕсли;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РасчетыСКлиентами.Период КАК Период,
	|	РасчетыСКлиентами.Валюта,
	|	СУММА(РасчетыСКлиентами.ДолгРегл) КАК СуммаРегл,
	|	СУММА(РасчетыСКлиентами.ДолгУпр) КАК СуммаУпр,
	|	СУММА(РасчетыСКлиентами.ПредоплатаРегл) КАК ПредоплатаРегл,
	|	СУММА(РасчетыСКлиентами.ПредоплатаУпр) КАК ПредоплатаУпр,
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентами.Подразделение КАК Подразделение,
	|	Аналитика.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РасчетыСКлиентами.Заказ КАК Заказ,
	|	Аналитика.Контрагент КАК Контрагент,
	|	Аналитика.Договор КАК Договор
	|
	|ПОМЕСТИТЬ РасчетыСКлиентамиПоОбъектамРасчета
	|ИЗ
	|	РасчетыСКлиентами
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО РасчетыСКлиентами.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|	
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентами.Период,
	|	РасчетыСКлиентами.Валюта,
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентами.Подразделение,
	|	Аналитика.НаправлениеДеятельности,
	|	РасчетыСКлиентами.Заказ,
	|	Аналитика.Контрагент,
	|	Аналитика.Договор";
	
	Если ТекстыЗапроса <> Неопределено Тогда
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтРасчетыСПоставщиками(ТекстыЗапроса = Неопределено)

	ИмяРегистра = "РасчетыСПоставщиками";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапросаВтРасчетыСКлиентами(),"РасчетыСКлиентами","РасчетыСПоставщиками");
	
	Если ТекстыЗапроса <> Неопределено Тогда
		ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтРасчетыСПоставщикамиПоОбъектамРасчета(ТекстыЗапроса)

	ИмяРегистра = "РасчетыСПоставщикамиПоОбъектамРасчета";

	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ТаблицаРасчетыСПоставщиками", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасчетыСПоставщикамиПоДокументам(ТекстыЗапроса);
	КонецЕсли;

	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("РасчетыСПоставщиками", ТекстыЗапроса) Тогда
		ТекстЗапросаВтРасчетыСПоставщиками(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапросаВтРасчетыСКлиентамиПоОбъектамРасчета(),"РасчетыСКлиентами","РасчетыСПоставщиками");
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ДобавитьВременныеТаблицы(ТекстыЗапроса, ГруппаТаблиц = "")
	
	Если ГруппаТаблиц = "ДенежныеСредства" ИЛИ ПустаяСтрока(ГруппаТаблиц) Тогда
		Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ТаблицаДенежныеСредстваБезналичные", ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаДенежныеСредстваБезналичные(ТекстыЗапроса);
		КонецЕсли;

		Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ТаблицаДенежныеСредстваНаличные", ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаДенежныеСредстваНаличные(ТекстыЗапроса);
		КонецЕсли;

		Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ТаблицаДенежныеСредстваВКассахККМ", ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаДенежныеСредстваВКассахККМ(ТекстыЗапроса);
		КонецЕсли;

		Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ТаблицаДенежныеСредстваУПодотчетныхЛиц", ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаДенежныеСредстваУПодотчетныхЛиц(ТекстыЗапроса);
		КонецЕсли;

		Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ТаблицаДенежныеСредстваВПути", ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаДенежныеСредстваВПути(ТекстыЗапроса);
		КонецЕсли;
	КонецЕсли;

	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ТаблицаКредитыИДепозиты", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаКредитыИДепозиты(ТекстыЗапроса);
	КонецЕсли;
	
	Если ГруппаТаблиц = "РасчетыСПартнерами" ИЛИ ПустаяСтрока(ГруппаТаблиц) Тогда
		Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("РасчетыСКлиентамиПоОбъектамРасчета", ТекстыЗапроса) Тогда
			ТекстЗапросаВтРасчетыСКлиентамиПоОбъектамРасчета(ТекстыЗапроса);
		КонецЕсли;

		Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("РасчетыСПоставщикамиПоОбъектамРасчета", ТекстыЗапроса) Тогда
			ТекстЗапросаВтРасчетыСПоставщикамиПоОбъектамРасчета(ТекстыЗапроса);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаПрочиеДоходы(ТекстыЗапроса, Регистры = Неопределено)

	ИмяРегистра = "ПрочиеДоходы";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	ДобавитьВременныеТаблицы(ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	&Организация КАК Организация,
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы) КАК СтатьяДоходов,
	|	ВложенныйЗапрос.АналитикаДоходов КАК АналитикаДоходов,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ВложенныйЗапрос.Сумма
	|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаУпр,
	|	СУММА(ВЫБОР
	|		КОГДА &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			ТОГДА ВложенныйЗапрос.СуммаРегл
	|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаРегл,
	|	ВложенныйЗапрос.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|
	|ИЗ (
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаДоходов,
	|		Таблица.СуммаУпр КАК Сумма,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|		Таблица.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|	ИЗ
	|		ТаблицаДенежныеСредстваБезналичные КАК Таблица
	|	ГДЕ
	|		Таблица.СуммаУпр <> 0
	|		И Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаДоходов,
	|		Таблица.СуммаУпр КАК Сумма,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|		Таблица.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|	ИЗ
	|		ТаблицаДенежныеСредстваНаличные КАК Таблица
	|	ГДЕ
	|		Таблица.СуммаУпр <> 0
	|		И Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаДоходов,
	|		Таблица.СуммаУпр КАК Сумма,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|		&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|	ИЗ
	|		ТаблицаДенежныеСредстваВКассахККМ КАК Таблица
	|	ГДЕ
	|		Таблица.СуммаУпр <> 0
	|		И Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаДоходов,
	|		Таблица.СуммаУпр КАК Сумма,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|		Таблица.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|	ИЗ
	|		ТаблицаДенежныеСредстваУПодотчетныхЛиц КАК Таблица
	|	ГДЕ
	|		Таблица.СуммаУпр <> 0
	|		И Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаДоходов,
	|		Таблица.СуммаУпр КАК Сумма,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|		&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|	ИЗ
	|		ТаблицаДенежныеСредстваВПути КАК Таблица
	|	ГДЕ
	|		Таблица.СуммаУпр <> 0
	|		И Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.КредитыИДепозиты) КАК АналитикаДоходов,
	|		Таблица.СуммаУпр КАК Сумма,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|		&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|	ИЗ
	|		ТаблицаКредитыИДепозиты КАК Таблица
	|	ГДЕ
	|		Таблица.СуммаУпр <> 0
	|		И Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	// Курсовые разницы от расчетов с клиентами
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСКлиентами) КАК АналитикаДоходов,
	|		Таблица.СуммаУпр КАК Сумма,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыПрибыль) КАК ХозяйственнаяОперация
	|	ИЗ
	|		РасчетыСКлиентамиПоОбъектамРасчета КАК Таблица
	|	ГДЕ
	|		Таблица.СуммаУпр > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	// Курсовые разницы от расчетов с клиентами: ошибки округления, возврат авансов
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСКлиентами) КАК АналитикаДоходов,
	|		Таблица.ПредоплатаУпр КАК Сумма,
	|		Таблица.ПредоплатаРегл КАК СуммаРегл,
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыПрибыль) КАК ХозяйственнаяОперация
	|	ИЗ
	|		РасчетыСКлиентамиПоОбъектамРасчета КАК Таблица
	|	ГДЕ
	|		Таблица.ПредоплатаУпр > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	// Курсовые разницы от расчетов с поставщиками
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСПоставщиками) КАК АналитикаДоходов,
	|		Таблица.СуммаУпр КАК Сумма,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиПрибыль) КАК ХозяйственнаяОперация
	|	ИЗ
	|		РасчетыСПоставщикамиПоОбъектамРасчета КАК Таблица
	|	ГДЕ
	|		Таблица.СуммаУпр > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	// Курсовые разницы от расчетов с поставщиками: ошибки округления, возврат авансов
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСПоставщиками) КАК АналитикаДоходов,
	|		Таблица.ПредоплатаУпр КАК Сумма,
	|		Таблица.ПредоплатаРегл КАК СуммаРегл,
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиПрибыль) КАК ХозяйственнаяОперация
	|	ИЗ
	|		РасчетыСПоставщикамиПоОбъектамРасчета КАК Таблица
	|	ГДЕ
	|		Таблица.ПредоплатаУпр > 0
	|
	|	) КАК ВложенныйЗапрос
	|
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности,
	|	ВложенныйЗапрос.АналитикаДоходов,
	|	ВложенныйЗапрос.ХозяйственнаяОперация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВложенныйЗапрос.Период";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса)

	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	ДобавитьВременныеТаблицы(ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы();
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ" + "
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы) КАК СтатьяРасходов,
	|	ВложенныйЗапрос.АналитикаРасходов КАК АналитикаРасходов,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК СуммаСНДС,
	|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК СуммаБезНДС,
	|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК СуммаБезНДСУпр,
	|	СУММА(ВложенныйЗапрос.СуммаРегл) КАК СуммаСНДСРегл,
	|	СУММА(ВложенныйЗапрос.СуммаРегл) КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	ВложенныйЗапрос.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры
	|ИЗ (
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаРасходов,
	|		Таблица.СуммаУпр КАК СуммаУпр,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|		Таблица.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|	ИЗ
	|		ТаблицаДенежныеСредстваБезналичные КАК Таблица
	|	ГДЕ
	|		Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаРасходов,
	|		Таблица.СуммаУпр КАК СуммаУпр,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|		Таблица.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|	ИЗ
	|		ТаблицаДенежныеСредстваНаличные КАК Таблица
	|	ГДЕ
	|		Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаРасходов,
	|		Таблица.СуммаУпр КАК СуммаУпр,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|		&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|	ИЗ
	|		ТаблицаДенежныеСредстваВКассахККМ КАК Таблица
	|	ГДЕ
	|		Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаРасходов,
	|		Таблица.СуммаУпр КАК СуммаУпр,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|		Таблица.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|	ИЗ
	|		ТаблицаДенежныеСредстваУПодотчетныхЛиц КАК Таблица
	|	ГДЕ
	|		Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства) КАК АналитикаРасходов,
	|		Таблица.СуммаУпр КАК СуммаУпр,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|		&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|	ИЗ
	|		ТаблицаДенежныеСредстваВПути КАК Таблица
	|	ГДЕ
	|		Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.КредитыИДепозиты) КАК АналитикаРасходов,
	|		Таблица.СуммаУпр КАК СуммаУпр,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|		&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|	ИЗ
	|		ТаблицаКредитыИДепозиты КАК Таблица
	|	ГДЕ
	|		Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	//Курсовые разницы от расчетов с клиентами
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСКлиентами) КАК АналитикаРасходов,
	|		ВЫБОР КОГДА Таблица.СуммаУпр < 0
	|			ТОГДА -Таблица.СуммаУпр
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаУпр,
	|		ВЫБОР КОГДА Таблица.СуммаРегл < 0
	|			ТОГДА -Таблица.СуммаРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаРегл,
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыУбыток) КАК ХозяйственнаяОперация
	|	ИЗ
	|		РасчетыСКлиентамиПоОбъектамРасчета КАК Таблица
	|	ГДЕ
	|		Таблица.СуммаРегл < 0 ИЛИ Таблица.СуммаУпр < 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	//Курсовые разницы от расчетов с клиентами: ошибки округления, возврат авансов
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСКлиентами) КАК АналитикаРасходов,
	|		ВЫБОР КОГДА Таблица.ПредоплатаУпр < 0
	|			ТОГДА -Таблица.ПредоплатаУпр
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаУпр,
	|		ВЫБОР КОГДА Таблица.ПредоплатаРегл < 0
	|			ТОГДА -Таблица.ПредоплатаРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаРегл,
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыУбыток) КАК ХозяйственнаяОперация
	|	ИЗ
	|		РасчетыСКлиентамиПоОбъектамРасчета КАК Таблица
	|	ГДЕ
	|		Таблица.ПредоплатаРегл < 0 ИЛИ Таблица.ПредоплатаУпр < 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	//Курсовые разницы от расчетов с поставщиками
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСПоставщиками) КАК АналитикаРасходов,
	|		ВЫБОР КОГДА Таблица.СуммаУпр < 0
	|			ТОГДА -Таблица.СуммаУпр
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаУпр,
	|		ВЫБОР КОГДА Таблица.СуммаРегл < 0
	|			ТОГДА -Таблица.СуммаРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаРегл,
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиУбыток) КАК ХозяйственнаяОперация
	|	ИЗ
	|		РасчетыСПоставщикамиПоОбъектамРасчета КАК Таблица
	|	ГДЕ
	|		Таблица.СуммаРегл < 0 ИЛИ Таблица.СуммаУпр < 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	//Курсовые разницы от расчетов с поставщиками: ошибки округления, возврат авансов
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСПоставщиками) КАК АналитикаРасходов,
	|		ВЫБОР КОГДА Таблица.ПредоплатаУпр < 0
	|			ТОГДА -Таблица.ПредоплатаУпр
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаУпр,
	|		ВЫБОР КОГДА Таблица.ПредоплатаРегл < 0
	|			ТОГДА -Таблица.ПредоплатаРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаРегл,
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиУбыток) КАК ХозяйственнаяОперация
	|	ИЗ
	|		РасчетыСПоставщикамиПоОбъектамРасчета КАК Таблица
	|	ГДЕ
	|		Таблица.ПредоплатаРегл < 0 ИЛИ Таблица.ПредоплатаУпр < 0
	|
	|	) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности,
	|	ВложенныйЗапрос.АналитикаРасходов,
	|	ВложенныйЗапрос.ХозяйственнаяОперация
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияКонтрагентДоходыРасходы(ТекстыЗапроса, Регистры = Неопределено)

	ИмяРегистра = "ДвиженияКонтрагентДоходыРасходы";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	ДобавитьВременныеТаблицы(ТекстыЗапроса, "РасчетыСПартнерами");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|
	|	РегистрАналитики.Партнер КАК Партнер,
	|	РегистрАналитики.Контрагент КАК Контрагент,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|
	|	ВложенныйЗапрос.Договор КАК Договор,
	|	ВложенныйЗапрос.Заказ КАК ОбъектРасчетов,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельностиСтатьи,
	|	ВложенныйЗапрос.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов,
	|	ВложенныйЗапрос.АналитикаДоходов КАК АналитикаДоходов,
	|	ВложенныйЗапрос.АналитикаРасходов КАК АналитикаРасходов,
	|	ВложенныйЗапрос.Валюта КАК Валюта,
	|	ВложенныйЗапрос.Валюта КАК ВалютаВзаиморасчетов,
	|
	|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК СуммаБезНДС,
	|	СУММА(ВложенныйЗапрос.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ВложенныйЗапрос.СуммаРегл) КАК СуммаРеглБезНДС,
	|
	|	ВложенныйЗапрос.Заказ КАК ИсточникГФУРасчетов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		ВЫБОР
	|			КОГДА Таблица.СуммаРегл > 0 ИЛИ Таблица.СуммаУпр > 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиПрибыль)
	|			КОГДА Таблица.СуммаРегл < 0 ИЛИ Таблица.СуммаУпр < 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиУбыток)
	|		КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Таблица.Договор КАК Договор,
	|		Таблица.Заказ КАК Заказ,
	|		Таблица.Валюта КАК Валюта,
	|		ВЫБОР
	|			КОГДА Таблица.СуммаУпр > 0 ИЛИ Таблица.СуммаРегл > 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСПоставщиками) 
	|			КОГДА Таблица.СуммаУпр < 0 ИЛИ Таблица.СуммаРегл < 0
	|				ТОГДА НЕОПРЕДЕЛЕНО 
	|		КОНЕЦ КАК АналитикаДоходов,
	|		ВЫБОР
	|			КОГДА Таблица.СуммаУпр > 0 ИЛИ Таблица.СуммаРегл > 0
	|				ТОГДА НЕОПРЕДЕЛЕНО
	|			КОГДА Таблица.СуммаУпр < 0 ИЛИ Таблица.СуммаРегл < 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСПоставщиками) 
	|		КОНЕЦ КАК АналитикаРасходов,
	|		ВЫБОР 
	|			КОГДА Таблица.СуммаУпр > 0 ИЛИ Таблица.СуммаРегл > 0
	|				ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы) 
	|			КОГДА Таблица.СуммаУпр < 0 ИЛИ Таблица.СуммаРегл < 0
	|				ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы) 
	|		КОНЕЦ КАК СтатьяДоходовРасходов,
	|		ВЫБОР КОГДА Таблица.СуммаУпр < 0
	|			ТОГДА -Таблица.СуммаУпр
	|			ИНАЧЕ Таблица.СуммаУпр
	|		КОНЕЦ КАК СуммаУпр,
	|		ВЫБОР КОГДА Таблица.СуммаРегл < 0
	|			ТОГДА -Таблица.СуммаРегл
	|			ИНАЧЕ Таблица.СуммаРегл
	|		КОНЕЦ КАК СуммаРегл
	|	ИЗ
	|		РасчетыСПоставщикамиПоОбъектамРасчета КАК Таблица
	|	ГДЕ
	|		Таблица.СуммаРегл <> 0 ИЛИ Таблица.СуммаУпр <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		ВЫБОР
	|			КОГДА Таблица.ПредоплатаРегл > 0 ИЛИ Таблица.ПредоплатаУпр > 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиПрибыль)
	|			КОГДА Таблица.ПредоплатаРегл < 0 ИЛИ Таблица.ПредоплатаУпр < 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиУбыток) 
	|		КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Таблица.Договор КАК Договор,
	|		Таблица.Заказ КАК Заказ,
	|		Таблица.Валюта КАК Валюта,
	|		ВЫБОР
	|			КОГДА Таблица.ПредоплатаРегл > 0 ИЛИ Таблица.ПредоплатаУпр > 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСПоставщиками)
	|			КОГДА Таблица.ПредоплатаРегл < 0 ИЛИ Таблица.ПредоплатаУпр < 0
	|				ТОГДА НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК АналитикаДоходов,
	|		ВЫБОР
	|			КОГДА Таблица.ПредоплатаРегл > 0 ИЛИ Таблица.ПредоплатаУпр > 0
	|				ТОГДА НЕОПРЕДЕЛЕНО
	|			КОГДА Таблица.ПредоплатаРегл < 0 ИЛИ Таблица.ПредоплатаУпр < 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСПоставщиками)
	|		КОНЕЦ КАК АналитикаРасходов,
	|		ВЫБОР
	|			КОГДА Таблица.ПредоплатаРегл > 0 ИЛИ Таблица.ПредоплатаУпр > 0
	|				ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|			КОГДА Таблица.ПредоплатаРегл < 0 ИЛИ Таблица.ПредоплатаУпр < 0
	|				ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		КОНЕЦ КАК СтатьяДоходовРасходов,
	|		ВЫБОР КОГДА Таблица.ПредоплатаУпр < 0
	|			ТОГДА -Таблица.ПредоплатаУпр
	|			ИНАЧЕ Таблица.ПредоплатаУпр
	|		КОНЕЦ КАК СуммаУпр,
	|		ВЫБОР КОГДА Таблица.ПредоплатаРегл < 0
	|			ТОГДА -Таблица.ПредоплатаРегл
	|			ИНАЧЕ Таблица.ПредоплатаРегл
	|		КОНЕЦ КАК СуммаРегл
	|	ИЗ
	|		РасчетыСПоставщикамиПоОбъектамРасчета КАК Таблица
	|	ГДЕ
	|		Таблица.ПредоплатаРегл <> 0 ИЛИ Таблица.ПредоплатаУпр <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		ВЫБОР
	|			КОГДА Таблица.СуммаРегл > 0 ИЛИ Таблица.СуммаУпр > 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыПрибыль)
	|			КОГДА Таблица.СуммаРегл < 0 ИЛИ Таблица.СуммаУпр < 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыУбыток) 
	|		КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Таблица.Договор КАК Договор,
	|		Таблица.Заказ КАК Заказ,
	|		Таблица.Валюта КАК Валюта,
	|		ВЫБОР
	|			КОГДА Таблица.СуммаРегл > 0 ИЛИ Таблица.СуммаУпр > 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСКлиентами)
	|			КОГДА Таблица.СуммаРегл < 0 ИЛИ Таблица.СуммаУпр < 0
	|				ТОГДА НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК АналитикаДоходов,
	|		ВЫБОР
	|			КОГДА Таблица.СуммаРегл > 0 ИЛИ Таблица.СуммаУпр > 0
	|				ТОГДА НЕОПРЕДЕЛЕНО
	|			КОГДА Таблица.СуммаРегл < 0 ИЛИ Таблица.СуммаУпр < 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСКлиентами)
	|		КОНЕЦ КАК АналитикаРасходов,
	|		ВЫБОР
	|			КОГДА Таблица.СуммаРегл > 0 ИЛИ Таблица.СуммаУпр > 0
	|				ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы) 
	|			КОГДА Таблица.СуммаРегл < 0 ИЛИ Таблица.СуммаУпр < 0
	|				ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы) 
	|		КОНЕЦ КАК СтатьяДоходовРасходов,
	|		ВЫБОР КОГДА Таблица.СуммаУпр < 0
	|			ТОГДА -Таблица.СуммаУпр
	|			ИНАЧЕ Таблица.СуммаУпр
	|		КОНЕЦ КАК СуммаУпр,
	|		ВЫБОР КОГДА Таблица.СуммаРегл < 0
	|			ТОГДА -Таблица.СуммаРегл
	|			ИНАЧЕ Таблица.СуммаРегл
	|		КОНЕЦ КАК СуммаРегл
	|	ИЗ
	|		РасчетыСКлиентамиПоОбъектамРасчета КАК Таблица
	|	ГДЕ
	|		Таблица.СуммаРегл <> 0 ИЛИ Таблица.СуммаУпр <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		ВЫБОР
	|			КОГДА Таблица.ПредоплатаРегл > 0 ИЛИ Таблица.ПредоплатаУпр > 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыПрибыль)
	|			КОГДА Таблица.ПредоплатаРегл < 0 ИЛИ Таблица.ПредоплатаУпр < 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыУбыток) 
	|		КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Таблица.Договор КАК Договор,
	|		Таблица.Заказ КАК Заказ,
	|		Таблица.Валюта КАК Валюта,
	|		ВЫБОР
	|			КОГДА Таблица.ПредоплатаРегл > 0 ИЛИ Таблица.ПредоплатаУпр > 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСКлиентами)
	|			КОГДА Таблица.ПредоплатаРегл < 0 ИЛИ Таблица.ПредоплатаУпр < 0
	|				ТОГДА НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК АналитикаДоходов,
	|		ВЫБОР
	|			КОГДА Таблица.ПредоплатаРегл > 0 ИЛИ Таблица.ПредоплатаУпр > 0
	|				ТОГДА НЕОПРЕДЕЛЕНО
	|			КОГДА Таблица.ПредоплатаРегл < 0 ИЛИ Таблица.ПредоплатаУпр < 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.РасчетыСКлиентами)
	|		КОНЕЦ КАК АналитикаРасходов,
	|		ВЫБОР
	|			КОГДА Таблица.ПредоплатаРегл > 0 ИЛИ Таблица.ПредоплатаУпр > 0
	|				ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|			КОГДА Таблица.ПредоплатаРегл < 0 ИЛИ Таблица.ПредоплатаУпр < 0
	|				ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		КОНЕЦ КАК СтатьяДоходовРасходов,
	|		ВЫБОР КОГДА Таблица.ПредоплатаУпр < 0
	|			ТОГДА -Таблица.ПредоплатаУпр
	|			ИНАЧЕ Таблица.ПредоплатаУпр
	|		КОНЕЦ КАК СуммаУпр,
	|		ВЫБОР КОГДА Таблица.ПредоплатаРегл < 0
	|			ТОГДА -Таблица.ПредоплатаРегл
	|			ИНАЧЕ Таблица.ПредоплатаРегл
	|		КОНЕЦ КАК СуммаРегл
	|	ИЗ
	|		РасчетыСКлиентамиПоОбъектамРасчета КАК Таблица
	|	ГДЕ
	|		Таблица.ПредоплатаРегл <> 0 ИЛИ Таблица.ПредоплатаУпр <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКредитыИЗаймыПрибыль) 
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКредитыИЗаймыУбыток) 
	|		КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Таблица.Договор КАК Договор,
	|		НЕОПРЕДЕЛЕНО КАК Заказ,
	|		Таблица.Валюта КАК Валюта,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.КредитыИДепозиты) 
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО 
	|		КОНЕЦ КАК АналитикаДоходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА НЕОПРЕДЕЛЕНО 
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.КредитыИДепозиты) 
	|		КОНЕЦ КАК АналитикаРасходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы) 
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы) 
	|		КОНЕЦ КАК СтатьяДоходовРасходов,
	|		Таблица.СуммаУпр КАК СуммаУпр,
	|		Таблица.СуммаРегл КАК СуммаРегл
	|	ИЗ
	|		ТаблицаКредитыИДепозиты КАК Таблица
	|	ГДЕ
	|		Таблица.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерДоговораКредитовИДепозитов.КредитИлиЗайм)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыЗаймыВыданныеПрибыль) 
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыЗаймыВыданныеУбыток) 
	|		КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Таблица.Договор КАК Договор,
	|		НЕОПРЕДЕЛЕНО КАК Заказ,
	|		Таблица.Валюта КАК Валюта,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.КредитыИДепозиты) 
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО 
	|		КОНЕЦ КАК АналитикаДоходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА НЕОПРЕДЕЛЕНО 
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.КредитыИДепозиты) 
	|		КОНЕЦ КАК АналитикаРасходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы) 
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы) 
	|		КОНЕЦ КАК СтатьяДоходовРасходов,
	|		Таблица.СуммаУпр КАК СуммаУпр,
	|		Таблица.СуммаРегл КАК СуммаРегл
	|	ИЗ
	|		ТаблицаКредитыИДепозиты КАК Таблица
	|	ГДЕ
	|		Таблица.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерДоговораКредитовИДепозитов.ЗаймВыданный)
	|	) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитики
	|		ПО ВложенныйЗапрос.АналитикаУчетаПоПартнерам = РегистрАналитики.КлючАналитики
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Подразделение,
	|	РегистрАналитики.Контрагент,
	|	РегистрАналитики.Партнер,
	|	ВложенныйЗапрос.СтатьяДоходовРасходов,
	|	ВложенныйЗапрос.ХозяйственнаяОперация,
	|	ВложенныйЗапрос.НаправлениеДеятельности,
	|	ВложенныйЗапрос.АналитикаДоходов,
	|	ВложенныйЗапрос.АналитикаРасходов,
	|	ВложенныйЗапрос.Договор,
	|	ВложенныйЗапрос.Заказ,
	|	ВложенныйЗапрос.Валюта
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВложенныйЗапрос.Период";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияДенежныеСредстваДоходыРасходы(ТекстыЗапроса, Регистры = Неопределено)

	ИмяРегистра = "ДвиженияДенежныеСредстваДоходыРасходы";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	ДобавитьВременныеТаблицы(ТекстыЗапроса, "ДенежныеСредства");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|	ВложенныйЗапрос.Подразделение КАК ПодразделениеДоходовРасходов,
	|
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельностиДС,
	|	ВложенныйЗапрос.ДенежныеСредства КАК ДенежныеСредства,
	|	ВложенныйЗапрос.ТипДенежныхСредств КАК ТипДенежныхСредств,
	|	ВложенныйЗапрос.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ВложенныйЗапрос.Валюта КАК Валюта,
	|
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельностиСтатьи,
	|	ВложенныйЗапрос.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов,
	|	ВложенныйЗапрос.АналитикаДоходов КАК АналитикаДоходов,
	|	ВложенныйЗапрос.АналитикаРасходов КАК АналитикаРасходов,
	|
	|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.СуммаРегл) КАК СуммаРегл,
	|	0 КАК СуммаВВалюте,
	|	
	|	ВложенныйЗапрос.ИсточникГФУДенежныхСредств КАК ИсточникГФУДенежныхСредств,
	|	ВложенныйЗапрос.ИсточникГФУДоходовРасходов КАК ИсточникГФУДоходовРасходов
	|
	|ИЗ (
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль) 
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток) 
	|		КОНЕЦ КАК ХозяйственнаяОперация,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Таблица.СтатьяДвиженияДенежныхСредств,
	|
	|		Таблица.БанковскийСчет КАК ДенежныеСредства,
	|		Таблица.Подразделение КАК Подразделение,
	|		Значение(Перечисление.ТипыДенежныхСредств.Безналичные) КАК ТипДенежныхСредств,
	|		Таблица.БанковскийСчет.ВалютаДенежныхСредств КАК Валюта,
	|
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		КОНЕЦ КАК СтатьяДоходовРасходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства)
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК АналитикаДоходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства)
	|		КОНЕЦ КАК АналитикаРасходов,
	|
	|		Таблица.СуммаУпр КАК СуммаУпр,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|
	|		Таблица.БанковскийСчет КАК ИсточникГФУДенежныхСредств,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		КОНЕЦ КАК ИсточникГФУДоходовРасходов
	|	
	|	ИЗ
	|		ТаблицаДенежныеСредстваБезналичные КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток)
	|		КОНЕЦ КАК ХозяйственнаяОперация,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Таблица.СтатьяДвиженияДенежныхСредств,
	|
	|		Таблица.Касса КАК ДенежныеСредства,
	|		Таблица.Подразделение КАК Подразделение,
	|		Значение(Перечисление.ТипыДенежныхСредств.Наличные) КАК ТипДенежныхСредств,
	|		Таблица.Касса.ВалютаДенежныхСредств КАК Валюта,
	|
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		КОНЕЦ КАК СтатьяДоходовРасходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства)
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК АналитикаДоходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства)
	|		КОНЕЦ КАК АналитикаРасходов,
	|
	|		Таблица.СуммаУпр КАК СуммаУпр,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|
	|		Таблица.Касса КАК ИсточникГФУДенежныхСредств,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		КОНЕЦ КАК ИсточникГФУДоходовРасходов
	|	
	|	ИЗ
	|		ТаблицаДенежныеСредстваНаличные КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток)
	|		КОНЕЦ КАК ХозяйственнаяОперация,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль) 
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток) 
	|		КОНЕЦ КАК СтатьяДвиженияДенежныхСредств,
	|
	|		Таблица.КассаККМ КАК ДенежныеСредства,
	|		Таблица.Подразделение КАК Подразделение,
	|		Значение(Перечисление.ТипыДенежныхСредств.Наличные) КАК ТипДенежныхСредств,
	|		Таблица.КассаККМ.ВалютаДенежныхСредств КАК Валюта,
	|
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		КОНЕЦ КАК СтатьяДоходовРасходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства)
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК АналитикаДоходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства)
	|		КОНЕЦ КАК АналитикаРасходов,
	|
	|		Таблица.СуммаУпр КАК СуммаУпр,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|
	|		Таблица.КассаККМ КАК ИсточникГФУДенежныхСредств,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		КОНЕЦ КАК ИсточникГФУДоходовРасходов
	|	
	|	ИЗ
	|		ТаблицаДенежныеСредстваВКассахККМ КАК Таблица
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток)
	|		КОНЕЦ КАК ХозяйственнаяОперация,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Таблица.СтатьяДвиженияДенежныхСредств,
	|
	|		Таблица.ПодотчетноеЛицо КАК ДенежныеСредства,
	|		Таблица.Подразделение КАК Подразделение,
	|		Значение(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУПодотчетногоЛица) КАК ТипДенежныхСредств,
	|		Таблица.Валюта КАК Валюта,
	|
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		КОНЕЦ КАК СтатьяДоходовРасходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства)
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК АналитикаДоходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства)
	|		КОНЕЦ КАК АналитикаРасходов,
	|
	|		Таблица.СуммаУпр КАК СуммаУпр,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|
	|		Таблица.ПодотчетноеЛицо КАК ИсточникГФУДенежныхСредств,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		КОНЕЦ КАК ИсточникГФУДоходовРасходов
	|	
	|	ИЗ
	|		ТаблицаДенежныеСредстваУПодотчетныхЛиц КАК Таблица
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток)
	|		КОНЕЦ КАК ХозяйственнаяОперация,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Таблица.СтатьяДвиженияДенежныхСредств,
	|
	|		Таблица.Контрагент КАК ДенежныеСредства,
	|		Таблица.Подразделение КАК Подразделение,
	|		Значение(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваВПути) КАК ТипДенежныхСредств,
	|		Таблица.Валюта КАК Валюта,
	|
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		КОНЕЦ КАК СтатьяДоходовРасходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства)
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК АналитикаДоходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.ДенежныеСредства)
	|		КОНЕЦ КАК АналитикаРасходов,
	|
	|		Таблица.СуммаУпр КАК СуммаУпр,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|
	|		Таблица.Контрагент КАК ИсточникГФУДенежныхСредств,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		КОНЕЦ КАК ИсточникГФУДоходовРасходов
	|	
	|	ИЗ
	|		ТаблицаДенежныеСредстваВПути КАК Таблица
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДепозитыПрибыль)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДепозитыУбыток)
	|		КОНЕЦ КАК ХозяйственнаяОперация,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль) 
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток) 
	|		КОНЕЦ КАК СтатьяДвиженияДенежныхСредств,
	|
	|		Таблица.Договор КАК ДенежныеСредства,
	|		Таблица.Подразделение КАК Подразделение,
	|		Значение(Перечисление.ТипыДенежныхСредств.Депозиты) КАК ТипДенежныхСредств,
	|		Таблица.Валюта КАК Валюта,
	|
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		КОНЕЦ КАК СтатьяДоходовРасходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.КредитыИДепозиты)
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК АналитикаДоходов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.АналитикаКурсовыхРазниц.КредитыИДепозиты)
	|		КОНЕЦ КАК АналитикаРасходов,
	|
	|		Таблица.СуммаУпр КАК СуммаУпр,
	|		Таблица.СуммаРегл КАК СуммаРегл,
	|
	|		Таблица.Договор КАК ИсточникГФУДенежныхСредств,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы)
	|		КОНЕЦ КАК ИсточникГФУДоходовРасходов
	|	
	|	ИЗ
	|		ТаблицаКредитыИДепозиты КАК Таблица
	|	ГДЕ
	|		Таблица.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерДоговораКредитовИДепозитов.Депозит)
	|	) КАК ВложенныйЗапрос
	|
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|	И &ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаДенежныхСредств),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаКредитовИДепозитов))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.ХозяйственнаяОперация,
	|	ВложенныйЗапрос.НаправлениеДеятельности,
	|	ВложенныйЗапрос.СтатьяДвиженияДенежныхСредств,
	|	ВложенныйЗапрос.ДенежныеСредства,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.ТипДенежныхСредств,
	|	ВложенныйЗапрос.Валюта,
	|	ВложенныйЗапрос.СтатьяДоходовРасходов,
	|	ВложенныйЗапрос.АналитикаДоходов,
	|	ВложенныйЗапрос.АналитикаРасходов,
	|	ВложенныйЗапрос.ИсточникГФУДенежныхСредств,
	|	ВложенныйЗапрос.ИсточникГФУДоходовРасходов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВложенныйЗапрос.Период";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции


#КонецОбласти

#Область РасчетКурсовыхРазниц

Процедура ВыполнитьРасчетКурсовыхРазниц(ДокументСсылка, ИменаРегистров, АналитикаРасчета = Неопределено, КоличествоОбработанныхЭлементов = 0)
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	Запрос.УстановитьПараметр("ИменаРегистров", ИменаРегистров);
	Если ЗначениеЗаполнено(АналитикаРасчета) Тогда
		КлючиОрганизации = ПолучитьСсылкиСправочника("КлючиАналитикиУчетаПоПартнерам", АналитикаРасчета, Запрос.Параметры.Организация);
		Если ЗначениеЗаполнено(КлючиОрганизации) Тогда
			Запрос.УстановитьПараметр("АналитикаРасчета", КлючиОрганизации);
		КонецЕсли;
		Кассы = ПолучитьСсылкиСправочника("Кассы", АналитикаРасчета);
		Если ЗначениеЗаполнено(Кассы) Тогда
			Запрос.УстановитьПараметр("Кассы", Кассы);
			Запрос.УстановитьПараметр("ПоВсемКассам", НЕ ЗначениеЗаполнено(Кассы));
		КонецЕсли;
		ФизическиеЛица = ПолучитьСсылкиСправочника("ФизическиеЛица", АналитикаРасчета);
		Если ЗначениеЗаполнено(ФизическиеЛица) Тогда
			Запрос.УстановитьПараметр("ПодотчетныеЛица", ФизическиеЛица);
			Запрос.УстановитьПараметр("ПоВсемПодотчетнымЛицам", НЕ ЗначениеЗаполнено(ФизическиеЛица));
		КонецЕсли;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		ПодготовитьНаборыЗаписейКРасчету(ДокументСсылка, ИменаРегистров, АналитикаРасчета);
		РассчитатьКурсовыеРазницы(Запрос.Параметры, КоличествоОбработанныхЭлементов);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ТекстСообщения = НСтр("ru = 'Не удалось выполнить расчет курсовых разниц по причине: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Расчет курсовых разниц'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			ДокументСсылка.Метаданные(),
			ДокументСсылка,
			ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры

Процедура РассчитатьКурсовыеРазницы(Параметры, КоличествоОбработанныхЭлементов = 0)
	
	ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(НСтр("ru='Расчет курсовых разниц'"), Параметры.ИменаРегистров);
	
	РасчетКурсовыхРазниц = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("Документ.РасчетКурсовыхРазниц.МодульМенеджера.РасчетКурсовыхРазниц");
	
	Запрос = ИнициализироватьЗапросКурсовыхРазниц(Параметры);
	
	// Определим дни в которые необходимо переоценить остатки
	ДниПереоценки = ДниПереоценки(Параметры);
	Для Каждого День Из ДниПереоценки Цикл
		
		МенеджерТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос.МенеджерВременныхТаблиц = МенеджерТаблиц;
		Запрос.УстановитьПараметр("НачалоПериода", День.Дата);
		Запрос.УстановитьПараметр("КонецПериода" , КонецДня(День.Дата));
		Запрос.УстановитьПараметр("ГраницаНачалоПериода", Новый Граница(День.Дата, ВидГраницы.Включая));
		Запрос.УстановитьПараметр("ГраницаКонецПериода" , Новый Граница(КонецДня(День.Дата), ВидГраницы.Включая));
		
		Запрос.ВыполнитьПакет();
		
		Для Каждого Регистр Из Параметры.ИменаРегистров Цикл
			ВсегоЗаписано = ЗаписатьДвижения(Параметры.Ссылка, МенеджерТаблиц, Регистр.Ключ);
			КоличествоОбработанныхЭлементов = КоличествоОбработанныхЭлементов + ВсегоЗаписано;
		КонецЦикла;
		
		МенеджерТаблиц.Закрыть();
		
	КонецЦикла;
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(РасчетКурсовыхРазниц, КоличествоОбработанныхЭлементов/1000);
	
КонецПроцедуры

// Определяет дни в которые необходимо переоценить текущие остатки для заданной хоз. операции:
// - для переоценки "На дату оплаты и на конец месяца" - дни в которые были оплаты в валюте и последнее число месяца.
// - для переоценки "На каждый день месяца" - дни в которые были оплаты в валюте и дни в которые менялся курс.
//
// Параметры:
//  <Параметры>  - структура с обязательными ключами:
//                 НачалоПериода - <Дата> - задает начало периода поиска дат переоценки.
//                 КонецПериода - <Дата> - задает конец периода поиска дат переоценки.
//                 Организация - <Справочник.Организации> - организация по которой необходимо переоценивать остатки.
//                 ПереоценкаПоДням - <Булево> - Истина - датами переоценки являются все даты изменения курсов валют и даты оплат, иначе только даты оплаты.
//                 ХозяйственнаяОперация - <Перечисление.ХозяйственныеОперации> - задает регистр накопления о которому определяются даты оплаты.
//                 АналитикаРасчета - <Массив> - ключи аналитики организации по которой необходимо переоценивать остатки.
//
// Возвращаемое значение:
//   <ТаблицаЗначений> - с одной колонкой "Дата" типа <Дата>.
//
Функция ДниПереоценки(Параметры)
	
	ОбъединитьВсе = "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|";
	
	ТекстЗапроса = "";
	Если Параметры.ИменаРегистров.Свойство("РасчетыСПоставщикамиПоДокументам") Тогда
		ПервыйЗапрос = ПустаяСтрока(ТекстЗапроса);
		ТекстЗапроса = ТекстЗапроса + ?(ПервыйЗапрос,"",ОбъединитьВсе) +
		"// Оплаты поставщикам в валюте
		|ВЫБРАТЬ
		|	Расчеты.Валюта,
		|	Расчеты.Период
		|//ПОМЕСТИТЬ втОборотыВалюты
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК Расчеты
		|ГДЕ
		|	Расчеты.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И (Расчеты.Валюта <> &ВалютаУправленческогоУчета ИЛИ Расчеты.Валюта <> &ВалютаРегламентированногоУчета)
		|	И (Расчеты.ХозяйственнаяОперация В (&ОплатаПоставщику) ИЛИ &ПереоценкаПоДням)
		|	И Расчеты.АналитикаУчетаПоПартнерам В (&АналитикаРасчета)
		|	И Расчеты.Долг <> 0
		|";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОМЕСТИТЬ втОборотыВалюты",?(ПервыйЗапрос,"ПОМЕСТИТЬ втОборотыВалюты",""));
	КонецЕсли;
	
	Если Параметры.ИменаРегистров.Свойство("РасчетыСКлиентамиПоДокументам") Тогда
		ПервыйЗапрос = ПустаяСтрока(ТекстЗапроса);
		ТекстЗапроса = ТекстЗапроса + ?(ПервыйЗапрос,"",ОбъединитьВсе) +
		"// Оплаты клиентов в валюте
		|ВЫБРАТЬ
		|	Расчеты.Валюта,
		|	Расчеты.Период
		|//ПОМЕСТИТЬ втОборотыВалюты
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
		|ГДЕ
		|	Расчеты.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И (Расчеты.Валюта <> &ВалютаУправленческогоУчета ИЛИ Расчеты.Валюта <> &ВалютаРегламентированногоУчета)
		|	И (Расчеты.ХозяйственнаяОперация В (&ОплатаКлиента) ИЛИ &ПереоценкаПоДням)
		|	И Расчеты.АналитикаУчетаПоПартнерам В (&АналитикаРасчета)
		|	И Расчеты.Долг <> 0
		|";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОМЕСТИТЬ втОборотыВалюты",?(ПервыйЗапрос,"ПОМЕСТИТЬ втОборотыВалюты",""));
	КонецЕсли;
	
	Если Параметры.ИменаРегистров.Свойство("ДенежныеСредстваБезналичные") Тогда
		ПервыйЗапрос = ПустаяСтрока(ТекстЗапроса);
		ТекстЗапроса = ТекстЗапроса + ?(ПервыйЗапрос,"",ОбъединитьВсе) +
		"// Безналичные денежные средства
		|ВЫБРАТЬ
		|	БезналичныеДС.БанковскийСчет.ВалютаДенежныхСредств КАК Валюта,
		|	БезналичныеДС.Период
		|//ПОМЕСТИТЬ втОборотыВалюты
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваБезналичные.Обороты(
		|			&ГраницаНачалоПериода,
		|			&ГраницаКонецПериода,
		|			ДЕНЬ,
		|			Организация = &Организация
		|				И (БанковскийСчет.ВалютаДенежныхСредств <> &ВалютаУправленческогоУчета
		|					ИЛИ БанковскийСчет.ВалютаДенежныхСредств <> &ВалютаРегламентированногоУчета)
		|	) КАК БезналичныеДС
		|";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОМЕСТИТЬ втОборотыВалюты",?(ПервыйЗапрос,"ПОМЕСТИТЬ втОборотыВалюты",""));
	КонецЕсли;
	
	Если Параметры.ИменаРегистров.Свойство("ДенежныеСредстваНаличные") Тогда
		ПервыйЗапрос = ПустаяСтрока(ТекстЗапроса);
		ТекстЗапроса = ТекстЗапроса + ?(ПервыйЗапрос,"",ОбъединитьВсе) +
		"// Наличные денежные средства
		|ВЫБРАТЬ
		|	НаличныеДС.Касса.ВалютаДенежныхСредств КАК Валюта,
		|	НаличныеДС.Период
		|//ПОМЕСТИТЬ втОборотыВалюты
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные.Обороты(
		|			&ГраницаНачалоПериода,
		|			&ГраницаКонецПериода,
		|			ДЕНЬ,
		|			Организация = &Организация
		|				И (Касса В (&Кассы) ИЛИ &ПоВсемКассам)
		|				И (Касса.ВалютаДенежныхСредств <> &ВалютаУправленческогоУчета
		|					ИЛИ Касса.ВалютаДенежныхСредств <> &ВалютаРегламентированногоУчета)
		|	) КАК НаличныеДС
		|";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОМЕСТИТЬ втОборотыВалюты",?(ПервыйЗапрос,"ПОМЕСТИТЬ втОборотыВалюты",""));
	КонецЕсли;
	
	Если Параметры.ИменаРегистров.Свойство("ДенежныеСредстваВКассахККМ") Тогда
		ПервыйЗапрос = ПустаяСтрока(ТекстЗапроса);
		ТекстЗапроса = ТекстЗапроса + ?(ПервыйЗапрос,"",ОбъединитьВсе) +
		"// Денежные средства в кассах ККМ
		|ВЫБРАТЬ
		|	ДСвКассахККМ.КассаККМ.ВалютаДенежныхСредств КАК Валюта,
		|	ДСвКассахККМ.Период
		|//ПОМЕСТИТЬ втОборотыВалюты
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваВКассахККМ.Обороты(
		|			&ГраницаНачалоПериода,
		|			&ГраницаКонецПериода,
		|			ДЕНЬ,
		|			Организация = &Организация
		|				И (КассаККМ.ВалютаДенежныхСредств <> &ВалютаУправленческогоУчета
		|					ИЛИ КассаККМ.ВалютаДенежныхСредств <> &ВалютаРегламентированногоУчета)
		|	) КАК ДСвКассахККМ
		|";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОМЕСТИТЬ втОборотыВалюты",?(ПервыйЗапрос,"ПОМЕСТИТЬ втОборотыВалюты",""));
	КонецЕсли;
	
	Если Параметры.ИменаРегистров.Свойство("ДенежныеСредстваВПути") Тогда
		ПервыйЗапрос = ПустаяСтрока(ТекстЗапроса);
		ТекстЗапроса = ТекстЗапроса + ?(ПервыйЗапрос,"",ОбъединитьВсе) +
		"// Денежные средства в пути
		|ВЫБРАТЬ
		|	ДСвПути.Валюта КАК Валюта,
		|	ДСвПути.Период
		|//ПОМЕСТИТЬ втОборотыВалюты
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваВПути.Обороты(
		|			&ГраницаНачалоПериода,
		|			&ГраницаКонецПериода,
		|			ДЕНЬ,
		|			Организация = &Организация
		|				И (Валюта <> &ВалютаУправленческогоУчета ИЛИ Валюта <> &ВалютаРегламентированногоУчета)
		|	) КАК ДСвПути
		|";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОМЕСТИТЬ втОборотыВалюты",?(ПервыйЗапрос,"ПОМЕСТИТЬ втОборотыВалюты",""));
	КонецЕсли;
	
	Если Параметры.ИменаРегистров.Свойство("ДенежныеСредстваУПодотчетныхЛиц") Тогда
		ПервыйЗапрос = ПустаяСтрока(ТекстЗапроса);
		ТекстЗапроса = ТекстЗапроса + ?(ПервыйЗапрос,"",ОбъединитьВсе) +
		"// Денежные средства у подотчетных лиц
		|ВЫБРАТЬ
		|	ДСуПодотчетныхЛиц.Валюта КАК Валюта,
		|	ДСуПодотчетныхЛиц.Период
		|//ПОМЕСТИТЬ втОборотыВалюты
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.Обороты(
		|			&ГраницаНачалоПериода,
		|			&ГраницаКонецПериода,
		|			ДЕНЬ,
		|			Организация = &Организация
		|				И (ПодотчетноеЛицо В (&ПодотчетныеЛица) ИЛИ &ПоВсемПодотчетнымЛицам)
		|				И (Валюта <> &ВалютаУправленческогоУчета ИЛИ Валюта <> &ВалютаРегламентированногоУчета)
		|	) КАК ДСуПодотчетныхЛиц
		|";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОМЕСТИТЬ втОборотыВалюты",?(ПервыйЗапрос,"ПОМЕСТИТЬ втОборотыВалюты",""));
	КонецЕсли;
	
	Если Параметры.ИменаРегистров.Свойство("РасчетыПоДоговорамКредитовИДепозитов") Тогда
		ПервыйЗапрос = ПустаяСтрока(ТекстЗапроса);
		ТекстЗапроса = ТекстЗапроса + ?(ПервыйЗапрос,"",ОбъединитьВсе) +
		"ВЫБРАТЬ
		|	РасчетыПоКредитамДепозитам.Договор.ВалютаВзаиморасчетов КАК Валюта,
		|	РасчетыПоКредитамДепозитам.Период
		|//ПОМЕСТИТЬ втОборотыВалюты
		|ИЗ
		|	РегистрНакопления.РасчетыПоДоговорамКредитовИДепозитов.Обороты(
		|			&ГраницаНачалоПериода,
		|			&ГраницаКонецПериода,
		|			ДЕНЬ,
		|			АналитикаУчетаПоПартнерам.Организация = &Организация
		|				И (Договор.ВалютаВзаиморасчетов <> &ВалютаУправленческогоУчета
		|					ИЛИ Договор.ВалютаВзаиморасчетов <> &ВалютаРегламентированногоУчета)
		|				И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаКредитовИДепозитов)
		|	) КАК РасчетыПоКредитамДепозитам";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"//ПОМЕСТИТЬ втОборотыВалюты",?(ПервыйЗапрос,"ПОМЕСТИТЬ втОборотыВалюты",""));
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + 
	"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(втОборотыВалюты.Период, ДЕНЬ) КАК Дата
	|ИЗ
	|	втОборотыВалюты КАК втОборотыВалюты
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КурсыВалют.Период
	|ИЗ
	|	РегистрСведений.КурсыВалют КАК КурсыВалют
	|ГДЕ
	|	КурсыВалют.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И &ПереоценкаПоДням
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(&КонецПериода, ДЕНЬ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Для Каждого Параметр Из Параметры Цикл
		Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	Запрос.УстановитьПараметр("ГраницаНачалоПериода", Новый Граница(Параметры.НачалоПериода, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ГраницаКонецПериода",  Новый Граница(Параметры.КонецПериода,  ВидГраницы.Включая));
	
	Выборка = Запрос.Выполнить().Выгрузить();
	Возврат Выборка;
	
КонецФункции

Функция ИнициализироватьЗапросКурсовыхРазниц(Параметры)
	
	ТекстыЗапросов = Новый Массив;
	Для Каждого Регистр Из Параметры.ИменаРегистров Цикл
		Если Регистр.Ключ = "РасчетыСПоставщикамиПоДокументам" Тогда
			ТекстыЗапросов.Добавить(ТекстЗапросаКурсовыеРазницыРасчетыСПоставщикамиПоДокументам());
		ИначеЕсли Регистр.Ключ = "РасчетыСКлиентамиПоДокументам" Тогда
			ТекстыЗапросов.Добавить(ТекстЗапросаКурсовыеРазницыРасчетыСКлиентамиПоДокументам());
		ИначеЕсли Регистр.Ключ = "ДенежныеСредстваБезналичные" Тогда
			ТекстыЗапросов.Добавить(ТекстЗапросаКурсовыеРазницыДенежныеСредстваБезналичные());
		ИначеЕсли Регистр.Ключ = "ДенежныеСредстваНаличные" Тогда
			ТекстыЗапросов.Добавить(ТекстЗапросаКурсовыеРазницыДенежныеСредстваНаличные());
		ИначеЕсли Регистр.Ключ = "ДенежныеСредстваВКассахККМ" Тогда
			ТекстыЗапросов.Добавить(ТекстЗапросаКурсовыеРазницыДенежныеСредстваВКассахККМ());
		ИначеЕсли Регистр.Ключ = "ДенежныеСредстваУПодотчетныхЛиц" Тогда
			ТекстыЗапросов.Добавить(ТекстЗапросаКурсовыеРазницыДенежныеСредстваУПодотчетныхЛиц());
		ИначеЕсли Регистр.Ключ = "ДенежныеСредстваВПути" Тогда
			ТекстыЗапросов.Добавить(ТекстЗапросаКурсовыеРазницыДенежныеСредстваВПути());
		ИначеЕсли Регистр.Ключ = "РасчетыПоДоговорамКредитовИДепозитов" Тогда
			ТекстыЗапросов.Добавить(ТекстЗапросаКурсовыеРазницыРасчетыПоДоговорамКредитовИДепозитов());
		КонецЕсли;
	КонецЦикла;
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросов);
	Запрос = Новый Запрос(ТекстЗапроса);
	Для Каждого Параметр Из Параметры Цикл
		Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	Возврат Запрос;
	
КонецФункции

Функция ТекстЗапросаКурсовыеРазницыРасчетыСПоставщикамиПоДокументам()
	
	Возврат 
	"
	|////////////////////////////////////////////////////////////////////////////////
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.АналитикаУчетаПоПартнерам КАК КлючАналитики,
	|	Расчеты.ЗаказПоставщику КАК ОбъектРасчетов
	|ПОМЕСТИТЬ втАналитикаЗаДень
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК Расчеты
	|ГДЕ
	|	Расчеты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И (Расчеты.Валюта <> &ВалютаУправленческогоУчета ИЛИ Расчеты.Валюта <> &ВалютаРегламентированногоУчета)
	|	И Расчеты.ХозяйственнаяОперация В (&ОплатаПоставщику)
	|	И Расчеты.АналитикаУчетаПоПартнерам В (&АналитикаРасчета)
	|	И Расчеты.Долг <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиДня.Валюта КАК Валюта,
	|	&КонецПериода КАК Период,
	|	ОстаткиДня.АналитикаУчетаПоПартнерам,
	|	ОстаткиДня.ЗаказПоставщику КАК Заказ,
	|	ОстаткиДня.РасчетныйДокумент,
	|	ОстаткиДня.ДолгОстаток КАК Долг,
	|	ОстаткиДня.ДолгРеглОстаток КАК ДолгРегл,
	|	ОстаткиДня.ДолгУпрОстаток КАК ДолгУпр,
	|	0 КАК ПредоплатаРегл,
	|	0 КАК ПредоплатаУпр
	|ПОМЕСТИТЬ втОстакиНаКонецДня
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам.Остатки(
	|			&ГраницаКонецПериода,
	|			АналитикаУчетаПоПартнерам В (&АналитикаРасчета)
	|				И (Валюта <> &ВалютаУправленческогоУчета ИЛИ Валюта <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиДня
	|	ЛЕВОЕ СОЕДИНЕНИЕ втАналитикаЗаДень КАК АналитикаДня
	|		ПО ОстаткиДня.АналитикаУчетаПоПартнерам = АналитикаДня.КлючАналитики
	|			И ОстаткиДня.ЗаказПоставщику = АналитикаДня.ОбъектРасчетов
	|ГДЕ
	|	ОстаткиДня.ДолгОстаток <> 0
	|	И (НЕ АналитикаДня.КлючАналитики ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиДня.Валюта,
	|	&КонецПериода,
	|	ОстаткиДня.АналитикаУчетаПоПартнерам,
	|	ОстаткиДня.ЗаказПоставщику,
	|	ОстаткиДня.РасчетныйДокумент,
	|	ОстаткиДня.ДолгОстаток,
	|	ОстаткиДня.ДолгРеглОстаток,
	|	ОстаткиДня.ДолгУпрОстаток,
	|	0,
	|	0
	|
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам.Остатки(
	|			&ГраницаКонецПериода,
	|			АналитикаУчетаПоПартнерам В (&АналитикаРасчета)
	|				И (Валюта <> &ВалютаУправленческогоУчета ИЛИ Валюта <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиДня
	|	ЛЕВОЕ СОЕДИНЕНИЕ втАналитикаЗаДень КАК АналитикаДня
	|		ПО ОстаткиДня.АналитикаУчетаПоПартнерам = АналитикаДня.КлючАналитики
	|			И ОстаткиДня.ЗаказПоставщику = АналитикаДня.ОбъектРасчетов
	|ГДЕ
	|	ОстаткиДня.ДолгОстаток = 0
	|	И (ОстаткиДня.ДолгРеглОстаток <> 0 ИЛИ ОстаткиДня.ДолгУпрОстаток <> 0)
	|	И (НЕ АналитикаДня.КлючАналитики ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиДня.Валюта,
	|	&КонецПериода,
	|	ОстаткиДня.АналитикаУчетаПоПартнерам,
	|	ОстаткиДня.ЗаказПоставщику,
	|	ОстаткиДня.РасчетныйДокумент,
	|	0,
	|	0,
	|	0,
	|	ОстаткиДня.ПредоплатаРеглОстаток,
	|	ОстаткиДня.ПредоплатаУпрОстаток
	|
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам.Остатки(
	|			&ГраницаКонецПериода,
	|			АналитикаУчетаПоПартнерам В (&АналитикаРасчета)
	|				И (Валюта <> &ВалютаУправленческогоУчета ИЛИ Валюта <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиДня
	|ГДЕ
	|	ОстаткиДня.КВозвратуОстаток = 0
	|	И ОстаткиДня.ПредоплатаОстаток = 0
	|	И (ОстаткиДня.ПредоплатаРеглОстаток <> 0 ИЛИ ОстаткиДня.ПредоплатаУпрОстаток <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Валюта КАК Валюта,
	|	&КонецПериода КАК Период,
	|	Остатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Остатки.Заказ КАК Заказ,
	|	Остатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	(ВЫРАЗИТЬ(Остатки.Долг * КурсВалюты.Курс / КурсВалюты.Кратность КАК ЧИСЛО(15, 2))) - Остатки.ДолгРегл КАК ДолгРегл,
	|	(ВЫРАЗИТЬ(Остатки.Долг * КурсВалюты.Курс / КурсВалюты.Кратность / (КурсВалютыУпр.Курс * КурсВалютыУпр.Кратность) КАК ЧИСЛО(15, 2))) - Остатки.ДолгУпр КАК ДолгУпр
	|
	|ПОМЕСТИТЬ втНеРеализованныеКР
	|ИЗ
	|	втОстакиНаКонецДня КАК Остатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, ) КАК КурсВалюты
	|			ПО Остатки.Валюта = КурсВалюты.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, Валюта = &ВалютаУправленческогоУчета) КАК КурсВалютыУпр
	|			ПО (ИСТИНА)
	|ГДЕ
	|	Остатки.Долг <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Валюта КАК Валюта,
	|	ВЫБОР КОГДА Остатки.ДолгРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ КАК ВидДвижения,
	|	&КонецПериода КАК Период,
	|	Остатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Остатки.Заказ КАК ЗаказПоставщику,
	|	Остатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ВЫБОР КОГДА Остатки.ДолгРегл < 0
	|		ТОГДА -Остатки.ДолгРегл
	|		ИНАЧЕ Остатки.ДолгРегл
	|	КОНЕЦ КАК ДолгРегл,
	|	0 КАК ДолгУпр,
	|	0 КАК ПредоплатаРегл,
	|	0 КАК ПредоплатаУпр,
	|	ВЫБОР КОГДА Остатки.ДолгРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиУбыток)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиПрибыль)
	|	КОНЕЦ КАК ХозяйственнаяОперация
	|ПОМЕСТИТЬ ТаблицаРасчетов
	|ИЗ
	|	втОстакиНаКонецДня КАК Остатки
	|ГДЕ
	|	Остатки.Долг = 0 И Остатки.ДолгРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР КОГДА Остатки.ДолгУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.АналитикаУчетаПоПартнерам,
	|	Остатки.Заказ,
	|	Остатки.РасчетныйДокумент,
	|	0,
	|	ВЫБОР КОГДА Остатки.ДолгУпр < 0
	|		ТОГДА -Остатки.ДолгУпр
	|		ИНАЧЕ Остатки.ДолгУпр
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР КОГДА Остатки.ДолгУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиУбыток)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиПрибыль)
	|	КОНЕЦ
	|ИЗ
	|	втОстакиНаКонецДня КАК Остатки
	|ГДЕ
	|	Остатки.Долг = 0 И Остатки.ДолгУпр <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР КОГДА Остатки.ПредоплатаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.АналитикаУчетаПоПартнерам,
	|	Остатки.Заказ,
	|	Остатки.РасчетныйДокумент,
	|	0,
	|	0,
	|	ВЫБОР КОГДА Остатки.ПредоплатаРегл < 0
	|		ТОГДА -Остатки.ПредоплатаРегл
	|		ИНАЧЕ Остатки.ПредоплатаРегл
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР КОГДА Остатки.ПредоплатаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиУбыток)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиПрибыль)
	|	КОНЕЦ
	|ИЗ
	|	втОстакиНаКонецДня КАК Остатки
	|ГДЕ
	|	Остатки.ПредоплатаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР КОГДА Остатки.ПредоплатаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.АналитикаУчетаПоПартнерам,
	|	Остатки.Заказ,
	|	Остатки.РасчетныйДокумент,
	|	0,
	|	0,
	|	0,
	|	ВЫБОР КОГДА Остатки.ПредоплатаУпр < 0
	|		ТОГДА -Остатки.ПредоплатаУпр
	|		ИНАЧЕ Остатки.ПредоплатаУпр
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Остатки.ПредоплатаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиУбыток)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиПрибыль)
	|	КОНЕЦ
	|ИЗ
	|	втОстакиНаКонецДня КАК Остатки
	|ГДЕ
	|	Остатки.ПредоплатаУпр <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР
	|		КОГДА Остатки.ДолгРегл < 0
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.АналитикаУчетаПоПартнерам,
	|	Остатки.Заказ,
	|	Остатки.РасчетныйДокумент,
	|	ВЫБОР КОГДА Остатки.ДолгРегл < 0
	|		ТОГДА -Остатки.ДолгРегл
	|		ИНАЧЕ Остатки.ДолгРегл
	|	КОНЕЦ,
	|	0,
	|	0,
	|	0,
	|	ВЫБОР КОГДА Остатки.ДолгРегл < 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиУбыток)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиПрибыль)
	|	КОНЕЦ
	|ИЗ
	|	втНеРеализованныеКР КАК Остатки
	|ГДЕ
	|	Остатки.ДолгРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР КОГДА Остатки.ДолгУпр < 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.АналитикаУчетаПоПартнерам,
	|	Остатки.Заказ,
	|	Остатки.РасчетныйДокумент,
	|	0,
	|	ВЫБОР КОГДА Остатки.ДолгУпр < 0
	|		ТОГДА -Остатки.ДолгУпр
	|		ИНАЧЕ Остатки.ДолгУпр
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР КОГДА Остатки.ДолгУпр < 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиУбыток)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыПоставщикиПрибыль)
	|	КОНЕЦ
	|ИЗ
	|	втНеРеализованныеКР КАК Остатки
	|ГДЕ
	|	Остатки.ДолгУпр <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРасчетов.Валюта,
	|	ТаблицаРасчетов.ВидДвижения,
	|	ТаблицаРасчетов.Период КАК Период,
	|	ТаблицаРасчетов.АналитикаУчетаПоПартнерам,
	|	ТаблицаРасчетов.ЗаказПоставщику,
	|	ТаблицаРасчетов.РасчетныйДокумент,
	|	СУММА(ТаблицаРасчетов.ДолгРегл) КАК ДолгРегл,
	|	СУММА(ТаблицаРасчетов.ДолгУпр) КАК ДолгУпр,
	|	СУММА(ТаблицаРасчетов.ПредоплатаРегл) КАК ПредоплатаРегл,
	|	СУММА(ТаблицаРасчетов.ПредоплатаУпр) КАК ПредоплатаУпр,
	|	ТаблицаРасчетов.ХозяйственнаяОперация
	|ПОМЕСТИТЬ КурсовыеРазницыРасчетыСПоставщикамиПоДокументам
	|ИЗ
	|	ТаблицаРасчетов КАК ТаблицаРасчетов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасчетов.Период,
	|	ТаблицаРасчетов.ХозяйственнаяОперация,
	|	ТаблицаРасчетов.АналитикаУчетаПоПартнерам,
	|	ТаблицаРасчетов.РасчетныйДокумент,
	|	ТаблицаРасчетов.ЗаказПоставщику,
	|	ТаблицаРасчетов.Валюта,
	|	ТаблицаРасчетов.ВидДвижения"
	+"
	|;
	|";
	
КонецФункции

Функция ТекстЗапросаКурсовыеРазницыРасчетыСКлиентамиПоДокументам()
	
	Возврат 
	"
	|////////////////////////////////////////////////////////////////////////////////
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.АналитикаУчетаПоПартнерам КАК КлючАналитики,
	|	Расчеты.ЗаказКлиента КАК ОбъектРасчетов
	|ПОМЕСТИТЬ втАналитикаЗаДень
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
	|ГДЕ
	|	Расчеты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И (Расчеты.Валюта <> &ВалютаУправленческогоУчета ИЛИ Расчеты.Валюта <> &ВалютаРегламентированногоУчета)
	|	И Расчеты.ХозяйственнаяОперация В (&ОплатаКлиента)
	|	И Расчеты.АналитикаУчетаПоПартнерам В (&АналитикаРасчета)
	|	И Расчеты.Долг <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиДня.Валюта КАК Валюта,
	|	&КонецПериода КАК Период,
	|	ОстаткиДня.АналитикаУчетаПоПартнерам,
	|	ОстаткиДня.ЗаказКлиента КАК Заказ,
	|	ОстаткиДня.РасчетныйДокумент,
	|	ОстаткиДня.ДолгОстаток КАК Долг,
	|	ОстаткиДня.ДолгРеглОстаток КАК ДолгРегл,
	|	ОстаткиДня.ДолгУпрОстаток КАК ДолгУпр,
	|	0 КАК ПредоплатаРегл,
	|	0 КАК ПредоплатаУпр
	|ПОМЕСТИТЬ втОстакиНаКонецДня
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам.Остатки(
	|			&ГраницаКонецПериода,
	|			АналитикаУчетаПоПартнерам В (&АналитикаРасчета)
	|				И (Валюта <> &ВалютаУправленческогоУчета ИЛИ Валюта <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиДня
	|	ЛЕВОЕ СОЕДИНЕНИЕ втАналитикаЗаДень КАК АналитикаДня
	|		ПО ОстаткиДня.АналитикаУчетаПоПартнерам = АналитикаДня.КлючАналитики
	|		И ОстаткиДня.ЗаказКлиента = АналитикаДня.ОбъектРасчетов
	|ГДЕ
	|	ОстаткиДня.ДолгОстаток <> 0
	|	И (НЕ АналитикаДня.КлючАналитики ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиДня.Валюта,
	|	&КонецПериода,
	|	ОстаткиДня.АналитикаУчетаПоПартнерам,
	|	ОстаткиДня.ЗаказКлиента,
	|	ОстаткиДня.РасчетныйДокумент,
	|	ОстаткиДня.ДолгОстаток,
	|	ОстаткиДня.ДолгРеглОстаток,
	|	ОстаткиДня.ДолгУпрОстаток,
	|	0,
	|	0
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам.Остатки(
	|			&ГраницаКонецПериода,
	|			АналитикаУчетаПоПартнерам В (&АналитикаРасчета)
	|				И (Валюта <> &ВалютаУправленческогоУчета ИЛИ Валюта <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиДня
	|	ЛЕВОЕ СОЕДИНЕНИЕ втАналитикаЗаДень КАК АналитикаДня
	|		ПО ОстаткиДня.АналитикаУчетаПоПартнерам = АналитикаДня.КлючАналитики
	|			И ОстаткиДня.ЗаказКлиента = АналитикаДня.ОбъектРасчетов
	|ГДЕ
	|	ОстаткиДня.ДолгОстаток = 0
	|	И (ОстаткиДня.ДолгРеглОстаток <> 0 ИЛИ ОстаткиДня.ДолгУпрОстаток <> 0)
	|	И (НЕ АналитикаДня.КлючАналитики ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиДня.Валюта,
	|	&КонецПериода,
	|	ОстаткиДня.АналитикаУчетаПоПартнерам,
	|	ОстаткиДня.ЗаказКлиента,
	|	ОстаткиДня.РасчетныйДокумент,
	|	0,
	|	0,
	|	0,
	|	ОстаткиДня.ПредоплатаРеглОстаток,
	|	ОстаткиДня.ПредоплатаУпрОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам.Остатки(
	|			&ГраницаКонецПериода,
	|			АналитикаУчетаПоПартнерам В (&АналитикаРасчета)
	|				И (Валюта <> &ВалютаУправленческогоУчета ИЛИ Валюта <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиДня
	|ГДЕ
	|	ОстаткиДня.КВозвратуОстаток = 0
	|	И ОстаткиДня.ПредоплатаОстаток = 0
	|	И (ОстаткиДня.ПредоплатаРеглОстаток <> 0 ИЛИ ОстаткиДня.ПредоплатаУпрОстаток <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Валюта КАК Валюта,
	|	&КонецПериода КАК Период,
	|	Остатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Остатки.Заказ КАК Заказ,
	|	Остатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	(ВЫРАЗИТЬ(Остатки.Долг * КурсВалюты.Курс / КурсВалюты.Кратность КАК ЧИСЛО(15, 2))) - Остатки.ДолгРегл КАК ДолгРегл,
	|	(ВЫРАЗИТЬ(Остатки.Долг * КурсВалюты.Курс / КурсВалюты.Кратность / (КурсВалютыУпр.Курс * КурсВалютыУпр.Кратность) КАК ЧИСЛО(15, 2))) - Остатки.ДолгУпр КАК ДолгУпр
	|ПОМЕСТИТЬ втНеРеализованныеКР
	|ИЗ
	|	втОстакиНаКонецДня КАК Остатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, ) КАК КурсВалюты
	|			ПО Остатки.Валюта = КурсВалюты.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, Валюта = &ВалютаУправленческогоУчета) КАК КурсВалютыУпр
	|			ПО (ИСТИНА)
	|ГДЕ
	|	Остатки.Долг <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Валюта КАК Валюта,
	|	ВЫБОР КОГДА Остатки.ДолгРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ КАК ВидДвижения,
	|	&КонецПериода КАК Период,
	|	Остатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Остатки.Заказ КАК ЗаказКлиента,
	|	Остатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ВЫБОР КОГДА Остатки.ДолгРегл < 0
	|		ТОГДА -Остатки.ДолгРегл
	|		ИНАЧЕ Остатки.ДолгРегл
	|	КОНЕЦ КАК ДолгРегл,
	|	0 КАК ДолгУпр,
	|	0 КАК ПредоплатаРегл,
	|	0 КАК ПредоплатаУпр,
	|	ВЫБОР КОГДА Остатки.ДолгРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыУбыток)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыПрибыль)
	|	КОНЕЦ КАК ХозяйственнаяОперация
	|ПОМЕСТИТЬ ТаблицаРасчетов
	|ИЗ
	|	втОстакиНаКонецДня КАК Остатки
	|ГДЕ
	|	Остатки.Долг = 0 И Остатки.ДолгРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР КОГДА Остатки.ДолгУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.АналитикаУчетаПоПартнерам,
	|	Остатки.Заказ,
	|	Остатки.РасчетныйДокумент,
	|	0,
	|	ВЫБОР КОГДА Остатки.ДолгУпр < 0
	|		ТОГДА -Остатки.ДолгУпр
	|		ИНАЧЕ Остатки.ДолгУпр
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР КОГДА Остатки.ДолгУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыУбыток)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыПрибыль)
	|	КОНЕЦ
	|ИЗ
	|	втОстакиНаКонецДня КАК Остатки
	|ГДЕ
	|	Остатки.Долг = 0 И Остатки.ДолгУпр <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР КОГДА Остатки.ПредоплатаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.АналитикаУчетаПоПартнерам,
	|	Остатки.Заказ,
	|	Остатки.РасчетныйДокумент,
	|	0,
	|	0,
	|	ВЫБОР КОГДА Остатки.ПредоплатаРегл < 0
	|		ТОГДА -Остатки.ПредоплатаРегл
	|		ИНАЧЕ Остатки.ПредоплатаРегл
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР КОГДА Остатки.ПредоплатаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыУбыток)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыПрибыль)
	|	КОНЕЦ
	|ИЗ
	|	втОстакиНаКонецДня КАК Остатки
	|ГДЕ
	|	Остатки.ПредоплатаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР КОГДА Остатки.ПредоплатаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.АналитикаУчетаПоПартнерам,
	|	Остатки.Заказ,
	|	Остатки.РасчетныйДокумент,
	|	0,
	|	0,
	|	0,
	|	ВЫБОР КОГДА Остатки.ПредоплатаУпр < 0
	|		ТОГДА -Остатки.ПредоплатаУпр
	|		ИНАЧЕ Остатки.ПредоплатаУпр
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Остатки.ПредоплатаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыУбыток)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыПрибыль)
	|	КОНЕЦ
	|ИЗ
	|	втОстакиНаКонецДня КАК Остатки
	|ГДЕ
	|	Остатки.ПредоплатаУпр <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР
	|		КОГДА Остатки.ДолгРегл < 0
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.АналитикаУчетаПоПартнерам,
	|	Остатки.Заказ,
	|	Остатки.РасчетныйДокумент,
	|	ВЫБОР КОГДА Остатки.ДолгРегл < 0
	|		ТОГДА -Остатки.ДолгРегл
	|		ИНАЧЕ Остатки.ДолгРегл
	|	КОНЕЦ,
	|	0,
	|	0,
	|	0,
	|	ВЫБОР КОГДА Остатки.ДолгРегл < 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыУбыток)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыПрибыль)
	|	КОНЕЦ
	|ИЗ
	|	втНеРеализованныеКР КАК Остатки
	|ГДЕ
	|	Остатки.ДолгРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР КОГДА Остатки.ДолгУпр < 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.АналитикаУчетаПоПартнерам,
	|	Остатки.Заказ,
	|	Остатки.РасчетныйДокумент,
	|	0,
	|	ВЫБОР КОГДА Остатки.ДолгУпр < 0
	|		ТОГДА -Остатки.ДолгУпр
	|		ИНАЧЕ Остатки.ДолгУпр
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР КОГДА Остатки.ДолгУпр < 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыУбыток)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыКлиентыПрибыль)
	|	КОНЕЦ
	|ИЗ
	|	втНеРеализованныеКР КАК Остатки
	|ГДЕ
	|	Остатки.ДолгУпр <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРасчетов.Валюта,
	|	ТаблицаРасчетов.ВидДвижения,
	|	ТаблицаРасчетов.Период КАК Период,
	|	ТаблицаРасчетов.АналитикаУчетаПоПартнерам,
	|	ТаблицаРасчетов.ЗаказКлиента,
	|	ТаблицаРасчетов.РасчетныйДокумент,
	|	СУММА(ТаблицаРасчетов.ДолгРегл) КАК ДолгРегл,
	|	СУММА(ТаблицаРасчетов.ДолгУпр) КАК ДолгУпр,
	|	СУММА(ТаблицаРасчетов.ПредоплатаРегл) КАК ПредоплатаРегл,
	|	СУММА(ТаблицаРасчетов.ПредоплатаУпр) КАК ПредоплатаУпр,
	|	ТаблицаРасчетов.ХозяйственнаяОперация
	|ПОМЕСТИТЬ КурсовыеРазницыРасчетыСКлиентамиПоДокументам
	|ИЗ
	|	ТаблицаРасчетов КАК ТаблицаРасчетов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасчетов.Период,
	|	ТаблицаРасчетов.ХозяйственнаяОперация,
	|	ТаблицаРасчетов.АналитикаУчетаПоПартнерам,
	|	ТаблицаРасчетов.РасчетныйДокумент,
	|	ТаблицаРасчетов.ЗаказКлиента,
	|	ТаблицаРасчетов.Валюта,
	|	ТаблицаРасчетов.ВидДвижения"
	+"
	|;
	|";
	
КонецФункции

Функция ТекстЗапросаКурсовыеРазницыДенежныеСредстваБезналичные()
	
	Возврат 
	"
	|////////////////////////////////////////////////////////////////////////////////
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОборотыЗаДень.БанковскийСчет КАК Счет
	|ПОМЕСТИТЬ СчетаЗаДень
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваБезналичные.Обороты(
	|			&ГраницаНачалоПериода,
	|			&ГраницаКонецПериода,
	|			,
	|			Организация = &Организация
	|			И (БанковскийСчет.ВалютаДенежныхСредств <> &ВалютаУправленческогоУчета
	|					ИЛИ БанковскийСчет.ВалютаДенежныхСредств <> &ВалютаРегламентированногоУчета)
	|	) КАК ОборотыЗаДень
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНаДень.БанковскийСчет.ВалютаДенежныхСредств КАК Валюта,
	|	КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ) КАК Период,
	|	ОстаткиНаДень.Организация КАК Организация,
	|	ОстаткиНаДень.БанковскийСчет КАК БанковскийСчет,
	|	(ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаРеглОстаток КАК СуммаРегл,
	|	(ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность / КурсВалютыУпр.Курс * КурсВалютыУпр.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаУпрОстаток КАК СуммаУпр
	|
	|ПОМЕСТИТЬ втОстакиБезналичных
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваБезналичные.Остатки(
	|			&ГраницаКонецПериода,
	|			Организация = &Организация
	|				И (БанковскийСчет.ВалютаДенежныхСредств <> &ВалютаУправленческогоУчета
	|					ИЛИ БанковскийСчет.ВалютаДенежныхСредств <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиНаДень
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаЗаДень
	|		ПО ОстаткиНаДень.БанковскийСчет = СчетаЗаДень.Счет
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, ) КАК КурсВалюты
	|		ПО ОстаткиНаДень.БанковскийСчет.ВалютаДенежныхСредств = КурсВалюты.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, Валюта = &ВалютаУправленческогоУчета) КАК КурсВалютыУпр
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ОстаткиНаДень.СуммаОстаток <> 0
	|	И (НЕ СчетаЗаДень.Счет ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	И ((ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаРеглОстаток <> 0
	|			ИЛИ (ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность / КурсВалютыУпр.Курс * КурсВалютыУпр.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаУпрОстаток <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиНаДень.БанковскийСчет.ВалютаДенежныхСредств,
	|	КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ),
	|	ОстаткиНаДень.Организация,
	|	ОстаткиНаДень.БанковскийСчет,
	|	-ОстаткиНаДень.СуммаРеглОстаток,
	|	-ОстаткиНаДень.СуммаУпрОстаток
	|
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваБезналичные.Остатки(
	|			&ГраницаКонецПериода,
	|			Организация = &Организация
	|				И (БанковскийСчет.ВалютаДенежныхСредств <> &ВалютаУправленческогоУчета
	|					ИЛИ БанковскийСчет.ВалютаДенежныхСредств <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиНаДень
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаЗаДень
	|		ПО ОстаткиНаДень.БанковскийСчет = СчетаЗаДень.Счет
	|ГДЕ
	|	ОстаткиНаДень.СуммаОстаток = 0
	|	И (НЕ СчетаЗаДень.Счет ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	И (ОстаткиНаДень.СуммаРеглОстаток <> 0 ИЛИ ОстаткиНаДень.СуммаУпрОстаток <> 0)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Валюта КАК Валюта,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	&КонецПериода КАК Период,
	|	Остатки.Организация КАК Организация,
	|	Остатки.БанковскийСчет КАК БанковскийСчет,
	|	ВЫБОР КОГДА Остатки.СуммаРегл < 0
	|		ТОГДА -Остатки.СуммаРегл
	|		ИНАЧЕ Остатки.СуммаРегл
	|	КОНЕЦ КАК СуммаРегл,
	|	0 КАК СуммаУпр,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток)
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ ТаблицаБезналичныхДС
	|ИЗ
	|	втОстакиБезналичных КАК Остатки
	|ГДЕ
	|	Остатки.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.Организация,
	|	Остатки.БанковскийСчет,
	|	0,
	|	ВЫБОР КОГДА Остатки.СуммаУпр < 0
	|		ТОГДА -Остатки.СуммаУпр
	|		ИНАЧЕ Остатки.СуммаУпр
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток)
	|	КОНЕЦ
	|ИЗ
	|	втОстакиБезналичных КАК Остатки
	|ГДЕ
	|	Остатки.СуммаУпр <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаБезналичныхДС.Валюта,
	|	ТаблицаБезналичныхДС.ВидДвижения,
	|	ТаблицаБезналичныхДС.Период КАК Период,
	|	ТаблицаБезналичныхДС.Организация,
	|	ТаблицаБезналичныхДС.БанковскийСчет,
	|	СУММА(ТаблицаБезналичныхДС.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ТаблицаБезналичныхДС.СуммаУпр) КАК СуммаУпр,
	|	ТаблицаБезналичныхДС.ХозяйственнаяОперация,
	|	ТаблицаБезналичныхДС.СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ КурсовыеРазницыДенежныеСредстваБезналичные
	|ИЗ
	|	ТаблицаБезналичныхДС КАК ТаблицаБезналичныхДС
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаБезналичныхДС.Валюта,
	|	ТаблицаБезналичныхДС.Период,
	|	ТаблицаБезналичныхДС.Организация,
	|	ТаблицаБезналичныхДС.БанковскийСчет,
	|	ТаблицаБезналичныхДС.ХозяйственнаяОперация,
	|	ТаблицаБезналичныхДС.СтатьяДвиженияДенежныхСредств,
	|	ТаблицаБезналичныхДС.ВидДвижения"
	+"
	|;
	|";
	
КонецФункции

Функция ТекстЗапросаКурсовыеРазницыДенежныеСредстваНаличные()
	
	Возврат 
	"
	|////////////////////////////////////////////////////////////////////////////////
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОборотыЗаДень.Касса КАК Касса
	|ПОМЕСТИТЬ КассыЗаДень
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные.Обороты(
	|			&ГраницаНачалоПериода,
	|			&ГраницаКонецПериода,
	|			,
	|			Организация = &Организация
	|			И (Касса В (&Кассы) ИЛИ &ПоВсемКассам)
	|			И (Касса.ВалютаДенежныхСредств <> &ВалютаУправленческогоУчета
	|					ИЛИ Касса.ВалютаДенежныхСредств <> &ВалютаРегламентированногоУчета)
	|	) КАК ОборотыЗаДень
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНаДень.Касса.ВалютаДенежныхСредств КАК Валюта,
	|	КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ) КАК Период,
	|	ОстаткиНаДень.Организация КАК Организация,
	|	ОстаткиНаДень.Касса КАК Касса,
	|	(ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаРеглОстаток КАК СуммаРегл,
	|	(ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность / КурсВалютыУпр.Курс * КурсВалютыУпр.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаУпрОстаток КАК СуммаУпр
	|
	|ПОМЕСТИТЬ втОстакиНаличных
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные.Остатки(
	|			&ГраницаКонецПериода,
	|			Организация = &Организация
	|			И (Касса В (&Кассы) ИЛИ &ПоВсемКассам)
	|			И (Касса.ВалютаДенежныхСредств <> &ВалютаУправленческогоУчета
	|				ИЛИ Касса.ВалютаДенежныхСредств <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиНаДень
	|		ЛЕВОЕ СОЕДИНЕНИЕ КассыЗаДень
	|		ПО ОстаткиНаДень.Касса = КассыЗаДень.Касса
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, ) КАК КурсВалюты
	|		ПО ОстаткиНаДень.Касса.ВалютаДенежныхСредств = КурсВалюты.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, Валюта = &ВалютаУправленческогоУчета) КАК КурсВалютыУпр
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ОстаткиНаДень.СуммаОстаток <> 0
	|	И (НЕ КассыЗаДень.Касса ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	И ((ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаРеглОстаток <> 0
	|			ИЛИ (ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность / КурсВалютыУпр.Курс * КурсВалютыУпр.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаУпрОстаток <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиНаДень.Касса.ВалютаДенежныхСредств,
	|	КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ),
	|	ОстаткиНаДень.Организация,
	|	ОстаткиНаДень.Касса,
	|	-ОстаткиНаДень.СуммаРеглОстаток,
	|	-ОстаткиНаДень.СуммаУпрОстаток
	|
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные.Остатки(
	|			&ГраницаКонецПериода,
	|			Организация = &Организация
	|			И (Касса В (&Кассы) ИЛИ &ПоВсемКассам)
	|			И (Касса.ВалютаДенежныхСредств <> &ВалютаУправленческогоУчета
	|				ИЛИ Касса.ВалютаДенежныхСредств <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиНаДень
	|		ЛЕВОЕ СОЕДИНЕНИЕ КассыЗаДень
	|		ПО ОстаткиНаДень.Касса = КассыЗаДень.Касса
	|ГДЕ
	|	ОстаткиНаДень.СуммаОстаток = 0
	|	И (НЕ КассыЗаДень.Касса ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	И (ОстаткиНаДень.СуммаРеглОстаток <> 0 ИЛИ ОстаткиНаДень.СуммаУпрОстаток <> 0)
	|
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Валюта КАК Валюта,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	&КонецПериода КАК Период,
	|	Остатки.Организация КАК Организация,
	|	Остатки.Касса КАК Касса,
	|	ВЫБОР КОГДА Остатки.СуммаРегл < 0
	|		ТОГДА -Остатки.СуммаРегл
	|		ИНАЧЕ Остатки.СуммаРегл
	|	КОНЕЦ КАК СуммаРегл,
	|	0 КАК СуммаУпр,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток)
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ ТаблицаНаличныхДС
	|ИЗ
	|	втОстакиНаличных КАК Остатки
	|ГДЕ
	|	Остатки.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.Организация,
	|	Остатки.Касса,
	|	0,
	|	ВЫБОР КОГДА Остатки.СуммаУпр < 0
	|		ТОГДА -Остатки.СуммаУпр
	|		ИНАЧЕ Остатки.СуммаУпр
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток)
	|	КОНЕЦ
	|ИЗ
	|	втОстакиНаличных КАК Остатки
	|ГДЕ
	|	Остатки.СуммаУпр <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНаличныхДС.Валюта,
	|	ТаблицаНаличныхДС.ВидДвижения,
	|	ТаблицаНаличныхДС.Период КАК Период,
	|	ТаблицаНаличныхДС.Организация,
	|	ТаблицаНаличныхДС.Касса,
	|	СУММА(ТаблицаНаличныхДС.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ТаблицаНаличныхДС.СуммаУпр) КАК СуммаУпр,
	|	ТаблицаНаличныхДС.ХозяйственнаяОперация,
	|	ТаблицаНаличныхДС.СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ КурсовыеРазницыДенежныеСредстваНаличные
	|ИЗ
	|	ТаблицаНаличныхДС КАК ТаблицаНаличныхДС
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНаличныхДС.Валюта,
	|	ТаблицаНаличныхДС.Период,
	|	ТаблицаНаличныхДС.Организация,
	|	ТаблицаНаличныхДС.Касса,
	|	ТаблицаНаличныхДС.ХозяйственнаяОперация,
	|	ТаблицаНаличныхДС.СтатьяДвиженияДенежныхСредств,
	|	ТаблицаНаличныхДС.ВидДвижения"
	+"
	|;
	|";
	
КонецФункции

Функция ТекстЗапросаКурсовыеРазницыДенежныеСредстваВКассахККМ()
	
	Возврат 
	"
	|////////////////////////////////////////////////////////////////////////////////
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОборотыЗаДень.КассаККМ КАК КассаККМ
	|ПОМЕСТИТЬ КассыККМЗаДень
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВКассахККМ.Обороты(
	|			&ГраницаНачалоПериода,
	|			&ГраницаКонецПериода,
	|			,
	|			Организация = &Организация
	|			И (КассаККМ.ВалютаДенежныхСредств <> &ВалютаУправленческогоУчета
	|					ИЛИ КассаККМ.ВалютаДенежныхСредств <> &ВалютаРегламентированногоУчета)
	|	) КАК ОборотыЗаДень
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНаДень.КассаККМ.ВалютаДенежныхСредств КАК Валюта,
	|	КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ) КАК Период,
	|	ОстаткиНаДень.Организация КАК Организация,
	|	ОстаткиНаДень.КассаККМ КАК Касса,
	|	ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность КАК ЧИСЛО(15,2)) - ОстаткиНаДень.СуммаРеглОстаток КАК СуммаРегл,
	|	ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность / КурсВалютыУпр.Курс * КурсВалютыУпр.Кратность КАК ЧИСЛО(15,2)) - ОстаткиНаДень.СуммаУпрОстаток КАК СуммаУпр
	|
	|ПОМЕСТИТЬ втОстакиВКассахККМ
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВКассахККМ.Остатки(
	|		&ГраницаКонецПериода,
	|		Организация = &Организация
	|			И (КассаККМ.ВалютаДенежныхСредств <> &ВалютаУправленческогоУчета 
	|				ИЛИ КассаККМ.ВалютаДенежныхСредств <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиНаДень
	|		ЛЕВОЕ СОЕДИНЕНИЕ КассыККМЗаДень
	|		ПО ОстаткиНаДень.КассаККМ = КассыККМЗаДень.КассаККМ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, ) КАК КурсВалюты
	|		ПО ОстаткиНаДень.КассаККМ.ВалютаДенежныхСредств = КурсВалюты.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, Валюта = &ВалютаУправленческогоУчета) КАК КурсВалютыУпр
	|		ПО ИСТИНА
	|ГДЕ
	|	ОстаткиНаДень.СуммаОстаток <> 0
	|	И (НЕ КассыККМЗаДень.КассаККМ ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	И (ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность КАК ЧИСЛО(15,2)) - ОстаткиНаДень.СуммаРеглОстаток <> 0
	|		ИЛИ ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность / КурсВалютыУпр.Курс * КурсВалютыУпр.Кратность КАК ЧИСЛО(15,2)) - ОстаткиНаДень.СуммаУпрОстаток <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиНаДень.КассаККМ.ВалютаДенежныхСредств,
	|	КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ),
	|	ОстаткиНаДень.Организация,
	|	ОстаткиНаДень.КассаККМ,
	|	-ОстаткиНаДень.СуммаРеглОстаток,
	|	-ОстаткиНаДень.СуммаУпрОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВКассахККМ.Остатки(
	|			&ГраницаКонецПериода,
	|			Организация = &Организация
	|				И (КассаККМ.ВалютаДенежныхСредств <> &ВалютаУправленческогоУчета
	|					ИЛИ КассаККМ.ВалютаДенежныхСредств <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиНаДень
	|		ЛЕВОЕ СОЕДИНЕНИЕ КассыККМЗаДень
	|		ПО ОстаткиНаДень.КассаККМ = КассыККМЗаДень.КассаККМ
	|ГДЕ
	|	ОстаткиНаДень.СуммаОстаток = 0
	|	И (НЕ КассыККМЗаДень.КассаККМ ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	И (ОстаткиНаДень.СуммаРеглОстаток <> 0 ИЛИ ОстаткиНаДень.СуммаУпрОстаток <> 0)
	|
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Валюта КАК Валюта,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	&КонецПериода КАК Период,
	|	Остатки.Организация КАК Организация,
	|	Остатки.Касса КАК Касса,
	|	ВЫБОР КОГДА Остатки.СуммаРегл < 0
	|		ТОГДА -Остатки.СуммаРегл
	|		ИНАЧЕ Остатки.СуммаРегл
	|	КОНЕЦ КАК СуммаРегл,
	|	0 КАК СуммаУпр,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток)
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств
	|
	|ПОМЕСТИТЬ ТаблицаДСВКассахККМ
	|ИЗ
	|	втОстакиВКассахККМ КАК Остатки
	|ГДЕ
	|	Остатки.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.Организация,
	|	Остатки.Касса,
	|	0,
	|	ВЫБОР КОГДА Остатки.СуммаУпр < 0
	|		ТОГДА -Остатки.СуммаУпр
	|		ИНАЧЕ Остатки.СуммаУпр
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток)
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств
	|ИЗ
	|	втОстакиВКассахККМ КАК Остатки
	|ГДЕ
	|	Остатки.СуммаУпр <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДСВКассахККМ.Валюта,
	|	ТаблицаДСВКассахККМ.ВидДвижения,
	|	ТаблицаДСВКассахККМ.Период КАК Период,
	|	ТаблицаДСВКассахККМ.Организация,
	|	ТаблицаДСВКассахККМ.Касса КАК КассаККМ,
	|	СУММА(ТаблицаДСВКассахККМ.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ТаблицаДСВКассахККМ.СуммаУпр) КАК СуммаУпр,
	|	ТаблицаДСВКассахККМ.ХозяйственнаяОперация,
	|	ТаблицаДСВКассахККМ.СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ КурсовыеРазницыДенежныеСредстваВКассахККМ
	|ИЗ
	|	ТаблицаДСВКассахККМ КАК ТаблицаДСВКассахККМ
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДСВКассахККМ.Валюта,
	|	ТаблицаДСВКассахККМ.Период,
	|	ТаблицаДСВКассахККМ.Организация,
	|	ТаблицаДСВКассахККМ.Касса,
	|	ТаблицаДСВКассахККМ.ХозяйственнаяОперация,
	|	ТаблицаДСВКассахККМ.СтатьяДвиженияДенежныхСредств,
	|	ТаблицаДСВКассахККМ.ВидДвижения"
	+"
	|;
	|";
	
КонецФункции

Функция ТекстЗапросаКурсовыеРазницыДенежныеСредстваУПодотчетныхЛиц()
	
	Возврат 
	"
	|////////////////////////////////////////////////////////////////////////////////
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОборотыЗаДень.Валюта КАК Валюта,
	|	ОборотыЗаДень.Организация КАК Организация,
	|	ОборотыЗаДень.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	ОборотыЗаДень.Подразделение КАК Подразделение,
	|	ОборотыЗаДень.ЦельВыдачи КАК ЦельВыдачи
	|
	|ПОМЕСТИТЬ ПодотчетникиЗаДень
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.Обороты(
	|			&ГраницаНачалоПериода,
	|			&ГраницаКонецПериода,
	|			,
	|			Организация = &Организация
	|			И (ПодотчетноеЛицо В (&ПодотчетныеЛица) ИЛИ &ПоВсемПодотчетнымЛицам)
	|			И (Валюта <> &ВалютаУправленческогоУчета ИЛИ Валюта <> &ВалютаРегламентированногоУчета)
	|	) КАК ОборотыЗаДень
	|ГДЕ
	|	ОборотыЗаДень.СуммаОборот <> 0
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНаДень.Валюта КАК Валюта,
	|	КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ) КАК Период,
	|	ОстаткиНаДень.Организация КАК Организация,
	|	ОстаткиНаДень.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	ОстаткиНаДень.Подразделение КАК Подразделение,
	|	ОстаткиНаДень.ЦельВыдачи КАК ЦельВыдачи,
	|	(ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаРеглОстаток КАК СуммаРегл,
	|	(ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность / КурсВалютыУпр.Курс * КурсВалютыУпр.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаУпрОстаток КАК СуммаУпр
	|
	|ПОМЕСТИТЬ втОстакиУПодотчетныхЛиц
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.Остатки(
	|			&ГраницаКонецПериода,
	|			Организация = &Организация
	|			И (ПодотчетноеЛицо В (&ПодотчетныеЛица) ИЛИ &ПоВсемПодотчетнымЛицам)
	|				И (Валюта <> &ВалютаУправленческогоУчета ИЛИ Валюта <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиНаДень
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПодотчетникиЗаДень
	|		ПО ОстаткиНаДень.Валюта = ПодотчетникиЗаДень.Валюта
	|			И ОстаткиНаДень.Валюта = ПодотчетникиЗаДень.Валюта
	|			И ОстаткиНаДень.Организация = ПодотчетникиЗаДень.Организация
	|			И ОстаткиНаДень.ПодотчетноеЛицо = ПодотчетникиЗаДень.ПодотчетноеЛицо
	|			И ОстаткиНаДень.Подразделение = ПодотчетникиЗаДень.Подразделение
	|			И ОстаткиНаДень.ЦельВыдачи = ПодотчетникиЗаДень.ЦельВыдачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, ) КАК КурсВалюты
	|		ПО ОстаткиНаДень.Валюта = КурсВалюты.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, Валюта = &ВалютаУправленческогоУчета) КАК КурсВалютыУпр
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ОстаткиНаДень.СуммаОстаток <> 0
	|	И (НЕ ПодотчетникиЗаДень.ПодотчетноеЛицо ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	И ((ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаРеглОстаток <> 0
	|			ИЛИ (ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность / КурсВалютыУпр.Курс * КурсВалютыУпр.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаУпрОстаток <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиНаДень.Валюта,
	|	КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ),
	|	ОстаткиНаДень.Организация,
	|	ОстаткиНаДень.ПодотчетноеЛицо,
	|	ОстаткиНаДень.Подразделение,
	|	ОстаткиНаДень.ЦельВыдачи,
	|	-ОстаткиНаДень.СуммаРеглОстаток,
	|	-ОстаткиНаДень.СуммаУпрОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.Остатки(
	|			&ГраницаКонецПериода,
	|			Организация = &Организация
	|			И (ПодотчетноеЛицо В (&ПодотчетныеЛица) ИЛИ &ПоВсемПодотчетнымЛицам)
	|				И (Валюта <> &ВалютаУправленческогоУчета ИЛИ Валюта <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиНаДень
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПодотчетникиЗаДень
	|		ПО ОстаткиНаДень.Валюта = ПодотчетникиЗаДень.Валюта
	|			И ОстаткиНаДень.Валюта = ПодотчетникиЗаДень.Валюта
	|			И ОстаткиНаДень.Организация = ПодотчетникиЗаДень.Организация
	|			И ОстаткиНаДень.ПодотчетноеЛицо = ПодотчетникиЗаДень.ПодотчетноеЛицо
	|			И ОстаткиНаДень.Подразделение = ПодотчетникиЗаДень.Подразделение
	|			И ОстаткиНаДень.ЦельВыдачи = ПодотчетникиЗаДень.ЦельВыдачи
	|ГДЕ
	|	ОстаткиНаДень.СуммаОстаток = 0
	|	И (НЕ ПодотчетникиЗаДень.ПодотчетноеЛицо ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	И (ОстаткиНаДень.СуммаРеглОстаток <> 0 ИЛИ ОстаткиНаДень.СуммаУпрОстаток <> 0)
	|
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Валюта КАК Валюта,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	&КонецПериода КАК Период,
	|	Остатки.Организация КАК Организация,
	|	Остатки.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	Остатки.Подразделение КАК Подразделение,
	|	Остатки.ЦельВыдачи КАК ЦельВыдачи,
	|	ВЫБОР КОГДА Остатки.СуммаРегл < 0
	|		ТОГДА -Остатки.СуммаРегл
	|		ИНАЧЕ Остатки.СуммаРегл
	|	КОНЕЦ КАК СуммаРегл,
	|	0 КАК СуммаУпр,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток)
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ ТаблицаДСУПодотчетныхЛиц
	|ИЗ
	|	втОстакиУПодотчетныхЛиц КАК Остатки
	|ГДЕ
	|	Остатки.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.Организация,
	|	Остатки.ПодотчетноеЛицо,
	|	Остатки.Подразделение,
	|	Остатки.ЦельВыдачи,
	|	0,
	|	ВЫБОР КОГДА Остатки.СуммаУпр < 0
	|		ТОГДА -Остатки.СуммаУпр
	|		ИНАЧЕ Остатки.СуммаУпр
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток)
	|	КОНЕЦ
	|ИЗ
	|	втОстакиУПодотчетныхЛиц КАК Остатки
	|ГДЕ
	|	Остатки.СуммаУпр <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДСУПодотчетныхЛиц.Валюта,
	|	ТаблицаДСУПодотчетныхЛиц.ВидДвижения,
	|	ТаблицаДСУПодотчетныхЛиц.Период КАК Период,
	|	ТаблицаДСУПодотчетныхЛиц.Организация,
	|	ТаблицаДСУПодотчетныхЛиц.ПодотчетноеЛицо,
	|	ТаблицаДСУПодотчетныхЛиц.Подразделение,
	|	ТаблицаДСУПодотчетныхЛиц.ЦельВыдачи,
	|	СУММА(ТаблицаДСУПодотчетныхЛиц.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ТаблицаДСУПодотчетныхЛиц.СуммаУпр) КАК СуммаУпр,
	|	ТаблицаДСУПодотчетныхЛиц.ХозяйственнаяОперация,
	|	ТаблицаДСУПодотчетныхЛиц.СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ КурсовыеРазницыДенежныеСредстваУПодотчетныхЛиц
	|ИЗ
	|	ТаблицаДСУПодотчетныхЛиц КАК ТаблицаДСУПодотчетныхЛиц
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДСУПодотчетныхЛиц.Валюта,
	|	ТаблицаДСУПодотчетныхЛиц.Период,
	|	ТаблицаДСУПодотчетныхЛиц.Организация,
	|	ТаблицаДСУПодотчетныхЛиц.ПодотчетноеЛицо,
	|	ТаблицаДСУПодотчетныхЛиц.Подразделение,
	|	ТаблицаДСУПодотчетныхЛиц.ЦельВыдачи,
	|	ТаблицаДСУПодотчетныхЛиц.ХозяйственнаяОперация,
	|	ТаблицаДСУПодотчетныхЛиц.СтатьяДвиженияДенежныхСредств,
	|	ТаблицаДСУПодотчетныхЛиц.ВидДвижения"
	+"
	|;
	|";
	
КонецФункции

Функция ТекстЗапросаКурсовыеРазницыДенежныеСредстваВПути()
	
	Возврат 
	"
	|////////////////////////////////////////////////////////////////////////////////
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОборотыЗаДень.Валюта КАК Валюта,
	|	ОборотыЗаДень.Организация КАК Организация,
	|	ОборотыЗаДень.Получатель КАК Получатель,
	|	ОборотыЗаДень.Отправитель КАК Отправитель,
	|	ОборотыЗаДень.ВидПереводаДенежныхСредств КАК ВидПеревода,
	|	ОборотыЗаДень.Контрагент КАК Контрагент
	|
	|ПОМЕСТИТЬ ДеньгиВПутиЗаДень
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВПути.Обороты(
	|			&ГраницаНачалоПериода,
	|			&ГраницаКонецПериода,
	|			,
	|			Организация = &Организация
	|			И (Валюта <> &ВалютаУправленческогоУчета ИЛИ Валюта <> &ВалютаРегламентированногоУчета)
	|	) КАК ОборотыЗаДень
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНаДень.Валюта КАК Валюта,
	|	КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ) КАК Период,
	|	ОстаткиНаДень.Организация КАК Организация,
	|	ОстаткиНаДень.Получатель КАК Получатель,
	|	ОстаткиНаДень.Отправитель КАК Отправитель,
	|	ОстаткиНаДень.ВидПереводаДенежныхСредств КАК ВидПереводаДенежныхСредств,
	|	ОстаткиНаДень.Контрагент КАК Контрагент,
	|	ОстаткиНаДень.СуммаОстаток КАК Сумма,
	|	(ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаРеглОстаток КАК СуммаРегл,
	|	(ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность / КурсВалютыУпр.Курс * КурсВалютыУпр.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаУпрОстаток КАК СуммаУпр
	|
	|ПОМЕСТИТЬ втОстакиВПути
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВПути.Остатки(
	|			&ГраницаКонецПериода,
	|			Организация = &Организация
	|				И (Валюта <> &ВалютаУправленческогоУчета ИЛИ Валюта <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиНаДень
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДеньгиВПутиЗаДень
	|		ПО ОстаткиНаДень.Валюта = ДеньгиВПутиЗаДень.Валюта
	|			И ОстаткиНаДень.Валюта = ДеньгиВПутиЗаДень.Валюта
	|			И ОстаткиНаДень.Организация = ДеньгиВПутиЗаДень.Организация
	|			И ОстаткиНаДень.Получатель = ДеньгиВПутиЗаДень.Получатель
	|			И ОстаткиНаДень.Отправитель = ДеньгиВПутиЗаДень.Отправитель
	|			И ОстаткиНаДень.ВидПереводаДенежныхСредств = ДеньгиВПутиЗаДень.ВидПеревода
	|			И ОстаткиНаДень.Контрагент = ДеньгиВПутиЗаДень.Контрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, ) КАК КурсВалюты
	|		ПО ОстаткиНаДень.Валюта = КурсВалюты.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, Валюта = &ВалютаУправленческогоУчета) КАК КурсВалютыУпр
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ОстаткиНаДень.СуммаОстаток <> 0
	|	И (НЕ ДеньгиВПутиЗаДень.Валюта ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	И ((ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаРеглОстаток <> 0
	|			ИЛИ (ВЫРАЗИТЬ(ОстаткиНаДень.СуммаОстаток * КурсВалюты.Курс / КурсВалюты.Кратность / КурсВалютыУпр.Курс * КурсВалютыУпр.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаУпрОстаток <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиНаДень.Валюта,
	|	КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ),
	|	ОстаткиНаДень.Организация,
	|	ОстаткиНаДень.Получатель,
	|	ОстаткиНаДень.Отправитель,
	|	ОстаткиНаДень.ВидПереводаДенежныхСредств,
	|	ОстаткиНаДень.Контрагент,
	|	-ОстаткиНаДень.СуммаОстаток,
	|	-ОстаткиНаДень.СуммаРеглОстаток,
	|	-ОстаткиНаДень.СуммаУпрОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВПути.Остатки(
	|			&ГраницаКонецПериода,
	|			Организация = &Организация
	|				И (Валюта <> &ВалютаУправленческогоУчета ИЛИ Валюта <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиНаДень
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДеньгиВПутиЗаДень
	|		ПО ОстаткиНаДень.Валюта = ДеньгиВПутиЗаДень.Валюта
	|			И ОстаткиНаДень.Валюта = ДеньгиВПутиЗаДень.Валюта
	|			И ОстаткиНаДень.Организация = ДеньгиВПутиЗаДень.Организация
	|			И ОстаткиНаДень.Получатель = ДеньгиВПутиЗаДень.Получатель
	|			И ОстаткиНаДень.ВидПереводаДенежныхСредств = ДеньгиВПутиЗаДень.ВидПеревода
	|			И ОстаткиНаДень.Контрагент = ДеньгиВПутиЗаДень.Контрагент
	|ГДЕ
	|	ОстаткиНаДень.СуммаОстаток = 0
	|	И (НЕ ДеньгиВПутиЗаДень.Валюта ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	И (ОстаткиНаДень.СуммаРеглОстаток <> 0 ИЛИ ОстаткиНаДень.СуммаУпрОстаток <> 0)
	|
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Валюта КАК Валюта,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	&КонецПериода КАК Период,
	|	Остатки.Организация КАК Организация,
	|	Остатки.Получатель КАК Получатель,
	|	Остатки.Отправитель КАК Отправитель,
	|	Остатки.ВидПереводаДенежныхСредств КАК ВидПереводаДенежныхСредств,
	|	Остатки.Контрагент КАК Контрагент,
	|	ВЫБОР КОГДА Остатки.СуммаРегл < 0
	|		ТОГДА -Остатки.СуммаРегл
	|		ИНАЧЕ Остатки.СуммаРегл
	|	КОНЕЦ КАК СуммаРегл,
	|	0 КАК СуммаУпр,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток)
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ ТаблицаДСвПути
	|ИЗ
	|	втОстакиВПути КАК Остатки
	|ГДЕ
	|	Остатки.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ,
	|	&КонецПериода,
	|	Остатки.Организация,
	|	Остатки.Получатель,
	|	Остатки.Отправитель,
	|	Остатки.ВидПереводаДенежныхСредств,
	|	Остатки.Контрагент,
	|	0,
	|	ВЫБОР КОГДА Остатки.СуммаУпр < 0
	|		ТОГДА -Остатки.СуммаУпр
	|		ИНАЧЕ Остатки.СуммаУпр
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КурсовыеРазницыДСУбыток)
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток)
	|	КОНЕЦ
	|ИЗ
	|	втОстакиВПути КАК Остатки
	|ГДЕ
	|	Остатки.СуммаУпр <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДСвПути.Валюта,
	|	ТаблицаДСвПути.ВидДвижения,
	|	ТаблицаДСвПути.Период КАК Период,
	|	ТаблицаДСвПути.Организация,
	|	ТаблицаДСвПути.Получатель,
	|	ТаблицаДСвПути.Отправитель КАК Отправитель,
	|	ТаблицаДСвПути.ВидПереводаДенежныхСредств,
	|	ТаблицаДСвПути.Контрагент,
	|	СУММА(ТаблицаДСвПути.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ТаблицаДСвПути.СуммаУпр) КАК СуммаУпр,
	|	ТаблицаДСвПути.ХозяйственнаяОперация,
	|	ТаблицаДСвПути.СтатьяДвиженияДенежныхСредств
	|ПОМЕСТИТЬ КурсовыеРазницыДенежныеСредстваВПути
	|ИЗ
	|	ТаблицаДСвПути КАК ТаблицаДСвПути
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДСвПути.Валюта,
	|	ТаблицаДСвПути.Период,
	|	ТаблицаДСвПути.Организация,
	|	ТаблицаДСвПути.Получатель,
	|	ТаблицаДСвПути.Отправитель,
	|	ТаблицаДСвПути.ВидПереводаДенежныхСредств,
	|	ТаблицаДСвПути.Контрагент,
	|	ТаблицаДСвПути.ХозяйственнаяОперация,
	|	ТаблицаДСвПути.СтатьяДвиженияДенежныхСредств,
	|	ТаблицаДСвПути.ВидДвижения"
	+"
	|;
	|";
	
КонецФункции

Функция ТекстЗапросаКурсовыеРазницыРасчетыПоДоговорамКредитовИДепозитов()
	
	Возврат 
	"
	|////////////////////////////////////////////////////////////////////////////////
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОборотыЗаДень.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ОборотыЗаДень.Договор КАК Договор,
	|	ОборотыЗаДень.ТипСуммы КАК ТипСуммы
	|ПОМЕСТИТЬ ДоговораЗаДень
	|ИЗ
	|	РегистрНакопления.РасчетыПоДоговорамКредитовИДепозитов.Обороты(
	|			&ГраницаНачалоПериода,
	|			&ГраницаКонецПериода,
	|			,
	|			(Договор.ВалютаВзаиморасчетов <> &ВалютаУправленческогоУчета 
	|				ИЛИ Договор.ВалютаВзаиморасчетов <> &ВалютаРегламентированногоУчета)
	|	) КАК ОборотыЗаДень
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНаДень.Валюта КАК Валюта,
	|	КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ) КАК Период,
	|	ОстаткиНаДень.СуммаВВалюте КАК ОстатокВалюты,
	|	(ВЫРАЗИТЬ(ОстаткиНаДень.СуммаВВалюте * КурсВалюты.Курс / КурсВалюты.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаРегл КАК СуммаРегл,
	|	(ВЫРАЗИТЬ(ОстаткиНаДень.СуммаВВалюте * КурсВалюты.Курс / КурсВалюты.Кратность / КурсВалютыУпр.Курс * КурсВалютыУпр.Кратность КАК ЧИСЛО(15, 2))) - ОстаткиНаДень.СуммаУпр КАК СуммаУпр,
	|	ОстаткиНаДень.ТипСуммы КАК ТипСуммы,
	|	ОстаткиНаДень.Договор КАК Договор,
	|	ОстаткиНаДень.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ОстаткиНаДень.ХарактерДоговора КАК ХарактерДоговора,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|
	|ПОМЕСТИТЬ втОстакиКредитовДепозитов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Остатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Остатки.Договор КАК Договор,
	|		Остатки.Договор.ХарактерДоговора КАК ХарактерДоговора,
	|		Остатки.ТипСуммы КАК ТипСуммы,
	|		Остатки.Договор.ВалютаВзаиморасчетов КАК Валюта,
	|		Остатки.СуммаВВалютеОстаток КАК СуммаВВалюте,
	|		Остатки.СуммаРеглОстаток КАК СуммаРегл,
	|		Остатки.СуммаУпрОстаток КАК СуммаУпр
	|	ИЗ
	|		РегистрНакопления.РасчетыПоДоговорамКредитовИДепозитов.Остатки(
	|				&ГраницаКонецПериода,
	|				АналитикаУчетаПоПартнерам.Организация = &Организация
	|					И (Договор.ВалютаВзаиморасчетов <> &ВалютаУправленческогоУчета
	|						ИЛИ Договор.ВалютаВзаиморасчетов <> &ВалютаРегламентированногоУчета)
	|		) КАК Остатки
	|	) КАК ОстаткиНаДень
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоговораЗаДень
	|		ПО ОстаткиНаДень.АналитикаУчетаПоПартнерам = ДоговораЗаДень.АналитикаУчетаПоПартнерам
	|			И ОстаткиНаДень.Договор = ДоговораЗаДень.Договор
	|			И ОстаткиНаДень.ТипСуммы = ДоговораЗаДень.ТипСуммы
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, ) КАК КурсВалюты
	|		ПО ОстаткиНаДень.Договор.ВалютаВзаиморасчетов = КурсВалюты.Валюта
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&КонецПериода, Валюта = &ВалютаУправленческогоУчета) КАК КурсВалютыУпр
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ОстаткиНаДень.СуммаВВалюте <> 0
	|	И (НЕ ДоговораЗаДень.Договор ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	И ((ВЫРАЗИТЬ(ОстаткиНаДень.СуммаВВалюте * КурсВалюты.Курс / КурсВалюты.Кратность КАК ЧИСЛО(15, 2))) <> ОстаткиНаДень.СуммаРегл
	|			ИЛИ (ВЫРАЗИТЬ(ОстаткиНаДень.СуммаВВалюте * КурсВалюты.Курс / КурсВалюты.Кратность / КурсВалютыУпр.Курс * КурсВалютыУпр.Кратность КАК ЧИСЛО(15, 2))) <> ОстаткиНаДень.СуммаУпр)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиНаДень.Договор.ВалютаВзаиморасчетов,
	|	КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ),
	|	0,
	|	-ОстаткиНаДень.СуммаРеглОстаток,
	|	-ОстаткиНаДень.СуммаУпрОстаток,
	|	ОстаткиНаДень.ТипСуммы,
	|	ОстаткиНаДень.Договор,
	|	ОстаткиНаДень.АналитикаУчетаПоПартнерам,
	|	ОстаткиНаДень.Договор.ХарактерДоговора,
	|	&ХозяйственнаяОперация
	|ИЗ
	|	РегистрНакопления.РасчетыПоДоговорамКредитовИДепозитов.Остатки(
	|			&ГраницаКонецПериода,
	|			АналитикаУчетаПоПартнерам.Организация = &Организация
	|				И (Договор.ВалютаВзаиморасчетов <> &ВалютаУправленческогоУчета
	|					ИЛИ Договор.ВалютаВзаиморасчетов <> &ВалютаРегламентированногоУчета)
	|	) КАК ОстаткиНаДень
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоговораЗаДень
	|		ПО ОстаткиНаДень.АналитикаУчетаПоПартнерам = ДоговораЗаДень.АналитикаУчетаПоПартнерам
	|			И ОстаткиНаДень.Договор = ДоговораЗаДень.Договор
	|			И ОстаткиНаДень.ТипСуммы = ДоговораЗаДень.ТипСуммы
	|ГДЕ
	|	ОстаткиНаДень.СуммаВВалютеОстаток = 0
	|	И (НЕ ДоговораЗаДень.Договор ЕСТЬ NULL
	|		ИЛИ &КонецПериода = КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	И (ОстаткиНаДень.СуммаРеглОстаток <> 0 ИЛИ ОстаткиНаДень.СуммаУпрОстаток <> 0)
	|
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Валюта КАК Валюта,
	|	&КонецПериода КАК Период,
	|	ВЫБОР КОГДА Остатки.СуммаРегл > 0 
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 
	|	КОНЕЦ КАК ВидДвижения,
	|	ВЫБОР КОГДА Остатки.СуммаРегл < 0
	|		ТОГДА -Остатки.СуммаРегл
	|		ИНАЧЕ Остатки.СуммаРегл
	|	КОНЕЦ КАК СуммаРегл,
	|	0 КАК СуммаУпр,
	|	Остатки.СуммаРегл КАК СуммаРеглРег,
	|	0 КАК СуммаУпрРег,
	|	Остатки.ТипСуммы КАК ТипСуммы,
	|	Остатки.Договор КАК Договор,
	|	Остатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Остатки.ХарактерДоговора КАК ХарактерДоговора,
	|	Остатки.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|
	|ПОМЕСТИТЬ ТаблицаКредитовДепозитов
	|ИЗ
	|	втОстакиКредитовДепозитов КАК Остатки
	|ГДЕ
	|	Остатки.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Остатки.Валюта,
	|	&КонецПериода,
	|	ВЫБОР КОГДА Остатки.СуммаУпр > 0 
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР КОГДА Остатки.СуммаУпр < 0
	|		ТОГДА -Остатки.СуммаУпр
	|		ИНАЧЕ Остатки.СуммаУпр
	|	КОНЕЦ,
	|	0,
	|	Остатки.СуммаУпр КАК СуммаУпрРег,
	|	Остатки.ТипСуммы,
	|	Остатки.Договор,
	|	Остатки.АналитикаУчетаПоПартнерам,
	|	Остатки.ХарактерДоговора КАК ХарактерДоговора,
	|	Остатки.ХозяйственнаяОперация
	|ИЗ
	|	втОстакиКредитовДепозитов КАК Остатки
	|ГДЕ
	|	Остатки.СуммаУпр <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКредитовДепозитов.Валюта,
	|	ТаблицаКредитовДепозитов.Период КАК Период,
	|	ТаблицаКредитовДепозитов.ВидДвижения,
	|	СУММА(ТаблицаКредитовДепозитов.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ТаблицаКредитовДепозитов.СуммаУпр) КАК СуммаУпр,
	|	СУММА(ТаблицаКредитовДепозитов.СуммаРеглРег) КАК СуммаРеглРег,
	|	СУММА(ТаблицаКредитовДепозитов.СуммаУпрРег) КАК СуммаУпрРег,
	|	ТаблицаКредитовДепозитов.ТипСуммы,
	|	ТаблицаКредитовДепозитов.Договор,
	|	ТаблицаКредитовДепозитов.АналитикаУчетаПоПартнерам,
	|	ТаблицаКредитовДепозитов.ХарактерДоговора КАК ХарактерДоговора,
	|	ТаблицаКредитовДепозитов.ХозяйственнаяОперация
	|ПОМЕСТИТЬ КурсовыеРазницыРасчетыПоДоговорамКредитовИДепозитов
	|ИЗ
	|	ТаблицаКредитовДепозитов КАК ТаблицаКредитовДепозитов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаКредитовДепозитов.Валюта,
	|	ТаблицаКредитовДепозитов.Период,
	|	ТаблицаКредитовДепозитов.АналитикаУчетаПоПартнерам,
	|	ТаблицаКредитовДепозитов.Договор,
	|	ТаблицаКредитовДепозитов.ХарактерДоговора,
	|	ТаблицаКредитовДепозитов.ТипСуммы,
	|	ТаблицаКредитовДепозитов.ХозяйственнаяОперация,
	|	ТаблицаКредитовДепозитов.ВидДвижения"
	+"
	|;
	|";
	
КонецФункции

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации, РегистрыДляРасчета)
	
	ВходящиеДанные = Новый Соответствие;
	ВходящиеДанные.Вставить(Метаданные.Документы.РасчетКурсовыхРазниц);
	Для Каждого Регистр Из РегистрыДляРасчета Цикл
		ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления[Регистр.Ключ]);
	КонецЦикла;
	ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.АналитикаУчетаПоПартнерам);
	ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.КурсыВалют);
	
	ЗакрытиеМесяцаСервер.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ЗаписатьДвижения(ДокументСсылка, МенеджерТаблиц, ИмяРегистра)
	
	ТекстЗапроса = "ВЫБРАТЬ * ИЗ КурсовыеРазницы" + ИмяРегистра + " УПОРЯДОЧИТЬ ПО Период";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерТаблиц;
	
	ВсегоЗаписано = 0;
	КурсовыеРазницы = Запрос.Выполнить().Выгрузить();
	Если КурсовыеРазницы.Количество() > 0 Тогда
		Набор = РегистрыНакопления[ИмяРегистра].СоздатьНаборЗаписей();
		Набор.Отбор.Регистратор.Установить(ДокументСсылка);
		Набор.Загрузить(КурсовыеРазницы);
		Набор.Записать(Ложь);
		ВсегоЗаписано = КурсовыеРазницы.Количество();
	КонецЕсли;
	Возврат ВсегоЗаписано;
	
КонецФункции

Процедура ПровестиДокументПереоценки(Организация, Период, Операция, ПараметрыПереоценки = Неопределено)
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПланыОбмена.ГлавныйУзел()) Тогда
		Возврат;
	КонецЕсли;
	
	#Область ИнициализацияПараметровПереоценки
	РегистрыДляРасчета = РегистрыРасчета(Операция);
	АналитикаРасчета = Неопределено;
	КоличествоОбработанныхЭлементов = 0;
	Если ЗначениеЗаполнено(ПараметрыПереоценки) Тогда
		Если ПараметрыПереоценки.Свойство("РегистрыРасчета") Тогда
			РегистрыДляРасчета = ПараметрыПереоценки.РегистрыРасчета;
		КонецЕсли;
		ПараметрыПереоценки.Свойство("АналитикаРасчета", АналитикаРасчета);
		Если ТипЗнч(АналитикаРасчета) = Тип("Массив") Тогда
			ВсеПустые = Истина;
			Для Каждого Аналитика Из АналитикаРасчета Цикл
				Если ЗначениеЗаполнено(Аналитика) Тогда
					ВсеПустые = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ВсеПустые Тогда
				АналитикаРасчета = Неопределено;
			КонецЕсли;
		КонецЕсли;
		Если ПараметрыПереоценки.Свойство("КоличествоОбработанныхЭлементов") Тогда
			КоличествоОбработанныхЭлементов = ПараметрыПереоценки.КоличествоОбработанныхЭлементов;
		КонецЕсли;
	КонецЕсли;
	#КонецОбласти
	
	Если НЕ ТребуетсяПереоценка(Организация, Период, РегистрыДляРасчета, АналитикаРасчета) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Организация КАК Организация
	|ПОМЕСТИТЬ втДокументПереоценки
	|ИЗ
	|	Документ.РасчетКурсовыхРазниц КАК Документ
	|ГДЕ
	|	Документ.ХозяйственнаяОперация = &Операция
	|	И НЕ Документ.ПометкаУдаления
	|	И Документ.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И (Документ.Организация В (&Организация) ИЛИ &ВсеОрганизации)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация,
	|	ЕСТЬNULL(Документ.Ссылка, НЕОПРЕДЕЛЕНО) КАК Документ
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДокументПереоценки КАК Документ
	|		ПО Организации.Ссылка = Документ.Организация
	|ГДЕ
	|	НЕ Организации.ПометкаУдаления
	|	И (Организации.Ссылка В (&Организация) ИЛИ &ВсеОрганизации)
	|	И (Организации.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		ИЛИ &УчитыватьУпрОрганизацию)");
	
	Запрос.УстановитьПараметр("Операция",       Операция);
	Запрос.УстановитьПараметр("НачалоПериода",  НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода",   КонецМесяца(Период));
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("ВсеОрганизации", НЕ ЗначениеЗаполнено(Организация));
	Запрос.УстановитьПараметр("УчитыватьУпрОрганизацию", ПолучитьФункциональнуюОпцию("ИспользоватьУправленческуюОрганизацию"));
	
	ПереоцениватьПоДням = Константы.ПереоцениватьВалютныеСредстваПоДням.Получить();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Документ) Тогда
			
			ВыполнитьРасчетКурсовыхРазниц(Выборка.Документ, РегистрыДляРасчета, АналитикаРасчета, КоличествоОбработанныхЭлементов);
			
			ДокументОбъект = Выборка.Документ.ПолучитьОбъект();
			ДокументОбъект.ПометкаУдаления = Ложь;
			ДокументОбъект.ДополнительныеСвойства.Вставить("ВыполненРасчет", Истина);// отключить повторный расчет при проведении
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
		Иначе
			
			ДокументОбъект = Документы.РасчетКурсовыхРазниц.СоздатьДокумент();
			ДокументОбъект.Дата = КонецМесяца(Период);
			ДокументОбъект.Организация = Выборка.Организация;
			ДокументОбъект.ХозяйственнаяОперация = Операция;
			ДокументОбъект.ПереоценкаПоДням = ПереоцениватьПоДням;
			ДокументОбъект.Записать();
			
			ВыполнитьРасчетКурсовыхРазниц(ДокументОбъект.Ссылка, РегистрыДляРасчета, АналитикаРасчета, КоличествоОбработанныхЭлементов);
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("ВыполненРасчет", Истина);// отключить повторный расчет при проведении
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
		КонецЕсли;
		
	КонецЦикла;// По организациям
	
	Если ЗначениеЗаполнено(ПараметрыПереоценки) И ПараметрыПереоценки.Свойство("КоличествоОбработанныхЭлементов") Тогда
		ПараметрыПереоценки.КоличествоОбработанныхЭлементов = КоличествоОбработанныхЭлементов;
	КонецЕсли;
	
КонецПроцедуры

Функция ЕстьВалютныеОстатки(Организация, Период, РегистрыДляРасчета = Неопределено, АналитикаРасчета = Неопределено) Экспорт
	
	ТекстЗапросаВТ =
	"ВЫБРАТЬ
	|	АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|ПОМЕСТИТЬ втКлючиАналитикиПоПартнерам
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ГДЕ
	|	(АналитикаУчетаПоПартнерам.Организация В (&Организация) ИЛИ &ВсеОрганизации)
	|	И (АналитикаУчетаПоПартнерам.КлючАналитики В (&АналитикаРасчета) ИЛИ &ПоВсемАналитикам)"
	+"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Если РегистрыДляРасчета = Неопределено Тогда
		РегистрыДляРасчета = РегистрыРасчета();
	КонецЕсли;
	Разделитель = Символы.ПС+Символы.ПС;
	ОБЪЕДИНИТЬ = Разделитель+"ОБЪЕДИНИТЬ ВСЕ"+Разделитель;
	ТекстЗапроса = "";
	Если РегистрыДляРасчета.Свойство("РасчетыСПоставщикамиПоДокументам") Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""РасчетыСПоставщикамиПоДокументам"" КАК ПредметПереоценки,
		|	Остатки.Валюта КАК Валюта
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам.Остатки(
		|		&ГраницаКонецПериода,
		|		АналитикаУчетаПоПартнерам В (ВЫБРАТЬ Т.КлючАналитики ИЗ втКлючиАналитикиПоПартнерам КАК Т)
		|			И (Валюта <> &ВалютаУпрУчета ИЛИ Валюта <> &ВалютаРеглУчета)
		|	) КАК Остатки";
	КонецЕсли;
	
	Если РегистрыДляРасчета.Свойство("РасчетыСКлиентамиПоДокументам") Тогда
		ТекстЗапроса = ТекстЗапроса + ?(НЕ ПустаяСтрока(ТекстЗапроса), ОБЪЕДИНИТЬ, "") +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""РасчетыСКлиентамиПоДокументам"" КАК ПредметПереоценки,
		|	Остатки.Валюта КАК Валюта
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоДокументам.Остатки(
		|		&ГраницаКонецПериода,
		|		АналитикаУчетаПоПартнерам В (ВЫБРАТЬ Т.КлючАналитики ИЗ втКлючиАналитикиПоПартнерам КАК Т)
		|			И (Валюта <> &ВалютаУпрУчета ИЛИ Валюта <> &ВалютаРеглУчета)
		|	) КАК Остатки";
	КонецЕсли;
	
	Если РегистрыДляРасчета.Свойство("ДенежныеСредстваБезналичные") Тогда
		ТекстЗапроса = ТекстЗапроса + ?(НЕ ПустаяСтрока(ТекстЗапроса), ОБЪЕДИНИТЬ, "") +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""ДенежныеСредстваБезналичные"" КАК ПредметПереоценки,
		|	Остатки.БанковскийСчет КАК ДенежныеСредства
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваБезналичные.Остатки(
		|		&ГраницаКонецПериода,
		|		(Организация В (&Организация) ИЛИ &ВсеОрганизации)
		|			И (БанковскийСчет.ВалютаДенежныхСредств <> &ВалютаУпрУчета
		|				ИЛИ БанковскийСчет.ВалютаДенежныхСредств <> &ВалютаРеглУчета)
		|	) КАК Остатки";
	КонецЕсли;
	
	Если РегистрыДляРасчета.Свойство("ДенежныеСредстваНаличные") Тогда
		ТекстЗапроса = ТекстЗапроса + ?(НЕ ПустаяСтрока(ТекстЗапроса), ОБЪЕДИНИТЬ, "") +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""ДенежныеСредстваНаличные"" КАК ПредметПереоценки,
		|	Остатки.Касса КАК ДенежныеСредства
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные.Остатки(
		|		&ГраницаКонецПериода,
		|		(Организация В (&Организация) ИЛИ &ВсеОрганизации)
		|			И (Касса В (&АналитикаРасчета) ИЛИ &ПоВсемАналитикам)
		|			И (Касса.ВалютаДенежныхСредств <> &ВалютаУпрУчета
		|				ИЛИ Касса.ВалютаДенежныхСредств <> &ВалютаРеглУчета)
		|	) КАК Остатки";
	КонецЕсли;
	
	Если РегистрыДляРасчета.Свойство("ДенежныеСредстваВКассахККМ") Тогда
		ТекстЗапроса = ТекстЗапроса + ?(НЕ ПустаяСтрока(ТекстЗапроса), ОБЪЕДИНИТЬ, "") +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""ДенежныеСредстваВКассахККМ"" КАК ПредметПереоценки,
		|	Остатки.КассаККМ КАК ДенежныеСредства
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваВКассахККМ.Остатки(
		|		&ГраницаКонецПериода,
		|		(Организация В (&Организация) ИЛИ &ВсеОрганизации)
		|			И (КассаККМ.ВалютаДенежныхСредств <> &ВалютаУпрУчета
		|			ИЛИ КассаККМ.ВалютаДенежныхСредств <> &ВалютаРеглУчета)
		|	) КАК Остатки";
	КонецЕсли;
	
	Если РегистрыДляРасчета.Свойство("ДенежныеСредстваУПодотчетныхЛиц") Тогда
		ТекстЗапроса = ТекстЗапроса + ?(НЕ ПустаяСтрока(ТекстЗапроса), ОБЪЕДИНИТЬ, "") +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""ДенежныеСредстваУПодотчетныхЛиц"" КАК ПредметПереоценки,
		|	Остатки.ПодотчетноеЛицо КАК ДенежныеСредства
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.Остатки(
		|		&ГраницаКонецПериода,
		|		(Организация В (&Организация) ИЛИ &ВсеОрганизации)
		|			И (ПодотчетноеЛицо В (&АналитикаРасчета) ИЛИ &ПоВсемАналитикам)
		|			И (Валюта <> &ВалютаУпрУчета ИЛИ Валюта <> &ВалютаРеглУчета)
		|	) КАК Остатки";
	КонецЕсли;
	
	Если РегистрыДляРасчета.Свойство("ДенежныеСредстваВПути") Тогда
		ТекстЗапроса = ТекстЗапроса + ?(НЕ ПустаяСтрока(ТекстЗапроса), ОБЪЕДИНИТЬ, "") +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""ДенежныеСредстваВПути"" КАК ПредметПереоценки,
		|	Остатки.ВидПереводаДенежныхСредств КАК ДенежныеСредства
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваВПути.Остатки(
		|		&ГраницаКонецПериода,
		|		(Организация В (&Организация) ИЛИ &ВсеОрганизации)
		|			И (Валюта <> &ВалютаУпрУчета ИЛИ Валюта <> &ВалютаРеглУчета)
		|	) КАК Остатки";
	КонецЕсли;
	
	Если РегистрыДляРасчета.Свойство("РасчетыПоДоговорамКредитовИДепозитов") Тогда
		ТекстЗапроса = ТекстЗапроса + ?(НЕ ПустаяСтрока(ТекстЗапроса), ОБЪЕДИНИТЬ, "") +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""РасчетыПоДоговорамКредитовИДепозитов"" КАК ПредметПереоценки,
		|	Остатки.Договор КАК Валюта
		|ИЗ
		|	РегистрНакопления.РасчетыПоДоговорамКредитовИДепозитов.Остатки(
		|		&ГраницаКонецПериода,
		|		(АналитикаУчетаПоПартнерам.Организация В (&Организация) ИЛИ &ВсеОрганизации)
		|			И (Договор.ВалютаВзаиморасчетов <> &ВалютаУпрУчета
		|				ИЛИ Договор.ВалютаВзаиморасчетов <> &ВалютаРеглУчета)
		|	) КАК Остатки";
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапросаВТ + ТекстЗапроса);
	Запрос.УстановитьПараметр("ГраницаКонецПериода", Новый Граница(КонецМесяца(Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ВалютаРеглУчета",     Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаУпрУчета",      Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("Организация",         Организация);
	Запрос.УстановитьПараметр("ВсеОрганизации",      НЕ ЗначениеЗаполнено(Организация));
	Запрос.УстановитьПараметр("АналитикаРасчета",    АналитикаРасчета);
	Запрос.УстановитьПараметр("ПоВсемАналитикам",    НЕ ЗначениеЗаполнено(АналитикаРасчета));
	
	Результат = Запрос.Выполнить();
	Возврат НЕ Результат.Пустой();
	
КонецФункции

Функция ТребуетсяПереоценка(Организация, Период, РегистрыДляРасчета = Неопределено, АналитикаРасчета = Неопределено) Экспорт
	
	Если РегистрыДляРасчета = Неопределено Тогда
		РегистрыДляРасчета = РегистрыРасчета();
	КонецЕсли;
	
	ТекстЗапросаВТ = "";
	Если РегистрыДляРасчета.Свойство("РасчетыСПоставщикамиПоДокументам")
		ИЛИ РегистрыДляРасчета.Свойство("РасчетыСКлиентамиПоДокументам") Тогда
		ТекстЗапросаВТ = ТекстЗапросаВТ + 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
		|ПОМЕСТИТЬ втКлючиАналитикиПоПартнерам
		|ИЗ
		|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
		|ГДЕ
		|	(АналитикаУчетаПоПартнерам.Организация В (&Организация) ИЛИ &ВсеОрганизации)
		|	И (АналитикаУчетаПоПартнерам.КлючАналитики В (&АналитикаРасчета) ИЛИ &ПоВсемАналитикам)
		|ИНДЕКСИРОВАТЬ ПО
		|	КлючАналитики
		|;
		|
		|"
	КонецЕсли;
	
	ТекстЗапросаВТ = ТекстЗапросаВТ + 
	"////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КурсыВалютСрезПоследних.Валюта
	|ПОМЕСТИТЬ втВалюты
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&НаДату,(Валюта <> &ВалютаУпрУчета ИЛИ Валюта <> &ВалютаРеглУчета)) КАК КурсыВалютСрезПоследних
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта,
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ПОМЕСТИТЬ втКурсы
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&НаДату,) КАК КурсыВалютСрезПоследних"
	+"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	
	Разделитель = Символы.ПС+Символы.ПС;
	ОБЪЕДИНИТЬ = Разделитель+"ОБЪЕДИНИТЬ ВСЕ"+Разделитель;
	ТекстЗапроса = "";
	Если РегистрыДляРасчета.Свойство("РасчетыСПоставщикамиПоДокументам") Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""РасчетыСПоставщикамиПоДокументам"" КАК РегистрНакопления,
		|	Остатки.Валюта КАК Валюта,
		|	Остатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Остатки.ЗаказПоставщику КАК Объект,
		|	Остатки.РасчетныйДокумент КАК РасчетныйДокумент
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам.Остатки(
		|		&ГраницаКонецПериода,
		|		(Валюта <> &ВалютаУпрУчета ИЛИ Валюта <> &ВалютаРеглУчета)
		|	) КАК Остатки
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКлючиАналитикиПоПартнерам КАК Аналитика
		|	ПО Остатки.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК КурсРегл
		|	ПО Остатки.Валюта = КурсРегл.Валюта
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК КурсУпр
		|	ПО &ВалютаУпрУчета = КурсУпр.Валюта
		|ГДЕ
		|	Остатки.ДолгОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.ДолгУпрОстаток >= 0.01
		|	ИЛИ Остатки.ДолгОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.ДолгУпрОстаток <= -0.01
		|	ИЛИ Остатки.ДолгОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.ДолгРеглОстаток >= 0.01
		|	ИЛИ Остатки.ДолгОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.ДолгРеглОстаток <= -0.01
		|	ИЛИ (Остатки.ПредоплатаОстаток = 0 И Остатки.КВозвратуОстаток = 0
		|		И (Остатки.ПредоплатаОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.ПредоплатаРеглОстаток >= 0.01
		|			ИЛИ Остатки.ПредоплатаОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.ПредоплатаРеглОстаток <= -0.01
		|			ИЛИ Остатки.ПредоплатаОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.ПредоплатаУпрОстаток >= 0.01
		|			ИЛИ Остатки.ПредоплатаОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.ПредоплатаУпрОстаток <= -0.01
		|			)
		|		)";
	КонецЕсли;
	
	Если РегистрыДляРасчета.Свойство("РасчетыСКлиентамиПоДокументам") Тогда
		ТекстЗапроса = ТекстЗапроса + ?(НЕ ПустаяСтрока(ТекстЗапроса), ОБЪЕДИНИТЬ, "") +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""РасчетыСКлиентамиПоДокументам"" КАК РегистрНакопления,
		|	Остатки.Валюта КАК Валюта,
		|	Остатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Остатки.ЗаказКлиента КАК Объект,
		|	Остатки.РасчетныйДокумент КАК РасчетныйДокумент
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоДокументам.Остатки(
		|		&ГраницаКонецПериода,
		|		(Валюта <> &ВалютаУпрУчета ИЛИ Валюта <> &ВалютаРеглУчета)
		|	) КАК Остатки
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКлючиАналитикиПоПартнерам КАК Аналитика
		|	ПО Остатки.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК КурсРегл
		|	ПО Остатки.Валюта = КурсРегл.Валюта
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК КурсУпр
		|	ПО &ВалютаУпрУчета = КурсУпр.Валюта
		|ГДЕ
		|	Остатки.ДолгОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.ДолгУпрОстаток >= 0.01
		|	ИЛИ Остатки.ДолгОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.ДолгУпрОстаток <= -0.01
		|	ИЛИ Остатки.ДолгОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.ДолгРеглОстаток >= 0.01
		|	ИЛИ Остатки.ДолгОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.ДолгРеглОстаток <= -0.01
		|	ИЛИ (Остатки.ПредоплатаОстаток = 0 И Остатки.КВозвратуОстаток = 0
		|		И (Остатки.ПредоплатаОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.ПредоплатаРеглОстаток >= 0.01
		|			ИЛИ Остатки.ПредоплатаОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.ПредоплатаРеглОстаток <= -0.01
		|			ИЛИ Остатки.ПредоплатаОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.ПредоплатаУпрОстаток >= 0.01
		|			ИЛИ Остатки.ПредоплатаОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.ПредоплатаУпрОстаток <= -0.01
		|			)
		|		)";
	КонецЕсли;
	
	Если РегистрыДляРасчета.Свойство("ДенежныеСредстваБезналичные") Тогда
		ТекстЗапроса = ТекстЗапроса + ?(НЕ ПустаяСтрока(ТекстЗапроса), ОБЪЕДИНИТЬ, "") +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""ДенежныеСредстваБезналичные"" КАК РегистрНакопления,
		|	НЕОПРЕДЕЛЕНО КАК Валюта,
		|	НЕОПРЕДЕЛЕНО КАК ЦельВыдачи,
		|	Остатки.БанковскийСчет КАК ДенежныеСредства,
		|	НЕОПРЕДЕЛЕНО КАК РасчетныйДокумент
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваБезналичные.Остатки(
		|		&ГраницаКонецПериода,
		|		(Организация В (&Организация) ИЛИ &ВсеОрганизации)
		|			И (БанковскийСчет.ВалютаДенежныхСредств <> &ВалютаУпрУчета
		|				ИЛИ БанковскийСчет.ВалютаДенежныхСредств <> &ВалютаРеглУчета)
		|	) КАК Остатки
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК КурсРегл
		|	ПО Остатки.БанковскийСчет.ВалютаДенежныхСредств = КурсРегл.Валюта
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК КурсУпр
		|	ПО &ВалютаУпрУчета = КурсУпр.Валюта
		|ГДЕ
		|	Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.СуммаУпрОстаток >= 0.01
		|	ИЛИ Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.СуммаУпрОстаток <= -0.01
		|	ИЛИ Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.СуммаРеглОстаток >= 0.01
		|	ИЛИ Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.СуммаРеглОстаток <= -0.01";
	КонецЕсли;
	
	Если РегистрыДляРасчета.Свойство("ДенежныеСредстваНаличные") Тогда
		ТекстЗапроса = ТекстЗапроса + ?(НЕ ПустаяСтрока(ТекстЗапроса), ОБЪЕДИНИТЬ, "") +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""ДенежныеСредстваНаличные"" КАК РегистрНакопления,
		|	НЕОПРЕДЕЛЕНО КАК Валюта,
		|	НЕОПРЕДЕЛЕНО КАК ЦельВыдачи,
		|	Остатки.Касса КАК ДенежныеСредства,
		|	НЕОПРЕДЕЛЕНО КАК РасчетныйДокумент
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные.Остатки(
		|		&ГраницаКонецПериода,
		|		(Организация В (&Организация) ИЛИ &ВсеОрганизации)
		|			И (Касса В (&АналитикаРасчета) ИЛИ &ПоВсемАналитикам)
		|			И (Касса.ВалютаДенежныхСредств <> &ВалютаУпрУчета
		|				ИЛИ Касса.ВалютаДенежныхСредств <> &ВалютаРеглУчета)
		|	) КАК Остатки
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК КурсРегл
		|	ПО Остатки.Касса.ВалютаДенежныхСредств = КурсРегл.Валюта
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК КурсУпр
		|	ПО &ВалютаУпрУчета = КурсУпр.Валюта
		|ГДЕ
		|	Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.СуммаУпрОстаток >= 0.01
		|	ИЛИ Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.СуммаУпрОстаток <= -0.01
		|	ИЛИ Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.СуммаРеглОстаток >= 0.01
		|	ИЛИ Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.СуммаРеглОстаток <= -0.01";
	КонецЕсли;
	
	Если РегистрыДляРасчета.Свойство("ДенежныеСредстваВКассахККМ") Тогда
		ТекстЗапроса = ТекстЗапроса + ?(НЕ ПустаяСтрока(ТекстЗапроса), ОБЪЕДИНИТЬ, "") +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""ДенежныеСредстваВКассахККМ"" КАК РегистрНакопления,
		|	НЕОПРЕДЕЛЕНО КАК Валюта,
		|	НЕОПРЕДЕЛЕНО КАК ЦельВыдачи,
		|	Остатки.КассаККМ КАК ДенежныеСредства,
		|	НЕОПРЕДЕЛЕНО КАК РасчетныйДокумент
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваВКассахККМ.Остатки(
		|		&ГраницаКонецПериода,
		|		(Организация В (&Организация) ИЛИ &ВсеОрганизации)
		|			И (КассаККМ.ВалютаДенежныхСредств <> &ВалютаУпрУчета
		|				ИЛИ КассаККМ.ВалютаДенежныхСредств <> &ВалютаРеглУчета)
		|	) КАК Остатки
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК КурсРегл
		|	ПО Остатки.КассаККМ.ВалютаДенежныхСредств = КурсРегл.Валюта
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК КурсУпр
		|	ПО &ВалютаУпрУчета = КурсУпр.Валюта
		|ГДЕ
		|	Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.СуммаУпрОстаток >= 0.01
		|	ИЛИ Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.СуммаУпрОстаток <= -0.01
		|	ИЛИ Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.СуммаРеглОстаток >= 0.01
		|	ИЛИ Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.СуммаРеглОстаток <= -0.01";
	КонецЕсли;
	
	Если РегистрыДляРасчета.Свойство("ДенежныеСредстваУПодотчетныхЛиц") Тогда
		ТекстЗапроса = ТекстЗапроса + ?(НЕ ПустаяСтрока(ТекстЗапроса), ОБЪЕДИНИТЬ, "") +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""ДенежныеСредстваУПодотчетныхЛиц"" КАК РегистрНакопления,
		|	Остатки.Валюта КАК Валюта,
		|	Остатки.ЦельВыдачи КАК ЦельВыдачи,
		|	Остатки.ПодотчетноеЛицо КАК ДенежныеСредства,
		|	НЕОПРЕДЕЛЕНО КАК РасчетныйДокумент
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваУПодотчетныхЛиц.Остатки(
		|		&ГраницаКонецПериода,
		|		(Организация В (&Организация) ИЛИ &ВсеОрганизации)
		|			И (ПодотчетноеЛицо В (&АналитикаРасчета) ИЛИ &ПоВсемАналитикам)
		|			И (Валюта <> &ВалютаУпрУчета ИЛИ Валюта <> &ВалютаРеглУчета)
		|	) КАК Остатки
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК КурсРегл
		|	ПО Остатки.Валюта = КурсРегл.Валюта
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК КурсУпр
		|	ПО &ВалютаУпрУчета = КурсУпр.Валюта
		|ГДЕ
		|	Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.СуммаУпрОстаток >= 0.01
		|	ИЛИ Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.СуммаУпрОстаток <= -0.01
		|	ИЛИ Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.СуммаРеглОстаток >= 0.01
		|	ИЛИ Остатки.СуммаОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.СуммаРеглОстаток <= -0.01";
	КонецЕсли;
	
	Если РегистрыДляРасчета.Свойство("РасчетыПоДоговорамКредитовИДепозитов") Тогда
		ТекстЗапроса = ТекстЗапроса + ?(НЕ ПустаяСтрока(ТекстЗапроса), ОБЪЕДИНИТЬ, "") +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	""РасчетыПоДоговорамКредитовИДепозитов"" КАК РегистрНакопления,
		|	НЕОПРЕДЕЛЕНО КАК Валюта,
		|	Остатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Остатки.Договор КАК ДенежныеСредства,
		|	Остатки.ТипСуммы КАК РасчетныйДокумент
		|ИЗ
		|	РегистрНакопления.РасчетыПоДоговорамКредитовИДепозитов.Остатки(
		|		&ГраницаКонецПериода,
		|		(АналитикаУчетаПоПартнерам.Организация В (&Организация) ИЛИ &ВсеОрганизации)
		|			И (Договор.ВалютаВзаиморасчетов <> &ВалютаУпрУчета
		|				ИЛИ Договор.ВалютаВзаиморасчетов <> &ВалютаРеглУчета)
		|	) КАК Остатки
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК КурсРегл
		|	ПО Остатки.Договор.ВалютаВзаиморасчетов = КурсРегл.Валюта
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК КурсУпр
		|	ПО &ВалютаУпрУчета = КурсУпр.Валюта
		|ГДЕ
		|	Остатки.СуммаВВалютеОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.СуммаУпрОстаток >= 0.01
		|	ИЛИ Остатки.СуммаВВалютеОстаток * КурсРегл.Курс / КурсРегл.Кратность / КурсУпр.Курс / КурсУпр.Кратность - Остатки.СуммаУпрОстаток <= -0.01
		|	ИЛИ Остатки.СуммаВВалютеОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.СуммаРеглОстаток >= 0.01
		|	ИЛИ Остатки.СуммаВВалютеОстаток * КурсРегл.Курс / КурсРегл.Кратность - Остатки.СуммаРеглОстаток <= -0.01";
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапросаВТ + ТекстЗапроса);
	Запрос.УстановитьПараметр("НаДату",              КонецМесяца(Период));
	Запрос.УстановитьПараметр("ГраницаКонецПериода", Новый Граница(КонецМесяца(Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ВалютаРеглУчета",     Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаУпрУчета",      Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("Организация",         Организация);
	Запрос.УстановитьПараметр("ВсеОрганизации",      НЕ ЗначениеЗаполнено(Организация));
	Запрос.УстановитьПараметр("АналитикаРасчета",    АналитикаРасчета);
	Запрос.УстановитьПараметр("ПоВсемАналитикам",    НЕ ЗначениеЗаполнено(АналитикаРасчета));
	
	Результат = Запрос.Выполнить();// нерассчитанные курсовые разницы
	Возврат НЕ Результат.Пустой();
	
КонецФункции

Функция ТребуетсяПереоценкаВзаиморасчетов(Организация, Период, АналитикаРасчета = Неопределено) Экспорт
	
	РегистрыДляРасчета = Новый Структура;
	РегистрыДляРасчета.Вставить("РасчетыСПоставщикамиПоДокументам");
	РегистрыДляРасчета.Вставить("РасчетыСКлиентамиПоДокументам");
	
	Возврат ТребуетсяПереоценка(Организация, Период, РегистрыДляРасчета, АналитикаРасчета);
	
КонецФункции

Процедура УдалитьДвиженияАналитики(ДокументСсылка, ИмяРегистра, АналитикаРасчета = Неопределено)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.Регистратор = &Регистратор
	|	И НЕ (ДанныеРегистра.Касса В (&АналитикаРасчета))
	|	И &ВсеКромеАналитикиРасчета";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДенежныеСредстваНаличные", ИмяРегистра);
	Если ИмяРегистра = "РасчетыСКлиентамиПоДокументам"
		ИЛИ ИмяРегистра = "РасчетыСПоставщикамиПоДокументам"
		ИЛИ ИмяРегистра = "РасчетыПоДоговорамКредитовИДепозитов" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Касса", "АналитикаУчетаПоПартнерам");
		
	ИначеЕсли ИмяРегистра = "ДенежныеСредстваБезналичные" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Касса", "БанковскийСчет");
		
	ИначеЕсли ИмяРегистра = "ДенежныеСредстваВКассахККМ" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Касса", "КассаККМ");
		
	ИначеЕсли ИмяРегистра = "ДенежныеСредстваВПути" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Касса", "Получатель");
		
	ИначеЕсли ИмяРегистра = "ДенежныеСредстваУПодотчетныхЛиц" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Касса", "ПодотчетноеЛицо");
		
	КонецЕсли;
	
	// Получим новый набор движений - набор без движений которые надо удалить
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Регистратор", ДокументСсылка);
	Запрос.УстановитьПараметр("АналитикаРасчета", АналитикаРасчета);
	Запрос.УстановитьПараметр("ВсеКромеАналитикиРасчета", ЗначениеЗаполнено(АналитикаРасчета));
	ДвиженияРегистра = Запрос.Выполнить().Выгрузить();
	
	Набор = РегистрыНакопления[ИмяРегистра].СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(ДокументСсылка);
	Набор.Загрузить(ДвиженияРегистра);
	Набор.ОбменДанными.Загрузка = Истина;
	Набор.Записать();
	
КонецПроцедуры

Процедура ПодготовитьНаборыЗаписейКРасчету(ДокументСсылка, РегистрыДляРасчета, АналитикаРасчета = Неопределено)
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Документ.РасчетКурсовыхРазниц");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	Для Каждого Регистр Из РегистрыДляРасчета Цикл
		ЭлементБлокировки = Блокировка.Добавить(СтрШаблон("РегистрНакопления.%1.НаборЗаписей", Регистр.Ключ));
		ЭлементБлокировки.УстановитьЗначение("Регистратор", ДокументСсылка);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	Для Каждого Регистр Из РегистрыДляРасчета Цикл
		Если АналитикаРасчета = Неопределено Тогда
			Набор = РегистрыНакопления[Регистр.Ключ].СоздатьНаборЗаписей();
			Набор.Отбор.Регистратор.Установить(ДокументСсылка);
			Набор.ОбменДанными.Загрузка = Истина;
			Набор.Записать();
		Иначе
			УдалитьДвиженияАналитики(ДокументСсылка, Регистр.Ключ, АналитикаРасчета);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСсылкиСправочника(ИмяСправочника, МассивСсылок, Организация = Неопределено)
	
	Шаблон = 
	"ВЫБРАТЬ
	|	Спр.Ссылка
	|ИЗ
	|	Справочник.%1 КАК Спр
	|ГДЕ
	|	Спр.Ссылка В(&МассивСсылок)
	|	И %2";
	ТекстЗапроса = СтрШаблон(Шаблон, ИмяСправочника, ?(ЗначениеЗаполнено(Организация),"Спр.Организация = &Организация","ИСТИНА"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	МассивКлючейОрганизации = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат МассивКлючейОрганизации;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы



#КонецОбласти

#КонецОбласти

#КонецЕсли
