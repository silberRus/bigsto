
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Организация", Параметры.Организация, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Партнер", Параметры.Партнер, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Контрагент", Параметры.Контрагент, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Договор", Параметры.Договор, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Соглашение", Параметры.Соглашение, Истина);
	
	Если ЗначениеЗаполнено(Параметры.Склад) Тогда
		Склады = ЗначениеНастроекПовтИсп.ИерархияГрупп(Параметры.Склад);
		Склады.Добавить(Справочники.Склады.ПустаяСсылка());
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "БезОтбораПоСкладу", Ложь, Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "БезОтбораПоСкладу", Истина, Истина);
		Склады = Новый Массив;
	КонецЕсли;
	  
	Склады.Добавить(Параметры.Склад);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Склады", Склады, Истина);
	
	ХозяйственныеОперацииСоглашения = Новый Массив;
	ХозяйственныеОперацииСоглашения.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС);
	ХозяйственныеОперацииСоглашения.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту);
	ХозяйственныеОперацииСоглашения.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
	
	ХозяйственныеОперацииДоговора = Новый Массив;
	ХозяйственныеОперацииДоговора.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки);
	ХозяйственныеОперацииДоговора.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути);
	ХозяйственныеОперацииДоговора.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
	ХозяйственныеОперацииДоговора.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки);
	ХозяйственныеОперацииДоговора.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
																		"ХозяйственныеОперацииДоговора",
																		ХозяйственныеОперацииДоговора,
																		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
																		"ХозяйственныеОперацииСоглашения",
																		ХозяйственныеОперацииСоглашения,
																		Истина);
	ВариантыПриемкиПоЗаказу = Новый Массив;
	ВариантыПриемкиПоЗаказу.Добавить(Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
																		"ВариантыПриемкиПоЗаказу",
																		ВариантыПриемкиПоЗаказу,
																		Истина);
																		
	ВариантыПриемкиПоНакладнаяПоЗаказу = Новый Массив;																	
	ВариантыПриемкиПоНакладнаяПоЗаказу.Добавить(Перечисления.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
																		"ВариантыПриемкиПоНакладнаяПоЗаказу",
																		ВариантыПриемкиПоНакладнаяПоЗаказу,
																		Истина);
																		
	ВариантыПриемкиПоНакладнаяБезЗаказа = Новый Массив;																	
	ВариантыПриемкиПоНакладнаяБезЗаказа.Добавить(Перечисления.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным);
	ВариантыПриемкиПоНакладнаяБезЗаказа.Добавить(Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
																		"ВариантыПриемкиПоНакладнаяБезЗаказа",
																		ВариантыПриемкиПоНакладнаяБезЗаказа,
																		Истина);

	ВариантыПриемкиПоСоглашению = Новый Массив;
	ВариантыПриемкиПоСоглашению.Добавить(Перечисления.ВариантыПриемкиТоваров.МожетПроисходитьБезЗаказовИНакладных);
	ВариантыПриемкиПоСоглашению.Добавить(Перечисления.ВариантыПриемкиТоваров.НеРазделенаПоЗаказамИНакладным);
	ВариантыПриемкиПоСоглашению.Добавить(Перечисления.ВариантыПриемкиТоваров.НеРазделенаПоНакладным);
	
	ВариантыПриемкиПоДоговору = Новый Массив;
	ВариантыПриемкиПоДоговору.Добавить(Перечисления.ВариантыПриемкиТоваров.ПоДоговорамБезЗаказовИНакладных);
	ВариантыПриемкиПоДоговору.Добавить(Перечисления.ВариантыПриемкиТоваров.ПоДоговорамПослеЗаказовИлиНакладных);
	ВариантыПриемкиПоДоговору.Добавить(Перечисления.ВариантыПриемкиТоваров.ПоДоговорамПослеНакладных);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
																		"ВариантыПриемкиПоСоглашению",
																		ВариантыПриемкиПоСоглашению,
																		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
																		"ВариантыПриемкиПоДоговору",
																		ВариантыПриемкиПоДоговору,
																		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
																		"КонстантаВариантПриемки",
																		Константы.ВариантПриемкиТоваров.Получить(),
																		Истина);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработатьВыбор(Элемент.ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ОбщегоНазначенияУТКлиент.ПроверитьНаличиеВыделенныхВСпискеСтрок(Элементы.Список) Тогда
		ОбработатьВыбор(Элементы.Список.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыбор(ТекущаяСтрока)
	
	ВыбранноеЗначение = Новый Структура;
	ВыбранноеЗначение.Вставить("Распоряжение", 		ТекущаяСтрока.Ссылка);
	ВыбранноеЗначение.Вставить("Партнер", 			ТекущаяСтрока.Партнер);
	ВыбранноеЗначение.Вставить("Контрагент", 		ТекущаяСтрока.Контрагент);
	ВыбранноеЗначение.Вставить("Организация", 		ТекущаяСтрока.Организация);
	ВыбранноеЗначение.Вставить("Склад", 			ТекущаяСтрока.Склад);
	ВыбранноеЗначение.Вставить("Соглашение", 		ТекущаяСтрока.Соглашение);
	ВыбранноеЗначение.Вставить("Договор", 			ТекущаяСтрока.Договор);
	ВыбранноеЗначение.Вставить("ВидЦеныПоставщика", ТекущаяСтрока.ВидЦеныПоставщика);
	ВыбранноеЗначение.Вставить("Валюта", 			ТекущаяСтрока.Валюта);
	ВыбранноеЗначение.Вставить("ЭтоГруппаСкладов", 	ТекущаяСтрока.ЭтоГруппаСкладов);
	
	Закрыть(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти