#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает текст запроса для определения реквизитов доставки.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстЗапросаРеквизитыДоставки() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Шапка.Номер             КАК Номер,
	|	Шапка.Проведен          КАК Проведен,
	|	Шапка.Ссылка            КАК Ссылка,
	|	Шапка.ДатаВыполнения    КАК Дата,
	|
	|	Шапка.Пункт
	|                           КАК ПолучательОтправитель,
	|
	|	НЕОПРЕДЕЛЕНО            КАК Перевозчик,
	|	Шапка.СпособДоставки    КАК СпособДоставки,
	|	Шапка.ЗонаДоставки      КАК Зона,
	|	Шапка.АдресДоставки     КАК Адрес,
	|
	|	Шапка.АдресДоставкиЗначенияПолей
	|		                    КАК АдресЗначенияПолей,
	|
	|	Шапка.ВремяДоставкиС    КАК ВремяС,
	|	Шапка.ВремяДоставкиПо   КАК ВремяПо,
	|	ДополнительнаяИнформацияПоДоставке КАК ДополнительнаяИнформация,
	|	Шапка.Склад             КАК Склад,
	|	ИСТИНА                  КАК ДоставитьПолностью,
	|	ИСТИНА                  КАК ОсобыеУсловияПеревозки,
	|	Шапка.ОсобыеУсловияПеревозкиОписание КАК ОсобыеУсловияПеревозкиОписание,
	|	ЛОЖЬ КАК РазбиватьРасходныеОрдераПоРаспоряжениям
	|
	|ИЗ
	|	Документ.ПоручениеЭкспедитору КАК Шапка
	|ГДЕ
	|	Шапка.Ссылка В (&Ссылки)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция извлекает из оснований данные для заполнения поручения.
//
// Параметры:
//  Основания	 - Массив	 - основания для ввода поручений экспедитору.
// 
// Возвращаемое значение:
//  Структура - со свойствами:
//  *Склады - Массив - СправочникСсылка.Склады
//  *Пункты - Массив - СправочникСсылка.Партнеры или СправочникСсылка.Склады или СправочникСсылка.СтруктураПредприятия
//  *Контакты - Массив - СправочникСсылка.КонтактныеЛицаПартнеров
//
Функция ДанныеОснований(Основания) Экспорт
	
	ШаблонЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка) КАК Партнер,
	|ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|ЗНАЧЕНИЕ(Справочник.КонтактныеЛицаПартнеров.ПустаяСсылка) КАК КонтактноеЛицо
	|ПОМЕСТИТЬ ДанныеОснований
	|ИЗ #ТаблицаДокумента КАК Т
	|
	|ГДЕ Т.Ссылка В (&Основания)";
	
	ОснованияПоТипам = ОбщегоНазначенияУТ.РазложитьМассивСсылокПоТипам(Основания);
	ПервыйЗапрос = Истина;
	ТекстЗапроса = "";
	Для Каждого КлючИЗначение Из ОснованияПоТипам Цикл
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(КлючИЗначение.Ключ);
		ИмяТаблицыОбъекта = МетаданныеОбъекта.ПолноеИмя();
		ТекстЗапросаПоТипу = ШаблонЗапроса;
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Склад", МетаданныеОбъекта) Тогда
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад", "Склад КАК Склад");
		КонецЕсли;
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Партнер", МетаданныеОбъекта) Тогда
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка) КАК Партнер", "Партнер КАК Партнер");
		КонецЕсли;
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Подразделение", МетаданныеОбъекта) Тогда
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение", "Подразделение КАК Подразделение");
		КонецЕсли;
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("КонтактноеЛицо", МетаданныеОбъекта) Тогда
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "ЗНАЧЕНИЕ(Справочник.КонтактныеЛицаПартнеров.ПустаяСсылка) КАК КонтактноеЛицо", "КонтактноеЛицо КАК КонтактноеЛицо");
		КонецЕсли;
		ДополнитьЗапросПоОснованиям(ТекстЗапроса, ТекстЗапросаПоТипу, ПервыйЗапрос, ИмяТаблицыОбъекта);
		
		ТекстЗапросаПоТипу = ШаблонЗапроса;
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СкладОтправитель", МетаданныеОбъекта) Тогда
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад", "СкладОтправитель КАК Склад");
		КонецЕсли;		
		ДополнитьЗапросПоОснованиям(ТекстЗапроса, ТекстЗапросаПоТипу, ПервыйЗапрос, ИмяТаблицыОбъекта);
		
		ТекстЗапросаПоТипу = ШаблонЗапроса;
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СкладПолучатель", МетаданныеОбъекта) Тогда
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад", "СкладПолучатель КАК Склад");
		КонецЕсли;		
		ДополнитьЗапросПоОснованиям(ТекстЗапроса, ТекстЗапросаПоТипу, ПервыйЗапрос, ИмяТаблицыОбъекта);
		
		Для Каждого ТЧ Из МетаданныеОбъекта.ТабличныеЧасти Цикл
			Если Не ОбщегоНазначения.ЕстьРеквизитОбъекта("Склад", ТЧ) Тогда
				Продолжить;
			КонецЕсли;
			ИмяТаблицыТЧ = ИмяТаблицыОбъекта + "." + ТЧ.Имя;
			ТекстЗапросаПоТипу = ШаблонЗапроса;
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад", "Склад КАК Склад");
			ДополнитьЗапросПоОснованиям(ТекстЗапроса, ТекстЗапросаПоТипу, ПервыйЗапрос, ИмяТаблицыТЧ);
		КонецЦикла;
		Для Каждого ТЧ Из МетаданныеОбъекта.ТабличныеЧасти Цикл
			Если Не ОбщегоНазначения.ЕстьРеквизитОбъекта("СкладОтправитель", ТЧ) Тогда
				Продолжить;
			КонецЕсли;
			ИмяТаблицыТЧ = ИмяТаблицыОбъекта + "." + ТЧ.Имя;
			ТекстЗапросаПоТипу = ШаблонЗапроса;
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад", "СкладОтправитель КАК Склад");			
			ДополнитьЗапросПоОснованиям(ТекстЗапроса, ТекстЗапросаПоТипу, ПервыйЗапрос, ИмяТаблицыТЧ);
		КонецЦикла;
		Для Каждого ТЧ Из МетаданныеОбъекта.ТабличныеЧасти Цикл
			Если Не ОбщегоНазначения.ЕстьРеквизитОбъекта("СкладПолучатель", ТЧ) Тогда
				Продолжить;
			КонецЕсли;
			ИмяТаблицыТЧ = ИмяТаблицыОбъекта + "." + ТЧ.Имя;
			ТекстЗапросаПоТипу = ШаблонЗапроса;
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад", "СкладПолучатель КАК Склад");
			ДополнитьЗапросПоОснованиям(ТекстЗапроса, ТекстЗапросаПоТипу, ПервыйЗапрос, ИмяТаблицыТЧ);
		КонецЦикла;
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса + "
	|//////////////////////////////////////////
	|;
	|" +
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Т.Склад
	|ИЗ
	|	ДанныеОснований КАК Т
	|ГДЕ
	|	Т.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|	И НЕ Т.Склад.ЭтоГруппа
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Склад
	|
	|УПОРЯДОЧИТЬ ПО
	|	КОЛИЧЕСТВО(*) УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Т.Пункт
	|ИЗ
	|	(ВЫБРАТЬ
	|		Т.Партнер КАК Пункт,
	|		1 КАК Порядок
	|	ИЗ
	|		ДанныеОснований КАК Т
	|	ГДЕ
	|		Т.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Т.Подразделение,
	|		2
	|	ИЗ
	|		ДанныеОснований КАК Т
	|	ГДЕ
	|		Т.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Т.Склад,
	|		3
	|	ИЗ
	|		ДанныеОснований КАК Т
	|	ГДЕ
	|		Т.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		И НЕ Т.Склад.ЭтоГруппа) КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Пункт,
	|	Т.Порядок
	|
	|УПОРЯДОЧИТЬ ПО
	|	Т.Порядок,
	|	КОЛИЧЕСТВО(*) УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.КонтактноеЛицо
	|ИЗ
	|	ДанныеОснований КАК Т
	|ГДЕ
	|	Т.КонтактноеЛицо <> ЗНАЧЕНИЕ(Справочник.КонтактныеЛицаПартнеров.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.КонтактноеЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	КОЛИЧЕСТВО(*) УБЫВ";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Основания", Основания);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	СтруктураВозврата = Новый Структура("Склады, Пункты, Контакты");
	СтруктураВозврата.Склады   = РезультатЗапроса[РезультатЗапроса.Количество()-3].Выгрузить().ВыгрузитьКолонку("Склад");
	СтруктураВозврата.Пункты   = РезультатЗапроса[РезультатЗапроса.Количество()-2].Выгрузить().ВыгрузитьКолонку("Пункт");
	СтруктураВозврата.Контакты = РезультатЗапроса[РезультатЗапроса.Количество()-1].Выгрузить().ВыгрузитьКолонку("КонтактноеЛицо");
	
	Возврат СтруктураВозврата;

КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Поручение экспедитору
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПоручениеЭкспедитору";
	КомандаПечати.Представление = НСтр("ru = 'Поручение экспедитору'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПоручениеЭкспедитору") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПоручениеЭкспедитору",
			НСтр("ru = 'Поручение экспедитору'"),
			СформироватьПечатнуюФормуПорученияЭкспедитору(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуПорученияЭкспедитору(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПОРУЧЕНИЕЭКСПЕДИТОРУ";
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеДляПечати = ПолучитьДанныеДляПечатнойФормыПоручениеЭкспедитору(МассивОбъектов, ПараметрыПечати);
	ЗаполнитьТабличныйДокументПоручениеЭкспедитору(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПолучитьДанныеДляПечатнойФормыПоручениеЭкспедитору(МассивОбъектов, ПараметрыПечати)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоручениеЭкспедитору.Ссылка КАК Ссылка,
	|	ПоручениеЭкспедитору.Номер КАК НомерПоручения,
	|	ПоручениеЭкспедитору.Дата КАК Дата,
	|	ПоручениеЭкспедитору.СпособДоставки КАК СпособДоставки,
	|	ПоручениеЭкспедитору.Склад.Представление КАК Склад,
	|	ПРЕДСТАВЛЕНИЕ(ПоручениеЭкспедитору.Пункт) КАК Пункт,
	|	ПоручениеЭкспедитору.КонтактноеЛицо.Представление КАК Контакт,
	|	ПоручениеЭкспедитору.АдресДоставки КАК АдресДоставки,
	|	ПоручениеЭкспедитору.ЗонаДоставки.Представление КАК ЗонаДоставки,
	|	ПоручениеЭкспедитору.ВремяДоставкиС КАК ВремяС,
	|	ПоручениеЭкспедитору.ВремяДоставкиПо КАК ВремяПо,
	|	ПоручениеЭкспедитору.ДополнительнаяИнформацияПоДоставке КАК ИнформацияПоПункту,
	|	ПоручениеЭкспедитору.ОсобыеУсловияПеревозкиОписание КАК ОписаниеПоручения,
	|	ПоручениеЭкспедитору.Ответственный.Представление КАК Ответственный
	|ИЗ
	|	Документ.ПоручениеЭкспедитору КАК ПоручениеЭкспедитору
	|ГДЕ
	|	ПоручениеЭкспедитору.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	ДанныеДляПечати = РезультатЗапроса.Выгрузить();
	
	Возврат ДанныеДляПечати;
	
КонецФункции

Процедура ЗаполнитьТабличныйДокументПоручениеЭкспедитору(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати)
	
	ДанныеДляПечатиСтрока = ДанныеДляПечати[0];
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПоручениеЭкспедитору.ПФ_MXL_ПоручениеЭкспедитору");
	
	// Области документа
	ОбластьШтрихкода = Макет.ПолучитьОбласть("ОбластьШтрихкода");
	ОбластьЗаголовока = Макет.ПолучитьОбласть("Заголовок");
	ОбластьПункта = Макет.ПолучитьОбласть("ОбластьПункт");
	ОбластьАдреса = Макет.ПолучитьОбласть("ОбластьАдресДоставки");
	ОбластьОписания = Макет.ПолучитьОбласть("ОбластьОписание");
	ОбластьПодписи = Макет.ПолучитьОбласть("Подписи");
	
	// Реквизиты заголовка документа
	РеквизитыДокумента = Новый Структура;
	РеквизитыДокумента.Вставить("Номер", ДанныеДляПечатиСтрока.НомерПоручения);
	РеквизитыДокумента.Вставить("Дата", ДанныеДляПечатиСтрока.Дата);
	
	ЗаголовокДокумента = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(РеквизитыДокумента,
		НСтр("ru='Поручение экспедитору'"));
	
	// Заполнение параметров областей ПФ_MXL_ПоручениеЭкспедитору
	ОбластьЗаголовока.Параметры.ТекстЗаголовка = ЗаголовокДокумента;
	
	ОбластьПункта.Параметры.Заполнить(ДанныеДляПечатиСтрока);
	ОбластьАдреса.Параметры.Заполнить(ДанныеДляПечатиСтрока);
	ОбластьОписания.Параметры.Заполнить(ДанныеДляПечатиСтрока);
	ОбластьПодписи.Параметры.Заполнить(ДанныеДляПечатиСтрока);
	
	// Вывод областей ПФ_MXL_ПоручениеЭкспедитору
	ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(
		ТабличныйДокумент,
		Макет,
		ОбластьЗаголовока,
		ДанныеДляПечатиСтрока.Ссылка);
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовока);
	
	Если ДанныеДляПечатиСтрока.СпособДоставки = Перечисления.СпособыДоставки.ПоручениеЭкспедиторуСоСклада
		Или ДанныеДляПечатиСтрока.СпособДоставки = Перечисления.СпособыДоставки.ПоручениеЭкспедиторуНаСклад Тогда
		
		ОбластьНаправлениеОткуда = Макет.ПолучитьОбласть("ЗаголовокНаправление");
		ОбластьСклада = Макет.ПолучитьОбласть("ОбластьСклад");
		ОбластьНаправлениеКуда = Макет.ПолучитьОбласть("ЗаголовокНаправление");
		
		ОбластьНаправлениеОткуда.Параметры.Направление = НСтр("ru='Откуда'");
		ОбластьСклада.Параметры.Склад = ДанныеДляПечатиСтрока.Склад;
		ОбластьНаправлениеКуда.Параметры.Направление = НСтр("ru='Куда'");
		
		Если ДанныеДляПечатиСтрока.СпособДоставки = Перечисления.СпособыДоставки.ПоручениеЭкспедиторуСоСклада Тогда
			
			ТабличныйДокумент.Вывести(ОбластьНаправлениеОткуда);
			ТабличныйДокумент.Вывести(ОбластьСклада);
			ТабличныйДокумент.Вывести(ОбластьНаправлениеКуда);
			
		Иначе
			
			ТабличныйДокумент.Вывести(ОбластьНаправлениеКуда);
			ТабличныйДокумент.Вывести(ОбластьСклада);
			ТабличныйДокумент.Вывести(ОбластьНаправлениеОткуда);
			
		КонецЕсли;
		
	Иначе
		ОбластьНаправление = Макет.ПолучитьОбласть("ЗаголовокНаправление");
		ОбластьНаправление.Параметры.Направление = НСтр("ru='Где'");
		
		ТабличныйДокумент.Вывести(ОбластьНаправление);
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(ОбластьПункта);
	
	Если ЗначениеЗаполнено(ДанныеДляПечатиСтрока.Контакт) Тогда
		ОбластьКонтакта = Макет.ПолучитьОбласть("ОбластьКонтакт");
		
		ОбластьКонтакта.Параметры.Заполнить(ДанныеДляПечатиСтрока);
		ТабличныйДокумент.Вывести(ОбластьКонтакта);
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(ОбластьАдреса);
	
	Если ЗначениеЗаполнено(ДанныеДляПечатиСтрока.ЗонаДоставки) Тогда
		ОбластьЗоны = Макет.ПолучитьОбласть("ОбластьЗона");
		
		ОбластьЗоны.Параметры.Заполнить(ДанныеДляПечатиСтрока);
		ТабличныйДокумент.Вывести(ОбластьЗоны);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеДляПечатиСтрока.ВремяС)
		Или ЗначениеЗаполнено(ДанныеДляПечатиСтрока.ВремяПо) Тогда
		
		ОбластьВремени = Макет.ПолучитьОбласть("ОбластьВремяДоставки");
		
		ВремяС = Формат(ДанныеДляПечатиСтрока.ВремяС, "ДФ=ЧЧ:ММ");
		ВремяПо = Формат(ДанныеДляПечатиСтрока.ВремяПо, "ДФ=ЧЧ:ММ");
		
		ОбластьВремени.Параметры.ВремяС = ВремяС;
		ОбластьВремени.Параметры.ВремяПо = ВремяПо;
		
		ТабличныйДокумент.Вывести(ОбластьВремени);
		
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(ОбластьОписания);
	ТабличныйДокумент.Вывести(ОбластьПодписи);
	
КонецПроцедуры

#КонецОбласти

#Область СозданиеНаОсновании

// Добавляет команду создания документа "Поручение экспедитору".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПоручениеЭкспедитору) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПоручениеЭкспедитору.ПолноеИмя();
		КомандаСоздатьНаОсновании.Обработчик = "СозданиеНаОснованииУТКлиент.ПоручениеЭкспедитору";
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПоручениеЭкспедитору);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьУправлениеДоставкой";
		КомандаСоздатьНаОсновании.МножественныйВыбор = Истина;
		
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

#КонецОбласти 

#Область Отчеты

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
КонецПроцедуры

#КонецОбласти 

#Область Прочее

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ДополнитьЗапросПоОснованиям(ТекстЗапроса, ТекстЗапросаДляДополнения, ПервыйЗапрос, ИмяТаблицы)
	
	Если Не ЗначениеЗаполнено(ТекстЗапросаДляДополнения) Тогда
		Возврат;
	КонецЕсли;
	ТекстЗапросаДляДополнения = СтрЗаменить(ТекстЗапросаДляДополнения, "#ТаблицаДокумента", ИмяТаблицы);
	Если ПервыйЗапрос Тогда
		ПервыйЗапрос = Ложь;
		ТекстЗапроса = ТекстЗапросаДляДополнения;
	Иначе
		ТекстЗапросаДляДополнения = СтрЗаменить(ТекстЗапросаДляДополнения, "ПОМЕСТИТЬ ДанныеОснований", "");
		ТекстЗапросаДляДополнения = СтрЗаменить(ТекстЗапросаДляДополнения, "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ");
		ТекстЗапроса = ТекстЗапроса +
		"
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|"
		+ ТекстЗапросаДляДополнения;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.ПоручениеЭкспедитору";
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументДляОбработки.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПоручениеЭкспедитору КАК ДокументДляОбработки
	|ГДЕ
	|	ДокументДляОбработки.АдресДоставки <> """"
	|	И ВЫРАЗИТЬ(ДокументДляОбработки.АдресДоставкиЗначенияПолей КАК СТРОКА (500)) <> """"
	|	И НЕ ДокументДляОбработки.АдресДоставкиЗначенияПолей ПОДОБНО ""%</КонтактнаяИнформация>""";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.ПоручениеЭкспедитору";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц);
	
	Если Не Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если Не Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОбъектыДляОбработки.Ссылка                       КАК Ссылка,
	|	ОбъектыДляОбработки.Ссылка.ВерсияДанных          КАК ВерсияДанных,
	|	ОбъектыДляОбработки.Ссылка.Проведен              КАК Проведен,
	|	ОбъектыДляОбработки.Ссылка.АдресДоставки         КАК АдресДоставки,
	|	ОбъектыДляОбработки.Ссылка.АдресДоставкиЗначенияПолей КАК АдресДоставкиЗначенияПолей
	|ПОМЕСТИТЬ ТаблицаСсылок
	|ИЗ
	|	ВТДокументыДляОбработки КАК ОбъектыДляОбработки
	|;
	|ВЫБРАТЬ
	|	ОбъектыДляОбработки.Ссылка                КАК Ссылка,
	|	ОбъектыДляОбработки.ВерсияДанных          КАК ВерсияДанных,
	|	ОбъектыДляОбработки.Проведен              КАК Проведен,
	|	ОбъектыДляОбработки.АдресДоставки <> """"
	|	И ВЫРАЗИТЬ(ОбъектыДляОбработки.АдресДоставкиЗначенияПолей КАК СТРОКА (500)) <> """"
	|	И НЕ ОбъектыДляОбработки.АдресДоставкиЗначенияПолей ПОДОБНО ""%</КонтактнаяИнформация>""
	|	КАК                                       ТребуетсяПересчетЗначенийПолейАдреса
	|ИЗ
	|	ТаблицаСсылок КАК ОбъектыДляОбработки";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТДокументыДляОбработки", Результат.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
		
			ДокументОбъект = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(Выборка.Ссылка, Выборка.ВерсияДанных, Параметры.Очередь);
			Если ДокументОбъект = Неопределено Тогда
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ОбъектИзменен = Ложь;
			
			Если Выборка.ТребуетсяПересчетЗначенийПолейАдреса Тогда
				НовыеЗначенияПолей = "";
				ОбщегоНазначенияУТ.ЗаполнитьЗначенияПолейКИПоПредставлению(ДокументОбъект.АдресДоставки, НовыеЗначенияПолей);
				ДокументОбъект.АдресДоставкиЗначенияПолей = НовыеЗначенияПолей;
				ОбъектИзменен = Истина;
			КонецЕсли;
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(),  Выборка.Ссылка);
			Продолжить;
		КонецПопытки;
	КонецЦикла;
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

