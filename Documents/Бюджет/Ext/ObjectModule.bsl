
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
		
	// Подготовка
	
	Документы[Метаданные().Имя].ИницилизироватьДополнительныеДанныеДокумента(Ссылка, ДополнительныеСвойства);
		
	ПроведенияДокументов.ПровестиПоВсемРегистрам(Метаданные().Движения, ДополнительныеСвойства, Движения, Отказ);
	
	// Запись
	
	Движения.Записать();

КонецПроцедуры                      	

Функция ВЗапретномПериоде(ЗапретДо)
	
	ЗапрПериод = Ложь;
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1 Значение ИЗ Константа.ДатаЗапретаИзмененийБюджета");
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ЗапретДо = Выборка.Значение;
		Для Каждого Строка Из Показатели Цикл
			Если Строка.Период < ЗапретДо Тогда
				ЗапрПериод = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ЗапрПериод;
	
КонецФункции
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Не ОбменДанными.Загрузка Тогда
		
		Если Автор.Пустая() Тогда 
			Автор = ПараметрыСеанса.ТекущийПользователь;
		КонецЕсли;
		Если Ответственный.Пустая() Тогда
			Ответственный = ПараметрыСеанса.ТекущийПользователь;
		КонецЕсли;
		
		ЗапретДо = '00010101';
		
		Если 	(	РежимЗаписи = РежимЗаписиДокумента.Проведение Или 
					РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения) И
				ВЗапретномПериоде(ЗапретДо) Тогда
			
			Отказ = Истина;
			Сообщить(СтрШаблон("Документ изменять запрещено, так как установлена дата запрета редактирования бюджета %1.", Формат(ЗапретДо, "ДЛФ=DD")));
			
		КонецЕсли;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		УтвВсего 	= Показатели.Итог("Утверждено");
		Статус 		= Перечисления.СтатусыБюджета[?(УтвВсего И УтвВсего = Показатели.Количество(), "Утвержден", ?(УтвВсего, "ЧастичноУтвержден", "НеУтвержден"))];
	Иначе
		Статус = Перечисления.СтатусыБюджета.Черновик;
	КонецЕсли;
	
	Сумма = ?(Статус = Перечисления.СтатусыБюджета.ЧастичноУтвержден,
					КонвертацияТипов.ПолучитьСуммуКолонкиПоУсловию(Показатели, "Сумма", Новый Структура("Утверждено", Истина)),
					Показатели.Итог("Сумма"));
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если Не ОбменДанными.Загрузка Тогда
		
		Если 	Показатели.Итог("Утверждено") И
				Не Бюджетирование.МожетУтверждать(Новый Структура("МодельБюджета, Статус", МодельБюджета, Статус)) Тогда
				
			Отказ = Истина;
			Сообщить("Утвержденный бюджет отменять запрещено.");
			
		ИначеЕсли 	Не Бюджетирование.ПолныеПрава() И
					Ответственный <> ПараметрыСеанса.ТекущийПользователь Тогда
			
			Отказ = Истина;
			Сообщить("Только ответственный может отменять бюджет.");
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
