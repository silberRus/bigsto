#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	СозданиеНаОснованииПереопределяемый.ДобавитьКомандуСоздатьНаОснованииБизнесПроцессЗадание(КомандыСозданияНаОсновании);
	
	Документы.РасходныйОрдерНаТовары.ДобавитьКомандуСоздатьНаОснованииРасходныйОрдерНаТоварыПоЗаданиюНаПеревозку(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Добавляет команду создания документа "Задание на перевозку".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ЗаданиеНаПеревозку) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ЗаданиеНаПеревозку.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ЗаданиеНаПеревозку);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьУправлениеДоставкой";
	
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
	Возврат; //В дальнейшем будет добавлен код команд
	
КонецПроцедуры

#Область ПроверкиПриСменеСтатуса

// Формирует запрос проверки при смене статуса списка документов
//
// Параметры:
//	МассивДокументов - Массив - Массив ссылок на документы, которые надо проверять
//	НовыйСтатус - Строка - Имя нового статуса
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров
//
// Возвращаемое значение:
//	Запрос - Запрос проверки перед сменой статуса
//
Функция СформироватьЗапросПроверкиПриСменеСтатуса(МассивДокументов, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	ЗначениеНовогоСтатуса = Перечисления.СтатусыЗаданийНаПеревозку[НовыйСтатус];
	
	Если ДополнительныеПараметры <> Неопределено И ДополнительныеПараметры.Свойство("КонтрольДоставки") Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЗаданиеНаПеревозкуМаршрут.Ссылка КАК Ссылка,
		|	МИНИМУМ(ЗаданиеНаПеревозкуМаршрут.Доставлено) КАК Доставлено
		|ПОМЕСТИТЬ
		|	втМаршрут
		|ИЗ
		|	Документ.ЗаданиеНаПеревозку.Маршрут КАК ЗаданиеНаПеревозкуМаршрут
		|ГДЕ
		|	ЗаданиеНаПеревозкуМаршрут.Ссылка В (&МассивДокументов)
		|СГРУППИРОВАТЬ ПО
		|	ЗаданиеНаПеревозкуМаршрут.Ссылка
		|;
		|ВЫБРАТЬ
		|	ТаблицаДокументов.Ссылка КАК Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Ссылка) КАК Представление,
		|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Статус) КАК ПредставлениеТекущегоСтатуса,
		|	ПРЕДСТАВЛЕНИЕ(&Статус) КАК ПредставлениеНовогоСтатуса,
		|	ВЫБОР
		|		КОГДА втМаршрут.Доставлено ЕСТЬ NULL 
		|				ИЛИ втМаршрут.Доставлено = ИСТИНА
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьНедоставленныеСтроки,
		|	ВЫБОР
		|		КОГДА ТаблицаДокументов.Статус = &Статус
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК СтатусСовпадает,
		|	ТаблицаДокументов.Проведен КАК Проведен,
		|	ТаблицаДокументов.ПометкаУдаления КАК ПометкаУдаления,
		|	ИСТИНА КАК ЗаписьПроведением
		|ИЗ
		|	Документ.ЗаданиеНаПеревозку КАК ТаблицаДокументов
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	втМаршрут КАК втМаршрут
		|ПО
		|	втМаршрут.Ссылка = ТаблицаДокументов.Ссылка
		|ГДЕ
		|	ТаблицаДокументов.Ссылка В(&МассивДокументов)
		|";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаДокументов.Ссылка КАК Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Ссылка) КАК Представление,
		|	ПРЕДСТАВЛЕНИЕ(ТаблицаДокументов.Статус) КАК ПредставлениеТекущегоСтатуса,
		|	ПРЕДСТАВЛЕНИЕ(&Статус) КАК ПредставлениеНовогоСтатуса,
		|	ВЫБОР
		|		КОГДА ТаблицаДокументов.Статус = &Статус
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК СтатусСовпадает,
		|	ТаблицаДокументов.Проведен КАК Проведен,
		|	ТаблицаДокументов.ПометкаУдаления КАК ПометкаУдаления,
		|	ИСТИНА КАК ЗаписьПроведением
		|ИЗ
		|	Документ.ЗаданиеНаПеревозку КАК ТаблицаДокументов
		|ГДЕ
		|	ТаблицаДокументов.Ссылка В(&МассивДокументов)
		|;";
	
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Статус", ЗначениеНовогоСтатуса);
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	Возврат Запрос;
	
КонецФункции

// Возвращает результат проверки при смене статуса документа
//
// Параметры:
//	ВыборкаПроверки - ВыборкаИзРезультатаЗапроса - Текущая строка выборки
//	НовыйСтатус - Перечисление - Новый статус
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров
//
// Возвращаемое значение:
//	Булево - Истина, в случае успешного завершения проверки
//
Функция ПроверкаПередСменойСтатуса(ВыборкаПроверки, НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	Отказ = Ложь;
	
	Если ДополнительныеПараметры = Неопределено Тогда
		Возврат Не Отказ;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("КонтрольДоставки")
		И ВыборкаПроверки.ЕстьНедоставленныеСтроки Тогда
		
		ТекстОшибки = НСтр("ru='У документа %Документ% статус ""%Статус%"" не установлен, т.к. задание доставлено не полностью'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ВыборкаПроверки.Ссылка);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Статус%", ВыборкаПроверки.ПредставлениеНовогоСтатуса);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ВыборкаПроверки.Ссылка);
		Отказ = Истина;
		
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

#КонецОбласти

// Возвращает текст запроса получения списка накладных из задания на перевозку.
// 
// Возвращаемое значение:
//  Строка - Строка с запросом
//
Функция ТекстЗапросаПолученияСпискаНакладныхИзЗаданийНаПеревозку() Экспорт
	
	Возврат	
	"ВЫБРАТЬ
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка КАК ЗаданиеНаПеревозку,
	|	ЗаданиеНаПеревозкуМаршрут.Адрес КАК АдресДоставки,
	|	ЗаданиеНаПеревозкуРаспоряжения.Распоряжение,
	|	ЗаданиеНаПеревозкуРаспоряжения.Склад,
	|	ЗаданиеНаПеревозкуМаршрут.НомерСтроки
	|ПОМЕСТИТЬ Распоряжения
	|ИЗ
	|	Документ.ЗаданиеНаПеревозку.Маршрут КАК ЗаданиеНаПеревозкуМаршрут
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозкуРаспоряжения
	|		ПО ЗаданиеНаПеревозкуМаршрут.КлючСвязи = ЗаданиеНаПеревозкуРаспоряжения.КлючСвязи
	|			И ЗаданиеНаПеревозкуМаршрут.Ссылка = ЗаданиеНаПеревозкуРаспоряжения.Ссылка
	|ГДЕ
	|	ЗаданиеНаПеревозкуМаршрут.Ссылка В(&ЗаданияНаПеревозку)
	|	И (ЗаданиеНаПеревозкуМаршрут.НомерСтроки В (&ВыделенныеСтрокиАдресов)
	|			ИЛИ &НетВыделенныхСтрокАдресов)
	|	И (ЗаданиеНаПеревозкуРаспоряжения.Распоряжение В (&Распоряжения)
	|			ИЛИ &ВсеРаспоряжения)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Распоряжения.ЗаданиеНаПеревозку,
	|	Распоряжения.АдресДоставки,
	|	ДокументТовары.Ссылка КАК Накладная,
	|	Распоряжения.НомерСтроки
	|ПОМЕСТИТЬ НакладныеПоЗаданиямНаПеревозку
	|ИЗ
	|	Распоряжения КАК Распоряжения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК ДокументТовары
	|		ПО Распоряжения.Распоряжение = ДокументТовары.ЗаказКлиента
	|			И Распоряжения.Склад = ДокументТовары.Склад
	|ГДЕ
	|	ДокументТовары.Ссылка.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Распоряжения.ЗаданиеНаПеревозку,
	|	Распоряжения.АдресДоставки,
	|	ДокументТовары.Ссылка,
	|	Распоряжения.НомерСтроки
	|ИЗ
	|	Распоряжения КАК Распоряжения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров.Товары КАК ДокументТовары
	|		ПО Распоряжения.Распоряжение = ДокументТовары.ЗаказНаПеремещение
	|			И Распоряжения.Склад = ДокументТовары.Ссылка.СкладОтправитель
	|ГДЕ
	|	ДокументТовары.Ссылка.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Распоряжения.ЗаданиеНаПеревозку,
	|	Распоряжения.АдресДоставки,
	|	Распоряжения.Распоряжение,
	|	Распоряжения.НомерСтроки
	|ИЗ
	|	Распоряжения КАК Распоряжения
	|ГДЕ
	|	(Распоряжения.Распоряжение ССЫЛКА Документ.РеализацияТоваровУслуг
	|		ИЛИ Распоряжения.Распоряжение ССЫЛКА Документ.ПеремещениеТоваров
	|		ИЛИ Распоряжения.Распоряжение ССЫЛКА Документ.ВозвратТоваровПоставщику)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Маршрутный лист
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "МаршрутныйЛист";
	КомандаПечати.Представление = НСтр("ru = 'Маршрутный лист'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

	Документы.ТранспортнаяНакладная.ДобавитьКомандыПечати(КомандыПечати);
	
	// Этикетки доставки
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюУТКлиент.ПечатьЭтикетокДоставки";
	КомандаПечати.МенеджерПечати = "";
	КомандаПечати.Идентификатор = "ЭтикеткаДоставки";
	КомандаПечати.Представление = НСтр("ru = 'Этикетки доставки'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.СертификатыНоменклатуры)
		И ПолучитьФункциональнуюОпцию("ИспользоватьСертификатыНоменклатуры") Тогда
		// Реестр сертификатов номенклатуры
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "СертификатыНоменклатурыРеестр";
		КомандаПечати.Представление = НСтр("ru = 'Сертификаты (реестр)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		
		// Изображения сертификатов номенклатуры
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "СертификатыНоменклатурыИзображенияИзДокументов";
		КомандаПечати.Представление = НСтр("ru = 'Сертификаты (по каждой позиции документа)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		
		// Изображения сертификатов номенклатуры
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "СертификатыНоменклатурыИзображенияИзДокументовБезДублей";
		КомандаПечати.Представление = НСтр("ru = 'Сертификаты (по одному на сертификат)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КонецЕсли;
	
	// Комплект документов на принтер
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюУТКлиент.ПечатьКомплектаДокументов";
	КомандаПечати.МенеджерПечати = "";
	КомандаПечати.Идентификатор = "КомплектДокументов";
	КомандаПечати.СразуНаПринтер = Истина;
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов на принтер'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МаршрутныйЛист") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
				КоллекцияПечатныхФорм,
				"МаршрутныйЛист",
				НСтр("ru = 'Маршрутный лист'"),
				СформироватьПечатнуюФормуМаршрутныйЛист(
					МассивОбъектов,
					ОбъектыПечати));
		
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН") Тогда
		СтруктураРаспоряжения = ПолучитьРаспоряжения(МассивОбъектов);
		Для Каждого СсылкаЗадание Из СтруктураРаспоряжения.ПустыеЗадания Цикл
			ТекстСообщения = НСтр("ru = 'Не удалось напечатать товарно-транспортные накладные по документу ""%Задание%"": не заполнены распоряжения на доставку.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Задание%", СсылкаЗадание);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
				КоллекцияПечатныхФорм,
				"ТТН",
				НСтр("ru = 'Товарно-транспортная накладная'"),
				Обработки.ПечатьТТН.СформироватьПечатнуюФормуТТН(
					СтруктураРаспоряжения.Распоряжения,
					ОбъектыПечати,
					ПараметрыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТранспортнаяНакладная") Тогда
		СтруктураРаспоряжения = ПолучитьРаспоряжения(МассивОбъектов);
		Для Каждого СсылкаЗадание Из СтруктураРаспоряжения.ПустыеЗадания Цикл
			ТекстСообщения = НСтр("ru = 'Не удалось напечатать транспортные накладные по документу ""%Задание%"": не заполнены распоряжения на доставку.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Задание%", СсылкаЗадание);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
				КоллекцияПечатныхФорм,
				"ТранспортнаяНакладная",
				НСтр("ru = 'Транспортная накладная'"),
				Обработки.ПечатьТранспортнойНакладной.СформироватьПечатнуюФормуТранспортнойНакладной(
					СтруктураРаспоряжения.Распоряжения,
					ОбъектыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СертификатыНоменклатурыРеестр") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СертификатыНоменклатурыРеестр",
			НСтр("ru = 'Сертификаты (реестр)'"),
			Справочники.СертификатыНоменклатуры.СформироватьПечатнуюФормуРеестрСертификатовНоменклатуры(МассивОбъектов, ОбъектыПечати),
			,
			"Справочник.СертификатыНоменклатуры.ПФ_MXL_РеестрСертификатов");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СертификатыНоменклатурыИзображенияИзДокументов") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СертификатыНоменклатурыИзображенияИзДокументов",
			НСтр("ru = 'Сертификаты (по каждой позиции документа)'"),
			Справочники.СертификатыНоменклатуры.СформироватьПечатнуюФормуИзображенияСертификатовИзДокументов(МассивОбъектов, ОбъектыПечати),
			,
			"Справочник.СертификатыНоменклатуры.ПФ_MXL_ИзображенияСертификатов");
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СертификатыНоменклатурыИзображенияИзДокументовБезДублей") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СертификатыНоменклатурыИзображенияИзДокументовБезДублей",
			НСтр("ru = 'Сертификаты (по одному на сертификат)'"),
			Справочники.СертификатыНоменклатуры.СформироватьПечатнуюФормуИзображенияСертификатовИзДокументовБезДублей(МассивОбъектов, ОбъектыПечати),
			,
			"Справочник.СертификатыНоменклатуры.ПФ_MXL_ИзображенияСертификатов");
	КонецЕсли;
		
КонецПроцедуры

Функция СформироватьПечатнуюФормуМаршрутныйЛист(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаданиеНаПеревозку_МаршрутныйЛист";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаданиеНаПеревозку.ПФ_MXL_МаршрутныйЛист");
	
	ОбластьЗаголовокДокумента		= Макет.ПолучитьОбласть("ЗаголовокДокумента");
	ОбластьШапкаДокумента 			= Макет.ПолучитьОбласть("ШапкаДокумента");
	ОбластьДополнительнаяИнформация = Макет.ПолучитьОбласть("ДополнительнаяИнформация");
	
	ЗапросПоДокументу = Новый Запрос;
	ЗапросПоДокументу.Текст = 
	"ВЫБРАТЬ
	|	ЗаданиеНаПеревозку.Номер КАК НомерЗадания,
	|	ЗаданиеНаПеревозку.ДатаВремяРейсаПланС КАК НачалоРейсаПлан,
	|	ЗаданиеНаПеревозку.ДатаВремяРейсаПланПо КАК ОкончаниеРейсаПлан,
	|	ПРЕДСТАВЛЕНИЕ(ЗаданиеНаПеревозку.Водитель) КАК Водитель,
	|	ПРЕДСТАВЛЕНИЕ(ЗаданиеНаПеревозку.КурьерЭкспедитор) КАК КурьерЭкспедитор,
	|	ПРЕДСТАВЛЕНИЕ(ЗаданиеНаПеревозку.ТранспортноеСредство) КАК ТранспортноеСредство,
	|	ЗаданиеНаПеревозку.Приоритет,
	|	ЗаданиеНаПеревозку.Дата КАК Дата,
	|	ЗаданиеНаПеревозку.ДатаВремяРейсаФактС КАК НачалоРейсаФакт,
	|	ЗаданиеНаПеревозку.ДатаВремяРейсаФактПо КАК ОкончаниеРейсаФакт,
	|	ЗаданиеНаПеревозку.ДополнительнаяИнформация КАК ДопИнформация,
	|	ЗаданиеНаПеревозку.Ссылка,
	|	ЗаданиеНаПеревозку.Вес КАК ИтогоВес,
	|	ЗаданиеНаПеревозку.Объем КАК ИтогоОбъем
	|ИЗ
	|	Документ.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку
	|ГДЕ
	|	ЗаданиеНаПеревозку.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаданиеНаПеревозку.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаданиеНаПеревозкуМаршрут.Адрес,
	|	ЗаданиеНаПеревозкуМаршрут.Зона,
	|	ЗаданиеНаПеревозкуМаршрут.ВремяС КАК ВремяС,
	|	ЗаданиеНаПеревозкуМаршрут.ВремяПо КАК ВремяПо,
	|	СУММА(ЗаданиеНаПеревозкуРаспоряжения.Вес) КАК Вес,
	|	СУММА(ЗаданиеНаПеревозкуРаспоряжения.Объем) КАК Объем,
	|	ВЫРАЗИТЬ(ЗаданиеНаПеревозкуМаршрут.ДополнительнаяИнформация КАК СТРОКА(1000)) КАК ДополнительнаяИнформация,
	|	ЗаданиеНаПеревозкуРаспоряжения.Распоряжение,
	|	ЗаданиеНаПеревозкуМаршрут.Ссылка КАК Ссылка,
	|	ЗаданиеНаПеревозкуРаспоряжения.ПолучательОтправитель КАК Получатель,
	|	СУММА(ВЫБОР
	|			КОГДА РасходныйОрдерНаТоварыОтгружаемыеТовары.УпаковочныйЛистРодитель = ЗНАЧЕНИЕ(Документ.УпаковочныйЛист.ПустаяСсылка)
	|				ТОГДА ЕСТЬNULL(РасходныйОрдерНаТоварыОтгружаемыеТовары.КоличествоУпаковок, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоМест
	|ИЗ
	|	Документ.ЗаданиеНаПеревозку.Маршрут КАК ЗаданиеНаПеревозкуМаршрут
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозкуРаспоряжения
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК РасходныйОрдерНаТоварыОтгружаемыеТовары
	|			ПО ЗаданиеНаПеревозкуРаспоряжения.Ссылка = РасходныйОрдерНаТоварыОтгружаемыеТовары.Ссылка.ЗаданиеНаПеревозку
	|				И ЗаданиеНаПеревозкуРаспоряжения.ПолучательОтправитель = РасходныйОрдерНаТоварыОтгружаемыеТовары.Ссылка.Получатель
	|				И (НЕ РасходныйОрдерНаТоварыОтгружаемыеТовары.Ссылка.ПометкаУдаления)
	|		ПО ЗаданиеНаПеревозкуМаршрут.КлючСвязи = ЗаданиеНаПеревозкуРаспоряжения.КлючСвязи
	|ГДЕ
	|	ЗаданиеНаПеревозкуМаршрут.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаданиеНаПеревозкуМаршрут.Адрес,
	|	ЗаданиеНаПеревозкуМаршрут.Зона,
	|	ЗаданиеНаПеревозкуМаршрут.ВремяС,
	|	ЗаданиеНаПеревозкуМаршрут.ВремяПо,
	|	ВЫРАЗИТЬ(ЗаданиеНаПеревозкуМаршрут.ДополнительнаяИнформация КАК СТРОКА(1000)),
	|	ЗаданиеНаПеревозкуРаспоряжения.Распоряжение,
	|	ЗаданиеНаПеревозкуМаршрут.Ссылка,
	|	ЗаданиеНаПеревозкуМаршрут.НомерСтроки,
	|	ЗаданиеНаПеревозкуМаршрут.КлючСвязи,
	|	ЗаданиеНаПеревозкуРаспоряжения.ПолучательОтправитель
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаданиеНаПеревозкуМаршрут.Ссылка,
	|	ЗаданиеНаПеревозкуМаршрут.НомерСтроки,
	|	МИНИМУМ(ЗаданиеНаПеревозкуРаспоряжения.НомерСтроки)
	|ИТОГИ ПО
	|	Ссылка,
	|	ЗаданиеНаПеревозкуМаршрут.НомерСтроки,
	|	ЗаданиеНаПеревозкуМаршрут.КлючСвязи,
	|	Получатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Склад,
	|	ВЫБОР
	|		КОГДА Т.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				И Т.Склад.ДатаНачалаОрдернойСхемыПриОтгрузке <= Т.Распоряжение.Дата
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриОтгрузке
	|ИЗ
	|	Документ.ЗаданиеНаПеревозку.Распоряжения КАК Т
	|ГДЕ
	|	Т.Ссылка В(&МассивОбъектов)
	|	И НЕ Т.Склад.ЭтоГруппа";
	
	ЗапросПоДокументу.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапросаПоДокументу = ЗапросПоДокументу.ВыполнитьПакет();
	
	РезультатШапкаДокументов = РезультатЗапросаПоДокументу[0].Выбрать();
	ВыборкаПоСсылкам = РезультатЗапросаПоДокументу[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	СкладыПогрузкиВыборка = РезультатЗапросаПоДокументу[2].Выбрать();
	
	Пока РезультатШапкаДокументов.Следующий() Цикл
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		РеквизитыДокумента = Новый Структура;
		РеквизитыДокумента.Вставить("Номер", РезультатШапкаДокументов.НомерЗадания);
		РеквизитыДокумента.Вставить("Дата", РезультатШапкаДокументов.Дата);
		
		ЗаголовокДокумента = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(РеквизитыДокумента, НСтр("ru='Маршрутный лист'"));
		ОбластьЗаголовокДокумента.Параметры.ЗаголовокДокумента = ЗаголовокДокумента;
		
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(
			ТабличныйДокумент,
			Макет,
			ОбластьЗаголовокДокумента,
			РезультатШапкаДокументов.Ссылка);
		
		ТабличныйДокумент.Вывести(ОбластьЗаголовокДокумента);
		
		ОбластьШапкаДокумента.Параметры.Заполнить(РезультатШапкаДокументов);
		
		СтрокаСкладыПогрузки = "";
				
		ЕстьОрдерныеСклады = Ложь;
		
		Пока СкладыПогрузкиВыборка.Следующий() Цикл
			Если СкладыПогрузкиВыборка.ОрдернаяСхемаПриОтгрузке И Не ЕстьОрдерныеСклады Тогда
				ЕстьОрдерныеСклады = Истина;
			КонецЕсли;
			СтрокаСкладыПогрузки = СтрокаСкладыПогрузки + СкладыПогрузкиВыборка.Склад + "," + " ";	
			
		КонецЦикла;
		
		Если ЕстьОрдерныеСклады Тогда 
			ОбластьШапкаТаблицы				= Макет.ПолучитьОбласть("ШапкаТаблицы");
			ОбластьСтрокаТаблицы			= Макет.ПолучитьОбласть("СтрокаТаблицы");
			ОбластьДопСтрокаТаблицы			= Макет.ПолучитьОбласть("ДопСтрокаТаблицы");
			ОбластьПодвалТаблицы 			= Макет.ПолучитьОбласть("ПодвалТаблицы");
			ОбластьИтоги 					= Макет.ПолучитьОбласть("Итоги"); 
		Иначе
			ОбластьШапкаТаблицы				= Макет.ПолучитьОбласть("ШапкаТаблицыБезМест");
			ОбластьСтрокаТаблицы			= Макет.ПолучитьОбласть("СтрокаТаблицыБезМест");
			ОбластьДопСтрокаТаблицы			= Макет.ПолучитьОбласть("ДопСтрокаТаблицыБезМест");
			ОбластьПодвалТаблицы 			= Макет.ПолучитьОбласть("ПодвалТаблицыБезМест");
			ОбластьИтоги 					= Макет.ПолучитьОбласть("ИтогиБезМест");
		КонецЕсли;
		
		Если СтрокаСкладыПогрузки <> "" Тогда 
			
			СтрокаСкладыПогрузки = Лев(СтрокаСкладыПогрузки, СтрДлина(СтрокаСкладыПогрузки) - 2);
			ОбластьШапкаДокумента.Параметры.СкладыПогрузки = СтрокаСкладыПогрузки;
						
		КонецЕсли;
			
		ТабличныйДокумент.Вывести(ОбластьШапкаДокумента);
		
		Пока ВыборкаПоСсылкам.НайтиСледующий(Новый Структура ("Ссылка", РезультатШапкаДокументов.Ссылка)) Цикл
			
			ИмяСтолбцаВес = НСтр("ru = 'Вес, %ЕдиницаИзмеренияВес%'");
			ИмяСтолбцаОбъем = НСтр("ru = 'Объем, %ЕдиницаИзмеренияОбъем%'");
			ИмяСтолбцаВес = СтрЗаменить(ИмяСтолбцаВес, "%ЕдиницаИзмеренияВес%", Константы.ЕдиницаИзмеренияВеса.Получить());
			ИмяСтолбцаОбъем = СтрЗаменить(ИмяСтолбцаОбъем, "%ЕдиницаИзмеренияОбъем%", Константы.ЕдиницаИзмеренияОбъема.Получить());
			
			ОбластьШапкаТаблицы.Параметры.ВесШапка = ИмяСтолбцаВес;
			ОбластьШапкаТаблицы.Параметры.ОбъемШапка = ИмяСтолбцаОбъем;
			
			ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
			
			ВыборкаПоНомерамСтрок = ВыборкаПоСсылкам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			НомерСтроки = 0;
			
			Пока ВыборкаПоНомерамСтрок.Следующий() Цикл
			
				ВыборкаПоКодамСтрок = ВыборкаПоНомерамСтрок.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаПоКодамСтрок.Следующий() Цикл
					
					НомерСтроки = НомерСтроки + 1;
					ОбластьСтрокаТаблицы.Параметры.НомерЗаказа = НомерСтроки;
					
					ВыборкаПоПолучателям = ВыборкаПоКодамСтрок.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					ПервыйПолучатель = Истина;
					
					КоличествоПолучателей = ВыборкаПоПолучателям.Количество();
					ИтогоКоличествомест = 0;
					
					Пока ВыборкаПоПолучателям.Следующий() Цикл
						
						Если ПервыйПолучатель Тогда 
							ЗаполняемаяОбласть = ОбластьСтрокаТаблицы;
							ПервыйПолучатель = Ложь;
						Иначе
							ЗаполняемаяОбласть = ОбластьДопСтрокаТаблицы;
						КонецЕсли;
						
						ИтогоВес = 0;
						ИтогоОбъем = 0;
						
						ИтогоКоличествомест = ИтогоКоличествомест + ВыборкаПоПолучателям.КоличествоМест;
						
						Выборка = ВыборкаПоПолучателям.Выбрать();
						
						Распоряжения = Новый Соответствие;
						
						Пока Выборка.Следующий() Цикл
							
							ИтогоВес = ИтогоВес + Выборка.Вес;
							ИтогоОбъем = ИтогоОбъем + Выборка.Объем;
							
							ЗаполняемаяОбласть.Параметры.Заполнить(Выборка);
							
							СтрокаРаспоряжения = Распоряжения.Получить(Выборка.Распоряжение.Метаданные());
							Если СтрокаРаспоряжения = Неопределено Тогда
								СтрокаРаспоряжения = Выборка.Распоряжение.Метаданные().ПредставлениеСписка + ": "
									+ ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.Распоряжение.Номер);	
							Иначе
								СтрокаРаспоряжения = СтрокаРаспоряжения + "," + " "
									+ ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.Распоряжение.Номер);
							КонецЕсли;	
									
							Распоряжения.Вставить(Выборка.Распоряжение.Метаданные(), СтрокаРаспоряжения);
						КонецЦикла;
						
						ЗаполняемаяОбласть.Параметры.Вес = ИтогоВес;
						ЗаполняемаяОбласть.Параметры.Объем = ИтогоОбъем;
						
						ЗаполняемаяОбласть.Параметры.Распоряжения = "";
						Для Каждого РаспоряжениеКлючЗначение Из Распоряжения Цикл
							
							ЗаполняемаяОбласть.Параметры.Распоряжения = ЗаполняемаяОбласть.Параметры.Распоряжения + РаспоряжениеКлючЗначение.Значение + Символы.ПС;
								
						КонецЦикла;
						
						ТабличныйДокумент.Вывести(ЗаполняемаяОбласть);
						
					КонецЦикла;
					
					Если ЕстьОрдерныеСклады Тогда 
						ОбъединитьОбластьСоСтрокамиНиже(ТабличныйДокумент, "НомерЗаказа", КоличествоПолучателей - 1);
						ОбъединитьОбластьСоСтрокамиНиже(ТабличныйДокумент, "Адрес", КоличествоПолучателей - 1);
						ОбъединитьОбластьСоСтрокамиНиже(ТабличныйДокумент, "Зона", КоличествоПолучателей - 1);
						ОбъединитьОбластьСоСтрокамиНиже(ТабличныйДокумент, "ВремяС", КоличествоПолучателей - 1);
						ОбъединитьОбластьСоСтрокамиНиже(ТабличныйДокумент, "ВремяПо", КоличествоПолучателей - 1);
						ОбъединитьОбластьСоСтрокамиНиже(ТабличныйДокумент, "ДополнительнаяИнформацияСтрока", КоличествоПолучателей - 1);
					Иначе
						ОбъединитьОбластьСоСтрокамиНиже(ТабличныйДокумент, "НомерЗаказаБезМест", КоличествоПолучателей - 1);
						ОбъединитьОбластьСоСтрокамиНиже(ТабличныйДокумент, "АдресБезМест", КоличествоПолучателей - 1);
						ОбъединитьОбластьСоСтрокамиНиже(ТабличныйДокумент, "ЗонаБезМест", КоличествоПолучателей - 1);
						ОбъединитьОбластьСоСтрокамиНиже(ТабличныйДокумент, "ВремяСБезМест", КоличествоПолучателей - 1);
						ОбъединитьОбластьСоСтрокамиНиже(ТабличныйДокумент, "ВремяПоБезМест", КоличествоПолучателей - 1);
						ОбъединитьОбластьСоСтрокамиНиже(ТабличныйДокумент, "ДополнительнаяИнформацияСтрокаБезМест", КоличествоПолучателей - 1);
					КонецЕсли;
					
				КонецЦикла;
			
			КонецЦикла;
			
			ТабличныйДокумент.Вывести(ОбластьПодвалТаблицы);
			
			ОбластьИтоги.Параметры.ИтогоВес = РезультатШапкаДокументов.ИтогоВес / НоменклатураСервер.КоэффициентПересчетаВТонны(Константы.ЕдиницаИзмеренияВеса.Получить());
			ОбластьИтоги.Параметры.ИтогоОбъем = РезультатШапкаДокументов.ИтогоОбъем;
			Если ЕстьОрдерныеСклады Тогда 
				ОбластьИтоги.Параметры.ИтогоКоличествоМест = ИтогоКоличествомест;
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьИтоги);
			
			Если ЗначениеЗаполнено(РезультатШапкаДокументов.ДопИнформация) Тогда
				
				ОбластьДополнительнаяИнформация.Параметры.ДопИнформация = СокрЛП(РезультатШапкаДокументов.ДопИнформация);
				ТабличныйДокумент.Вывести(ОбластьДополнительнаяИнформация);
				
			КонецЕсли;
			
		КонецЦикла;
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
			ТабличныйДокумент,
			НомерСтрокиНачало,
			ОбъектыПечати,
			РезультатШапкаДокументов.Ссылка);
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ОбъединитьОбластьСоСтрокамиНиже(ТабличныйДокумент, ИмяОбласти, КоличествоДопСтрок)
	
	Если КоличествоДопСтрок < 1 Тогда
		Возврат
	КонецЕсли;
	
	Область = ТабличныйДокумент.Область(ИмяОбласти);
	Область.Разъединить();
	Область.Имя = Неопределено;
	АдресОбласти = Область.Имя;
	МассивАдресОбласти = СтрРазделить(АдресОбласти, ":");
	Если МассивАдресОбласти.Количество() = 1 Тогда
		МассивАдресОбласти.Добавить(МассивАдресОбласти[0]);
	КонецЕсли;
	АдресОкончания = МассивАдресОбласти[1];
	МассивАдресОкончания = СтрРазделить(АдресОкончания, "C");
	НомерПоследнейСтрокиСтрока = Сред(МассивАдресОкончания[0],2);
	НовыйНомерПоследнейСтроки = Число(НомерПоследнейСтрокиСтрока) + КоличествоДопСтрок;
	НовыйАдрес = МассивАдресОбласти[0] + ":R" + НовыйНомерПоследнейСтроки + "C" + МассивАдресОкончания[1];
	Область = ТабличныйДокумент.Область(НовыйАдрес);	
	
	Область.Объединить();	
	
КонецПроцедуры

Процедура СформироватьКомплектПечатныхФорм(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати) Экспорт
	
	НоваяСтрока = КоллекцияПечатныхФорм.Добавить();
	НоваяСтрока.ИмяМакета = "МаршрутныйЛист";
	НоваяСтрока.Экземпляров = 1;
	НоваяСтрока.ИмяВРЕГ = ВРЕГ(НоваяСтрока.ИмяМакета);
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
				КоллекцияПечатныхФорм,
				"МаршрутныйЛист",
				НСтр("ru = 'Маршрутный лист'"),
				СформироватьПечатнуюФормуМаршрутныйЛист(МассивОбъектов, ОбъектыПечати));
				
	МассивНакладных = ПолучитьНакладныеПоЗаданиюНаПеревозку(МассивОбъектов);			
					
	Для Каждого Соответствие Из ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивНакладных, "Проведен") Цикл
		Если НЕ Соответствие.Значение.Проведен Тогда
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Документ %1 не проведен. Печать комплекта по этому документу не будет выполнена.'"),
				Соответствие.Ключ);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				Соответствие.Ключ);
			МассивНакладных.Удалить(МассивНакладных.Найти(Соответствие.Ключ));
		КонецЕсли;
	КонецЦикла;
	
	СоответствиеТипов = ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(МассивНакладных);
	
	Для Каждого ТекТипОбъекта Из СоответствиеТипов Цикл
		
		МенеджерПечати = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ТекТипОбъекта.Ключ);
		МенеджерПечати.СформироватьКомплектПечатныхФорм(
			ТекТипОбъекта.Значение,
			ПараметрыПечати,
			КоллекцияПечатныхФорм,
			ОбъектыПечати);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьРаспоряжения(МассивОбъектов)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ЗаданиеНаПеревозку.Ссылка,
	|	ЗаданиеНаПеревозку.Дата,
	|	ЗаданиеНаПеревозку.ДатаВремяРейсаПланС
	|ПОМЕСТИТЬ ВТЗадания
	|ИЗ
	|	Документ.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку
	|ГДЕ
	|	ЗаданиеНаПеревозку.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаданиеНаПеревозкуМаршрут.НомерСтроки,
	|	ЗаданиеНаПеревозкуМаршрут.КлючСвязи
	|ПОМЕСТИТЬ ВТПунктыМаршрута
	|ИЗ
	|	Документ.ЗаданиеНаПеревозку.Маршрут КАК ЗаданиеНаПеревозкуМаршрут
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗадания КАК ВТЗадания
	|		ПО (ВТЗадания.Ссылка = ЗаданиеНаПеревозкуМаршрут.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаданиеНаПеревозкуРаспоряжения.Распоряжение
	|ИЗ
	|	Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозкуРаспоряжения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗадания КАК ВТЗадания
	|		ПО (ВТЗадания.Ссылка = ЗаданиеНаПеревозкуРаспоряжения.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПунктыМаршрута КАК ВТПунктыМаршрута
	|		ПО (ВТПунктыМаршрута.КлючСвязи = ЗаданиеНаПеревозкуРаспоряжения.КлючСвязи)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВЫБОР
	|		КОГДА ВТЗадания.ДатаВремяРейсаПланС = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ВТЗадания.Дата
	|		ИНАЧЕ ВТЗадания.ДатаВремяРейсаПланС
	|	КОНЕЦ,
	|	ВТПунктыМаршрута.НомерСтроки,
	|	ЗаданиеНаПеревозкуРаспоряжения.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПустыеЗадания.Ссылка КАК Ссылка
	|ИЗ
	|	ВТЗадания КАК ПустыеЗадания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозкуРаспоряжения
	|		ПО ПустыеЗадания.Ссылка = ЗаданиеНаПеревозкуРаспоряжения.Ссылка
	|ГДЕ
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка ЕСТЬ NULL");
	Запрос.УстановитьПараметр("МассивОбъектов",МассивОбъектов);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Распоряжения", РезультатЗапроса[2].Выгрузить().ВыгрузитьКолонку("Распоряжение"));
	СтруктураВозврата.Вставить("ПустыеЗадания", РезультатЗапроса[3].Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция ПолучитьНакладныеПоЗаданиюНаПеревозку(МассивОбъектов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПолученияСпискаНакладныхИзЗаданийНаПеревозку() +
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НакладныеПоЗаданиямНаПеревозку.Накладная
	|ИЗ
	|	НакладныеПоЗаданиямНаПеревозку КАК НакладныеПоЗаданиямНаПеревозку";
	Запрос.УстановитьПараметр("ЗаданияНаПеревозку", 		МассивОбъектов);
	Запрос.УстановитьПараметр("НетВыделенныхСтрокАдресов", 	Истина);
	Запрос.УстановитьПараметр("ВыделенныеСтрокиАдресов",	Новый Массив);
	Запрос.УстановитьПараметр("ВсеРаспоряжения", 			Истина);
	Запрос.УстановитьПараметр("Распоряжения", 				Новый Массив);
	
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаРезультат.ВыгрузитьКолонку("Накладная");
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.ЗаданиеНаПеревозку";
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказПоставщику.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаданиеНаПеревозку.Маршрут КАК ЗаказПоставщику
	|ГДЕ
	|	ВЫРАЗИТЬ(ЗаказПоставщику.АдресЗначенияПолей КАК СТРОКА (500)) <> """"
	|	И НЕ ЗаказПоставщику.АдресЗначенияПолей ПОДОБНО ""%</КонтактнаяИнформация>""";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.ЗаданиеНаПеревозку";
	
	ИмяДокумента = СтрРазделить(ПолноеИмяОбъекта,".")[1];
	МетаданныеДокумента = Метаданные.Документы.ЗаданиеНаПеревозку;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц);
	
	Если Не Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если Не Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОбъектыДляОбработки.Ссылка                       КАК Ссылка,
	|	ОбъектыДляОбработки.Ссылка.ВерсияДанных          КАК ВерсияДанных,
	|	ОбъектыДляОбработки.Ссылка.Проведен              КАК Проведен
	|ПОМЕСТИТЬ ТаблицаСсылок
	|ИЗ
	|	ВТДокументыДляОбработки КАК ОбъектыДляОбработки
	|;
	|ВЫБРАТЬ
	|	ОбъектыДляОбработки.Ссылка                КАК Ссылка,
	|	ОбъектыДляОбработки.ВерсияДанных          КАК ВерсияДанных,
	|	ОбъектыДляОбработки.Проведен              КАК Проведен,
	|	МАКСИМУМ(ВЫРАЗИТЬ(Маршрут.АдресЗначенияПолей КАК СТРОКА (500)) <> """"
	|		И НЕ Маршрут.АдресЗначенияПолей ПОДОБНО ""%</КонтактнаяИнформация>"")
	|	КАК                                       ТребуетсяПересчетЗначенийПолейАдреса
	|ИЗ
	|	ТаблицаСсылок КАК ОбъектыДляОбработки
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаПеревозку.Маршрут КАК Маршрут
	|	ПО ОбъектыДляОбработки.Ссылка = Маршрут.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбъектыДляОбработки.Ссылка,
	|	ОбъектыДляОбработки.ВерсияДанных,
	|	ОбъектыДляОбработки.Проведен";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТДокументыДляОбработки", Результат.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос();
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
		
			ДокументОбъект = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(Выборка.Ссылка, Выборка.ВерсияДанных, Параметры.Очередь);
			Если ДокументОбъект = Неопределено Тогда
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ОбъектИзменен = Ложь;
			
			Если Выборка.ТребуетсяПересчетЗначенийПолейАдреса Тогда
				Для Каждого Строка Из ДокументОбъект.Маршрут Цикл
					Если Строка.АдресЗначенияПолей <> ""
						И СтрНайти(Строка.АдресЗначенияПолей, "</КонтактнаяИнформация>") = 0 Тогда
						НовыеЗначенияПолей = "";
						ОбщегоНазначенияУТ.ЗаполнитьЗначенияПолейКИПоПредставлению(Строка.Адрес, НовыеЗначенияПолей);
						Строка.АдресЗначенияПолей = НовыеЗначенияПолей;
						ОбъектИзменен = Истина;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(),  Выборка.Ссылка);
			Продолжить;
		КонецПопытки;
	КонецЦикла;
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура СформироватьОчередьПереоформленияРасходныхОрдеров(ЗаданиеНаПеревозку, Отказ) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаКонтроляРегистровТоварыКОтгрузкеИТоварыДоставке() +
	"ВЫБРАТЬ
	|	ТоварыКОтгрузкеОстатки.ДокументОтгрузки,
	|	ТоварыКОтгрузкеОстатки.Склад,
	|	ТоварыКОтгрузкеОстатки.Получатель,
	|	ТоварыКОтгрузкеОстатки.Номенклатура,
	|	ТоварыКОтгрузкеОстатки.Характеристика,
	|	ТоварыКОтгрузкеОстатки.Назначение,
	|	ТоварыКОтгрузкеОстатки.Серия,
	|	СУММА(ТоварыКОтгрузкеОстатки.КОтгрузкеПриход) КАК КОтгрузкеПриход,
	|	СУММА(ТоварыКОтгрузкеОстатки.КОтгрузкеРасход) КАК КОтгрузкеРасход,
	|	СУММА(ТоварыКОтгрузкеОстатки.СобираетсяСобраноОстаток) КАК СобираетсяСобраноОстаток,
	|	СУММА(ТоварыКОтгрузкеОстатки.КСборкеОстаток) КАК КСборкеОстаток
	|ПОМЕСТИТЬ ТоварыКОтгрузкеОстатки
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыКОтгрузкеИДоставке.ДокументОтгрузки КАК ДокументОтгрузки,
	|		ТоварыКОтгрузкеИДоставке.Склад КАК Склад,
	|		ТоварыКОтгрузкеИДоставке.Получатель КАК Получатель,
	|		ТоварыКОтгрузкеИДоставке.Номенклатура КАК Номенклатура,
	|		ТоварыКОтгрузкеИДоставке.Характеристика КАК Характеристика,
	|		ТоварыКОтгрузкеИДоставке.Назначение КАК Назначение,
	|		ТоварыКОтгрузкеИДоставке.Серия КАК Серия,
	|		ВЫБОР
	|			КОГДА ТоварыКОтгрузкеИДоставке.Количество < ТоварыКОтгрузкеИДоставке.КоличествоКотгрузке
	|				ТОГДА ТоварыКОтгрузкеИДоставке.Количество
	|			ИНАЧЕ ТоварыКОтгрузкеИДоставке.КоличествоКотгрузке
	|		КОНЕЦ КАК КОтгрузкеПриход,
	|		0 КАК КОтгрузкеРасход,
	|		0 КАК СобираетсяСобраноОстаток,
	|		0 КАК КСборкеОстаток
	|	ИЗ
	|		ТоварыКОтгрузкеИДоставке КАК ТоварыКОтгрузкеИДоставке
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыКотгрузкеРасход.ДокументОтгрузки,
	|		ТоварыКотгрузкеРасход.Склад,
	|		ТоварыКотгрузкеРасход.Получатель,
	|		ТоварыКотгрузкеРасход.Номенклатура,
	|		ТоварыКотгрузкеРасход.Характеристика,
	|		ТоварыКотгрузкеРасход.Назначение,
	|		ТоварыКотгрузкеРасход.Серия,
	|		0,
	|		ТоварыКотгрузкеРасход.Количество,
	|		0,
	|		0
	|	ИЗ
	|		ТоварыКотгрузкеРасход КАК ТоварыКотгрузкеРасход
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыСобраноСобирается.ДокументОтгрузки,
	|		ТоварыСобраноСобирается.Склад,
	|		ТоварыСобраноСобирается.Получатель,
	|		ТоварыСобраноСобирается.Номенклатура,
	|		ТоварыСобраноСобирается.Характеристика,
	|		ТоварыСобраноСобирается.Назначение,
	|		ТоварыСобраноСобирается.Серия,
	|		0,
	|		0,
	|		ТоварыСобраноСобирается.Количество,
	|		0
	|	ИЗ
	|		ТоварыСобраноСобирается КАК ТоварыСобраноСобирается
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыКСборке.ДокументОтгрузки,
	|		ТоварыКСборке.Склад,
	|		ТоварыКСборке.Получатель,
	|		ТоварыКСборке.Номенклатура,
	|		ТоварыКСборке.Характеристика,
	|		ТоварыКСборке.Назначение,
	|		ТоварыКСборке.Серия,
	|		0,
	|		0,
	|		0,
	|		ТоварыКСборке.Количество
	|	ИЗ
	|		ТоварыКСборке КАК ТоварыКСборке) КАК ТоварыКОтгрузкеОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОтгрузкеОстатки.ДокументОтгрузки,
	|	ТоварыКОтгрузкеОстатки.Склад,
	|	ТоварыКОтгрузкеОстатки.Получатель,
	|	ТоварыКОтгрузкеОстатки.Номенклатура,
	|	ТоварыКОтгрузкеОстатки.Характеристика,
	|	ТоварыКОтгрузкеОстатки.Назначение,
	|	ТоварыКОтгрузкеОстатки.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварыКОтгрузкеОстатки.Склад,
	|	ТоварыКОтгрузкеОстатки.Получатель
	|ИЗ
	|	ТоварыКОтгрузкеОстатки КАК ТоварыКОтгрузкеОстатки
	|ГДЕ
	|	ТоварыКОтгрузкеОстатки.КОтгрузкеПриход - ТоварыКОтгрузкеОстатки.КОтгрузкеРасход - ТоварыКОтгрузкеОстатки.СобираетсяСобраноОстаток > ТоварыКОтгрузкеОстатки.КСборкеОстаток
	|			ИЛИ ТоварыКОтгрузкеОстатки.КСборкеОстаток > 0 И
	|	ТоварыКОтгрузкеОстатки.КОтгрузкеПриход - ТоварыКОтгрузкеОстатки.КОтгрузкеРасход - ТоварыКОтгрузкеОстатки.СобираетсяСобраноОстаток < ТоварыКОтгрузкеОстатки.КСборкеОстаток";
	
	ОформлятьСначалаНакладные = Константы.ПорядокОформленияНакладныхРасходныхОрдеров.Получить() = Перечисления.ПорядокОформленияНакладныхРасходныхОрдеров.СначалаНакладные;
	Запрос.УстановитьПараметр("ОформлятьСначалаНакладные", ОформлятьСначалаНакладные);
	Запрос.УстановитьПараметр("ЗаданиеНаПеревозку", ЗаданиеНаПеревозку); 
	Выборка = Запрос.Выполнить().Выбрать();
		                    
	Пока Выборка.Следующий() Цикл
			
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьПереоформленияРасходныхОрдеров");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Склад",Выборка.Склад);
		ЭлементБлокировки.УстановитьЗначение("Получатель",Выборка.Получатель);
		Блокировка.Заблокировать();
		
		НаборЗаписейОчереди = РегистрыСведений.ОчередьПереоформленияРасходныхОрдеров.СоздатьНаборЗаписей();
		
		НаборЗаписейОчереди.Отбор.Склад.Установить(Выборка.Склад);
		НаборЗаписейОчереди.Отбор.Получатель.Установить(Выборка.Получатель);
		
		НаборЗаписейОчереди.Прочитать();
		
		Если НаборЗаписейОчереди.Количество() = 0 Тогда
			
			ЗаписьОчереди = НаборЗаписейОчереди.Добавить();
			
			ЗаписьОчереди.Склад = Выборка.Склад;
			ЗаписьОчереди.Получатель = Выборка.Получатель;
			
			Попытка
				НаборЗаписейОчереди.Записать();
			Исключение
				
				ТекстСообщения = НСтр("ru = 'Не удалось выполнить проведение документа: %Ссылка% по причине: %Причина%''");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", ЗаданиеНаПеревозку);
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,
				ЗаданиеНаПеревозку.Метаданные(), ЗаданиеНаПеревозку, ТекстСообщения);
				
				Отказ = Истина;
				Возврат;
				
			КонецПопытки;
			
			СтруктураПараметров = Новый Структура("Склад, Получатель", Выборка.Склад, Выборка.Получатель);
			СкладыСервер.ЗапускВыполненияФоновогоПереоформленияРасходныхОрдеров(СтруктураПараметров);
			
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаКонтроляРегистровТоварыКОтгрузкеИТоварыДоставке() Экспорт
	
 	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	 |	ЗаданиеНаПеревозкуРаспоряжения.Склад,
	 |	ЗаданиеНаПеревозкуРаспоряжения.Распоряжение КАК ДокументОтгрузки
	 |ПОМЕСТИТЬ РаспоряженияНаОтгузку
	 |ИЗ
	 |	Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозкуРаспоряжения
	 |ГДЕ
	 |	ЗаданиеНаПеревозкуРаспоряжения.Ссылка = &ЗаданиеНаПеревозку
	 |	И ЗаданиеНаПеревозкуРаспоряжения.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
	 |	И ЗаданиеНаПеревозкуРаспоряжения.Склад.ДатаНачалаОрдернойСхемыПриОтгрузке <= ЗаданиеНаПеревозкуРаспоряжения.Ссылка.ДатаВремяРейсаПланС
	 |	И ЗаданиеНаПеревозкуРаспоряжения.Склад.НачинатьОтгрузкуПослеФормированияЗаданияНаПеревозку
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ТоварыКОтгрузкеОбороты.ДокументОтгрузки КАК ДокументОтгрузки,
	 |	ТоварыКОтгрузкеОбороты.Получатель КАК Получатель,
	 |	ТоварыКОтгрузкеОбороты.Склад КАК Склад,
	 |	ТоварыКОтгрузкеОбороты.Номенклатура КАК Номенклатура,
	 |	ТоварыКОтгрузкеОбороты.Характеристика КАК Характеристика,
	 |	ТоварыКОтгрузкеОбороты.Назначение КАК Назначение,
	 |	ТоварыКОтгрузкеОбороты.Серия КАК Серия,
	 |	СУММА(ВЫБОР
	 |		КОГДА ТоварыКОтгрузкеОбороты.КОтгрузкеРасход < 0
	 |			ТОГДА -ТоварыКОтгрузкеОбороты.КОтгрузкеРасход
	 |		ИНАЧЕ ВЫБОР
	 |				КОГДА &ОформлятьСначалаНакладные
	 |					ТОГДА ТоварыКОтгрузкеОбороты.КОтгрузкеПриход - ТоварыКОтгрузкеОбороты.КОформлениюПриход
	 |				ИНАЧЕ ТоварыКОтгрузкеОбороты.КОтгрузкеПриход
	 |			КОНЕЦ
	 |	КОНЕЦ) КАК КоличествоКотгрузке
	 |ПОМЕСТИТЬ ТоварыКотгрузке
	 |ИЗ
	 |	РегистрНакопления.ТоварыКОтгрузке.Обороты(
	 |			,
	 |			,
	 |			,
	 |			(Склад, ДокументОтгрузки) В
	 |				(ВЫБРАТЬ
	 |					РаспоряженияНаОтгузку.Склад,
	 |					РаспоряженияНаОтгузку.ДокументОтгрузки
	 |				ИЗ
	 |					РаспоряженияНаОтгузку)) КАК ТоварыКОтгрузкеОбороты
	 |ГДЕ
	 |	(ВЫБОР
	 |		КОГДА ТоварыКОтгрузкеОбороты.КОтгрузкеРасход < 0
	 |			ТОГДА -ТоварыКОтгрузкеОбороты.КОтгрузкеРасход
	 |		ИНАЧЕ ВЫБОР
	 |				КОГДА &ОформлятьСначалаНакладные
	 |					ТОГДА ТоварыКОтгрузкеОбороты.КОтгрузкеПриход - ТоварыКОтгрузкеОбороты.КОформлениюПриход
	 |				ИНАЧЕ ТоварыКОтгрузкеОбороты.КОтгрузкеПриход
	 |			КОНЕЦ
	 |	КОНЕЦ <> 0
	 |				И ТоварыКОтгрузкеОбороты.ДокументОтгрузки.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ДоКлиента)
	 |			ИЛИ ТоварыКОтгрузкеОбороты.ДокументОтгрузки.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчикаПоАдресу))
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	ТоварыКОтгрузкеОбороты.Номенклатура,
	 |	ТоварыКОтгрузкеОбороты.Получатель,
	 |	ТоварыКОтгрузкеОбороты.Характеристика,
	 |	ТоварыКОтгрузкеОбороты.ДокументОтгрузки,
	 |	ТоварыКОтгрузкеОбороты.Серия,
	 |	ТоварыКОтгрузкеОбороты.Назначение,
	 |	ТоварыКОтгрузкеОбороты.Склад
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	ТоварыКОтгрузкеОбороты.ДокументОтгрузки,
	 |	ТоварыКОтгрузкеОбороты.Получатель,
	 |	ТоварыКОтгрузкеОбороты.Склад,
	 |	ТоварыКОтгрузкеОбороты.Номенклатура,
	 |	ТоварыКОтгрузкеОбороты.Характеристика,
	 |	ТоварыКОтгрузкеОбороты.Назначение,
	 |	ТоварыКОтгрузкеОбороты.Серия,
	 |	СУММА(ТоварыКОтгрузкеОбороты.КОформлениюРасход)
	 |ИЗ
	 |	РегистрНакопления.ТоварыКОтгрузке.Обороты(
	 |			,
	 |			,
	 |			,
	 |			(Склад, ДокументОтгрузки) В
	 |				(ВЫБРАТЬ
	 |					РаспоряженияНаОтгузку.Склад,
	 |					РаспоряженияНаОтгузку.ДокументОтгрузки
	 |				ИЗ
	 |					РаспоряженияНаОтгузку)) КАК ТоварыКОтгрузкеОбороты
	 |ГДЕ
	 |	(&ОформлятьСначалаНакладные
	 |				И ТоварыКОтгрузкеОбороты.КОформлениюРасход <> 0
	 |				И ТоварыКОтгрузкеОбороты.ДокументОтгрузки.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ДоКлиента)
	 |			ИЛИ ТоварыКОтгрузкеОбороты.ДокументОтгрузки.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчикаПоАдресу))
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	ТоварыКОтгрузкеОбороты.Назначение,
	 |	ТоварыКОтгрузкеОбороты.Характеристика,
	 |	ТоварыКОтгрузкеОбороты.Склад,
	 |	ТоварыКОтгрузкеОбороты.Номенклатура,
	 |	ТоварыКОтгрузкеОбороты.Серия,
	 |	ТоварыКОтгрузкеОбороты.ДокументОтгрузки,
	 |	ТоварыКОтгрузкеОбороты.Получатель
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ТоварыКотгрузке.ДокументОтгрузки КАК ДокументОтгрузки,
	 |	ТоварыКотгрузке.Склад КАК Склад,
	 |	ТоварыКотгрузке.Получатель КАК Получатель,
	 |	ТоварыКотгрузке.Номенклатура КАК Номенклатура,
	 |	ТоварыКотгрузке.Характеристика КАК Характеристика,
	 |	ТоварыКотгрузке.Назначение КАК Назначение,
	 |	ТоварыКотгрузке.Серия КАК Серия,
	 |	ТоварыКотгрузке.КоличествоКотгрузке,
	 |	ВЫБОР
	 |		КОГДА ТоварыКДоставке.ВсеТовары
	 |			ТОГДА ТоварыКотгрузке.КоличествоКотгрузке
	 |		ИНАЧЕ ВЫБОР
	 |				КОГДА ТоварыКотгрузке.КоличествоКотгрузке >= ТоварыКДоставке.Количество
	 |					ТОГДА ТоварыКДоставке.Количество
	 |				ИНАЧЕ ТоварыКотгрузке.КоличествоКотгрузке
	 |			КОНЕЦ
	 |	КОНЕЦ КАК Количество
	 |ПОМЕСТИТЬ ТоварыКОтгрузкеИДоставке
	 |ИЗ
	 |	ТоварыКотгрузке КАК ТоварыКотгрузке
	 |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТоварыКДоставке КАК ТоварыКДоставке
	 |		ПО ТоварыКотгрузке.ДокументОтгрузки = ТоварыКДоставке.Распоряжение
	 |			И ТоварыКотгрузке.Склад = ТоварыКДоставке.Склад
	 |			И (ТоварыКотгрузке.Номенклатура = ТоварыКДоставке.Номенклатура
	 |					И ТоварыКотгрузке.Характеристика = ТоварыКДоставке.Характеристика
	 |					И ТоварыКотгрузке.Назначение = ТоварыКДоставке.Назначение
	 |					И ТоварыКотгрузке.Серия = ТоварыКДоставке.Серия
	 |				ИЛИ ТоварыКДоставке.ВсеТовары)
	 |			И (ТоварыКДоставке.ЗаданиеНаПеревозку = &ЗаданиеНаПеревозку)
	 |			И (ТоварыКДоставке.ЗаданиеНаПеревозку.Проведен)
	 |			И (ТоварыКДоставке.ЗаданиеНаПеревозку.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаданийНаПеревозку.Формируется))
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ТоварыКОтгрузке.ДокументОтгрузки КАК ДокументОтгрузки,
	 |	ТоварыКОтгрузке.Склад,
	 |	ТоварыКОтгрузке.Получатель,
	 |	ТоварыКОтгрузке.Номенклатура,
	 |	ТоварыКОтгрузке.Характеристика,
	 |	ТоварыКОтгрузке.Назначение,
	 |	ТоварыКОтгрузке.Серия,
	 |	СУММА(ТоварыКОтгрузке.КОтгрузке) КАК Количество
	 |ПОМЕСТИТЬ ТоварыКотгрузкеРасход
	 |ИЗ
	 |	РегистрНакопления.ТоварыКОтгрузке КАК ТоварыКОтгрузке
	 |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаспоряженияНаОтгузку КАК РаспоряженияНаОтгузку
	 |		ПО ТоварыКОтгрузке.Склад = РаспоряженияНаОтгузку.Склад
	 |			И ТоварыКОтгрузке.ДокументОтгрузки = РаспоряженияНаОтгузку.ДокументОтгрузки
	 |ГДЕ
	 |	ТоварыКОтгрузке.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	 |	И ТоварыКОтгрузке.КОтгрузке > 0
	 |	И ТоварыКОтгрузке.ЗаданиеНаПеревозку = &ЗаданиеНаПеревозку
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	ТоварыКОтгрузке.Серия,
	 |	ТоварыКОтгрузке.Назначение,
	 |	ТоварыКОтгрузке.Номенклатура,
	 |	ТоварыКОтгрузке.Характеристика,
	 |	ТоварыКОтгрузке.ДокументОтгрузки,
	 |	ТоварыКОтгрузке.Получатель,
	 |	ТоварыКОтгрузке.Склад
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ТоварыКОтгрузке.ДокументОтгрузки КАК ДокументОтгрузки,
	 |	ТоварыКОтгрузке.Получатель,
	 |	ТоварыКОтгрузке.Склад,
	 |	ТоварыКОтгрузке.Номенклатура,
	 |	ТоварыКОтгрузке.Характеристика,
	 |	ТоварыКОтгрузке.Назначение,
	 |	ТоварыКОтгрузке.Серия,
	 |	СУММА(ТоварыКОтгрузке.Собирается + ТоварыКОтгрузке.Собрано) КАК Количество
	 |ПОМЕСТИТЬ ТоварыСобраноСобирается
	 |ИЗ
	 |	РегистрНакопления.ТоварыКОтгрузке КАК ТоварыКОтгрузке
	 |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаспоряженияНаОтгузку КАК РаспоряженияНаОтгузку
	 |		ПО ТоварыКОтгрузке.Склад = РаспоряженияНаОтгузку.Склад
	 |			И ТоварыКОтгрузке.ДокументОтгрузки = РаспоряженияНаОтгузку.ДокументОтгрузки
	 |ГДЕ
	 |	ТоварыКОтгрузке.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	 |	И ТоварыКОтгрузке.Собирается + ТоварыКОтгрузке.Собрано > 0
	 |	И ТоварыКОтгрузке.ЗаданиеНаПеревозку = &ЗаданиеНаПеревозку
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	ТоварыКОтгрузке.Серия,
	 |	ТоварыКОтгрузке.Назначение,
	 |	ТоварыКОтгрузке.Номенклатура,
	 |	ТоварыКОтгрузке.Характеристика,
	 |	ТоварыКОтгрузке.ДокументОтгрузки,
	 |	ТоварыКОтгрузке.Получатель,
	 |	ТоварыКОтгрузке.Склад
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ТоварыКОтгрузке.ДокументОтгрузки КАК ДокументОтгрузки,
	 |	ТоварыКОтгрузке.Получатель,
	 |	ТоварыКОтгрузке.Склад,
	 |	ТоварыКОтгрузке.Номенклатура,
	 |	ТоварыКОтгрузке.Характеристика,
	 |	ТоварыКОтгрузке.Назначение,
	 |	ТоварыКОтгрузке.Серия,
	 |	СУММА(ТоварыКОтгрузке.КСборке) КАК Количество
	 |ПОМЕСТИТЬ ТоварыКСборке
	 |ИЗ
	 |	РегистрНакопления.ТоварыКОтгрузке КАК ТоварыКОтгрузке
	 |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаспоряженияНаОтгузку КАК РаспоряженияНаОтгузку
	 |		ПО ТоварыКОтгрузке.Склад = РаспоряженияНаОтгузку.Склад
	 |			И ТоварыКОтгрузке.ДокументОтгрузки = РаспоряженияНаОтгузку.ДокументОтгрузки
	 |ГДЕ
	 |	ТоварыКОтгрузке.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	 |	И ТоварыКОтгрузке.КСборке > 0
	 |	И ТоварыКОтгрузке.ЗаданиеНаПеревозку = &ЗаданиеНаПеревозку
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	ТоварыКОтгрузке.Серия,
	 |	ТоварыКОтгрузке.Назначение,
	 |	ТоварыКОтгрузке.Номенклатура,
	 |	ТоварыКОтгрузке.Характеристика,
	 |	ТоварыКОтгрузке.ДокументОтгрузки,
	 |	ТоварыКОтгрузке.Получатель,
	 |	ТоварыКОтгрузке.Склад
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |";
	 
	 Возврат ТекстЗапроса
		
КонецФункции	

Функция ЗаполнитьВремяРейсаПланФакт(Объект) Экспорт
	
	МаксимальноеВремяПо = ДоставкаТоваров.МаксимальноеВремяПо(Объект);
	
	ТекстыСообщений = Новый Массив;
	
	Если Объект.Статус <> Перечисления.СтатусыЗаданийНаПеревозку.Формируется
		И Не ЗначениеЗаполнено(Объект.ДатаВремяРейсаПланС) Тогда
		
		Объект.ДатаВремяРейсаПланС = ТекущаяДатаСеанса();
		ТекстыСообщений.Добавить(НСтр("ru='план. время начала рейса'"));
		
	КонецЕсли;
	
	Если Объект.Статус <> Перечисления.СтатусыЗаданийНаПеревозку.Формируется
		И Объект.Статус <> Перечисления.СтатусыЗаданийНаПеревозку.КПогрузке
		И Не ЗначениеЗаполнено(Объект.ДатаВремяРейсаПланПо) Тогда
		
		Объект.ДатаВремяРейсаПланПо = Макс(МаксимальноеВремяПо,Объект.ДатаВремяРейсаПланС);
		ТекстыСообщений.Добавить(НСтр("ru='план. время окончания рейса'"));
		
	КонецЕсли;
	
	Если (Объект.Статус = Перечисления.СтатусыЗаданийНаПеревозку.Отправлено
			Или Объект.Статус = Перечисления.СтатусыЗаданийНаПеревозку.Закрыто)
		И Не ЗначениеЗаполнено(Объект.ДатаВремяРейсаФактС) Тогда
		
		Объект.ДатаВремяРейсаФактС = ТекущаяДатаСеанса();
		ТекстыСообщений.Добавить(НСтр("ru='факт. время начала рейса'"));
		
	КонецЕсли;
	
	Если Объект.Статус = Перечисления.СтатусыЗаданийНаПеревозку.Закрыто
		И Не ЗначениеЗаполнено(Объект.ДатаВремяРейсаФактПо) Тогда
		
		Объект.ДатаВремяРейсаФактПо = ТекущаяДатаСеанса();
		ТекстыСообщений.Добавить(НСтр("ru='факт. время окончания рейса'"));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ДатаВремяРейсаПланПо) Тогда
		Объект.ДатаВремяРейсаПланПо = Макс(Объект.ДатаВремяРейсаПланС, Объект.ДатаВремяРейсаПланПо);
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.ДатаВремяРейсаФактПо) Тогда
		Объект.ДатаВремяРейсаФактПо = Макс(Объект.ДатаВремяРейсаФактС, Объект.ДатаВремяРейсаФактПо);
	КонецЕсли;
	
	Если ТекстыСообщений.Количество() > 0 Тогда
		ТекстСообщенияОВремени = НСтр("ru='Заполнено %Время0, %Время1, %Время2, %Время3.'");
		Для Сч = 0 По ТекстыСообщений.ВГраница() Цикл
			ТекстСообщенияОВремени = СтрЗаменить(ТекстСообщенияОВремени, "%Время"+Сч, ТекстыСообщений[Сч]);
		КонецЦикла;
		Для Сч = 1 По 3 Цикл
			ТекстСообщенияОВремени = СтрЗаменить(ТекстСообщенияОВремени, ", %Время"+Сч, "");
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТекстСообщенияОВремени;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
