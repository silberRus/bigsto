#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСерверУТ.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Организация                  КАК Организация,
	|	Таблица.Поставщик                    КАК Поставщик,
	|	Таблица.ДокументПоступления          КАК ДокументПоступления,
	|	Таблица.АналитикаУчетаНоменклатуры   КАК АналитикаУчетаНоменклатуры,
	|	Таблица.ВидЗапасов                   КАК ВидЗапасов,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.Количество
	|		ИНАЧЕ
	|			-Таблица.Количество
	|	КОНЕЦ                                КАК КоличествоПередЗаписью
	|ПОМЕСТИТЬ ДвиженияТоварыКОформлениюТаможенныхДекларацийПередЗаписью
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюТаможенныхДеклараций КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|";
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСерверУТ.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Организация                 КАК Организация,
	|	ТаблицаИзменений.Поставщик                   КАК Поставщик,
	|	ТаблицаИзменений.ДокументПоступления         КАК ДокументПоступления,
	|	ТаблицаИзменений.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаИзменений.ВидЗапасов                  КАК ВидЗапасов,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение)  КАК КоличествоИзменение
	|ПОМЕСТИТЬ ДвиженияТоварыКОформлениюТаможенныхДекларацийИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Организация                 КАК Организация,
	|		Таблица.Поставщик                   КАК Поставщик,
	|		Таблица.ДокументПоступления         КАК ДокументПоступления,
	|		Таблица.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
	|		Таблица.ВидЗапасов                  КАК ВидЗапасов,
	|		Таблица.КоличествоПередЗаписью      КАК КоличествоИзменение
	|	ИЗ
	|		ДвиженияТоварыКОформлениюТаможенныхДекларацийПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Организация                 КАК Организация,
	|		Таблица.Поставщик                   КАК Поставщик,
	|		Таблица.ДокументПоступления         КАК ДокументПоступления,
	|		Таблица.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
	|		Таблица.ВидЗапасов                  КАК ВидЗапасов,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.Количество
	|			ИНАЧЕ
	|				Таблица.Количество
	|		КОНЕЦ                               КАК КоличествоИзменение
	|	ИЗ
	|		РегистрНакопления.ТоварыКОформлениюТаможенныхДеклараций КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Поставщик,
	|	ТаблицаИзменений.ДокументПоступления,
	|	ТаблицаИзменений.АналитикаУчетаНоменклатуры,
	|	ТаблицаИзменений.ВидЗапасов
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|;
	|УНИЧТОЖИТЬ ДвиженияТоварыКОформлениюТаможенныхДекларацийПередЗаписью
	|";
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Новые изменения были помещены во временную таблицу.
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияТоварыКОформлениюТаможенныхДекларацийИзменение", Выборка.Количество > 0);

КонецПроцедуры

#КонецОбласти

#КонецЕсли