#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ПроведениеСерверУТ.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;
	
	ЕстьКонтрольОстатков = ПолучитьФункциональнуюОпцию("КонтролироватьОстаткиНомеровГТДПриЗакупкеУПоставщикаПоСхемеТоварыВПути")
							Или ПолучитьФункциональнуюОпцию("КонтролироватьОстаткиНомеровГТДПриИмпортеПоСхемеТоварыВПути");
	
	Если Не ЕстьКонтрольОстатков Тогда
		Возврат;
	КонецЕсли;
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Номенклатура   КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Серия          КАК Серия,
	|	Таблица.Назначение     КАК Назначение,
	|	Таблица.ВидЗапасов     КАК ВидЗапасов,
	|	Таблица.Партнер        КАК Партнер,
	|	Таблица.Контрагент     КАК Контрагент,
	|	Таблица.НомерГТД       КАК НомерГТД,
	|	Таблица.Договор        КАК Договор,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.Передано
	|		ИНАЧЕ -Таблица.Передано
	|	КОНЕЦ                  КАК ПереданоПередЗаписью
	|ПОМЕСТИТЬ ДвиженияТоварыУПартнеровПередЗаписью
	|ИЗ
	|	РегистрНакопления.ТоварыУПартнеров КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьКонтрольОстатков = ПолучитьФункциональнуюОпцию("КонтролироватьОстаткиНомеровГТДПриЗакупкеУПоставщикаПоСхемеТоварыВПути")
							Или ПолучитьФункциональнуюОпцию("КонтролироватьОстаткиНомеровГТДПриИмпортеПоСхемеТоварыВПути");
	
	Если Не ЕстьКонтрольОстатков Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Номенклатура   КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика КАК Характеристика,
	|	ТаблицаИзменений.Серия          КАК Серия,
	|	ТаблицаИзменений.Назначение     КАК Назначение,
	|	ТаблицаИзменений.ВидЗапасов     КАК ВидЗапасов,
	|	ТаблицаИзменений.Партнер        КАК Партнер,
	|	ТаблицаИзменений.Контрагент     КАК Контрагент,
	|	ТаблицаИзменений.НомерГТД       КАК НомерГТД,
	|	ТаблицаИзменений.Договор        КАК Договор,
	|	СУММА(ТаблицаИзменений.ПереданоИзменение) КАК ПереданоИзменение
	|ПОМЕСТИТЬ ДвиженияТоварыУПартнеровИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Номенклатура   КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		Таблица.Серия          КАК Серия,
	|		Таблица.Назначение     КАК Назначение,
	|		Таблица.ВидЗапасов     КАК ВидЗапасов,
	|		Таблица.Партнер        КАК Партнер,
	|		Таблица.Контрагент     КАК Контрагент,
	|		Таблица.НомерГТД       КАК НомерГТД,
	|		Таблица.Договор        КАК Договор,
	|		Таблица.ПереданоПередЗаписью КАК ПереданоИзменение
	|	ИЗ
	|		ДвиженияТоварыУПартнеровПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Номенклатура   КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		Таблица.Серия          КАК Серия,
	|		Таблица.Назначение     КАК Назначение,
	|		Таблица.ВидЗапасов     КАК ВидЗапасов,
	|		Таблица.Партнер        КАК Партнер,
	|		Таблица.Контрагент     КАК Контрагент,
	|		Таблица.НомерГТД       КАК НомерГТД,
	|		Таблица.Договор        КАК Договор,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.Передано
	|			ИНАЧЕ
	|				Таблица.Передано
	|		КОНЕЦ                  КАК ПереданоИзменение
	|	ИЗ
	|		РегистрНакопления.ТоварыУПартнеров КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Номенклатура ,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.Серия,
	|	ТаблицаИзменений.Назначение,
	|	ТаблицаИзменений.ВидЗапасов,
	|	ТаблицаИзменений.Партнер,
	|	ТаблицаИзменений.Контрагент,
	|	ТаблицаИзменений.НомерГТД,
	|	ТаблицаИзменений.Договор
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.ПереданоИзменение) > 0
	|;
	|УНИЧТОЖИТЬ ДвиженияТоварыУПартнеровПередЗаписью
	|";
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Новые изменения были помещены во временную таблицу.
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияТоварыУПартнеровИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
