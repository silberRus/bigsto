#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Текст запроса сторно записей заказа.
//
// Возвращаемое значение:
//  Строка - Текст запроса сторно записей. Получаемый текст запроса использует параметры запроса:
//   * Ссылка - ДокументСсылка - документ, сторно записи которого необходимо получить.
//   * Отбор  - Произвольный   - фиктивный параметр, предназначенный для постобработки полученного текста запроса, путем замены
//                               данного параметра на произвольный текст. Предполагается что произвольный текст будет формулировать
//                               дополнительные условия отбора на получаемые данные. Пример подмены подстроки "&Отбор":
//                               "Таблица.Номенклатура = &Номенклатура И Таблица.Характеристика = &Характеристика".
//
Функция ТекстЗапросаСторноЗаписейЗаказа() Экспорт

	Текст =
		"ВЫБРАТЬ
		|	Таблица.Номенклатура     КАК Номенклатура,
		|	Таблица.Характеристика   КАК Характеристика,
		|	Таблица.Склад            КАК Склад,
		|	Таблица.Назначение       КАК Назначение,
		|
		|	Таблица.ДатаСобытия          КАК ДатаСобытия,
		|	-Таблица.КоличествоИзЗаказов КАК КоличествоИзЗаказов,
		|	-Таблица.КоличествоПодЗаказ  КАК КоличествоПодЗаказ
		|
		|ИЗ
		|	РегистрНакопления.ГрафикПоступленияТоваров КАК Таблица
		|
		|ГДЕ
		|	Таблица.Активность
		|	И Таблица.Регистратор В(&Ссылка)
		|	И &Отбор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Таблица.Номенклатура     КАК Номенклатура,
		|	Таблица.Характеристика   КАК Характеристика,
		|	Таблица.Склад            КАК Склад,
		|	Таблица.Назначение       КАК Назначение,
		|
		|	Таблица.ДатаОтгрузки         КАК ДатаСобытия,
		|	Таблица.КоличествоИзЗаказов  КАК КоличествоИзЗаказов,
		|	0                            КАК КоличествоПодЗаказ
		|
		|ИЗ
		|	РегистрНакопления.ГрафикОтгрузкиТоваров КАК Таблица
		|
		|ГДЕ
		|	Таблица.Активность
		|	И Таблица.Регистратор В(&Ссылка)
		|	И &Отбор
		|;
		|
		|//////////////////////////////////////////////////
		|";

	Возврат Текст;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьВременнуюТаблицуОстатков(МенеджерВременныхТаблиц, ИмяВременнойТаблицы, Индексировать, УсловиеОтбора = Неопределено) Экспорт
	
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Остатки.Номенклатура   КАК Номенклатура,
		|	Остатки.Характеристика КАК Характеристика,
		|	Остатки.Назначение     КАК Назначение,
		|	Остатки.Склад          КАК Склад,
		|	Остатки.ДатаОтгрузки   КАК ДатаОтгрузки,
		|	
		|	Остатки.КоличествоПодЗаказОстаток КАК КоличествоПодЗаказ
		|ПОМЕСТИТЬ ВтРезультат
		|ИЗ
		|	РегистрНакопления.ГрафикОтгрузкиТоваров.Остатки(, &УсловиеОтбораВиртуальнойТаблицы) КАК Остатки";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВтРезультат", ИмяВременнойТаблицы);
	Если Индексировать Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Назначение, Склад";
		
	КонецЕсли;
	
	Запрос = Новый Запрос();
	
	МассивУсловийОтбораВиртуальнойТаблицы = Новый Массив;
	
	Если УсловиеОтбора <> Неопределено Тогда
		
		Если УсловиеОтбора.Свойство("Склад") Тогда
			
			Запрос.УстановитьПараметр("Склад", УсловиеОтбора.Склад);
			МассивУсловийОтбораВиртуальнойТаблицы.Добавить("Склад = &Склад");
			
		КонецЕсли;
		
		Если УсловиеОтбора.Свойство("Таблица") Тогда
			
			Поля = Неопределено;
			Если Не УсловиеОтбора.Таблица.Свойство("Поля", Поля) Тогда
				Поля = "Номенклатура,Характеристика,Назначение";
			КонецЕсли;
			
			УсловиеОтбораВиртуальнойТаблицы = "
				|		(&Поля) В(
				|			ВЫБРАТЬ РАЗЛИЧНЫЕ
				|				&Поле
				|			ИЗ
				|				ВтАналитики КАК Аналитики)";
			
			УсловиеОтбораВиртуальнойТаблицы = СтрЗаменить(УсловиеОтбораВиртуальнойТаблицы, "&Поля", Поля);
			ВыбираемыеПоля = СтрЗаменить(Поля, ",", ", &ПсевдонимТаблицы");
			ВыбираемыеПоля = "&ПсевдонимТаблицы" + ВыбираемыеПоля;
			ВыбираемыеПоля = СтрЗаменить(ВыбираемыеПоля, "&ПсевдонимТаблицы", "Аналитики.");
			УсловиеОтбораВиртуальнойТаблицы = СтрЗаменить(УсловиеОтбораВиртуальнойТаблицы, "&Поле", ВыбираемыеПоля);
			УсловиеОтбораВиртуальнойТаблицы = СтрЗаменить(УсловиеОтбораВиртуальнойТаблицы, "ВтАналитики", УсловиеОтбора.Таблица.Имя);
			МассивУсловийОтбораВиртуальнойТаблицы.Добавить(УсловиеОтбораВиртуальнойТаблицы);
			
		КонецЕсли;
		
	КонецЕсли;
	
	УсловиеОтбораВиртуальнойТаблицы = СтрСоединить(МассивУсловийОтбораВиртуальнойТаблицы, " И ");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораВиртуальнойТаблицы", УсловиеОтбораВиртуальнойТаблицы);
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ТекстЗапросаОбособленныхПотребностей(ИменаПараметров) Экспорт
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Регистратор    КАК Заказ,
		|	Таблица.Номенклатура   КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад          КАК Склад,
		|	Таблица.Назначение     КАК Назначение,
		|	
		|	СУММА(Таблица.КоличествоПодЗаказ) КАК Количество
		|
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	РегистрНакопления.ГрафикОтгрузкиТоваров КАК Таблица
		|	
		|ГДЕ
		|	Таблица.Регистратор В(&МассивЗаказов)
		|	И Таблица.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	И Таблица.Активность
		|	И Таблица.КоличествоПодЗаказ <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Регистратор, Таблица.Номенклатура, Таблица.Характеристика, Таблица.Склад, Таблица.Назначение
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|//////////////////////////////////////////////////
		|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВтТовары", ИменаПараметров.ВтТовары);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "МассивЗаказов", ИменаПараметров.МассивЗаказов);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы


#КонецОбласти

#КонецОбласти

#КонецЕсли
