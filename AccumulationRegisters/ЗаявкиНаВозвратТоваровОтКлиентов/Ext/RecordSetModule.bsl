#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСерверУТ.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу "ДвиженияЗаявкиНаВозвратТоваровОтКлиентовПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.ЗаявкаНаВозвратТоваровОтКлиента КАК ЗаявкаНаВозвратТоваровОтКлиента,
	|	Таблица.Номенклатура                    КАК Номенклатура,
	|	Таблица.Характеристика                  КАК Характеристика,
	|	Таблица.КодСтроки                       КАК КодСтроки,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.КОформлению
	|		ИНАЧЕ
	|			-Таблица.КОформлению
	|	КОНЕЦ                   КАК КОформлениюПередЗаписью
	|ПОМЕСТИТЬ ДвиженияЗаявкиНаВозвратТоваровОтКлиентовПередЗаписью
	|ИЗ
	|	РегистрНакопления.ЗаявкиНаВозвратТоваровОтКлиентов КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|";
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСерверУТ.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров =
		ПолучитьФункциональнуюОпцию("ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров");
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.ЗаявкаНаВозвратТоваровОтКлиента КАК ЗаявкаНаВозвратТоваровОтКлиента,
	|	ТаблицаИзменений.Номенклатура                    КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика                  КАК Характеристика,
	|	ТаблицаИзменений.КодСтроки                       КАК КодСтроки,
	|	СУММА(ТаблицаИзменений.КОформлениюИзменение)     КАК КОформлениюИзменение
	|ПОМЕСТИТЬ ДвиженияЗаявкиНаВозвратТоваровОтКлиентовИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.ЗаявкаНаВозвратТоваровОтКлиента КАК ЗаявкаНаВозвратТоваровОтКлиента,
	|		Таблица.Номенклатура                    КАК Номенклатура,
	|		Таблица.Характеристика                  КАК Характеристика,
	|		Таблица.КодСтроки                       КАК КодСтроки,
	|		Таблица.КОформлениюПередЗаписью         КАК КОформлениюИзменение
	|	ИЗ
	|		ДвиженияЗаявкиНаВозвратТоваровОтКлиентовПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.ЗаявкаНаВозвратТоваровОтКлиента КАК ЗаявкаНаВозвратТоваровОтКлиента,
	|		Таблица.Номенклатура                    КАК Номенклатура,
	|		Таблица.Характеристика                  КАК Характеристика,
	|		Таблица.КодСтроки                       КАК КодСтроки,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.КОформлению
	|			ИНАЧЕ
	|				Таблица.КОформлению
	|		КОНЕЦ                          КАК КОформлениюИзменение
	|	ИЗ
	|		РегистрНакопления.ЗаявкиНаВозвратТоваровОтКлиентов КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.ЗаявкаНаВозвратТоваровОтКлиента,
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.КодСтроки
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КОформлениюИзменение) <> 0
	|;
	|УНИЧТОЖИТЬ ДвиженияЗаявкиНаВозвратТоваровОтКлиентовПередЗаписью
	|";
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Новые изменения были помещены во временную таблицу "ДвиженияЗаявкиНаВозвратТоваровОтКлиентовИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияЗаявкиНаВозвратТоваровОтКлиентовИзменение", Выборка.Количество > 0);

	Если ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров Тогда
		
		Регистратор = Отбор.Регистратор.Значение;
		
		СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
		
		Если СтруктураВременныеТаблицы.ДвиженияЗаявкиНаВозвратТоваровОтКлиентовИзменение Тогда
		
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров",
				Константы.ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров.Получить());
			Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений",
				Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
			Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
			
			ТекстЗапроса = "
			|ВЫБРАТЬ
			|	Таблица.ЗаявкаНаВозвратТоваровОтКлиента,
			|	Таблица.Номенклатура,
			|	Таблица.Характеристика
			|ПОМЕСТИТЬ ДвиженияЗаявкиНаВозвратТоваровОтКлиентовИзменениеМерныеТовары
			|ИЗ
			|	ДвиженияЗаявкиНаВозвратТоваровОтКлиентовИзменение КАК Таблица
			|ГДЕ 
			|	Таблица.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины В (&МерныеТипыЕдиницИзмерений)
			|СГРУППИРОВАТЬ ПО
			|	Таблица.ЗаявкаНаВозвратТоваровОтКлиента,
			|	Таблица.Номенклатура,
			|	Таблица.Характеристика
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|";
			
			//Основная таблица
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	ЗаявкиНаВозвратТоваровОтКлиентов.ВидДвижения                      КАК ВидДвижения,
			|	ЗаявкиНаВозвратТоваровОтКлиентов.ЗаявкаНаВозвратТоваровОтКлиента  КАК ЗаявкаНаВозвратТоваровОтКлиента,
			|	ЗаявкиНаВозвратТоваровОтКлиентов.Номенклатура                     КАК Номенклатура,
			|	ЗаявкиНаВозвратТоваровОтКлиентов.Характеристика                   КАК Характеристика,
			|	ЗаявкиНаВозвратТоваровОтКлиентов.КОформлению                      КАК КОформлению
			|ПОМЕСТИТЬ ВТЗаявкиНаВозвратТоваровОтКлиентов
			|ИЗ
			|	РегистрНакопления.ЗаявкиНаВозвратТоваровОтКлиентов КАК ЗаявкиНаВозвратТоваровОтКлиентов
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		ДвиженияЗаявкиНаВозвратТоваровОтКлиентовИзменение КАК Изменения
			|		ПО ЗаявкиНаВозвратТоваровОтКлиентов.ЗаявкаНаВозвратТоваровОтКлиента = Изменения.ЗаявкаНаВозвратТоваровОтКлиента
			|			И ЗаявкиНаВозвратТоваровОтКлиентов.Номенклатура                  = Изменения.Номенклатура
			|			И ЗаявкиНаВозвратТоваровОтКлиентов.Характеристика                = Изменения.Характеристика
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|";
			
			//Допустимые отклонения
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	ЗаявкиНаВозвратТоваровОтКлиентов.ЗаявкаНаВозвратТоваровОтКлиента  КАК ЗаявкаНаВозвратТоваровОтКлиента,
			|	ЗаявкиНаВозвратТоваровОтКлиентов.Номенклатура                     КАК Номенклатура,
			|	ЗаявкиНаВозвратТоваровОтКлиентов.Характеристика                   КАК Характеристика,
			|	СУММА(ЗаявкиНаВозвратТоваровОтКлиентов.КОформлению
			|		* (&ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров / 100)) КАК ДопустимоеОтклонение
			|ПОМЕСТИТЬ ВТДопустимыеОтклоненияЗаявкиНаВозвратТоваровОтКлиентов
			|ИЗ
			|	ВТЗаявкиНаВозвратТоваровОтКлиентов КАК ЗаявкиНаВозвратТоваровОтКлиентов
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		ДвиженияЗаявкиНаВозвратТоваровОтКлиентовИзменениеМерныеТовары КАК Изменения
			|		ПО ЗаявкиНаВозвратТоваровОтКлиентов.ЗаявкаНаВозвратТоваровОтКлиента = Изменения.ЗаявкаНаВозвратТоваровОтКлиента
			|			И ЗаявкиНаВозвратТоваровОтКлиентов.Номенклатура                  = Изменения.Номенклатура
			|			И ЗаявкиНаВозвратТоваровОтКлиентов.Характеристика                = Изменения.Характеристика
			|ГДЕ
			|	ЗаявкиНаВозвратТоваровОтКлиентов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|СГРУППИРОВАТЬ ПО
			|	ЗаявкиНаВозвратТоваровОтКлиентов.ЗаявкаНаВозвратТоваровОтКлиента,
			|	ЗаявкиНаВозвратТоваровОтКлиентов.Номенклатура,
			|	ЗаявкиНаВозвратТоваровОтКлиентов.Характеристика
			|ИНДЕКСИРОВАТЬ ПО
			|	ЗаявкаНаВозвратТоваровОтКлиента,
			|	Номенклатура,
			|	Характеристика
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|";
			
			//Остатки
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	ЗаявкиНаВозвратТоваровОтКлиентов.ЗаявкаНаВозвратТоваровОтКлиента  КАК ЗаявкаНаВозвратТоваровОтКлиента,
			|	ЗаявкиНаВозвратТоваровОтКлиентов.Номенклатура                     КАК Номенклатура,
			|	ЗаявкиНаВозвратТоваровОтКлиентов.Характеристика                   КАК Характеристика,
			|	СУММА(ВЫБОР КОГДА ЗаявкиНаВозвратТоваровОтКлиентов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
			|			ЗаявкиНаВозвратТоваровОтКлиентов.КОформлению
			|		ИНАЧЕ
			|			-ЗаявкиНаВозвратТоваровОтКлиентов.КОформлению
			|	КОНЕЦ)                                                            КАК КОформлениюОстаток
			|ПОМЕСТИТЬ ВТЗаявкиНаВозвратТоваровОтКлиентовОстатки
			|ИЗ
			|	ВТЗаявкиНаВозвратТоваровОтКлиентов КАК ЗаявкиНаВозвратТоваровОтКлиентов
			|СГРУППИРОВАТЬ ПО
			|	ЗаявкиНаВозвратТоваровОтКлиентов.ЗаявкаНаВозвратТоваровОтКлиента,
			|	ЗаявкиНаВозвратТоваровОтКлиентов.Номенклатура,
			|	ЗаявкиНаВозвратТоваровОтКлиентов.Характеристика
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|";
			
			ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ЗаказыОстатки.ЗаявкаНаВозвратТоваровОтКлиента КАК ЗаявкаНаВозвратТоваровОтКлиента
			|ИЗ
			|	ВТЗаявкиНаВозвратТоваровОтКлиентовОстатки КАК ЗаказыОстатки
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		ВТДопустимыеОтклоненияЗаявкиНаВозвратТоваровОтКлиентов КАК ДопустимыеОтклонения
			|		ПО
			|			ЗаказыОстатки.ЗаявкаНаВозвратТоваровОтКлиента  = ДопустимыеОтклонения.ЗаявкаНаВозвратТоваровОтКлиента
			|			И ЗаказыОстатки.Номенклатура                   = ДопустимыеОтклонения.Номенклатура
			|			И ЗаказыОстатки.Характеристика                 = ДопустимыеОтклонения.Характеристика
			|ГДЕ
			|	ЗаказыОстатки.КОформлениюОстаток <= ДопустимыеОтклонения.ДопустимоеОтклонение
			|	И ЗаказыОстатки.КОформлениюОстаток >= -ДопустимыеОтклонения.ДопустимоеОтклонение
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|";
			
			Запрос.Текст = ТекстЗапроса;
			
			ВыборкаЗаказ = Запрос.Выполнить().Выбрать();
			
			Пока ВыборкаЗаказ.Следующий() Цикл
				
				РегистрыСведений.ОчередьЗаказовККорректировкеСтрокМерныхТоваров.ДобавитьЗаказВОчередь(
					ВыборкаЗаказ.ЗаявкаНаВозвратТоваровОтКлиента);
				
			КонецЦикла;
		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли