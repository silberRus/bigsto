
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УзелОбмена = Параметры.УзелОбмена;
	ПринудительнаяПолноеОбновлениеДанныхТовара = Параметры.ПринудительнаяПолноеОбновлениеДанныхТовара;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаВыполняетсяОбмен;
	ПодключитьОбработчикОжидания("ЗапуститьВыполнениеОбмена", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьВыполнениеОбмена()
	
	ПараметрыОбмена = Новый Структура;
	ПараметрыОбмена.Вставить("УзелОбмена"									, УзелОбмена);
	ПараметрыОбмена.Вставить("ПринудительнаяПолноеОбновлениеДанныхТовара"	, ПринудительнаяПолноеОбновлениеДанныхТовара);
	
	ДлительнаяОперация = ВыполнитьОбменССайтомНаСервере(ПараметрыОбмена);
	ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;

	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииОперацииЗагрузки", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииОперацииЗагрузки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.ПодробноеПредставлениеОшибки) тогда
		СообщитьООшибкеСервер(Результат.ПодробноеПредставлениеОшибки);	
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 ""%2""'"),
			Формат(ТекущаяДата(), "ДЛФ=DT"),
			УзелОбмена) 
		,,
		НСтр("ru = 'Обмен с сайтом завершен'"),
		БиблиотекаКартинок.Информация32);
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьОбменССайтомНаСервере(ПараметрыОбмена)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Выполнение обмена с сайтом Битрикс'");
	
	Результат = ДлительныеОперации.ВыполнитьВФоне("Б_ОбменССайтомСервер.ВыполнитьОбменИнтерактивно", ПараметрыОбмена, ПараметрыВыполнения);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура СообщитьООшибкеСервер(Ошибка)
	Б_ОбменССайтомСервер.ДобавитьИнформациюООшибкеВОбмене(УзелОбмена, Ошибка);
КонецПроцедуры

