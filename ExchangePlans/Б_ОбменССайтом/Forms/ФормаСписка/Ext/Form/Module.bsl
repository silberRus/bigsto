#Область ОбработчикиСобытийФормы
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОбновлениеМодуляОбмена();
	
КонецПроцедуры

&НаСервере
Процедура ОбновлениеМодуляОбмена()
	
	Если Б_ОбменССайтомСервер.Версия() <> Константы.Б_НомерВерсииМодуляОбменаССайтом.Получить() тогда	
		
		Попытка
			ОбновлениеУспешно = Б_ОбменССайтомСервер.ПроверкаОбновленияДанныхМодуляОбменаССайтом(Неопределено);
		Исключение
			Сообщить(ОписаниеОшибки());	
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗавершенСеансОбменаССайтом" Тогда
		
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
&НаКлиенте
Процедура ЭкспортНастроекВФайл(Команда)
	
	Если Элементы.Список.ТекущиеДанные <> Неопределено тогда
		
		КодНастройки = Элементы.Список.ТекущиеДанные.Код;
		
		Если КодНастройки = "" тогда
			Возврат;
		КонецЕсли;
		
		Если Не ПодключитьРасширениеРаботыСФайлами() Тогда
			Предупреждение(НСтр("ru = 'Для данной операции необходимо установить расширение работы с файлами!'"));
			Возврат;
		КонецЕсли;
		
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		
		Диалог.Заголовок = НСтр("ru = 'Укажите сохраняемый файл'");
		
		Если Диалог.Выбрать() Тогда
			
			ЭкспортНастроекВФайлНаСервере(КодНастройки, Диалог.ПолноеИмяФайла);
			
		КонецЕсли;
	
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура ЭкспортНастроекВФайлНаСервере(КодНастройки, ПутьКФайлу)
	
	
	НайденныйПлан = ПланыОбмена.Б_ОбменССайтом.НайтиПоКоду(КодНастройки);
	
	Если НЕ НайденныйПлан.Пустая() тогда
		
		Результат = Новый Структура;
		
		Для каждого ТекСтрока из Метаданные.ПланыОбмена.Б_ОбменССайтом.Реквизиты Цикл
			
			Результат.Вставить(ТекСтрока.Имя, НайденныйПлан[ТекСтрока.Имя]); 
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЗначениеВФайл(ПутьКФайлу, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмпортНастрокеИзФайла(Команда)
	
	Если Не ПодключитьРасширениеРаботыСФайлами() Тогда
		Предупреждение(НСтр("ru = 'Для данной операции необходимо установить расширение работы с файлами!'"));
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	Диалог.Заголовок = НСтр("ru = 'Укажите файл с настройками'");
	
	Если Диалог.Выбрать() Тогда
		
		лСтруктура = новый Структура;
		лСтруктура.Вставить("ПолноеИмяФайла", Диалог.ПолноеИмяФайла);
		
		Подсказка = "Введите  название будущей настройки";
		Оповещение = Новый ОписаниеОповещения("ПослеВводаНазванияНастройки", ЭтотОбъект, лСтруктура);
		ПоказатьВводСтроки(Оповещение, "", Подсказка, 0, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаНазванияНастройки(Строка, пПараметры) Экспорт
	
	Если НЕ Строка = Неопределено Тогда
		ИмпортНастрокеИзФайлаНаСервере(Строка, пПараметры);
	КонецЕсли;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ИмпортНастрокеИзФайлаНаСервере(НазваниеНастройки, пПараметры)
	
	СохраненныеЗначения = ЗначениеИзФайла(пПараметры.ПолноеИмяФайла);
	
	НовыйПлан = ПланыОбмена.Б_ОбменССайтом.СоздатьУзел();
	НовыйПлан.УстановитьНовыйКод();
	НовыйПлан.Наименование = НазваниеНастройки; 
	
	Для каждого ТекСтрока из СохраненныеЗначения Цикл
		
		Если НовыйПлан.Метаданные().Реквизиты.Найти(ТекСтрока.Ключ) <> Неопределено тогда
			НовыйПлан[ТекСтрока.Ключ] = ТекСтрока.Значение;	
		Иначе
			Сообщить("Не найдена настройка " + ТекСтрока.Ключ);
		КонецЕсли;
		
	КонецЦикла;
	
	НовыйПлан.Записать();
	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции
////////////////////////////////////////////////////////////////////////////////
// ПРОЧИЕ

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
		
	Если Поле.Имя = "Ошибка" Тогда
		Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.Ошибка) тогда
			СтандартнаяОбработка = Ложь;
			Оповещение = Новый ОписаниеОповещения("ПослеВводаСтроки", ЭтаФорма);
			ПоказатьВводСтроки(Оповещение, Элемент.ТекущиеДанные.Ошибка, "Информация о ошибке во время выпонения обмена с сайтом",,Истина);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаСтроки(Строка, Параметры) Экспорт
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОВерсииМодуля(Результат, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти
