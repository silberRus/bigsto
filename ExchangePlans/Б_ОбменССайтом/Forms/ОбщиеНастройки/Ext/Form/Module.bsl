
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьМеханизмОбменаВРеальномВремени 	= Константы.Б_ИспользоватьМеханизмОбменаВРеальномВремени.Получить();
	НастройкаОбменаДокументовВРеальномВремени 	= Константы.Б_НастройкаОбменаДокументовВРеальномВремени.Получить();
	ИспользоватьМеханизмПечатиЧеков 			= Константы.Б_ИспользоватьМеханизмПечатиЧеков.Получить();
	
	лОбщиеНастройки = Константы.Б_ОбщиеНастройки.Получить().Получить();
	
	Если лОбщиеНастройки <> Неопределено тогда
		лОбщиеНастройки.Свойство("КаталогЛога"						, КаталогЛога);
		лОбщиеНастройки.Свойство("РабочееМесто"						, РабочееМесто);
		лОбщиеНастройки.Свойство("ПодключаемоеОборудование"			, ПодключаемоеОборудование);
		лОбщиеНастройки.Свойство("ВариантОтправкиЭлектронногоЧека"	, ВариантОтправкиЭлектронногоЧека);
	КонецЕсли;
	
	ВариантОтправкиЭлектронногоЧека = ?(ЗначениеЗаполнено(ВариантОтправкиЭлектронногоЧека), ВариантОтправкиЭлектронногоЧека, Перечисления.ВариантыОтправкиЭлектронногоЧекаПокупателю.НеОтправлять);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
		
	ОбновитьОтображениеЭлементовФормы();
	
	ОбновитьИнформациюРМиПО();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	СохранениеКонстант();
КонецПроцедуры

&НаСервере
Процедура СохранениеКонстант()
	
	Константы.Б_НастройкаОбменаДокументовВРеальномВремени.Установить(НастройкаОбменаДокументовВРеальномВремени);
	Константы.Б_ИспользоватьМеханизмПечатиЧеков.Установить(ИспользоватьМеханизмПечатиЧеков);
	Константы.Б_ИспользоватьМеханизмОбменаВРеальномВремени.Установить(ИспользоватьМеханизмОбменаВРеальномВремени);
	
	лОбщиеНастройки = новый Структура;
	лОбщиеНастройки.Вставить("КаталогЛога"						, КаталогЛога);
	лОбщиеНастройки.Вставить("РабочееМесто"						, РабочееМесто);
	лОбщиеНастройки.Вставить("ПодключаемоеОборудование"			, ПодключаемоеОборудование);
	лОбщиеНастройки.Вставить("ВариантОтправкиЭлектронногоЧека"	, ВариантОтправкиЭлектронногоЧека);
	
	Константы.Б_ОбщиеНастройки.Установить(новый ХранилищеЗначения(лОбщиеНастройки));
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗадатьРасписаниеLongPulling(Команда)
	
	НашеРасписание = ПолучитьРасписаниеРегламетногоЗаданияОнлайнОбмена();
	                                                    
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(НашеРасписание);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьИзменятьРасписаниеLongPulling", ЭтотОбъект);
	
	Диалог.Показать(ОписаниеОповещения);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьИзменятьРасписаниеLongPulling(РасписаниеЗадания, ДополнительныеПараметры) Экспорт
	
	Если РасписаниеЗадания = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НовоеРасписание = РасписаниеЗадания;
	
	ЗаписатьРегламетноеЗаданиеОнлайнОбмена(НовоеРасписание);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьРасписаниеРегламетногоЗаданияОнлайнОбмена()
	
	Возврат РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.Б_ЗаданиеОбменССайтомОнлайн).Расписание;
	
КонецФункции

&НаСервере
Процедура ЗаписатьРегламетноеЗаданиеОнлайнОбмена(Расписание)
	
	РегламентноеЗадание = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.Б_ЗаданиеОбменССайтомОнлайн);
	РегламентноеЗадание.Расписание = Расписание;
	РегламентноеЗадание.Записать();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура ОбновитьОтображениеЭлементовФормы()
	
	Если ИспользоватьМеханизмОбменаВРеальномВремени тогда
		
		Элементы.НастройкаОбменаДокументовВРеальномВремени.Доступность = Истина;
		
		Если ЗначениеЗаполнено(НастройкаОбменаДокументовВРеальномВремени) тогда
			Элементы.ЗадатьРасписаниеLongPulling.Доступность 		= Истина;	
		Иначе
			Элементы.ЗадатьРасписаниеLongPulling.Доступность 		= Ложь;	
		КонецЕсли;
		
	Иначе
		Элементы.НастройкаОбменаДокументовВРеальномВремени.Доступность 	= Ложь;	
		Элементы.ЗадатьРасписаниеLongPulling.Доступность 				= Ложь;	
		
	КонецЕсли;
	
	Если ИспользоватьМеханизмПечатиЧеков Тогда
		Элементы.ГруппаНастройкаТО.Видимость = Истина;	
	Иначе
		Элементы.ГруппаНастройкаТО.Видимость = Ложь;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюРМиПО()
	
	Если НЕ ИспользоватьМеханизмПечатиЧеков Тогда
		Возврат;
	КонецЕсли;
	
	
	Если НЕ ЗначениеЗаполнено(РабочееМесто) тогда
		РабочееМесто = МенеджерОборудованияКлиентПовтИсп.ПолучитьРабочееМестоКлиента();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПодключаемоеОборудование) тогда
		ВыбратьПодключаемоеОборудование();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПодключаемоеОборудование()
	
	ТипОборудования = Новый Массив;
	ТипОборудования.Добавить( "ККТ");
	
	СписокДоступныхУстройств = МенеджерОборудованияВызовСервера.ОборудованиеПоПараметрам(ТипОборудования);
	Если СписокДоступныхУстройств.Количество() = 0 Тогда
		Сообщить("Нет подключенных ККТ. Продолжение невозможно.");
		Закрыть();
	Иначе
		
		СписокУстройств = Новый СписокЗначений();
		
		Для Каждого Устройства Из СписокДоступныхУстройств Цикл
			СписокУстройств.Добавить(Устройства.Ссылка, Устройства.Наименование);
		КонецЦикла;
		
		Если СписокУстройств.Количество() = 1 Тогда
			
			ПодключаемоеОборудование = СписокУстройств[0].Значение;
			Сообщить("Подключенных ККТ всего 1. Будет установлено по умолчанию.");
			
		Иначе
			
		Контекст = Новый Структура;
		ОписаниеОповещения = Новый ОписаниеОповещения("ПредложитьВыбратьУстройствоЗавершение", ЭтотОбъект, Контекст);
		СписокУстройств.ПоказатьВыборЭлемента(ОписаниеОповещения, "Выберите ККТ");
		
	КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 
Процедура ПредложитьВыбратьУстройствоЗавершение(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено тогда
		ПодключаемоеОборудование = Результат.Значение;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура НастройкаОбменаДокументовВРеальномВремениПриИзменении(Элемент)
	
	СохранениеКонстант();
		
	ОбновитьОтображениеЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьМеханизмПечатиЧековПриИзменении(Элемент)
	
	ОбновитьОтображениеЭлементовФормы();
	
	ОбновитьИнформациюРМиПО();
	
	СохранениеКонстант();
	
	ОбновитьИнтерфейс();
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогЛогаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	
	Если Не ПодключитьРасширениеРаботыСФайлами() Тогда
		Предупреждение(НСтр("ru = 'Для данной операции необходимо установить расширение работы с файлами!'"));
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	
	Диалог.Заголовок = НСтр("ru = 'Укажите каталог, где будут лежать логи операций'");
	Диалог.Каталог   = КаталогЛога;
	
	Если Диалог.Выбрать() Тогда
		
		КаталогЛога = Диалог.Каталог;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьМеханизмОбменаВРеальномВремениПриИзменении(Элемент)
	
	Если НЕ ИспользоватьМеханизмОбменаВРеальномВремени тогда
		НастройкаОбменаДокументовВРеальномВремени = Неопределено;	
	КонецЕсли;
	
	ОбновитьОтображениеЭлементовФормы();
	
	СохранениеКонстант();
	
	ОбновитьИнтерфейс();

КонецПроцедуры

#КонецОбласти

