#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область Обновление

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.ЖурналФискальныхОпераций";
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК ДокументОснование
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ДанныеДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ЖурналФискальныхОпераций КАК Журнал
	|	ПО
	|		Журнал.ДокументОснование = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.УдалитьНомерЧекаККМ <> 0
	|	И Журнал.ДокументОснование ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК ДокументОснование
	|ИЗ
	|	Документ.ОперацияПоПлатежнойКарте КАК ДанныеДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ЖурналФискальныхОпераций КАК Журнал
	|	ПО
	|		Журнал.ДокументОснование = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.УдалитьНомерЧекаККМ <> 0
	|	И Журнал.ДокументОснование ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК ДокументОснование
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК ДанныеДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ЖурналФискальныхОпераций КАК Журнал
	|	ПО
	|		Журнал.ДокументОснование = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.УдалитьНомерЧекаККМ <> 0
	|	И Журнал.ДокументОснование ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК ДокументОснование
	|ИЗ
	|	Документ.ЧекККМ КАК ДанныеДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ЖурналФискальныхОпераций КАК Журнал
	|	ПО
	|		Журнал.ДокументОснование = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.УдалитьНомерЧекаККМ <> 0
	|	И Журнал.ДокументОснование ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК ДокументОснование
	|ИЗ
	|	Документ.ЧекККМВозврат КАК ДанныеДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ЖурналФискальныхОпераций КАК Журнал
	|	ПО
	|		Журнал.ДокументОснование = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.УдалитьНомерЧекаККМ <> 0
	|	И Журнал.ДокументОснование ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК ДокументОснование
	|ИЗ
	|	Документ.РеализацияПодарочныхСертификатов КАК ДанныеДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ЖурналФискальныхОпераций КАК Журнал
	|	ПО
	|		Журнал.ДокументОснование = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.УдалитьНомерЧекаККМ <> 0
	|	И Журнал.ДокументОснование ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК ДокументОснование
	|ИЗ
	|	Документ.ВозвратПодарочныхСертификатов КАК ДанныеДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ЖурналФискальныхОпераций КАК Журнал
	|	ПО
	|		Журнал.ДокументОснование = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.УдалитьНомерЧекаККМ <> 0
	|	И Журнал.ДокументОснование ЕСТЬ NULL
	|";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить(), ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		"РегистрСведений.ЖурналФискальныхОпераций",
		МенеджерВременныхТаблиц);
	
	Если Не Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если Не Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                     КАК ДокументОснование,
	|	ДанныеДокумента.Дата                                       КАК Дата,
	|	ДанныеДокумента.Организация                                КАК Организация,
	|	ВЫБОР ДанныеДокумента.ХозяйственнаяОперация
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыККМ) ТОГДА
	|			ДанныеДокумента.КассаККМ
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойКассы) ТОГДА
	|			ДанныеДокумента.КассаОтправитель
	|		ИНАЧЕ
	|			ДанныеДокумента.Касса
	|	КОНЕЦ КАК ТорговыйОбъект,
	|	НЕОПРЕДЕЛЕНО                                               КАК Устройство,
	|	ВЫБОР ДанныеДокумента.ХозяйственнаяОперация
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыККМ) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ТипыОперацииКассовогоУзла.ВыемкаДенежныхСредств)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойКассы) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ТипыОперацииКассовогоУзла.ВыемкаДенежныхСредств)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ТипыОперацииКассовогоУзла.ФискальнаяОперация)
	|	КОНЕЦ КАК ТипОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств) КАК ФискальнаяОперацияТипРасчета,
	|	ДанныеДокумента.УдалитьВариантОтправкиЭлектронногоЧека     КАК ФискальнаяОперацияВариантОтправкиЭлектронногоЧека,
	|	ВЫРАЗИТЬ(ДанныеДокумента.УдалитьКонтактныеДанныеЭлектронногоЧека КАК Строка(255)) КАК ФискальнаяОперацияКонтактныеДанныеЭлектронногоЧека,
	|	ДанныеДокумента.УдалитьНомерЧекаККМ                        КАК ФискальнаяОперацияНомерЧекаККМ,
	|	НЕОПРЕДЕЛЕНО                                               КАК ФискальнаяОперацияНомерСменыККМ,
	|	ДанныеДокумента.СуммаДокумента                             КАК ФискальнаяОперацияСумма,
	|	ДанныеДокумента.СуммаДокумента                             КАК ФискальнаяОперацияСуммаОплатыНаличные,
	|	0 КАК ФискальнаяОперацияСуммаОплатыПлатежнаяКарта,
	|	0 КАК ФискальнаяОперацияСуммаПредоплаты,
	|	0 КАК ФискальнаяОперацияСуммаКредит
	|	
	|ИЗ
	|	&ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриходныйКассовыйОрдер КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = СсылкиДляОбработки.ДокументОснование
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ПриходныйКассовыйОрдерЗаблокировано
	|	ПО ПриходныйКассовыйОрдерЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ПриходныйКассовыйОрдерЗаблокировано.Ссылка ЕСТЬ NULL
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                     КАК ДокументОснование,
	|	ДанныеДокумента.Дата                                       КАК Дата,
	|	ДанныеДокумента.Организация                                КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                               КАК ТорговыйОбъект,
	|	НЕОПРЕДЕЛЕНО                                               КАК Устройство,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОперацииКассовогоУзла.ФискальнаяОперация) КАК ТипОперации,
	|	ВЫБОР ДанныеДокумента.ХозяйственнаяОперация
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств)
	|	КОНЕЦ КАК ФискальнаяОперацияТипРасчета,
	|	ДанныеДокумента.УдалитьВариантОтправкиЭлектронногоЧека     КАК ФискальнаяОперацияВариантОтправкиЭлектронногоЧека,
	|	ВЫРАЗИТЬ(ДанныеДокумента.УдалитьКонтактныеДанныеЭлектронногоЧека КАК Строка(255)) КАК ФискальнаяОперацияКонтактныеДанныеЭлектронногоЧека,
	|	ДанныеДокумента.УдалитьНомерЧекаККМ                        КАК ФискальнаяОперацияНомерЧекаККМ,
	|	НЕОПРЕДЕЛЕНО                                               КАК ФискальнаяОперацияНомерСменыККМ,
	|	ДанныеДокумента.СуммаДокумента                             КАК ФискальнаяОперацияСумма,
	|	0 КАК ФискальнаяОперацияСуммаОплатыНаличные,
	|	ДанныеДокумента.СуммаДокумента                             КАК ФискальнаяОперацияСуммаОплатыПлатежнаяКарта,
	|	0 КАК ФискальнаяОперацияСуммаПредоплаты,
	|	0 КАК ФискальнаяОперацияСуммаКредит
	|	
	|ИЗ
	|	&ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОперацияПоПлатежнойКарте КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = СсылкиДляОбработки.ДокументОснование
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ОперацияПоПлатежнойКартеЗаблокировано
	|	ПО ОперацияПоПлатежнойКартеЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ОперацияПоПлатежнойКартеЗаблокировано.Ссылка ЕСТЬ NULL
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                     КАК ДокументОснование,
	|	ДанныеДокумента.Дата                                       КАК Дата,
	|	ДанныеДокумента.Организация                                КАК Организация,
	|	ВЫБОР ДанныеДокумента.ХозяйственнаяОперация
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствВКассуККМ) ТОГДА
	|			ДанныеДокумента.КассаККМ
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствВДругуюКассу) ТОГДА
	|			ДанныеДокумента.КассаПолучатель
	|		ИНАЧЕ
	|			ДанныеДокумента.Касса
	|	КОНЕЦ КАК ТорговыйОбъект,
	|	НЕОПРЕДЕЛЕНО                                               КАК Устройство,
	|	ВЫБОР ДанныеДокумента.ХозяйственнаяОперация
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствВКассуККМ) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ТипыОперацииКассовогоУзла.ВнесениеДенежныхСредств)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствВДругуюКассу) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ТипыОперацииКассовогоУзла.ВнесениеДенежныхСредств)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ТипыОперацииКассовогоУзла.ФискальнаяОперация)
	|	КОНЕЦ КАК ТипОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств) КАК ФискальнаяОперацияТипРасчета,
	|	ДанныеДокумента.УдалитьВариантОтправкиЭлектронногоЧека     КАК ФискальнаяОперацияВариантОтправкиЭлектронногоЧека,
	|	ВЫРАЗИТЬ(ДанныеДокумента.УдалитьКонтактныеДанныеЭлектронногоЧека КАК Строка(255)) КАК ФискальнаяОперацияКонтактныеДанныеЭлектронногоЧека,
	|	ДанныеДокумента.УдалитьНомерЧекаККМ                        КАК ФискальнаяОперацияНомерЧекаККМ,
	|	НЕОПРЕДЕЛЕНО                                               КАК ФискальнаяОперацияНомерСменыККМ,
	|	ДанныеДокумента.СуммаДокумента                             КАК ФискальнаяОперацияСумма,
	|	ДанныеДокумента.СуммаДокумента                             КАК ФискальнаяОперацияСуммаОплатыНаличные,
	|	0 КАК ФискальнаяОперацияСуммаОплатыПлатежнаяКарта,
	|	0 КАК ФискальнаяОперацияСуммаПредоплаты,
	|	0 КАК ФискальнаяОперацияСуммаКредит
	|	
	|ИЗ
	|	&ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйКассовыйОрдер КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = СсылкиДляОбработки.ДокументОснование
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК РасходныйКассовыйОрдерЗаблокировано
	|	ПО РасходныйКассовыйОрдерЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	РасходныйКассовыйОрдерЗаблокировано.Ссылка ЕСТЬ NULL
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                     КАК ДокументОснование,
	|	ДанныеДокумента.Дата                                       КАК Дата,
	|	ДанныеДокумента.Организация                                КАК Организация,
	|	ДанныеДокумента.КассаККМ                                   КАК ТорговыйОбъект,
	|	НЕОПРЕДЕЛЕНО                                               КАК Устройство,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОперацииКассовогоУзла.ФискальнаяОперация)         КАК ТипОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств) КАК ФискальнаяОперацияТипРасчета,
	|	ДанныеДокумента.УдалитьВариантОтправкиЭлектронногоЧека     КАК ФискальнаяОперацияВариантОтправкиЭлектронногоЧека,
	|	ВЫРАЗИТЬ(ДанныеДокумента.УдалитьКонтактныеДанныеЭлектронногоЧека КАК Строка(255)) КАК ФискальнаяОперацияКонтактныеДанныеЭлектронногоЧека,
	|	ДанныеДокумента.УдалитьНомерЧекаККМ                        КАК ФискальнаяОперацияНомерЧекаККМ,
	|	НЕОПРЕДЕЛЕНО                                               КАК ФискальнаяОперацияНомерСменыККМ,
	|	ДанныеДокумента.СуммаДокумента                             КАК ФискальнаяОперацияСумма,
	|	ДанныеДокумента.СуммаДокумента - ЕСТЬNULL(СУММА(ПлатежныеКарты.Сумма), 0) - ЕСТЬNULL(СУММА(Сертификаты.Сумма), 0) КАК ФискальнаяОперацияСуммаОплатыНаличные,
	|	ЕСТЬNULL(СУММА(ПлатежныеКарты.Сумма), 0)                   КАК ФискальнаяОперацияСуммаОплатыПлатежнаяКарта,
	|	ЕСТЬNULL(СУММА(Сертификаты.Сумма), 0)                      КАК ФискальнаяОперацияСуммаПредоплаты,
	|	0 КАК ФискальнаяОперацияСуммаКредит
	|	
	|ИЗ
	|	&ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЧекККМ КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = СсылкиДляОбработки.ДокументОснование
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЧекККМ.ОплатаПлатежнымиКартами КАК ПлатежныеКарты
	|	ПО ПлатежныеКарты.Ссылка = ДанныеДокумента.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЧекККМ.ПодарочныеСертификаты КАК Сертификаты
	|	ПО Сертификаты.Ссылка = ДанныеДокумента.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ЧекККМЗаблокировано
	|	ПО ЧекККМЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ЧекККМЗаблокировано.Ссылка ЕСТЬ NULL
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                     КАК ДокументОснование,
	|	ДанныеДокумента.Дата                                       КАК Дата,
	|	ДанныеДокумента.Организация                                КАК Организация,
	|	ДанныеДокумента.КассаККМ                                   КАК ТорговыйОбъект,
	|	НЕОПРЕДЕЛЕНО                                               КАК Устройство,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОперацииКассовогоУзла.ФискальнаяОперация)          КАК ТипОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств) КАК ФискальнаяОперацияТипРасчета,
	|	ДанныеДокумента.УдалитьВариантОтправкиЭлектронногоЧека     КАК ФискальнаяОперацияВариантОтправкиЭлектронногоЧека,
	|	ВЫРАЗИТЬ(ДанныеДокумента.УдалитьКонтактныеДанныеЭлектронногоЧека КАК Строка(255)) КАК ФискальнаяОперацияКонтактныеДанныеЭлектронногоЧека,
	|	ДанныеДокумента.УдалитьНомерЧекаККМ                        КАК ФискальнаяОперацияНомерЧекаККМ,
	|	НЕОПРЕДЕЛЕНО                                               КАК ФискальнаяОперацияНомерСменыККМ,
	|	ДанныеДокумента.СуммаДокумента                             КАК ФискальнаяОперацияСумма,
	|	ДанныеДокумента.СуммаДокумента - ЕСТЬNULL(СУММА(ПлатежныеКарты.Сумма), 0) КАК ФискальнаяОперацияСуммаОплатыНаличные,
	|	ЕСТЬNULL(СУММА(ПлатежныеКарты.Сумма), 0)                   КАК ФискальнаяОперацияСуммаОплатыПлатежнаяКарта,
	|	0 КАК ФискальнаяОперацияСуммаПредоплаты,
	|	0 КАК ФискальнаяОперацияСуммаКредит
	|	
	|ИЗ
	|	&ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЧекККМВозврат КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = СсылкиДляОбработки.ДокументОснование
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЧекККМВозврат.ОплатаПлатежнымиКартами КАК ПлатежныеКарты
	|	ПО ПлатежныеКарты.Ссылка = ДанныеДокумента.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ЧекККМВозвратЗаблокировано
	|	ПО ЧекККМВозвратЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ЧекККМВозвратЗаблокировано.Ссылка ЕСТЬ NULL
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                     КАК ДокументОснование,
	|	ДанныеДокумента.Дата                                       КАК Дата,
	|	ДанныеДокумента.Организация                                КАК Организация,
	|	ДанныеДокумента.КассаККМ                                   КАК ТорговыйОбъект,
	|	НЕОПРЕДЕЛЕНО                                               КАК Устройство,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОперацииКассовогоУзла.ФискальнаяОперация)         КАК ТипОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств) КАК ФискальнаяОперацияТипРасчета,
	|	ДанныеДокумента.УдалитьВариантОтправкиЭлектронногоЧека     КАК ФискальнаяОперацияВариантОтправкиЭлектронногоЧека,
	|	ВЫРАЗИТЬ(ДанныеДокумента.УдалитьКонтактныеДанныеЭлектронногоЧека КАК Строка(255)) КАК ФискальнаяОперацияКонтактныеДанныеЭлектронногоЧека,
	|	ДанныеДокумента.УдалитьНомерЧекаККМ                        КАК ФискальнаяОперацияНомерЧекаККМ,
	|	НЕОПРЕДЕЛЕНО                                               КАК ФискальнаяОперацияНомерСменыККМ,
	|	ДанныеДокумента.СуммаДокумента                             КАК ФискальнаяОперацияСумма,
	|	ДанныеДокумента.СуммаДокумента - ЕСТЬNULL(СУММА(ПлатежныеКарты.Сумма), 0) КАК ФискальнаяОперацияСуммаОплатыНаличные,
	|	ЕСТЬNULL(СУММА(ПлатежныеКарты.Сумма), 0)                   КАК ФискальнаяОперацияСуммаОплатыПлатежнаяКарта,
	|	0 КАК ФискальнаяОперацияСуммаПредоплаты,
	|	0 КАК ФискальнаяОперацияСуммаКредит
	|	
	|ИЗ
	|	&ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияПодарочныхСертификатов КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = СсылкиДляОбработки.ДокументОснование
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияПодарочныхСертификатов.ОплатаПлатежнымиКартами КАК ПлатежныеКарты
	|	ПО ПлатежныеКарты.Ссылка = ДанныеДокумента.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК РеализацияПодарочныхСертификатовЗаблокировано
	|	ПО РеализацияПодарочныхСертификатовЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	РеализацияПодарочныхСертификатовЗаблокировано.Ссылка ЕСТЬ NULL
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                     КАК ДокументОснование,
	|	ДанныеДокумента.Дата                                       КАК Дата,
	|	ДанныеДокумента.Организация                                КАК Организация,
	|	ДанныеДокумента.КассаККМ                                   КАК ТорговыйОбъект,
	|	НЕОПРЕДЕЛЕНО                                               КАК Устройство,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОперацииКассовогоУзла.ФискальнаяОперация)          КАК ТипОперации,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств) КАК ФискальнаяОперацияТипРасчета,
	|	ДанныеДокумента.УдалитьВариантОтправкиЭлектронногоЧека     КАК ФискальнаяОперацияВариантОтправкиЭлектронногоЧека,
	|	ВЫРАЗИТЬ(ДанныеДокумента.УдалитьКонтактныеДанныеЭлектронногоЧека КАК Строка(255)) КАК ФискальнаяОперацияКонтактныеДанныеЭлектронногоЧека,
	|	ДанныеДокумента.УдалитьНомерЧекаККМ                        КАК ФискальнаяОперацияНомерЧекаККМ,
	|	НЕОПРЕДЕЛЕНО                                               КАК ФискальнаяОперацияНомерСменыККМ,
	|	ДанныеДокумента.СуммаДокумента                             КАК ФискальнаяОперацияСумма,
	|	ДанныеДокумента.СуммаДокумента - ЕСТЬNULL(СУММА(ПлатежныеКарты.Сумма), 0) КАК ФискальнаяОперацияСуммаОплатыНаличные,
	|	ЕСТЬNULL(СУММА(ПлатежныеКарты.Сумма), 0)                   КАК ФискальнаяОперацияСуммаОплатыПлатежнаяКарта,
	|	0 КАК ФискальнаяОперацияСуммаПредоплаты,
	|	0 КАК ФискальнаяОперацияСуммаКредит
	|	
	|ИЗ
	|	&ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратПодарочныхСертификатов КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = СсылкиДляОбработки.ДокументОснование
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратПодарочныхСертификатов.ОплатаПлатежнымиКартами КАК ПлатежныеКарты
	|	ПО ПлатежныеКарты.Ссылка = ДанныеДокумента.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ВозвратПодарочныхСертификатовЗаблокировано
	|	ПО ВозвратПодарочныхСертификатовЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ВозвратПодарочныхСертификатовЗаблокировано.Ссылка ЕСТЬ NULL
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ВТДляОбработкиСсылка", Результат.ИмяВременнойТаблицы);
	
	ТаблицыДляЧтения = Новый Массив;
	ТаблицыДляЧтения.Добавить("Документ.ПриходныйКассовыйОрдер");
	ТаблицыДляЧтения.Добавить("Документ.РасходныйКассовыйОрдер");
	ТаблицыДляЧтения.Добавить("Документ.ОперацияПоПлатежнойКарте");
	ТаблицыДляЧтения.Добавить("Документ.ЧекККМ");
	ТаблицыДляЧтения.Добавить("Документ.ЧекККМВозврат");
	ТаблицыДляЧтения.Добавить("Документ.РеализацияПодарочныхСертификатов");
	ТаблицыДляЧтения.Добавить("Документ.ВозвратПодарочныхСертификатов");
	
	РезультатЗаблокировано = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияСсылок(
		Параметры.Очередь,
		ТаблицыДляЧтения,
		МенеджерВременныхТаблиц);
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			НаборЗаписей = РегистрыСведений.ЖурналФискальныхОпераций.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ДокументОснование.Установить(Выборка.ДокументОснование);
			
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			НоваяЗапись.ИдентификаторЗаписи = Строка(Новый УникальныйИдентификатор());;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru = 'Не удалось сделать запись в журнал фискальных операций по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,,,
				ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.ЖурналФискальныхОпераций";
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.ДокументОснование
	|ИЗ
	|	&ВТДляОбработкиСсылка КАК СсылкиДляОбработки
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ВТДляОбработкиСсылка", Результат.ИмяВременнойТаблицы);
	ТаблицаОснований = Запрос.Выполнить().Выгрузить();
	Если ТаблицаОснований.Количество() Тогда
		ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ТаблицаОснований, ДополнительныеПараметры, Параметры.Очередь);
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь,
		"РегистрСведений.ЖурналФискальныхОпераций");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли