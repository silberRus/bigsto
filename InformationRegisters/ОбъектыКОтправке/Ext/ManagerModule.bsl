#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет запись объекта к отправке в регистр.
//
// Параметры:
//  ДанныеЗаписи - Структура - данные для записи с полями:
//      * УчетнаяСистема - СправоченикСсылка - учетная система.
//      * ИдентификаторОбъекта - Строка(50) - идентификатор объекта. 
//      * ИдентификаторФайла - УникальныйИдентификатор - идентификатор файла объекта. 
//
Процедура ДобавитьЗапись(ДанныеЗаписи) Экспорт
    
    МенеджерЗаписи = СоздатьМенеджерЗаписи();
    ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ДанныеЗаписи);
    МенеджерЗаписи.Записать();
    
КонецПроцедуры

// Удаляет запись объекта к отправке из регистра.
//
// Параметры:
//  Отбор - Структура - данные для удаления с полями:
//      * УчетнаяСистема - СправочникСсылка - учетная система
//      * ИдентификаторОбъекта - Строка(50) - индентификатор объекта
//
Процедура УдалитьЗаписи(Отбор) Экспорт
    
    Набор = СоздатьНаборЗаписей();
    
    Набор.Отбор.УчетнаяСистема.Установить(Отбор.УчетнаяСистема, Истина);
    Если Отбор.Свойство("ИдентификаторОбъекта") И ЗначениеЗаполнено(Отбор.ИдентификаторОбъекта) Тогда
        Набор.Отбор.ИдентификаторОбъекта.Установить(Отбор.ИдентификаторОбъекта, Истина);
    КонецЕсли; 
    Набор.Записать();
    
КонецПроцедуры

// Возвращает количество объектов к отправке по учетной системе.
//
// Параметры:
//  УчетнаяСистема - ОпределяемыйТип.УчетныеСистемыИнтеграцииОбластейДанных - учетная система. 
// 
// Возвращаемое значение:
//   - Число - количество объектов.
//
Функция Количество(УчетнаяСистема) Экспорт
	
    Запрос = Новый Запрос;
    Запрос.Текст = 
    	"ВЫБРАТЬ
        |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОбъектыКОтправке.ИдентификаторОбъекта) КАК Количество
        |ИЗ
        |	РегистрСведений.ОбъектыКОтправке КАК ОбъектыКОтправке
        |ГДЕ
        |	ОбъектыКОтправке.УчетнаяСистема = &УчетнаяСистема";
    
    Запрос.УстановитьПараметр("УчетнаяСистема", УчетнаяСистема);
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
    Если Выборка.Следующий() Тогда
        Возврат Выборка.Количество;
    Иначе 
        Возврат 0;
    КонецЕсли; 
	
КонецФункции
 
Функция НовыйДанныеЗаписи() Экспорт
	
    ДанныеЗаписи = Новый Структура;
    ДанныеЗаписи.Вставить("УчетнаяСистема");
    ДанныеЗаписи.Вставить("ИдентификаторОбъекта");
    ДанныеЗаписи.Вставить("ИдентификаторФайла");
    ДанныеЗаписи.Вставить("Обработчик");
    Возврат ДанныеЗаписи;
	
КонецФункции
 
#КонецОбласти

#КонецЕсли