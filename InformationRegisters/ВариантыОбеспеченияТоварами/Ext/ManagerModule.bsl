#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает список используемых способов для обеспечения товаром на складе.
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - Номенклатура товара,
//  Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - Характеристика товара,
//  Склад - СправочникСсылка.Склады - Склад,
//  ДоступныеТипыОбеспечения - Массив - Отбор по типам способов обеспечения.
//
// Возвращаемое значение:
//  Список значений - Список, содержащий способы обеспечения товаром на складе.
//
Функция СписокСпособовОбеспеченияТоваромНаСкладе(Номенклатура, Характеристика, Склад,
													ДоступныеТипыОбеспечения = Неопределено) Экспорт

	Запрос = Новый Запрос();

	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НаборДанных.Способ                             КАК СпособОбеспечения,
		|	ПРЕДСТАВЛЕНИЕ(НаборДанных.Способ)              КАК СпособОбеспеченияПредставление,
		|	МИНИМУМ(НаборДанных.Приоритет)                 КАК Приоритет,
		|	МИНИМУМ(НаборДанных.РеквизитДопУпорядочивания) КАК РеквизитДопУпорядочивания
		|ИЗ(
		|	ВЫБРАТЬ
		|		Способы.СпособОбеспеченияПотребностей КАК Способ,
		|		1                                     КАК Приоритет,
		|		Способы.РеквизитДопУпорядочивания     КАК РеквизитДопУпорядочивания
		|	ИЗ
		|		РегистрСведений.ВариантыОбеспеченияТоварами КАК Способы
		|	ГДЕ
		|		Способы.Характеристика            = &Характеристика
		|		И Способы.Номенклатура            = &Номенклатура
		|		И Способы.Склад                   = &Склад
		|		И &Характеристика <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Способы.СпособОбеспеченияПотребностей КАК Способ,
		|		2                                     КАК Приоритет,
		|		Способы.РеквизитДопУпорядочивания     КАК РеквизитДопУпорядочивания
		|	ИЗ
		|		РегистрСведений.ВариантыОбеспеченияТоварами КАК Способы
		|	ГДЕ
		|		Способы.Характеристика            = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		И Способы.Номенклатура            = &Номенклатура
		|		И Способы.Склад                   = &Склад
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Способы.СпособОбеспеченияПотребностей КАК Способ,
		|		3                                     КАК Приоритет,
		|		Способы.РеквизитДопУпорядочивания     КАК РеквизитДопУпорядочивания
		|	ИЗ
		|		РегистрСведений.ВариантыОбеспеченияТоварами КАК Способы
		|	ГДЕ
		|		Способы.Характеристика            = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		И Способы.Номенклатура            = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		И Способы.Склад                   = &Склад
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		КонстантаОсновнойСпособ.Значение КАК Способ,
		|		4                                КАК Приоритет,
		|		1                                КАК РеквизитДопУпорядочивания
		|	ИЗ
		|		Константа.ОсновнойСпособОбеспеченияПотребностей КАК КонстантаОсновнойСпособ
		|	ГДЕ
		|		КонстантаОсновнойСпособ.Значение <> ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка)) КАК НаборДанных
		|ГДЕ
		|	&ОтборПоТипуОбеспечения
		|СГРУППИРОВАТЬ ПО
		|	НаборДанных.Способ
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет,
		|	РеквизитДопУпорядочивания";

	Запрос.УстановитьПараметр("Склад",          Склад);
	Запрос.УстановитьПараметр("Номенклатура",   Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоТипуОбеспечения", ?(ДоступныеТипыОбеспечения = Неопределено,
		"Истина", "НаборДанных.Способ.ТипОбеспечения В(&ДоступныеТипыОбеспечения)"));
	Запрос.УстановитьПараметр("ДоступныеТипыОбеспечения", ДоступныеТипыОбеспечения);
	
	Выборка = Запрос.Выполнить().Выбрать();

	Список = Новый СписокЗначений();
	Пока Выборка.Следующий() Цикл
		Список.Добавить(Выборка.СпособОбеспечения, Выборка.СпособОбеспеченияПредставление);
	КонецЦикла;

	Возврат Список;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

Функция ЗаполнитьСпособОбеспечения(Таблица, СпособОбеспечения) Экспорт
	
	Набор = РегистрыСведений.ВариантыОбеспеченияТоварами.СоздатьНаборЗаписей();
	ОбработанныеСтроки = Новый Массив();
	
	Если ЗначениеЗаполнено(СпособОбеспечения) Тогда
		
		Набор.Добавить();
		Запись = Набор[0];
		Запись.РеквизитДопУпорядочивания = 1;
		Запись.СпособОбеспеченияПотребностей = СпособОбеспечения;
		
		Для Каждого СтрокаТаблицы Из Таблица Цикл
			
			ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
			Набор.Отбор.Номенклатура.Установить(СтрокаТаблицы.Номенклатура);
			Набор.Отбор.Характеристика.Установить(СтрокаТаблицы.Характеристика);
			Набор.Отбор.Склад.Установить(СтрокаТаблицы.Склад);
			
			Попытка
				
				Набор.Записать();
				ОбработанныеСтроки.Добавить(СтрокаТаблицы.Идентификатор);
				
			Исключение
				
				КомментарийКОшибке = ИнформацияОбОшибке();
				ИмяСобытияЖурнала = НСтр("ru = 'Заполнение способа обеспечения товаров'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
				ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала,
					УровеньЖурналаРегистрации.Ошибка,
					Метаданные.РегистрыСведений.ВариантыОбеспеченияТоварами,
					,
					КомментарийКОшибке);
				
			КонецПопытки;
			
		КонецЦикла;
		
	Иначе
		
		Для Каждого СтрокаТаблицы Из Таблица Цикл
			
			Набор.Отбор.Номенклатура.Установить(СтрокаТаблицы.Номенклатура);
			Набор.Отбор.Характеристика.Установить(СтрокаТаблицы.Характеристика);
			Набор.Отбор.Склад.Установить(СтрокаТаблицы.Склад);
			
			Попытка
				
				Набор.Записать();
				ОбработанныеСтроки.Добавить(СтрокаТаблицы.Идентификатор);
				
			Исключение
				
				КомментарийКОшибке = ИнформацияОбОшибке();
				ИмяСобытияЖурнала = НСтр("ru = 'Заполнение способа обеспечения товаров'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
				ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала,
					УровеньЖурналаРегистрации.Ошибка,
					Метаданные.РегистрыСведений.ВариантыОбеспеченияТоварами,
					,
					КомментарийКОшибке);
				
			КонецПопытки;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ТекстДанныеНеЗаписаны = НСтр("ru = 'Не удалось записать изменения способа обеспечения товарами.'");
	ТекстДанныеЗаписаныЧастично = НСтр("ru = 'Не все изменения способа обеспечения товарами удалось записать.'");
	ОбеспечениеСервер.СообщитьОбОшибкахЗаписиПараметровОбеспеченияПотребностей(
		Таблица, ОбработанныеСтроки, ТекстДанныеНеЗаписаны, ТекстДанныеЗаписаныЧастично);
	
	Возврат ОбработанныеСтроки;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#КонецОбласти

#КонецЕсли