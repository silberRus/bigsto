
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВладелецФормы.СоздатьЭлементыДляДоставки(АдресЭлементов);
	Отказ = Истина;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЭлемент(Элемент)
	
	Тип = ТипЗнч(Элемент);
	
	Структура = Новый Структура("Имя, Тип, Родитель, Вид, ТолькоПросмотр", 
							Элемент.Имя, 
							Тип, 
							?(ТипЗнч(Элемент.Родитель) 	= Тип("УправляемаяФорма"), "", Элемент.Родитель.Имя), 
							?(ТипЗнч(Элемент) 			= Тип("ТаблицаФормы"), "", Элемент.Вид));
			
	Структура.Вставить("Заголовок", 	Элемент.Заголовок);
	//Структура.Вставить("ЦветТекста",	Элемент.ЦветТекста);
	
	Если Тип = Тип("ТаблицаФормы") Тогда
		
		Структура.Вставить("ИзменятьПорядокСтрок", 		Элемент.ИзменятьПорядокСтрок);
		Структура.Вставить("ИзменятьСоставСтрок", 		Элемент.ИзменятьСоставСтрок);
		Структура.Вставить("Шапка", 					Элемент.Шапка);
		Структура.Вставить("Отображение", 				Элемент.Отображение);
		Структура.Вставить("ПутьКДанным",				Элемент.ПутьКДанным);
		Структура.Вставить("ВысотаВСтрокахТаблицы",		Элемент.ВысотаВСтрокахТаблицы);
		Структура.Вставить("ПоложениеКоманднойПанели",	Элемент.ПоложениеКоманднойПанели);
		Структура.Вставить("ЦветРамки",					Элемент.ЦветРамки);
		Структура.Вставить("РастягиватьПоВертикали",	Элемент.РастягиватьПоВертикали);
		Структура.Вставить("ТолькоПросмотр",			Элемент.ТолькоПросмотр);
		
	ИначеЕсли Тип = Тип("ПолеФормы") Тогда
		
		Если Элемент.Вид = ВидПоляФормы.ПолеПереключателя Тогда
			Структура.Вставить("ВидПереключателя", 	Элемент.ВидПереключателя);
		ИначеЕсли Элемент.Вид = ВидПоляФормы.ПолеФлажка Тогда
			
		Иначе
			Структура.Вставить("Высота", 					Элемент.Высота);
			Структура.Вставить("Ширина", 					Элемент.Ширина);
			Структура.Вставить("МногострочныйРежим", 		Элемент.МногострочныйРежим);
			Структура.Вставить("АвтоМаксимальнаяШирина", 	Элемент.АвтоМаксимальнаяШирина);
			
			Структура.Вставить("КнопкаВыпадающегоСписка",	Элемент.КнопкаВыпадающегоСписка);
			Структура.Вставить("КнопкаВыбора",				Элемент.КнопкаВыбора);
			Структура.Вставить("КнопкаОткрытия",			Элемент.КнопкаОткрытия);
			Структура.Вставить("ОтображениеКнопкиВыбора",	Элемент.ОтображениеКнопкиВыбора);
			Структура.Вставить("РежимВыбораИзСписка",		Элемент.РежимВыбораИзСписка);
		
		КонецЕсли;
	 
		Структура.Вставить("ПоложениеЗаголовка", 		Элемент.ПоложениеЗаголовка);
		Структура.Вставить("ТолькоПросмотр",			Элемент.ТолькоПросмотр);
		Структура.Вставить("ПутьКДанным", 				?(СтрНачинаетсяС(Элемент.ПутьКДанным, "Запись."), 
																	Сред(Элемент.ПутьКДанным, 8), 
																	Элемент.ПутьКДанным));
	ИначеЕсли Тип = Тип("ГруппаФормы") Тогда
		
		Структура.Вставить("ЦветФона", 				Элемент.ЦветФона);
		Структура.Вставить("Объединенная", 			Элемент.Объединенная);
		Структура.Вставить("Группировка", 			Элемент.Группировка);
		Структура.Вставить("ОтображатьЗаголовок", 	Элемент.ОтображатьЗаголовок);
		
	ИначеЕсли Тип = Тип("ДекорацияФормы") Тогда
		
		Структура.Вставить("Высота", 					Элемент.Высота);
		Структура.Вставить("Ширина", 					Элемент.Ширина);
		
		Структура.Вставить("Гиперссылка", 	Элемент.Гиперссылка);
		Структура.Вставить("АвтоМаксимальнаяШирина", 	Элемент.АвтоМаксимальнаяШирина);
		Структура.Вставить("РастягиватьПоГоризонтали", 	Элемент.РастягиватьПоГоризонтали);
		Структура.Вставить("Шрифт", 					Элемент.Шрифт);
		
		Если Элемент.Вид = ВидДекорацииФормы.Картинка Тогда
			Структура.Вставить("Картинка", 			Элемент.Картинка);
			Структура.Вставить("РазмерКартинки", 	Элемент.РазмерКартинки);
			Структура.Вставить("ГоризонтальноеПоложениеВГруппе", 	Элемент.ГоризонтальноеПоложениеВГруппе);
		Иначе
			Структура.Вставить("ГоризонтальноеПоложение", 			Элемент.ГоризонтальноеПоложение);
		КонецЕсли;
		
	ИначеЕсли Тип = Тип("КнопкаФормы") Тогда
		
		Структура.Вставить("Высота", 					Элемент.Высота);
		Структура.Вставить("Ширина", 					Элемент.Ширина);
		
		Структура.Вставить("ЦветФона", 			Элемент.ЦветФона);
		Структура.Вставить("ИмяКоманды", 		Элемент.ИмяКоманды);
		Структура.Вставить("Фигура", 			Элемент.Фигура);
		Структура.Вставить("ОтображениеФигуры", Элемент.ОтображениеФигуры);
		
	КонецЕсли;
			
	Возврат Структура;
	
КонецФункции
&НаСервере
Функция ПолучитьКоманды()
	
	Ком = Новый Массив;
	
	Для Каждого Команда Из Команды Цикл
		Структура = Новый Структура("Имя, Действие, Заголовок, Подсказка,ИзменяетСохраняемыеДанные, Отображение, Картинка");
		ЗаполнитьЗначенияСвойств(Структура, Команда);
		Ком.Добавить(Структура);
	КонецЦикла;
	
	Возврат Ком;
	
КонецФункции
&НаСервере
Функция ДобавитьЭлементы(Элемент, ЭлементыШаблона)
	
	ТипГруппа = Тип("ГруппаФормы");
	ТипТаблица= Тип("ТаблицаФормы");
	
	РазрТипы = Новый Массив;
	РазрТипы.Добавить(ТипГруппа);
	РазрТипы.Добавить(Тип("ПолеФормы"));
	РазрТипы.Добавить(Тип("ДекорацияТекстФормы"));
	РазрТипы.Добавить(Тип("ДекорацияКартинкаФормы"));
	РазрТипы.Добавить(Тип("ДекорацияФормы"));
	РазрТипы.Добавить(Тип("КнопкаФормы"));
	РазрТипы.Добавить(Тип("ТаблицаФормы"));
	
	Для Каждого текЭлемент Из Элемент.ПодчиненныеЭлементы Цикл
		Тип = ТипЗнч(текЭлемент);
		Если РазрТипы.Найти(Тип) <> Неопределено Тогда
			
			ЭлементыШаблона.Добавить(ПолучитьЭлемент(текЭлемент));
			Если Тип = ТипГруппа Или Тип = ТипТаблица Тогда
				ДобавитьЭлементы(текЭлемент, ЭлементыШаблона);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецФункции
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭлементыШаблона = Новый Массив;
	ДобавитьЭлементы(ЭтаФорма, ЭлементыШаблона);
	
	АдресЭлементов = ПоместитьВоВременноеХранилище(Новый Структура("Элементы, Реквизиты, Команды", 
						ЭлементыШаблона, АТ_ДоставкаСервер.ПолучитьРеквизиты(), ПолучитьКоманды()), Параметры.УникальныйИдентификаторВладельца);
КонецПроцедуры
