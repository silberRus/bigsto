#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбработатьПереданныеПараметры();
	ОбработатьРазрешениеРедактирования(ЭтотОбъект, РазрешитьРедактирование)
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если РазрешитьРедактирование
		и Не ТекущиеДанные = Неопределено
		и Поле.Имя = "ДатаПоследнегоУспешногоОбмена" Тогда 
		
		СписокВыборЗавершение = Новый ОписаниеОповещения("СписокВыборЗавершение",
			ЭтотОбъект,
			Новый Структура("НастройкаЯндексКассы", ТекущиеДанные.НастройкаЯндексКассы));
		
		ПоказатьВводДаты(СписокВыборЗавершение,
			ТекущиеДанные.ДатаПоследнегоУспешногоОбмена,
			НСтр("ru = 'Укажите дату последней загрузки'"),
			ЧастиДаты.ДатаВремя);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗавершение(Дата, ДополнительныеПараметры) Экспорт 
	
	Если ЗначениеЗаполнено(Дата) Тогда 
		
		СписокВыборЗавершениеНаСервере(Дата, ДополнительныеПараметры.НастройкаЯндексКассы);
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокВыборЗавершениеНаСервере(Дата, НастройкаЯндексКассы)
	
	РегистрыСведений.СтатусОбменовСЯндексКассой.ОбновитьСтатус(НастройкаЯндексКассы, Дата);
	
КонецПроцедуры

#КонецОбласти



#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьПереданныеПараметры()
	
	ВысотаВСтрокахТаблицы = 0;
	Если Параметры.Свойство("ВысотаВСтрокахТаблицы", ВысотаВСтрокахТаблицы) Тогда 
		Элементы.Список.ВысотаВСтрокахТаблицы = ВысотаВСтрокахТаблицы;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбработатьРазрешениеРедактирования(Форма, РазрешитьРедактирование)
	
	Форма.Элементы.ДатаПоследнегоУспешногоОбмена.ТолькоПросмотр = Не РазрешитьРедактирование;
	Форма.Элементы.ФормаРазрешитьРедактирование.Картинка = ?(РазрешитьРедактирование, БиблиотекаКартинок.ФлагУстановлен, БиблиотекаКартинок.ФлагНеУстановлен);
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактирование(Команда)
	
	РазрешитьРедактирование = Не РазрешитьРедактирование;
	ОбработатьРазрешениеРедактирования(ЭтотОбъект, РазрешитьРедактирование);
	
КонецПроцедуры

#КонецОбласти
