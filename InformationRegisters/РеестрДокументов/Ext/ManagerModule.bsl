#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записывает данные документы в регистр сведений ДанныеВнутреннихДокументов
//
// Параметры:
//  Ссылка					 - ДокументСсылка - 
//  ДополнительныеСвойства	 - Структура - содержит по ключу ТаблицыДляДвижений структуру
//  										имеющую ключ ТаблицаРеестрДокументов (ТаблицаЗначений)
//  Отказ					 - Булево - 
//
Процедура ЗаписатьДанныеДокумента(Ссылка, ДополнительныеСвойства, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// silber {
	АТ_Сервер.ПриРегистрацииДокументаВРеестреДокументов(Ссылка, ДополнительныеСвойства, Отказ);
	// } silber
	
	УстановитьПривилегированныйРежим(Истина);
	Набор = РегистрыСведений.РеестрДокументов.СоздатьНаборЗаписей();
	Набор.Отбор.Ссылка.Установить(Ссылка);
	Набор.Загрузить(ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРеестрДокументов);
	Набор.Записать();
	
КонецПроцедуры

// Записывает в регистр данные по переданному разделителю записи.
//
// Параметры:
//  РазделительЗаписи		 - ОпределяемыйТип.РазделительЗаписиРеестраДокументов - измерение, по которому необходимо выполнить запись. 
//  ДополнительныеСвойства	 - Структура - содержит по ключу ТаблицыДляДвижений структуру
//  	имеющую ключ ТаблицаРеестрДокументов (ТаблицаЗначений)
// ЗамещатьЗаписи			 - Булево - определяет режим замещения существующих записей разделителя. Истина - перед записью существующие
//		записи будут удалены. Ложь - записи будут дописаны к уже существующим в информационной базе записям.
//
Процедура ЗаписатьДанныеРазделителя(РазделительЗаписи, ДополнительныеСвойства, ЗамещатьЗаписи = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Набор = РегистрыСведений.РеестрДокументов.СоздатьНаборЗаписей();
	Набор.Отбор.РазделительЗаписи.Установить(РазделительЗаписи);
	Набор.Загрузить(ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРеестрДокументов);
	Набор.Записать(ЗамещатьЗаписи);
	
КонецПроцедуры

// Инициализирует и записывает данные документы в регистр сведений ДанныеВнутреннихДокументов
//
// Параметры:
//  Ссылка				 - 	 - 
//
Процедура ИнициализироватьИЗаписатьДанныеДокумента(Ссылка, ДополнительныеСвойства, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Ссылка);
	МенеджерДокумента.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства, "РеестрДокументов");
	РегистрыСведений.РеестрДокументов.ЗаписатьДанныеДокумента(Ссылка, ДополнительныеСвойства, Отказ);
	
КонецПроцедуры

#Область ОбменДанными

// Возвращает признак, того отражаются ли данные полученного объекта в реестре сведений или нет.
//
// Параметры:
//	ПроверяемыйОбъект - ДокументОбъект, ДокументСсылка, ДанныеФормыСтруктура, ОбъектМетаданных
//
// Возвращаемое значение
//	Булево - Истина, если данные полученного объекта, отражаются в реестре сведений.
//
Функция ОбъектВключенВСоставДанныхРеестра(ПроверяемыйОбъект) Экспорт
	
	ТипЗначения = ТипЗнч(ПроверяемыйОбъект);
	Если ТипЗначения = Тип("ДанныеФормыСтруктура") Тогда
		Если Не ПроверяемыйОбъект.Свойство("Ссылка") Тогда
			Возврат Ложь;
		КонецЕсли;
		ТипСсылки = ТипЗнч(ПроверяемыйОбъект.Ссылка);
	Иначе	
		Если ТипЗначения = Тип("ОбъектМетаданных") Тогда
			МетаданныеЗначения = ПроверяемыйОбъект;
		Иначе
			МетаданныеЗначения = Метаданные.НайтиПоТипу(ТипЗначения);
			
			Если МетаданныеЗначения = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ОбщегоНазначения.ЭтоДокумент(МетаданныеЗначения) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ТипСсылки = ТипЗнч(МетаданныеЗначения.СтандартныеРеквизиты.Ссылка.Тип.ПривестиЗначение());
	КонецЕсли;

	МетаданныеРегистра	= Метаданные.НайтиПоПолномуИмени("РегистрСведений.РеестрДокументов");
	Возврат МетаданныеРегистра.Измерения.Ссылка.Тип.СодержитТип(ТипСсылки);
	
КонецФункции

// Инициализирует и записывает данные документов, полученных объектов метаданных, в регистр сведений.
//
// Параметры:
//	ОбъектыМетаданных - Соответствие - объекты метаданных:
//		* Ключ		- ОбъектМетаданных: Документ	- объект метаданных документа.
//		* Значение	- Неопределено					- пустое значение.
//
Процедура ОтразитьДанныеДокументовВРеестре(ОбъектыМетаданных) Экспорт
	
	ИменаОбъектов		= Новый Массив;
	СсылкиДокументов	= Новый Массив;
	
	Для Каждого ЭлементДанных Из ОбъектыМетаданных Цикл
		ОбъектДанных		= ЭлементДанных.Ключ;
		ПолноеИмяОбъекта	= ОбъектДанных.ПолноеИмя();
		
		ИменаОбъектов.Добавить(ПолноеИмяОбъекта);
	КонецЦикла;
	
	НеиспользуемыеПоля = Новый Массив;
	НеиспользуемыеПоля.Добавить("Дополнительно");
	НеиспользуемыеПоля.Добавить("НомерПервичногоДокумента");
	
	Для Каждого ПолноеИмяОбъекта Из ИменаОбъектов Цикл
		
		ИмяДокумента = СтрРазделить(ПолноеИмяОбъекта, ".")[1];
		РезультатАдаптацииЗапроса = Документы[ИмяДокумента].АдаптированныйТекстЗапросаДвиженийПоРегистру("РеестрДокументов");
		Регистраторы = ОбновлениеИнформационнойБазыУТ.ДанныеНезависимогоРегистраДляПерепроведения(
							РезультатАдаптацииЗапроса,
							"РегистрСведений.РеестрДокументов",
							ПолноеИмяОбъекта,
							НеиспользуемыеПоля);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СсылкиДокументов, Регистраторы.ВыгрузитьКолонку("Ссылка"));
		
	КонецЦикла;
	
	Для Каждого Ссылка Из СсылкиДокументов Цикл
		ДополнительныеСвойства = Новый Структура;
		
		РегистрыСведений.РеестрДокументов.ИнициализироватьИЗаписатьДанныеДокумента(Ссылка, ДополнительныеСвойства, Ложь);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт 
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", "РегистрСведений.РеестрДокументов");
	
	ДокументыИспользующиеАдаптированныйТекстЗапроса = Новый Массив;
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.АктВыполненныхРабот");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.АктОРасхожденияхПослеОтгрузки");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.АктОРасхожденияхПослеПеремещения");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.АктОРасхожденияхПослеПриемки");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ВнутреннееПотреблениеТоваров");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ВозвратТоваровМеждуОрганизациями");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ВозвратТоваровОтКлиента");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ВозвратТоваровПоставщику");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ЗаказКлиента");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ЗаказНаВнутреннееПотребление");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ЗаказНаПеремещение");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ЗаказНаСборку");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ЗаказПоставщику");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ЗаявкаНаВозвратТоваровОтКлиента");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ЗаявлениеОВвозеТоваров");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ИнвентаризационнаяОпись");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.КорректировкаПриобретения");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.КорректировкаРеализации");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ОприходованиеИзлишковТоваров");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ОтчетПоКомиссииМеждуОрганизациями");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ОтчетПоКомиссииМеждуОрганизациямиОСписании");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ПередачаТоваровМеждуОрганизациями");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ПеремещениеТоваров");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ПересортицаТоваров");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ПорчаТоваров");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ПоступлениеТоваров");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ПриобретениеТоваровУслуг");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ПриобретениеУслугПрочихАктивов");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ПрочееОприходованиеТоваров");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.РеализацияТоваровУслуг");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.РеализацияУслугПрочихАктивов");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.СборкаТоваров");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.СписаниеНедостачТоваров");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.ТаможеннаяДекларацияИмпорт");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.СчетФактураВыданный");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.СчетФактураВыданныйАванс");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.СчетФактураПолученный");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.СчетФактураПолученныйНалоговыйАгент");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.СчетФактураПолученныйАванс");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.СчетФактураКомиссионеру");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.СчетФактураКомитента");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.СчетФактураНалоговыйАгент");
	ДокументыИспользующиеАдаптированныйТекстЗапроса.Добавить("Документ.СчетФактураНаНеподтвержденнуюРеализацию0");
	
	
	// Регистрация через АдаптированныйТекстЗапроса
	Для каждого ПолноеИмяДокумента Из ДокументыИспользующиеАдаптированныйТекстЗапроса Цикл
		
		НеиспользуемыеПоля = Новый Массив;
		НеиспользуемыеПоля.Добавить("Дополнительно");
		НеиспользуемыеПоля.Добавить("РазделительЗаписи");
		НеиспользуемыеПоля.Добавить("НомерПервичногоДокумента");
		
		ИмяДокумента = СтрРазделить(ПолноеИмяДокумента, ".")[1];
		РезультатАдаптацииЗапроса = Документы[ИмяДокумента].АдаптированныйТекстЗапросаДвиженийПоРегистру("РеестрДокументов");
		Регистраторы = ОбновлениеИнформационнойБазыУТ.ДанныеНезависимогоРегистраДляПерепроведения(
							РезультатАдаптацииЗапроса, 
							"РегистрСведений.РеестрДокументов", 
							ПолноеИмяДокумента, 
							НеиспользуемыеПоля);
		
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазыУТ.ДополнительныеПараметрыПерезаписиДвиженийИзОчереди();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ИмяИзмеренияДляОтбора = "Ссылка";
	ПоляУпорядочивания = Новый Массив;
	ПоляУпорядочивания.Добавить("Ссылка.Дата УБЫВ");
	ДополнительныеПараметры.ПоляУпорядочивания = ПоляУпорядочивания;
	ВсеСделано = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(Неопределено,
																			"РегистрСведений.РеестрДокументов",
																			Параметры.Очередь,
																			ДополнительныеПараметры);
	
	Параметры.ОбработкаЗавершена = ВсеСделано;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
