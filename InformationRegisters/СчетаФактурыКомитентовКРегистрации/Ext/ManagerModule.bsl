#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Переформировывает распоряжения на регистрацию счетов-фактур от комитентов.
//
// Параметры:
// 	 СчетаФактурыВыданные - Массив - Счета-фактуры, выданные покупателям комиссионого товара, 
// 										по которым необходимо выполнить формирование распоряжений.
//
Процедура ОбновитьСостояние(Знач СчетаФактурыВыданные) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(СчетаФактурыВыданные, Неопределено);
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(СчетаФактурыВыданные, Документы.СчетФактураВыданный.ПустаяСсылка());
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(СчетаФактурыВыданные, Документы.СчетФактураКомиссионеру.ПустаяСсылка());
	СчетаФактурыВыданные = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СчетаФактурыВыданные);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложеннаяТаблица.Организация,
	|	ВложеннаяТаблица.Комитент,
	|	ВложеннаяТаблица.Дата,
	|	ВложеннаяТаблица.Покупатель,
	|	ВложеннаяТаблица.СчетФактураВыданный,
	|	ВложеннаяТаблица.Валюта,
	|	СУММА(ВложеннаяТаблица.СуммаСНДС) КАК СуммаСНДС,
	|	СУММА(ВложеннаяТаблица.СуммаНДС) КАК СуммаНДС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОтчетКомитенту.Организация КАК Организация,
	|		ОтчетКомитенту.Контрагент КАК Комитент,
	|		НАЧАЛОПЕРИОДА(ЕСТЬNULL(СчетФактураВыданный.Дата, ЕСТЬNULL(СчетФактураКомиссионеру.Ссылка.Дата, ОтчетКомитенту.Дата)), ДЕНЬ) КАК Дата,
	|		ЕСТЬNULL(СчетФактураВыданный.Контрагент, ЕСТЬNULL(СчетФактураКомиссионеру.Покупатель, НЕОПРЕДЕЛЕНО)) КАК Покупатель,
	|		ТаблицаТовары.СчетФактураВыставленный КАК СчетФактураВыданный,
	|		ОтчетКомитенту.Валюта КАК Валюта,
	|		ТаблицаТовары.СуммаПродажи КАК СуммаСНДС,
	|		(ТаблицаТовары.СуммаПродажи 
	|			* ВЫБОР 
	|				КОГДА ТаблицаТовары.СтавкаНДС В (&Ставки18) ТОГДА &ЗначениеСтавки18
	|				КОГДА ТаблицаТовары.СтавкаНДС В (&Ставки10) ТОГДА &ЗначениеСтавки10
	|				ИНАЧЕ 0
	|			  КОНЕЦ
	|			/ ВЫБОР 
	|				КОГДА ТаблицаТовары.СтавкаНДС В (&Ставки18) ТОГДА 1 + &ЗначениеСтавки18
	|				КОГДА ТаблицаТовары.СтавкаНДС В (&Ставки10) ТОГДА 1 + &ЗначениеСтавки10
	|				ИНАЧЕ 1
	|			  КОНЕЦ) КАК СуммаНДС
	|	ИЗ
	|		Документ.ОтчетКомитенту.Товары КАК ТаблицаТовары
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.ОтчетКомитенту КАК ОтчетКомитенту
	|		ПО
	|			ТаблицаТовары.Ссылка = ОтчетКомитенту.Ссылка
	|			И ОтчетКомитенту.Проведен
	|			
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ПО
	|			ТаблицаТовары.СчетФактураВыставленный = СчетФактураВыданный.Ссылка
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			Документ.СчетФактураКомиссионеру.Покупатели КАК СчетФактураКомиссионеру
	|		ПО
	|			ТаблицаТовары.СчетФактураВыставленный = СчетФактураКомиссионеру.Ссылка
	|			И ТаблицаТовары.Покупатель = СчетФактураКомиссионеру.Покупатель
	|	
	|	ГДЕ
	|		ТаблицаТовары.СчетФактураВыставленный В (&СчетаФактурыВыданные)
	|		И ОтчетКомитенту.Проведен
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СчетФактураКомитента.Организация КАК Организация,
	|		СчетФактураКомитента.Комитент КАК Комитент,
	|		НАЧАЛОПЕРИОДА(СчетФактураКомитента.ДатаСоставления, ДЕНЬ) КАК Дата,
	|		ТаблицаПокупатели.Покупатель КАК Покупатель,
	|		ТаблицаПокупатели.СчетФактураВыданный КАК СчетФактураВыданный,
	|		СчетФактураКомитента.Валюта КАК Валюта,
	|		-ТаблицаПокупатели.СуммаСНДС КАК СуммаСНДС,
	|		-ТаблицаПокупатели.СуммаНДС  КАК СуммаНДС
	|	ИЗ
	|		Документ.СчетФактураКомитента.Покупатели КАК ТаблицаПокупатели
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.СчетФактураКомитента КАК СчетФактураКомитента
	|		ПО
	|			ТаблицаПокупатели.Ссылка = СчетФактураКомитента.Ссылка
	|	ГДЕ
	|		ТаблицаПокупатели.СчетФактураВыданный В (&СчетаФактурыВыданные)
	|		И СчетФактураКомитента.Проведен) ВложеннаяТаблица
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложеннаяТаблица.Организация,
	|	ВложеннаяТаблица.Комитент,
	|	ВложеннаяТаблица.Дата,
	|	ВложеннаяТаблица.Покупатель,
	|	ВложеннаяТаблица.СчетФактураВыданный,
	|	ВложеннаяТаблица.Валюта
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложеннаяТаблица.СуммаСНДС) > 0
	|	И СУММА(ВложеннаяТаблица.СуммаНДС) > 0
	|";
	
	Ставки18 = Новый Массив;
	Ставки18.Добавить(Перечисления.СтавкиНДС.НДС18);
	Ставки18.Добавить(Перечисления.СтавкиНДС.НДС18_118);
	ЗначениеСтавки18 = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(Ставки18[0]);
	
	Ставки10 = Новый Массив;
	Ставки10.Добавить(Перечисления.СтавкиНДС.НДС10);
	Ставки10.Добавить(Перечисления.СтавкиНДС.НДС10_110);
	ЗначениеСтавки10 = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(Ставки10[0]);
	
	Запрос.УстановитьПараметр("СчетаФактурыВыданные", СчетаФактурыВыданные);
	Запрос.УстановитьПараметр("Ставки18", Ставки18);
	Запрос.УстановитьПараметр("ЗначениеСтавки18", ЗначениеСтавки18);
	Запрос.УстановитьПараметр("Ставки10", Ставки10);
	Запрос.УстановитьПараметр("ЗначениеСтавки10", ЗначениеСтавки10);
	
	СчетаФактурыКомитентовКРегистрации = Запрос.Выполнить().Выгрузить();
	СчетаФактурыКомитентовКРегистрации.Индексы.Добавить("СчетФактураВыданный");
	
	Для каждого СчетФактураВыданный Из СчетаФактурыВыданные Цикл
		
		НаборЗаписей = РегистрыСведений.СчетаФактурыКомитентовКРегистрации.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.СчетФактураВыданный.Установить(СчетФактураВыданный);
		
		Отбор = Новый Структура("СчетФактураВыданный", СчетФактураВыданный);
		Строки = СчетаФактурыКомитентовКРегистрации.НайтиСтроки(Отбор);
		Для каждого Строка Из Строки Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Строка);
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#КонецЕсли