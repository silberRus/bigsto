
&НаКлиенте
Перем Сохранено;

&НаСервере
Процедура УправлениеВидимостью()
	
	Элементы.АналогиСохранить.Видимость = Модифицированность;
	
КонецПроцедуры
&НаКлиенте
Процедура АналогиПриИзменении(Элемент)
	
	Модифицированность = Истина;
	УправлениеВидимостью();
	
КонецПроцедуры
&НаКлиенте
Процедура АналогиПослеУдаления(Элемент)
	
	Модифицированность = Истина;
	УправлениеВидимостью();
	
КонецПроцедуры


&НаСервере
Процедура ЗагрузитьАртикулы()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ 	Артикул, Бренд, Регистратор, Регистратор.Ответственный Ответственный
	|ИЗ 		РегистрСведений.АналогиТоваров.СрезПоследних(,Номенклатура = &Номенклатура)
	|ГДЕ 		Не Удалено УПОРЯДОЧИТЬ ПО Бренд, Артикул");
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Аналоги.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Номенклатура = Параметры.Номенклатура;
	ЗагрузитьАртикулы();
	УправлениеВидимостью();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	
	Запрос = Новый Запрос("ВЫБРАТЬ Таб.Бренд, Таб.Артикул ПОМЕСТИТЬ Таб ИЗ &Таблица Таб;
	
	|ВЫБРАТЬ 	
	|	&Номенклатура 								Номенклатура, 
	|	ЕСТЬNULL(Рег.Бренд, Таб.Бренд) 				Бренд, 
	|	ЕСТЬNULL(Рег.Артикул, Таб.Артикул) 			Артикул,
	|	ВЫБОР КОГДА Таб.Артикул ЕСТЬ NULL ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ Удалено
	|ИЗ
	|	(	ВЫБРАТЬ Бренд, Артикул
	|		ИЗ РегистрСведений.АналогиТоваров.СрезПоследних(,Номенклатура = &Номенклатура)
	|		ГДЕ НЕ Удалено
	|	) Рег
	|
	|ПОЛНОЕ СОЕДИНЕНИЕ Таб Таб
	|ПО Рег.Бренд 	= Таб.Бренд И
	|	Рег.Артикул = Таб.Артикул
	|
	|ГДЕ
	|	Таб.Артикул ЕСТЬ NULL ИЛИ 
	|	Рег.Артикул ЕСТЬ NULL
	|");
	 
	Запрос.УстановитьПараметр("Номенклатура", 	Номенклатура);
	Запрос.УстановитьПараметр("Таблица", 		Аналоги.Выгрузить());
	                                   
	новДок = Документы.ЗагрузкаАналогов.СоздатьДокумент();
	новДок.Дата = ТекущаяДата();
	новДок.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Если новДок.Товары.Количество() Тогда
		новДок.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	
КонецПроцедуры
&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьНаСервере();
	Сохранено = Истина;
	Закрыть();
	
КонецПроцедуры


&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы И Модифицированность И Сохранено <> Истина Тогда
		СохранитьНаСервере();
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура АналогиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.АналогиОтветственный Или Поле = Элементы.Аналогирегистратор Тогда
		
		текСтрока = Элементы.Аналоги.ТекущиеДанные;
		Если текСтрока <> Неопределено Тогда
			ПоказатьЗначение(,текСтрока.Регистратор);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура АналогиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Копирование Тогда
		
		текСтрока = Элементы.Аналоги.ТекущиеДанные;
		Если текСтрока <> Неопределено Тогда
			текСтрока.Регистратор 	= Неопределено;
			текСтрока.Ответственный = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

