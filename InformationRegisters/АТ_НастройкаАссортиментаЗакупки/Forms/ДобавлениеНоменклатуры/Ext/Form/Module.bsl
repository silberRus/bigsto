#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Склад") Тогда
		Склад = Параметры.Склад;
	КонецЕсли;
	
	Если Параметры.Свойство("СпособОбеспечения") Тогда
		СпособОбеспечения = Параметры.СпособОбеспечения;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ПараметрыОповещения = Новый Структура;
	ИмяСобытия = "ДобавлениеНоменклатуры";
	ФормаИсточник = "РегистрСведений.АТ_НастройкаАссортиментаЗакупки.Форма.ФормаСписка";
	Оповестить(ИмяСобытия, ПараметрыОповещения, ФормаИсточник);
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ВыбратьПриИзменении(Элемент)
	ВыбратьПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВыбратьПриИзмененииНаСервере()
	
	Для Каждого Строка Из Номенклатура Цикл
		Строка.Выбран = Выбрать;
	КонецЦикла;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ДобавитьНоменклатуру(Команда)
	
	ДобавитьНоменклатуруНаСервере();
	
	Если Не ЗначениеЗаполнено(Склад) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнен склад!";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СпособОбеспечения) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнен способ обеспечения!";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Не ЗначениеЗаполнено(Производитель) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнен производитель!";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Склад) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнен склад!";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ЗаполнитьНаСервере();
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ДобавитьНоменклатуруНаСервере()
	
	Для Каждого Строка Из Номенклатура Цикл
		
		Если Строка.Выбран Тогда
			МенеджерЗаписи = РегистрыСведений.АТ_НастройкаАссортиментаЗакупки.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Склад = Склад;
			МенеджерЗаписи.Номенклатура = Строка.Номенклатура;
			МенеджерЗаписи.СпособОбеспечения = СпособОбеспечения;
			МенеджерЗаписи.Записать(Истина);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СправочникНоменклатура.Артикул КАК Артикул,
	|	СправочникНоменклатура.Ссылка КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АТ_НастройкаАссортиментаЗакупки КАК АТ_НастройкаАссортиментаЗакупки
	|		ПО СправочникНоменклатура.Ссылка = АТ_НастройкаАссортиментаЗакупки.Номенклатура
	|			И (АТ_НастройкаАссортиментаЗакупки.Склад = &Склад)
	|ГДЕ
	|	СправочникНоменклатура.Производитель = &Производитель
	|	И АТ_НастройкаАссортиментаЗакупки.СпособОбеспечения ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА КАК Выбран,
	|	ВложенныйЗапрос.Артикул КАК Артикул,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	СУММА(ВложенныйЗапрос.ТекущийОстаток) КАК ТекущийОстаток,
	|	СУММА(ВложенныйЗапрос.ПродажиЗа3Месяца) КАК ПродажиЗа3Месяца
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаНоменклатуры.Артикул КАК Артикул,
	|		ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
	|		СУММА(ЕСТЬNULL(ТоварыОрганизацийОстатки.КоличествоОстаток, 0)) КАК ТекущийОстаток,
	|		0 КАК ПродажиЗа3Месяца
	|	ИЗ
	|		ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций.Остатки КАК ТоварыОрганизацийОстатки
	|			ПО ТаблицаНоменклатуры.Номенклатура = ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.Номенклатура
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаНоменклатуры.Артикул,
	|		ТаблицаНоменклатуры.Номенклатура
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаНоменклатуры.Артикул,
	|		ТаблицаНоменклатуры.Номенклатура,
	|		0,
	|		СУММА(ЕСТЬNULL(ВыручкаИСебестоимостьПродажОбороты.КоличествоОборот, 0))
	|	ИЗ
	|		ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&ДатаН, &ДатаК, ПЕРИОД, ) КАК ВыручкаИСебестоимостьПродажОбороты
	|			ПО ТаблицаНоменклатуры.Номенклатура = ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаНоменклатуры.Артикул,
	|		ТаблицаНоменклатуры.Номенклатура) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Артикул,
	|	ВложенныйЗапрос.Номенклатура";
	Запрос.УстановитьПараметр("Производитель", Производитель);
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Запрос.УстановитьПараметр("ДатаН", ДобавитьМесяц(ТекущаяДата(), - 3));
	Запрос.УстановитьПараметр("ДатаК", ТекущаяДата());
	
	Номенклатура.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Если Номенклатура.Количество() > 0 Тогда
		Выбрать = Истина;
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти










