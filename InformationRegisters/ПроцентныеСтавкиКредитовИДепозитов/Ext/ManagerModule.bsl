#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записывает ставку процентов и ставку комиссии по договору кредита, депозита, займа.
//
// Параметры:
//	- Договор - СправочникСсылка.ДоговорыКредитовИДепозитов - договор кредита, депозита, займа.
//	- НоваяСтавка - Число - ставка процентов по договору.
//	- НоваяКомиссия - Число - процент комиссии по договору.
//	- Период - Дата - дата с которой начинают действовать процентные ставки по договору. По умолчанию текущая дата.
//
Процедура ЗаписатьПроцентнуюСтавкуКомисиию(Договор, НоваяСтавка, НоваяКомиссия, Период = Неопределено) Экспорт
	
	Если Период = Неопределено Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Договор.Установить(Договор);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() <= 1 Тогда
		ИсторияСтавок = НаборЗаписей.Выгрузить();
		Если ИсторияСтавок.Количество() = 0 Тогда
			СтрокаДанных = ИсторияСтавок.Добавить();
		Иначе
			СтрокаДанных = ИсторияСтавок[0];
		КонецЕсли;
		СтрокаДанных.Период		= Договор.Дата;
		СтрокаДанных.Договор	= Договор.Ссылка;
		СтрокаДанных.Процент	= НоваяСтавка;
		СтрокаДанных.Комиссия	= НоваяКомиссия;
		НаборЗаписей.Загрузить(ИсторияСтавок);
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Проверяет наличие истории изменений процентных ставок по договору
//
// Параметры:
//	- Договор - СправочникСсылка.ДоговорыКредитовИДепозитов - договор кредита, депозита, займа.
//
// Возвращаемое значание:
//	Булево - Истина, если были изменения процентных ставок по договору.
//
Функция ЕстьИстория(Договор) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Договор.Установить(Договор);
	НаборЗаписей.Прочитать();
	
	Возврат НаборЗаписей.Количество() > 1;
	
КонецФункции

// Получает ставку процентов и ставку комиссии по договору кредита, депозита, займа.
//
// Параметры:
//	- Договор - СправочникСсылка.ДоговорыКредитовИДепозитов - договор кредита, депозита, займа.
//	- НоваяСтавка - Число - будет помещена ставка процентов по договору.
//	- НоваяКомиссия - Число - будет помещен процент комиссии по договору.
//	- Период - Дата - дата с которой начинают действовать процентные ставки по договору. По умолчанию текущая дата.
//
Процедура ПрочитатьПроцентнуюСтавкуКомисиию(Договор, НоваяСтавка, НоваяКомиссия, Период = Неопределено) Экспорт
	
	НоваяСтавка = 0;
	НоваяКомиссия = 0;
	
	Если Период = Неопределено Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ИсторияСтавок = СрезПоследних(Период, Новый Структура("Договор",Договор));
	Если ИсторияСтавок.Количество() > 0 Тогда
		
		НоваяСтавка = ИсторияСтавок[0].Процент;
		НоваяКомиссия = ИсторияСтавок[0].Комиссия;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли