#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик обновления БЭД 1.3.6.42.
// Заполняет пустые реквизиты Организация в регистре сведений СостоянияСинхронизацииТорговыеПредложения.
// Выполняется при наличии в прикладном решении подсистемы ТорговыеПредложения.
//
// Параметры:
//  Параметры - Структура - параметры обработчика обновления.
//
Процедура ЗаполнениеОрганизаций(Параметры) Экспорт
	
	НаборЗаписей = РегистрыСведений.СостоянияСинхронизацииТорговыеПредложения.СоздатьНаборЗаписей();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СостоянияСинхронизацииТорговыеПредложения.ТорговоеПредложение КАК ТорговоеПредложение,
	|	СостоянияСинхронизацииТорговыеПредложения.ТорговоеПредложение.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.СостоянияСинхронизацииТорговыеПредложения КАК СостоянияСинхронизацииТорговыеПредложения
	|ГДЕ
	|	СостоянияСинхронизацииТорговыеПредложения.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НачатьТранзакцию();
	Попытка
		Пока Выборка.Следующий() Цикл
			НаборЗаписей.Отбор.ТорговоеПредложение.Установить(Выборка.ТорговоеПредложение);
			НаборЗаписей.Прочитать();
			НаборЗаписей[0].Организация = Выборка.Организация;
			НаборЗаписей.Записать(Истина);
		КонецЦикла;
		Параметры.Вставить("ОбработкаЗавершена", Истина);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru = 'Не удалось обработать запись регистра сведений: Состояние синхронизации (Торговые предложения) по причине: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		СобытиеЖурналаРегистрации = ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации();
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации, УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.СостоянияСинхронизацииТорговыеПредложения,, ТекстСообщения);
		Параметры.Вставить("ОбработкаЗавершена", Ложь);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли