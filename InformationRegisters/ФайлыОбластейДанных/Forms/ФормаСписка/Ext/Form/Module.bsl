

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок
    
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    
    СтандартнаяОбработка = Ложь;
    ВыгрузитьФайл(ВыбраннаяСтрока);
    
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
    
    Отказ = Истина;
    
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
    
    Отказ = Истина;
    Если Копирование Тогда 
        Возврат;
    КонецЕсли; 
    Оповещение = Новый ОписаниеОповещения("ОбработкаПомещенияФайла", ЭтотОбъект);
    НачатьПомещениеФайла(Оповещение);    
    
КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
    
    Отказ = Истина;
    ВыбраннаяСтрока = Элементы.Список.ТекущаяСтрока;
    Если ЗначениеЗаполнено(ВыбраннаяСтрока) Тогда
        Оповещение = Новый ОписаниеОповещения("ОбработкаОтветаНаВопросУдаленияФайла", ЭтотОбъект, ВыбраннаяСтрока);
        ИмяФайла = Элементы.Список.ТекущиеДанные.Имя;
        ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Удалить файл %1?'"), ИмяФайла); 
        ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);  
    КонецЕсли; 
    
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаОтветаНаВопросУдаленияФайла(КодВозврата, ВыбраннаяСтрока) Экспорт
	
    Если КодВозврата = КодВозвратаДиалога.Да Тогда
        УдалитьНаСервере(ВыбраннаяСтрока);
        Элементы.Список.Обновить();
    КонецЕсли; 
	
КонецПроцедуры
 
&НаКлиенте
Процедура ВыгрузитьФайл(ВыбраннаяСтрока)
	
    ОписаниеПередаваемогоФайла = ОписаниеПередаваемогоФайла(ВыбраннаяСтрока);
    Оповещение = Новый ОписаниеОповещения("НачатьПолучениеФайловЗавершение", ЭтотОбъект);
    ПолучаемыеФайлы = Новый Массив;
    ПолучаемыеФайлы.Добавить(ОписаниеПередаваемогоФайла);
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
    ДиалогВыбораФайла.Фильтр = НСтр("ru='Все файлы (*.*)|*.*'"); 

    НачатьПолучениеФайлов(Оповещение, ПолучаемыеФайлы, ДиалогВыбораФайла);
	
КонецПроцедуры
 
&НаСервереБезКонтекста
Процедура УдалитьНаСервере(ВыбраннаяСтрока)
    
    РегистрыСведений.ФайлыОбластейДанных.Удалить(ВыбраннаяСтрока.Идентификатор);
    
КонецПроцедуры

&НаСервере
Функция ОписаниеПередаваемогоФайла(ВыбраннаяСтрока)
    
    Идентификатор = ВыбраннаяСтрока.Идентификатор;
    ОписаниеФайла = РегистрыСведений.ФайлыОбластейДанных.ОписаниеФайла(Идентификатор);
    Если ЗначениеЗаполнено(ОписаниеФайла.ПолноеИмя) Тогда
        ДвоичныеДанные = РегистрыСведений.ФайлыОбластейДанных.ДвоичныеДанныеФайла(Идентификатор);
    Иначе
        ДвоичныеДанные = ОписаниеФайла.Данные;
    КонецЕсли; 
    АдресХранилища = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
    
    ОписаниеПередаваемогоФайла = Новый ОписаниеПередаваемогоФайла;
    ОписаниеПередаваемогоФайла.Имя = ОписаниеФайла.Имя;
    ОписаниеПередаваемогоФайла.Хранение = АдресХранилища;
    
    Возврат ОписаниеПередаваемогоФайла;
    
КонецФункции

&НаКлиенте 
Процедура НачатьПолучениеФайловЗавершение(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
    
    Если ЗначениеЗаполнено(ПолученныеФайлы) Тогда
        Оповещение = Новый ОписаниеОповещения("ПриНажатииОповещенияПолученияФайла", ЭтотОбъект, ПолученныеФайлы[0].Имя);
        ПоказатьОповещениеПользователя(НСтр("ru='Файл сохранен'"), Оповещение, ПолученныеФайлы[0].Имя, БиблиотекаКартинок.Информация32);     
    КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПомещенияФайла(ВыборВыполнен, АдресХранилища, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
    Если ВыборВыполнен Тогда
        ЗагрузитьФайл(АдресХранилища, ТолькоИмяФайла(ВыбранноеИмяФайла));
        Элементы.Список.Обновить();
    КонецЕсли; 	
    
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьФайл(АдресХранилища, ИмяФайла)
    
    Данные = ПолучитьИзВременногоХранилища(АдресХранилища);
	РегистрыСведений.ФайлыОбластейДанных.Загрузить(ИмяФайла, Данные); 
	
КонецПроцедуры
 
&НаКлиенте
Функция ТолькоИмяФайла(ВыбранноеИмяФайла)
	
	// Использовать критично на клиенте, т.к. ПолучитьРазделительПути() на сервере может быть другим.
	МассивПодстрок = СтрРазделить(ВыбранноеИмяФайла, ПолучитьРазделительПути(), Ложь);
	Возврат МассивПодстрок.Получить(МассивПодстрок.ВГраница());
	
КонецФункции

&НаКлиенте
Функция ПриНажатииОповещенияПолученияФайла(ПолноеИмяФайла) Экспорт
    
    ПустоеОповещение = Новый ОписаниеОповещения("ПустоеОповещение", ЭтотОбъект);
    НачатьЗапускПриложения(ПустоеОповещение, ПолноеИмяФайла,, Ложь);
	
КонецФункции

&НаКлиенте
Процедура ПустоеОповещение(КодВозврата, ДополнительныеПараметры) Экспорт
    
    Возврат;	
    
КонецПроцедуры
 
#КонецОбласти
