#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.ТипНоменклатурыНабор.Видимость  = ПолучитьФункциональнуюОпцию("ИспользоватьНаборы");
	Элементы.ТипНоменклатурыМногооборотнаяТара.Видимость   = ПолучитьФункциональнуюОпцию("ИспользоватьМногооборотнуюТару");
	Элементы.ТипНоменклатурыРабота.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВидовНоменклатуры")
												И Не ПолучитьФункциональнуюОпцию("БазоваяВерсия");
												
	СуффиксыИменЭлементов = Новый Массив;
	
	СуффиксыИменЭлементов.Добавить("Набор");
	СуффиксыИменЭлементов.Добавить("МногооборотнаяТара");
	СуффиксыИменЭлементов.Добавить("Работа");
	СуффиксыИменЭлементов.Добавить("Товар");
	СуффиксыИменЭлементов.Добавить("Услуга");
	
	Для Каждого Суффикс из СуффиксыИменЭлементов Цикл
		Элементы["ТипНоменклатуры" + Суффикс].Подсказка = Перечисления.ТипыНоменклатуры.ПодсказкаПоТипуНоменклатуры(
															ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры." + Суффикс));
	КонецЦикла;
		
	ТипНоменклатуры = Параметры.ТипНоменклатуры;
	
	Элементы.ОсобенностьУчетаАлкогольнаяПродукция.Видимость                 = ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции");
	Элементы.ОсобенностьУчетаСодержитДрагоценныеМатериалы.Видимость         = ПолучитьФункциональнуюОпцию("ИспользоватьУчетДрагоценныхМатериалов");
	Элементы.ОсобенностьУчетаПродукцияМаркируемаяДляГИСМ.Видимость          = ПолучитьФункциональнуюОпцию("ВестиУчетМаркировкиПродукцииВГИСМ");
	Элементы.ОсобенностьУчетаКиЗГИСМ.Видимость                              = ПолучитьФункциональнуюОпцию("ВестиУчетМаркировкиПродукцииВГИСМ");
	
	Элементы.ОсобенностьУчетаБезОсобенностейУчета.Видимость = Перечисления.ТипыНоменклатуры.ВключеныФООсобенностейУчетаНоменклатуры(); 
	
	СуффиксыИменЭлементов = Новый Массив;
	
	СуффиксыИменЭлементов.Добавить("БезОсобенностейУчета");
	СуффиксыИменЭлементов.Добавить("АлкогольнаяПродукция");
	СуффиксыИменЭлементов.Добавить("СодержитДрагоценныеМатериалы");
	СуффиксыИменЭлементов.Добавить("КиЗГИСМ");
	СуффиксыИменЭлементов.Добавить("ПродукцияМаркируемаяДляГИСМ");
		
	Для Каждого Суффикс из СуффиксыИменЭлементов Цикл
		Элементы["ОсобенностьУчета" + Суффикс].Подсказка = Перечисления.ОсобенностиУчетаНоменклатуры.ПодсказкаПоОсобенностиУчетаНоменклатуры(
															ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры." + Суффикс));
	КонецЦикла;

	Если ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар Тогда
		
		Если Не ЗначениеЗаполнено(Параметры.ОсобенностьУчета) Тогда
			ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.БезОсобенностейУчета;
		Иначе
			ОсобенностьУчета = Параметры.ОсобенностьУчета;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Результат = Новый Структура("ТипНоменклатуры,ОсобенностьУчета");
	Результат.ТипНоменклатуры = ТипНоменклатуры;	
	
	Если ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.БезОсобенностейУчета") Тогда
		Результат.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ПустаяСсылка");
	ИначеЕсли ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар") Тогда
		Результат.ОсобенностьУчета = ОсобенностьУчета;	
	Иначе
		Результат.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ПустаяСсылка");
	КонецЕсли;
	
	Закрыть(Результат);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипНоменклатурыПриИзменении(Элемент)
	Если ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар") Тогда
		ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.БезОсобенностейУчета");
	Иначе
		ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ПустаяСсылка");
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ОсобенностьУчетаПриИзменении(Элемент)
	ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар");
КонецПроцедуры

#КонецОбласти


