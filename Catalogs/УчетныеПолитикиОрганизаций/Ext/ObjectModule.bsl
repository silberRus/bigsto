
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не ФормироватьРезервОтпусковБУ Тогда
		МассивНепроверяемыхРеквизитов.Добавить("МетодНачисленияРезерваОтпусков");
	КонецЕсли;
	
		МассивНепроверяемыхРеквизитов.Добавить("НормативОтчисленийВРезервОтпусков");
		МассивНепроверяемыхРеквизитов.Добавить("ПредельнаяВеличинаОтчисленийВРезервОтпусков");
	
	
	Если Не ПрименяетсяЕНВД Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяРасходовЕНВД");
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовЕНВД");
	КонецЕсли;
	
	Если ВариантУчетаНДСПриИзмененииВидаДеятельности = Перечисления.ВариантыУчетаНДСПриИзмененииВидаДеятельностиНаНеоблагаемую.ВключатьВСтоимость 
			ИЛИ СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяРасходовНеНДС");
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовНеНДС");
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяРасходовЕНВД");
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовЕНВД");
	Иначе
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтатьяРасходовЕНВД, "КонтролироватьЗаполнениеАналитики") <> Истина Тогда
			МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовЕНВД");
		КонецЕсли;
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтатьяРасходовНеНДС, "КонтролироватьЗаполнениеАналитики") <> Истина Тогда
			МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовНеНДС");
		КонецЕсли;
	КонецЕсли;
	
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Запись настроек в регистр УчетнаяПолитикаОрганизаций
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УчетнаяПолитикаОрганизаций.Организация КАК Организация,
	|	УчетнаяПолитикаОрганизаций.Организация.ЮрФизЛицо КАК ЮрФизЛицо,
	|	УчетнаяПолитикаОрганизаций.Период КАК Период
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних КАК УчетнаяПолитикаОрганизаций
	|ГДЕ
	|	УчетнаяПолитикаОрганизаций.УчетнаяПолитика = &УчетнаяПолитика";
	
	Запрос.УстановитьПараметр("УчетнаяПолитика", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		
		Набор = РегистрыСведений.УчетнаяПолитикаОрганизаций.СоздатьНаборЗаписей();
		Набор.Отбор.Организация.Установить(Выборка.Организация);
		Набор.Отбор.Период.Установить(Выборка.Период);
		
		СтрокаНабора = Набор.Добавить();
		СтрокаНабора.Организация             = Выборка.Организация;
		СтрокаНабора.Период                  = Выборка.Период;
		СтрокаНабора.УчетнаяПолитика         = ЭтотОбъект.Ссылка;
		
		// прочие параметры учетной политики
		СтрокаНабора.ПрименяетсяПБУ18 = ЭтотОбъект.ПрименяетсяПБУ18;
		СтрокаНабора.ПлательщикНалогаНаПрибыль = (Не ЭтотОбъект.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная 
		                                             И Выборка.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо);
		СтрокаНабора.ПрименяетсяУСН = (ЭтотОбъект.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная);
		СтрокаНабора.ПрименяетсяУСНДоходыМинусРасходы = (ЭтотОбъект.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы);
		// ЕНВД
		СтрокаНабора.ПлательщикЕНВД = ЭтотОбъект.ПрименяетсяЕНВД;
		
		Набор.Записать(Истина);
	КонецЦикла;
	
	
КонецПроцедуры


#КонецОбласти


#КонецЕсли

