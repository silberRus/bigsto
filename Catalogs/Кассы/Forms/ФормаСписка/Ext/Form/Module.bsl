
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьОтборыСписка();
	
	Список.Параметры.УстановитьЗначениеПараметра("ОсновнаяКассоваяКнига", НСтр("ru = '<Основная кассовая книга организации>'"));
	
	ЗаполнитьСписокВыбораКассовыхКниг();
	
	ДоступноСоздание = ПравоДоступа("Изменение", Метаданные.Справочники.Кассы);
	Элементы.СписокСоздать.Видимость = ДоступноСоздание;
	Элементы.СписокСоздатьКассовуюКнигу.Видимость = ДоступноСоздание;
	Элементы.СписокСкопировать.Видимость = ДоступноСоздание;
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.Кассы);
	Элементы.СписокИзменитьВыделенные.Видимость = МожноРедактировать;
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборот.ПриСозданииНаСервере(ЭтаФорма, Элементы.ГруппаГлобальныеКоманды);
	// Конец ИнтеграцияС1СДокументооборотом

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Элементы.ОрганизацияОтбор.ТолькоПросмотр Тогда
		Настройки.Удалить("ОрганизацияОтбор");
	Иначе
		ОрганизацияОтбор = Настройки.Получить("ОрганизацияОтбор");
	КонецЕсли;
	
	Если Элементы.КассоваяКнигаОтбор.ТолькоПросмотр Тогда
		Настройки.Удалить("КассоваяКнигаОтбор");
	Иначе
		КассоваяКнигаОтбор = Настройки.Получить("КассоваяКнигаОтбор");
	КонецЕсли;
	
	ЗаполнитьСписокВыбораКассовыхКниг();
	
	УстановитьОтборДинамическогоСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_КассоваяКнига" Тогда
		ЗаполнитьСписокВыбораКассовыхКниг();
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияОтборПриИзменении(Элемент)
	
	ОрганизацияОтборПриИзмененииСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияОтборПриИзмененииСервер()
	
	ЗаполнитьСписокВыбораКассовыхКниг();
	
	УстановитьОтборДинамическогоСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура КассоваяКнигаОтборПриИзменении(Элемент)
	
	КассоваяКнигаОтборПриИзмененииСервер();
	
КонецПроцедуры

&НаСервере
Процедура КассоваяКнигаОтборПриИзмененииСервер()
	
	УстановитьОтборДинамическогоСписка();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовСписка

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбраннаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение( ,ВыбраннаяСтрока.Ключ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКассовуюКнигу(Команда)
	
	СтруктураОтбор = Новый Структура;
	СтруктураОтбор.Вставить("Владелец", ОрганизацияОтбор);
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("Основание", СтруктураОтбор);
	
	ОткрытьФорму("Справочник.КассовыеКниги.ФормаОбъекта", СтруктураПараметры, ЭтаФорма);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтаФорма, Элементы.Список);
	
КонецПроцедуры
//Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораКассовыхКниг()
	
	Справочники.КассовыеКниги.КассовыеКнигиОрганизации(ОрганизацияОтбор, Элементы.КассоваяКнигаОтбор.СписокВыбора);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборДинамическогоСписка()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "Организация", ОрганизацияОтбор, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(ОрганизацияОтбор));
		
	Если КассоваяКнигаОтбор = Справочники.КассовыеКниги.ПустаяСсылка() Тогда
		КассоваяКнигаЭлементОтбора = НСтр("ru = '<Основная кассовая книга организации>'");
	Иначе
		КассоваяКнигаЭлементОтбора = КассоваяКнигаОтбор;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "КассоваяКнига", КассоваяКнигаЭлементОтбора, ВидСравненияКомпоновкиДанных.Равно,, КассоваяКнигаОтбор <> Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборыСписка()
	
	Если Параметры.Свойство("Отбор") Тогда
		Если Параметры.Отбор.Свойство("Организация") Тогда
			ОрганизацияОтбор = Параметры.Отбор.Организация;
			Элементы.ОрганизацияОтбор.ТолькоПросмотр = Истина;
		КонецЕсли;
		Если Параметры.Отбор.Свойство("КассоваяКнига") Тогда
			КассоваяКнигаОтбор = Параметры.Отбор.КассоваяКнига;
			Элементы.КассоваяКнигаОтбор.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти