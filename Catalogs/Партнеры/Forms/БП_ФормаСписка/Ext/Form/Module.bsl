
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ПартнерыИКонтрагенты.ПартнерыФормаВыбораСпискаПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	//Обработчик подсистемы "Внешние обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	
	//Электронные документы
	Если ПравоДоступа("Изменение", Метаданные.Справочники.Партнеры) Тогда
		Элементы.СписокЗагрузитьРеквизиты.Видимость = Истина;
	КонецЕсли;
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.Партнеры);
	Элементы.СписокИзменитьВыделенные.Видимость = МожноРедактировать;
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать

	// МенюОтчеты
	//МенюОтчеты.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюОтчеты);
	// Конец МенюОтчеты

	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборот.ПриСозданииНаСервере(ЭтаФорма, Элементы.ГруппаГлобальныеКоманды);
	// Конец ИнтеграцияС1СДокументооборотом

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	УстановитьВидимостьКомандКонтекстныхОтчетов();
	
	ЗаполнитьТаблицыОтбора()
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицыОтбора()
	
	нвСтрока = Ассортимент.Добавить();
	нвСтрока.Ссылка = Перечисления.БП_Ассортимент.Грузовые;
	нвСтрока = Ассортимент.Добавить();
	нвСтрока.Ссылка = Перечисления.БП_Ассортимент.Легковые;
	
	нвСтрока = ВидыДеятельности.Добавить();
	нвСтрока.Ссылка = Перечисления.БП_ВидыДеятельности.АТП;
	нвСтрока = ВидыДеятельности.Добавить();
	нвСтрока.Ссылка = Перечисления.БП_ВидыДеятельности.Магазин;
	нвСтрока = ВидыДеятельности.Добавить();
	нвСтрока.Ссылка = Перечисления.БП_ВидыДеятельности.СТО;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БП_ТипыТС.Ссылка
	|ИЗ
	|	Справочник.БП_ТипыТС КАК БП_ТипыТС
	|ГДЕ
	|	НЕ БП_ТипыТС.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	БП_ТипыТС.Наименование";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		нвСтрока = ТипыТС.Добавить();
		нвСтрока.Ссылка = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БП_Страны.Ссылка
	|ИЗ
	|	Справочник.БП_Страны КАК БП_Страны
	|ГДЕ
	|	НЕ БП_Страны.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	БП_Страны.Наименование";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		нвСтрока = Страны.Добавить();
		нвСтрока.Ссылка = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БП_ВидыРабот.Ссылка
	|ИЗ
	|	Справочник.БП_ВидыРабот КАК БП_ВидыРабот
	|ГДЕ
	|	НЕ БП_ВидыРабот.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	БП_ВидыРабот.Наименование";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		нвСтрока = ВидыРабот.Добавить();
		нвСтрока.Ссылка = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БП_ТоварнаяМатрица.Ссылка
	|ИЗ
	|	Справочник.БП_ТоварнаяМатрица КАК БП_ТоварнаяМатрица
	|ГДЕ
	|	НЕ БП_ТоварнаяМатрица.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	БП_ТоварнаяМатрица.Наименование";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		нвСтрока = ТоварнаяМатрица.Добавить();
		нвСтрока.Ссылка = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ТребуетсяОбновлениеПанелиИнформации = Ложь;
	
	ПартнерыИКонтрагентыКлиент.ПартнерыФормаСпискаВыбораОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник, ТребуетсяОбновлениеПанелиИнформации);
	
	Если ТребуетсяОбновлениеПанелиИнформации Тогда
		ИгнорироватьСохранениеТекущейПозиции = Истина;
		ОбработатьАктивизациюСтрокиСписка();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	ПартнерыИКонтрагенты.ПередЗагрузкойДанныхИзНастроекНаСервере(ЭтаФорма, Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПартнерыИКонтрагентыКлиент.ПанельНавигацииУправлениеДоступностью(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		ПартнерыИКонтрагентыКлиент.ФормаСпискаВыбораПриЗакрытии(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТолькоМоиПриИзменении(Элемент)
	
	ИзменитьОтборСписок();
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	ВыполнитьПоиск(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеВыбораНажатие(Элемент, СтандартнаяОбработка)
	
	ПартнерыИКонтрагентыКлиент.ПартнерыФормаСпискаВыбораОснованиеВыбораНажатие(ЭтаФорма, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СегментПриИзменении(Элемент)
	
	ПартнерыИКонтрагентыКлиент.ПартнерыФормаСпискаВыбораСегментПриИзменении(ЭтаФорма, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	СпискиВыбораКлиентСервер.АвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипФильтраОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьФильтрПриИзменении(Элемент)
	
	ПартнерыИКонтрагентыКлиент.ПанельНавигацииУправлениеДоступностью(ЭтаФорма);
	ОбработатьАктивизациюСтрокиДинамическогоСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ДинамическийСписокФильтрыПриАктивизацииСтроки(Элемент)
	
	Если Элемент.Имя = "БизнесРегионы" И Элементы.СтраницыТипФильтра.ТекущаяСтраница <> Элементы.СтраницаБизнесРегионы Тогда
		Возврат;
	ИначеЕсли Элемент.Имя = "ГруппыДоступаПартнеров" И Элементы.СтраницыТипФильтра.ТекущаяСтраница <> Элементы.СтраницаГруппыДоступа Тогда
		Возврат;
	ИначеЕсли Элемент.Имя = "Менеджеры" И Элементы.СтраницыТипФильтра.ТекущаяСтраница <> Элементы.СтраницаМенеджеры Тогда
		Возврат;
	ИначеЕсли Элемент.Имя = "Свойства" И Элементы.СтраницыТипФильтра.ТекущаяСтраница <> Элементы.СтраницаСвойства Тогда
		Возврат;
	ИначеЕсли Элемент.Имя = "Категории" И Элементы.СтраницыТипФильтра.ТекущаяСтраница <> Элементы.СтраницаКатегории Тогда
		Возврат;
	КонецЕсли;
	
	Если НеОтрабатыватьАктивизациюПанелиНавигации Тогда
		НеОтрабатыватьАктивизациюПанелиНавигации = Ложь;
	Иначе
		Если Элемент.Имя = "БизнесРегионы" И ТекущееЗначениеФильтра = Элементы.БизнесРегионы.ТекущаяСтрока Тогда
			Возврат;
		ИначеЕсли Элемент.Имя = "ГруппыДоступаПартнеров" И ТекущееЗначениеФильтра = Элементы.ГруппыДоступаПартнеров.ТекущаяСтрока Тогда
			Возврат;
		ИначеЕсли Элемент.Имя = "Менеджеры" И ТекущееЗначениеФильтра = Элементы.Менеджеры.ТекущаяСтрока Тогда
			Возврат;
		ИначеЕсли Элемент.Имя = "Свойства" И ТекущееЗначениеФильтра = Свойства.НайтиПоИдентификатору(Элементы.Свойства.ТекущаяСтрока) Тогда
			Возврат;
		ИначеЕсли Элемент.Имя = "Категории" И ТекущееЗначениеФильтра = Категории.НайтиПоИдентификатору(Элементы.Категории.ТекущаяСтрока) Тогда
			Возврат;
		КонецЕсли;
		
		ПодключитьОбработчикОжидания("ОбработатьАктивизациюСтрокиДинамическогоСписка",0.2,Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентыПартнераНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПартнерыИКонтрагентыКлиент.КонтрагентыПартнераНажатие(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактныеЛицаПартнераНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПартнерыИКонтрагентыКлиент.КонтактныеЛицаПартнераНажатие(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоВсемПриИзменении(Элемент)
	
	ИзменитьОтборСписок(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипФильтраПриИзменении(Элемент)
	
	ТребуетсяЗаполнениеСтраницыСвойств = ЛОЖЬ;
	ПартнерыИКонтрагентыКлиент.ФильтрыПанельНавигацииТипФильтраПриИзменении(ЭтаФорма, Элемент, ТребуетсяЗаполнениеСтраницыСвойств);
	ИзменитьОтборСписок(Истина, ТребуетсяЗаполнениеСтраницыСвойств);
	Если ТребуетсяЗаполнениеСтраницыСвойств Тогда
		Для каждого СтрокаДерева Из Свойства.ПолучитьЭлементы() Цикл
			Элементы.Свойства.Развернуть(СтрокаДерева.ПолучитьИдентификатор());
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФильтрыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	ПартнерыИКонтрагентыКлиент.ФильтрыПанельНавигацииПроверкаПеретаскивания(ЭтаФорма, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле) 
	
КонецПроцедуры

&НаКлиенте
Процедура ФильтрыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	КоличествоЗаписанных = 0;
	ПартнерыИКонтрагентыКлиент.ФильтрыПанельНавигацииПеретаскивание(КоличествоЗаписанных, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле);
	
	Если КоличествоЗаписанных > 0 Тогда
		ИзменитьОтборСписок();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.Найти("ГоловнойКонтрагент") Тогда
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТекущиеДанные.ОбособленноеПодразделение И Не ЗначениеЗаполнено(ТекущиеДанные.ГоловнойКонтрагент) Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ПараметрыЗаполнения = Новый Структура;
			ПараметрыЗаполнения.Вставить("Контрагент", ТекущиеДанные.Контрагент);
			ПараметрыЗаполнения.Вставить("ИНН",        ТекущиеДанные.ИНН);
			ПараметрыЗаполнения.Вставить("Партнер",    ТекущиеДанные.Ссылка);
			ПараметрыЗаполнения.Вставить("ИспользоватьПартнеровКакКонтрагентов", Истина);
			
			Оповещение = Новый ОписаниеОповещения("ЗаполнитьГоловногоКонтрагентаЗавершение", ЭтотОбъект);
			ПартнерыИКонтрагентыКлиент.ЗаполнитьГоловногоКонтрагента(ЭтотОбъект, ПараметрыЗаполнения, Истина, Оповещение);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбработатьАктивизациюСтрокиСписка", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	ПартнерыИКонтрагентыКлиент.ПартнерыФормаСпискаВыбораСписокПередНачаломДобавления(ЭтаФорма, Элемент, Отказ, Копирование, Родитель, Группа);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьНового(Команда)
	
	ПартнерыИКонтрагентыКлиент.ПартнерыФормаСпискаВыбораСоздатьНового(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиПП(Команда)
	
	ВыполнитьПоиск(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьРеквизиты(Команда)
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.Фильтр = НСтр("ru='XML файл'; en='XML file'")+"(*.xml)|*.xml";
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите файл'");
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЗагрузитьРеквизитыВыборФайла",
		ЭтотОбъект);
	
	ДиалогОткрытияФайла.Показать(ОписаниеОповещения);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтаФорма, Элементы.Список);
	
КонецПроцедуры
//Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчетЗаполненностьСвойствПартнеров(Команда)
	ПараметрыОтчета = Новый Структура("СформироватьПриОткрытии", Истина);
	ОткрытьФорму("Отчет.ЗаполнениеСвойствПартнеров.Форма", ПараметрыОтчета, ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	ПартнерыИКонтрагенты.ПартнерыФормаВыбораСпискаУсловноеОформление(ЭтаФорма);
	ПартнерыИКонтрагенты.УстановитьОформлениеГоловногоКонтрагентаВСписке(ЭтаФорма);

КонецПроцедуры

#Область ПолнотекстовыйПоиск

&НаКлиенте
Процедура ПроверитьИндексПолнотекстовогоПоиска(Знач Оповещение)
	
	Если Не ИндексПолнотекстовогоПоискаАктуален И ИнформационнаяБазаФайловая Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ПроверитьИндексПолнотекстовогоПоискаЗавершение", ЭтотОбъект, Новый Структура("Оповещение", Оповещение)), 
			НСтр("ru='Индекс полнотекстового поиска неактуален. Обновить индекс?'"), РежимДиалогаВопрос.ДаНет);
        Возврат;
		
	КонецЕсли;
	
	ПроверитьИндексПолнотекстовогоПоискаФрагмент(Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьИндексПолнотекстовогоПоискаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Оповещение = ДополнительныеПараметры.Оповещение;
    
    
    Результат = РезультатВопроса; 
    
    Если Результат = КодВозвратаДиалога.Да Тогда
        ПодключитьОбработчикОжидания("ОбновитьИндексПолнотекстовогоПоиска",0.2,Истина);
        ВыполнитьОбработкуОповещения(Оповещение);
        Возврат;
    КонецЕсли;
    
    
    ПроверитьИндексПолнотекстовогоПоискаФрагмент(Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьИндексПолнотекстовогоПоискаФрагмент(Знач Оповещение)
    
    ВыполнитьПолнотекстовыйПоиск();
    
    ВыполнитьОбработкуОповещения(Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИндексПолнотекстовогоПоиска()
	
	Состояние(НСтр("ru = 'Идет обновление индекса полнотекстового поиска...'"));
	ОбновитьИндексПолнотекстовогоПоискаСервер();
	ИндексПолнотекстовогоПоискаАктуален = Истина;
	Состояние(НСтр("ru = 'Обновление индекса полнотекстового поиска завершено...'"));
	ВыполнитьПолнотекстовыйПоиск();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПолнотекстовыйПоиск()
	
	ТекстОшибки = НайтиПартнеровПолнотекстовыйПоиск();
	Если ТекстОшибки = Неопределено Тогда
		РасширенныйПоиск = Истина;
		ЗаполнитьСтрокуОснования();
	Иначе
		Если НЕ ТекстОшибки = НСтр("ru = 'Ничего не найдено'") Тогда
			ПоказатьОповещениеПользователя(ТекстОшибки);
		Иначе
			ПартнерыИКонтрагентыКлиент.ВостановитьОтображениеСпискаПослеПолнотекстовогоПоиска(ЭтаФорма);
			РасширенныйПоиск = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИндексПолнотекстовогоПоискаСервер()
	
	ПартнерыИКонтрагенты.ОбновитьИндексПолнотекстовогоПоиска();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтрокуОснования()
	
	Основание = Основания.НайтиСтроки(Новый Структура("Партнер", Элементы.Список.ТекущаяСтрока));
	Если Основание.Количество() = 0 Тогда
		ОснованиеВыбора = "";
	Иначе
		ОснованиеВыбора = Основание[0].Основание;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НайтиПартнеровПолнотекстовыйПоиск()

	Возврат ПартнерыИКонтрагенты.НайтиПартнеровПолнотекстовыйПоиск(ЭтаФорма)

КонецФункции

&НаКлиенте
Процедура ВыполнитьПоиск(Знач Оповещение)
	
	Если СтрокаПоиска <> "" Тогда
		
		ПроверитьИндексПолнотекстовогоПоиска(Новый ОписаниеОповещения("ВыполнитьПоискЗавершение", ЭтотОбъект, Новый Структура("Оповещение", Оповещение)));
        Возврат;
		
	Иначе
		ПартнерыИКонтрагентыКлиент.ВостановитьОтображениеСпискаПослеПолнотекстовогоПоиска(ЭтаФорма);
		РасширенныйПоиск = Ложь;
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
		                                                                   "ОтборПоПолнотекстовомуПоискуУстановлен",
		                                                                   Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
		                                                                   "ОтборПоПолнотекстовомуПоиску",
		                                                                   Неопределено);
		ОснованиеВыбора = "";
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПоискЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Оповещение = ДополнительныеПараметры.Оповещение;
    
    
    ЭтаФорма.ТекущийЭлемент = ?(НЕ РасширенныйПоиск, Элементы.СтрокаПоиска, Элементы.Список);
    
    
    ВыполнитьОбработкуОповещения(Оповещение);

КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура ОбработатьАктивизациюСтрокиСписка()
	
	Если Не ПартнерыИКонтрагентыКлиент.ПозиционированиеКорректно("Список",ЭтаФорма) Тогда
		
		Если ТекущийАктивныйПартнер <> ПредопределенноеЗначение("Справочник.Партнеры.ПустаяСсылка") Тогда
			ЗаполнитьПанельИнформацииПоДаннымПартнера(Неопределено);
		КонецЕсли;
		ОснованиеВыбора = "";
		
	Иначе
		
		Если ТекущийАктивныйПартнер <> Элементы.Список.ТекущаяСтрока ИЛИ ИгнорироватьСохранениеТекущейПозиции Тогда
			ЗаполнитьПанельИнформацииПоДаннымПартнера(Элементы.Список.ТекущаяСтрока);
		КонецЕсли;
		
		Если РасширенныйПоиск Тогда
			ПартнерыИКонтрагентыКлиент.ЗаполнитьСтрокуОснования(ЭтаФорма);
		Иначе
			ОснованиеВыбора = "";
		КонецЕсли;
		
	КонецЕсли;
	
	ИгнорироватьСохранениеТекущейПозиции = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПанельИнформацииПоДаннымПартнера(Партнер)
	
	ПартнерыИКонтрагенты.ЗаполнитьПанельИнформацииПоДаннымПартнера(ЭтаФорма, Партнер);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьАктивизациюСтрокиДинамическогоСписка()

	ИзменитьОтборСписок();

КонецПроцедуры

&НаСервере
Процедура ИзменитьОтборСписок(ПереформированиеПанелиНавигации = Ложь, ТребуетсяЗаполнениеСтраницыСвойств = Ложь)

	ПартнерыИКонтрагенты.ИзменитьОтборСписок(ЭтаФорма, ПереформированиеПанелиНавигации, ТребуетсяЗаполнениеСтраницыСвойств);

КонецПроцедуры

&НаСервере 
Процедура УстановитьВидимостьКомандКонтекстныхОтчетов()
	КоличествоДоступныхОтчетов = 0;
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ЗаполнениеСвойствПартнеров) Тогда
		Элементы.ОткрытьОтчетЗаполненностьСвойствПартнеров.Видимость = Истина;
		КоличествоДоступныхОтчетов = КоличествоДоступныхОтчетов + 1;
	Иначе
		Элементы.ОткрытьОтчетЗаполненностьСвойствПартнеров.Видимость = Ложь;
	КонецЕсли;
	
	Если КоличествоДоступныхОтчетов > 0 Тогда
		Элементы.СтраницыКонтекстныеОтчеты.ТекущаяСтраница = Элементы.СтраницаДоступныеОтчеты;
	Иначе
		Элементы.СтраницыКонтекстныеОтчеты.ТекущаяСтраница = Элементы.СтраницаНетДоступныхОтчетов;
	КонецЕсли;
КонецПроцедуры

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Элементы.Список);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

// МенюОтчеты
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуОтчет(Команда)
	
	//МенюОтчетыКлиент.ВыполнитьПодключаемуюКомандуОтчет(Команда, ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры
// Конец МенюОтчеты

#КонецОбласти

&НаКлиенте
Процедура ЗаполнитьГоловногоКонтрагентаЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьРеквизитыВыборФайла(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	ФайлЗагрузки = ВыбранныеФайлы[0];
	ДвоичныеДанные  = Новый ДвоичныеДанные(ФайлЗагрузки);
	СсылкаНаФайл = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	ОткрытьФорму("Справочник.Партнеры.Форма.ПомощникНового",
		Новый Структура("СсылкаНаФайл", СсылкаНаФайл),
		,
		УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОтборВидыДеятельностиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОткрытьФормуНастройкиТаблицыОтбора("ВидыДеятельности");
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТаблицуДанныхВХранилище(ИмяТаблицы)
	Возврат ПоместитьВоВременноеХранилище(ЭтаФорма[ИмяТаблицы].Выгрузить(),УникальныйИдентификатор);
КонецФункции


&НаКлиенте
Процедура ОтборАссортиментНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОткрытьФормуНастройкиТаблицыОтбора("Ассортимент");

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройкиТаблицыОтбора(ИмяТаблицы)
	
	Адрес = ПоместитьТаблицуДанныхВХранилище(ИмяТаблицы);
	ОписаниеОповещения = Новый ОписаниеОповещения("РезультатВыбораДанныхТаблицы",ЭтаФорма,Новый Структура("ИмяТаблицы",ИмяТаблицы));
	ОткрытьФорму("Справочник.Партнеры.Форма.БП_ФормаВыбораСпискаЗначений",Новый Структура("АдресВХранилище",Адрес),ЭтаФорма,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Процедура РезультатВыбораДанныхТаблицы(Результат,ДопПараметры)
	Если Результат <> Неопределено Тогда
		ТЧ = ПолучитьИзВременногоХранилища(Результат);
		ЭтаФорма[ДопПараметры.ИмяТаблицы].Загрузить(ТЧ);
		СтрокаОтбора = "";
		Для каждого текСтрока Из ТЧ Цикл
			Если текСтрока.Использование Тогда
				СтрокаОтбора = СтрокаОтбора + Лев(текСтрока.Ссылка,8)+"..;";
			КонецЕсли;
		КонецЦикла;
		ЭтаФорма["Отбор"+ДопПараметры.ИмяТаблицы] = СтрокаОтбора;
	КонецЕсли;
	УстановитьОтборСписка();
КонецПроцедуры


&НаКлиенте
Процедура ОтборТипыТСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОткрытьФормуНастройкиТаблицыОтбора("ТипыТС");
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСписка()
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПартнерыБП_ВидыДеятельности.Ссылка
		|ПОМЕСТИТЬ ВТ_ПоВидамДеятельности
		|ИЗ
		|	Справочник.Партнеры.БП_ВидыДеятельности КАК ПартнерыБП_ВидыДеятельности
		|ГДЕ
		|	ПартнерыБП_ВидыДеятельности.ВидДеятельности В(&ВидДеятельности)
		|	И ПартнерыБП_ВидыДеятельности.Использование = &Использование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартнерыБП_ВидыДеятельности.Ссылка
		|ПОМЕСТИТЬ ВТ_ПоАссортименту
		|ИЗ
		|	Справочник.Партнеры.БП_ВидыДеятельности КАК ПартнерыБП_ВидыДеятельности
		|ГДЕ
		|	ПартнерыБП_ВидыДеятельности.Ассортимент В(&Ассортимент)
		|	И (ПартнерыБП_ВидыДеятельности.ВидДеятельности В (&ВидДеятельности)
		|			ИЛИ &БезВидаДеятельности)
		|	И ПартнерыБП_ВидыДеятельности.Использование = &Использование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартнерыБП_ВидыДеятельности.Ссылка
		|ПОМЕСТИТЬ ВТ_ПоТипамТС
		|ИЗ
		|	Справочник.Партнеры.БП_ВидыДеятельности КАК ПартнерыБП_ВидыДеятельности
		|ГДЕ
		|	ПартнерыБП_ВидыДеятельности.ТипТС В(&ТипТС)
		|	И (ПартнерыБП_ВидыДеятельности.ВидДеятельности В (&ВидДеятельности)
		|			ИЛИ &БезВидаДеятельности)
		|	И ПартнерыБП_ВидыДеятельности.Использование = &Использование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартнерыБП_ВидыДеятельности.Ссылка
		|ПОМЕСТИТЬ ВТ_ПоСтранам
		|ИЗ
		|	Справочник.Партнеры.БП_ВидыДеятельности КАК ПартнерыБП_ВидыДеятельности
		|ГДЕ
		|	ПартнерыБП_ВидыДеятельности.Страна В(&Страна)
		|	И (ПартнерыБП_ВидыДеятельности.ВидДеятельности В (&ВидДеятельности)
		|			ИЛИ &БезВидаДеятельности)
		|	И ПартнерыБП_ВидыДеятельности.Использование = &Использование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартнерыБП_ТоварнаяМатрица.Ссылка
		|ПОМЕСТИТЬ ВТ_ПоТоварнаяМатрица
		|ИЗ
		|	Справочник.Партнеры.БП_ТоварнаяМатрица КАК ПартнерыБП_ТоварнаяМатрица
		|ГДЕ
		|	ПартнерыБП_ТоварнаяМатрица.Товар В(&Товар)
		|	И ПартнерыБП_ТоварнаяМатрица.Использование = &Использование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартнерыБП_ВидыРабот.Ссылка
		|ПОМЕСТИТЬ ВТ_ПоВидРабот
		|ИЗ
		|	Справочник.Партнеры.БП_ВидыРабот КАК ПартнерыБП_ВидыРабот
		|ГДЕ
		|	ПартнерыБП_ВидыРабот.ВидРабот В(&ВидРабот)
		|	И ПартнерыБП_ВидыРабот.Использование = &Использование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Партнеры.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_БезВидов
		|ИЗ
		|	Справочник.Партнеры КАК Партнеры
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры.БП_ВидыДеятельности КАК ПартнерыБП_ВидыДеятельности
		|		ПО ПартнерыБП_ВидыДеятельности.Ссылка = Партнеры.Ссылка
		|ГДЕ
		|	ПартнерыБП_ВидыДеятельности.Ссылка ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Партнеры.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Партнеры КАК Партнеры
		|ГДЕ
		|	ИСТИНА";
		//|	И Партнеры.Ссылка В
		//|			(ВЫБРАТЬ
		//|				ВТ_ПоТипамТС.Ссылка
		//|			ИЗ
		//|				ВТ_ПоТипамТС)
		//|	И Партнеры.Ссылка В
		//|			(ВЫБРАТЬ
		//|				ВТ_ПоАссортименту.Ссылка
		//|			ИЗ
		//|				ВТ_ПоАссортименту)
		//|	И Партнеры.Ссылка В
		//|			(ВЫБРАТЬ
		//|				ВТ_ПоВидамДеятельности.Ссылка
		//|			ИЗ
		//|				ВТ_ПоВидамДеятельности)";
	Массив = Новый Массив;
	Запрос.УстановитьПараметр("Использование", Истина);
	Запрос.УстановитьПараметр("ВидДеятельности", Массив);
	Запрос.УстановитьПараметр("Ассортимент", Массив);
	Запрос.УстановитьПараметр("ТипТС", Массив);
	Запрос.УстановитьПараметр("Страна", Массив);
	Запрос.УстановитьПараметр("Товар", Массив);
	Запрос.УстановитьПараметр("ВидРабот", Массив);
	Запрос.УстановитьПараметр("БезВидаДеятельности", Истина);
	ДополнитьЗапросДинамическимиОтборами(Запрос);
	РезультатЗапроса = Запрос.Выполнить();
	
	СписокПартнеров = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");

	ЭлементОтбора = Неопределено;
	Для каждого текСтрока Из Список.Отбор.Элементы Цикл
		Если текСтрока.Представление = "Отбор по доп.параметрам" Тогда
			ЭлементОтбора = текСтрока;
		КонецЕсли;
	КонецЦикла;
	
	Если ЭлементОтбора = Неопределено Тогда
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.Представление = "Отбор по доп.параметрам";
	КонецЕсли;
	ЭлементОтбора.ПравоеЗначение = СписокПартнеров;
//Элементы.Список.отоб
КонецПроцедуры

&НаСервере
Процедура ДополнитьЗапросДинамическимиОтборами(Запрос)
	
//БезВидов	
	Если ОтборБезВидов Тогда
		Запрос.Текст = Запрос.Текст +" И Партнеры.Ссылка В
		|			(ВЫБРАТЬ
		|				ВТ_БезВидов.Ссылка
		|			ИЗ
		|				ВТ_БезВидов)";
		Возврат;
	КонецЕсли;	
//Виды деятельности	
	Массив = Новый Массив;
	Для каждого текСтрока Из ВидыДеятельности Цикл
		Если текСтрока.Использование Тогда
			Массив.Добавить(текСтрока.Ссылка);
		КонецЕсли;	
	КонецЦикла;
	Если Массив.Количество() <> 0 Тогда
		Запрос.Текст = Запрос.Текст +" И Партнеры.Ссылка В
		|			(ВЫБРАТЬ
		|				ВТ_ПоВидамДеятельности.Ссылка
		|			ИЗ
		|				ВТ_ПоВидамДеятельности)";
		Запрос.УстановитьПараметр("БезВидаДеятельности", Ложь);
	Иначе 
		Запрос.УстановитьПараметр("БезВидаДеятельности", Истина);
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидДеятельности", Массив);
//Ассортимент	
	Массив = Новый Массив;
	Для каждого текСтрока Из Ассортимент Цикл
		Если текСтрока.Использование Тогда
			Массив.Добавить(текСтрока.Ссылка);
		КонецЕсли;	
	КонецЦикла;
	Если Массив.Количество() <> 0 Тогда
		Запрос.Текст = Запрос.Текст +"  И Партнеры.Ссылка В
		|			(ВЫБРАТЬ
		|				ВТ_ПоАссортименту.Ссылка
		|			ИЗ
		|				ВТ_ПоАссортименту)";
	КонецЕсли;
	Запрос.УстановитьПараметр("Ассортимент", Массив);
//ТипТС	
	Массив = Новый Массив;
	Для каждого текСтрока Из ТипыТС Цикл
		Если текСтрока.Использование Тогда
			Массив.Добавить(текСтрока.Ссылка);
		КонецЕсли;	
	КонецЦикла;
	Если Массив.Количество() <> 0 Тогда
		Запрос.Текст = Запрос.Текст +" И Партнеры.Ссылка В
		|			(ВЫБРАТЬ
		|				ВТ_ПоТипамТС.Ссылка
		|			ИЗ
		|				ВТ_ПоТипамТС)";
	КонецЕсли;
	Запрос.УстановитьПараметр("ТипТС", Массив);
//Страна	
	Массив = Новый Массив;
	Для каждого текСтрока Из Страны Цикл
		Если текСтрока.Использование Тогда
			Массив.Добавить(текСтрока.Ссылка);
		КонецЕсли;	
	КонецЦикла;
	Если Массив.Количество() <> 0 Тогда
		Запрос.Текст = Запрос.Текст +" И Партнеры.Ссылка В
		|			(ВЫБРАТЬ
		|				ВТ_ПоСтранам.Ссылка
		|			ИЗ
		|				ВТ_ПоСтранам)";
	КонецЕсли;
	Запрос.УстановитьПараметр("Страна", Массив);
//Товары	
	Массив = Новый Массив;
	Для каждого текСтрока Из ТоварнаяМатрица Цикл
		Если текСтрока.Использование Тогда
			Массив.Добавить(текСтрока.Ссылка);
		КонецЕсли;	
	КонецЦикла;
	Если Массив.Количество() <> 0 Тогда
		Запрос.Текст = Запрос.Текст +" И Партнеры.Ссылка В
		|			(ВЫБРАТЬ
		|				ВТ_ПоТоварнаяМатрица.Ссылка
		|			ИЗ
		|				ВТ_ПоТоварнаяМатрица)";
	КонецЕсли;
	Запрос.УстановитьПараметр("Товар", Массив);
//ВидыРабот	
	Массив = Новый Массив;
	Для каждого текСтрока Из ВидыРабот Цикл
		Если текСтрока.Использование Тогда
			Массив.Добавить(текСтрока.Ссылка);
		КонецЕсли;	
	КонецЦикла;
	Если Массив.Количество() <> 0 Тогда
		Запрос.Текст = Запрос.Текст +" И Партнеры.Ссылка В
		|			(ВЫБРАТЬ
		|				ВТ_ПоВидРабот.Ссылка
		|			ИЗ
		|				ВТ_ПоВидРабот)";
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидРабот", Массив);
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтраныНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОткрытьФормуНастройкиТаблицыОтбора("Страны");
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидыРаботНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОткрытьФормуНастройкиТаблицыОтбора("ВидыРабот");
КонецПроцедуры

&НаКлиенте
Процедура ОтборТоварнаяМатрицаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОткрытьФормуНастройкиТаблицыОтбора("ТоварнаяМатрица");
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТаблицу(ИмяТаблицы)
	Для каждого текСтрока Из ЭтаФорма[ИмяТаблицы] Цикл
		текСтрока.Использование = Ложь;
		ЭтаФорма["Отбор"+ИмяТаблицы] = "";
	КонецЦикла;	
	УстановитьОтборСписка();	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидыДеятельностиОчистка(Элемент, СтандартнаяОбработка)
	ОчиститьТаблицу("ВидыДеятельности");
КонецПроцедуры

&НаКлиенте
Процедура ОтборАссортиментОчистка(Элемент, СтандартнаяОбработка)
	ОчиститьТаблицу("Ассортимент");
КонецПроцедуры

&НаКлиенте
Процедура ОтборТипыТСОчистка(Элемент, СтандартнаяОбработка)
	ОчиститьТаблицу("ТипыТС");
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтраныОчистка(Элемент, СтандартнаяОбработка)
	ОчиститьТаблицу("Страны");
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидыРаботОчистка(Элемент, СтандартнаяОбработка)
	ОчиститьТаблицу("ВидыРабот");
КонецПроцедуры

&НаКлиенте
Процедура ОтборТоварнаяМатрицаОчистка(Элемент, СтандартнаяОбработка)
	ОчиститьТаблицу("ТоварнаяМатрица");
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКарточкуКлиента(Команда)
	текСтрока = Элементы.Список.ТекущиеДанные;
	Если текСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбновитьСписокПослеКарточкиКлиента",ЭтаФорма);
	ОткрытьФорму("Справочник.Партнеры.Форма.БП_ФормаНастройкиВидовДеятельности",Новый Структура("СсылкаНаОбъект",текСтрока.Ссылка),ЭтаФорма,,,,ОписаниеОповещения);
																				
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокПослеКарточкиКлиента(Результат,ДопПараметры) 	Экспорт
	ОповеститьОбИзменении(Тип("СправочникСсылка.Партнеры"));
	УстановитьОтборСписка();
КонецПроцедуры

&НаКлиенте
Процедура ОтборБезВидовПриИзменении(Элемент)
	
	Элементы.ОтборАссортимент.ТолькоПросмотр = ОтборБезВидов;
	Элементы.ОтборВидыДеятельности.ТолькоПросмотр = ОтборБезВидов;
	Элементы.ОтборВидыРабот.ТолькоПросмотр = ОтборБезВидов;
	
	Элементы.ОтборСтраны.ТолькоПросмотр = ОтборБезВидов;
	Элементы.ОтборТипыТС.ТолькоПросмотр = ОтборБезВидов;
	Элементы.ОтборТоварнаяМатрица.ТолькоПросмотр = ОтборБезВидов;
	
	УстановитьОтборСписка();
КонецПроцедуры
