
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	НесколькоВидовНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВидовНоменклатуры");
	Элементы.ДекорацияРасшифровкаОтбора.Видимость = Ложь;

	Если ЗначениеЗаполнено(Параметры.ТекущийВид) Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																				"Ссылка",
																				Параметры.ТекущийВид,
																				ВидСравненияКомпоновкиДанных.НеРавно,
																				,
																				Истина);
	ИначеЕсли Параметры.Свойство("Номенклатура") Тогда
		
		НастроитьСписок(Параметры.Номенклатура);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НастроитьСписок(Номенклатура = Неопределено, ОтключениеОтбора = Ложь)
	
	УсловияВыбораНовогоВидаНоменклатуры = Справочники.Номенклатура.УсловияВыбораНовогоВидаНоменклатуры();
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВидНоменклатурыПереопределяемый.Ссылка,
	|	ВидНоменклатурыПереопределяемый.ПометкаУдаления,
	|	ВидНоменклатурыПереопределяемый.Родитель,
	|	ВидНоменклатурыПереопределяемый.ЭтоГруппа,
	|	ВидНоменклатурыПереопределяемый.Наименование,
	|	ВидНоменклатурыПереопределяемый.АлкогольнаяПродукция,
	|	ВидНоменклатурыПереопределяемый.ВариантОказанияУслуг,
	|	ВидНоменклатурыПереопределяемый.ВариантОформленияПродажи,
	|	ВидНоменклатурыПереопределяемый.ВариантПредставленияНабораВПечатныхФормах,
	|	ВидНоменклатурыПереопределяемый.ВариантРасчетаЦеныНабора,
	|	ВидНоменклатурыПереопределяемый.ВестиУчетПоГТД,
	|	ВидНоменклатурыПереопределяемый.ВестиУчетСертификатовНоменклатуры,
	|	ВидНоменклатурыПереопределяемый.ВидАлкогольнойПродукции,
	|	ВидНоменклатурыПереопределяемый.ВладелецСерий,
	|	ВидНоменклатурыПереопределяемый.ВладелецТоварныхКатегорий,
	|	ВидНоменклатурыПереопределяемый.ВладелецХарактеристик,
	|	ВидНоменклатурыПереопределяемый.ГруппаАналитическогоУчета,
	|	ВидНоменклатурыПереопределяемый.ГруппаДоступа,
	|	ВидНоменклатурыПереопределяемый.ГруппаФинансовогоУчета,
	|	ВидНоменклатурыПереопределяемый.ЕдиницаДляОтчетов,
	|	ВидНоменклатурыПереопределяемый.ЕдиницаИзмерения,
	|	ВидНоменклатурыПереопределяемый.ЗапретРедактированияНаименованияДляПечатиНоменклатуры,
	|	ВидНоменклатурыПереопределяемый.ЗапретРедактированияНаименованияДляПечатиХарактеристики,
	|	ВидНоменклатурыПереопределяемый.ЗапретРедактированияРабочегоНаименованияНоменклатуры,
	|	ВидНоменклатурыПереопределяемый.ЗапретРедактированияРабочегоНаименованияХарактеристики,
	|	ВидНоменклатурыПереопределяемый.ИмпортнаяАлкогольнаяПродукция,
	|	ВидНоменклатурыПереопределяемый.ИспользованиеХарактеристик,
	|	ВидНоменклатурыПереопределяемый.ИспользоватьИндивидуальноеНаименованиеПриПечати,
	|	ВидНоменклатурыПереопределяемый.ИспользоватьКоличествоСерии,
	|	ВидНоменклатурыПереопределяемый.ИспользоватьНомерСерии,
	|	ВидНоменклатурыПереопределяемый.ИспользоватьСерии,
	|	ВидНоменклатурыПереопределяемый.ИспользоватьСрокГодностиСерии,
	|	ВидНоменклатурыПереопределяемый.ИспользоватьУпаковки,
	|	ВидНоменклатурыПереопределяемый.ИспользоватьХарактеристики,
	|	ВидНоменклатурыПереопределяемый.КонтролироватьДублиНоменклатуры,
	|	ВидНоменклатурыПереопределяемый.КонтролироватьДублиХарактеристик,
	|	ВидНоменклатурыПереопределяемый.КоэффициентЕдиницыДляОтчетов,
	|	ВидНоменклатурыПереопределяемый.НаборСвойств,
	|	ВидНоменклатурыПереопределяемый.НаборСвойствСерий,
	|	ВидНоменклатурыПереопределяемый.НаборСвойствХарактеристик,
	|	ВидНоменклатурыПереопределяемый.НаборУпаковок,
	|	ВидНоменклатурыПереопределяемый.НаименованиеДляПечати,
	|	ВидНоменклатурыПереопределяемый.НастройкаИспользованияСерий,
	|	ВидНоменклатурыПереопределяемый.НоменклатураМногооборотнаяТара,
	|	ВидНоменклатурыПереопределяемый.ОбособленнаяЗакупкаПродажа,
	|	ВидНоменклатурыПереопределяемый.НастройкиСерийБерутсяИзДругогоВидаНоменклатуры,
	|	ВидНоменклатурыПереопределяемый.Описание,
	|	ВидНоменклатурыПереопределяемый.ПодакцизныйТовар,
	|	ВидНоменклатурыПереопределяемый.ПоставляетсяВМногооборотнойТаре,
	|	ВидНоменклатурыПереопределяемый.СезоннаяГруппа,
	|	ВидНоменклатурыПереопределяемый.ПолитикаУчетаСерий,
	|	ВидНоменклатурыПереопределяемый.СкладскаяГруппа,
	|	ВидНоменклатурыПереопределяемый.СодержитДрагоценныеМатериалы,
	|	ВидНоменклатурыПереопределяемый.ТипНоменклатуры,
	|	ВидНоменклатурыПереопределяемый.ТочностьУказанияСрокаГодностиСерии,
	|	ВидНоменклатурыПереопределяемый.ХарактеристикаМногооборотнаяТара,
	|	ВидНоменклатурыПереопределяемый.ТоварныеКатегорииОбщиеСДругимВидомНоменклатуры,
	|	ВидНоменклатурыПереопределяемый.ШаблонНаименованияДляПечатиНоменклатуры,
	|	ВидНоменклатурыПереопределяемый.ШаблонНаименованияДляПечатиХарактеристики,
	|	ВидНоменклатурыПереопределяемый.ШаблонРабочегоНаименованияНоменклатуры,
	|	ВидНоменклатурыПереопределяемый.ШаблонРабочегоНаименованияСерии,
	|	ВидНоменклатурыПереопределяемый.ШаблонРабочегоНаименованияХарактеристики,
	|	ВидНоменклатурыПереопределяемый.ШаблонЦенника,
	|	ВидНоменклатурыПереопределяемый.ШаблонЭтикетки,
	|	ВидНоменклатурыПереопределяемый.ШаблонЭтикеткиСерии,
	|	ВидНоменклатурыПереопределяемый.КодОКВЭД,
	|	ВидНоменклатурыПереопределяемый.КодТНВЭД,
	|	ВидНоменклатурыПереопределяемый.Представление
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК ВидНоменклатурыПереопределяемый";
	
	Если Не ЗначениеЗаполнено(Номенклатура)
		Или ОтключениеОтбора Тогда
		Список.ТекстЗапроса = ТекстЗапроса;
		Элементы.ДекорацияРасшифровкаОтбора.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|ГДЕ
	|	ВидНоменклатурыПереопределяемый.ЭтоГруппа
	|	ИЛИ ИСТИНА ";
	
	Для Каждого КлючЗначение Из УсловияВыбораНовогоВидаНоменклатуры Цикл
		
		ТекстПараметра = СтрЗаменить(КлючЗначение.Значение, "НовыйВидНоменклатуры", "ВидНоменклатурыПереопределяемый");
		ТекстЗапроса = ТекстЗапроса + " И НЕ " + ТекстПараметра;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	ПараметрыЗапроса = Запрос.НайтиПараметры();

	ИменаРеквизитов = Новый Структура;
	Для Каждого Параметр из ПараметрыЗапроса Цикл
		ИменаРеквизитов.Вставить(Параметр.Имя, "ВидНоменклатуры." + Параметр.Имя);
	КонецЦикла;
	
	СтарыеРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, ИменаРеквизитов);
	
	Список.ТекстЗапроса = ТекстЗапроса;
	
	Для Каждого Параметр из ПараметрыЗапроса Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
																			Параметр.Имя,
																			СтарыеРеквизиты[Параметр.Имя],
																			Истина);
	КонецЦикла;
	
	Элементы.Список.Обновить();
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Отобраны виды номенклатуры, выбор которых в номенклатуре, которая ранее уже использовалась в программе, не приведет к проблемам в учете.'")));
	
	Если Пользователи.РолиДоступны("РедактированиеРеквизитовОбъектов") Тогда
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Отключить отбор'"),
								,
								,
								,
								"ОтключитьОтбор"));
	КонецЕсли;
	Элементы.ДекорацияРасшифровкаОтбора.Видимость = Истина;
	Элементы.ДекорацияРасшифровкаОтбора.Заголовок = Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРасшифровкаОтбораОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылка = "ОтключитьОтбор" Тогда 
		НастроитьСписок(,Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

