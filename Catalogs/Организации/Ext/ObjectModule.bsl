#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) И ДанныеЗаполнения.Свойство("ВидОрганизации") Тогда
		Если ДанныеЗаполнения.ВидОрганизации = "ЮридическоеЛицо" Тогда
			ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо");
			ОбособленноеПодразделение = Ложь;
		ИначеЕсли ДанныеЗаполнения.ВидОрганизации = "ИндивидуальныйПредприниматель" Тогда 
			ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель");
			ОбособленноеПодразделение = Ложь;
			ИзменитьИНН(ИндивидуальныйПредприниматель);
		ИначеЕсли ДанныеЗаполнения.ВидОрганизации = "ОбособленноеПодразделение" Тогда
			ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо");
			ОбособленноеПодразделение = Истина;
			ИзменитьИНН(ГоловнаяОрганизация);
		Иначе ВызватьИсключение Нстр("ru = 'Создание организации невозможно. На форму переданы неверные параметры.
											|Обратитесь к администратору.'");
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЮридическоеФизическоеЛицо) Тогда
		Если ЗначениеЗаполнено(ДанныеЗаполнения)
		 И ДанныеЗаполнения.Свойство("ВидОрганизации")
		 И ДанныеЗаполнения.ВидОрганизации = "ИндивидуальныйПредприниматель" Тогда
			ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		Иначе
			ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Перем ТекстСообщения;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ЗначениеЗаполнено(ОГРН)
	 И Не РегламентированныеДанныеКлиентСервер.ОГРНСоответствуетТребованиям(ОГРН, ПартнерыИКонтрагенты.ЭтоЮрЛицо(ЮрФизЛицо), ТекстСообщения) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОГРН",, Отказ);
	КонецЕсли;

	Если Не ОбособленноеПодразделение Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГоловнаяОрганизация");
	КонецЕсли;
	
	Если Не КрупнейшийНалогоплательщик Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КодНалоговогоОрганаПолучателя");
	КонецЕсли;
	
	Если Не ЭтоНовый()
		И ОбособленноеПодразделение
		И ЕстьОбособленныеПодразделенияОрганизации() Тогда
		
		ТекстСообщения = НСтр("ru = 'Организация не может быть обособленным подразделением, потому что она уже выбрана в качестве головной для других организаций.'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,, Отказ);
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ПартнерыИКонтрагенты.ЭтоЮрЛицо(ЮрФизЛицо) = Ложь Тогда

		ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		КПП = "";
		
	Иначе
		
		ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
		СвидетельствоДатаВыдачи   = Дата(1,1,1);
		СвидетельствоСерияНомер   = "";
		
		Если ПустаяСтрока(ИНН) И НЕ ПустаяСтрока(КПП) Тогда
			КПП = "";
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ОбособленноеПодразделение Тогда
		
		Если ЭтоНовый() Тогда
			Если ПолучитьСсылкуНового().Пустая() Тогда
				УстановитьСсылкуНового(Справочники.Организации.ПолучитьСсылку());
				ГоловнаяОрганизация = ПолучитьСсылкуНового();
			КонецЕсли;
		ИначеЕсли ГоловнаяОрганизация <> Ссылка Тогда
			ГоловнаяОрганизация = Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	// Обработка смены пометки удаления.
	Если Не ЭтоНовый() И Не ЭтоГруппа Тогда

		Если ПометкаУдаления <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления") Тогда
			Справочники.КлючиАналитикиУчетаПоПартнерам.УстановитьПометкуУдаления(Новый Структура("Организация", Ссылка), ПометкаУдаления);
		КонецЕсли;

	КонецЕсли;
	
	Если ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		КодНалоговогоОргана = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РегистрацияВНалоговомОргане, "Код");
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		УстановитьПривилегированныйРежим(Истина);
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Организации.Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.Ссылка <> &Ссылка
		|	И Организации.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)");
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Если Не Запрос.Выполнить().Пустой() Тогда
			Если Не ПолучитьФункциональнуюОпцию("БазоваяВерсия") Тогда
				Константы.ИспользоватьНесколькоОрганизаций.Установить(Истина);
				Константы.ИспользоватьНесколькоКасс.Установить(Истина);
				Константы.ИспользоватьНесколькоРасчетныхСчетов.Установить(Истина);
				Константы.ИспользоватьНесколькоРасчетныхСчетовКасс.Установить(Истина);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Базовая версия может вести учет только по одной организации'"),,,, Отказ);
			КонецЕсли;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СинхронизироватьНастройкиОбособленныхПодразделений(Отказ);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ФайлЛоготип            = Неопределено;
	ФайлФаксимильнаяПечать = Неопределено;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СинхронизироватьНастройкиОбособленныхПодразделений(Отказ)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеПодразделенияВыделенныеНаБаланс") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Организации.Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ОбособленноеПодразделение
		|	И Организации.ГоловнаяОрганизация = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
		Исключение
			
			ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        Выборка.Ссылка);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			
		КонецПопытки;
			
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.ИНН = ИНН;
		Объект.ЮрФизЛицо = ЮрФизЛицо;
		
		Попытка
			
			Объект.Записать();
			
		Исключение
			
			ТекстОшибки = НСтр("ru='Не удалось записать %Элемент%. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        Выборка.Ссылка);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			
		КонецПопытки
		
	КонецЦикла;
	
	Если ОбособленноеПодразделение Тогда
		
		// Синхронизация настроек учетной политики
	
		НастройкиГоловнойОрганизации = РегистрыСведений.УчетнаяПолитикаОрганизаций.СоздатьНаборЗаписей();
		НастройкиГоловнойОрганизации.Отбор.Организация.Установить(ГоловнаяОрганизация);
		НастройкиГоловнойОрганизации.Прочитать();
		
		НастройкиОбособленногоПодразделения = НастройкиГоловнойОрганизации;
		НастройкиОбособленногоПодразделения.Отбор.Организация.Установить(Ссылка);
		Для Каждого НастройкаОбособленногоПодразделения Из НастройкиОбособленногоПодразделения Цикл
			НастройкаОбособленногоПодразделения.Организация = Ссылка;
		КонецЦикла;
		
		Попытка
			
			НастройкиОбособленногоПодразделения.ДополнительныеСвойства.Вставить("СинхронизацияНастроек", Истина);
			НастройкиОбособленногоПодразделения.Записать();
			
		Исключение
			
			ТекстОшибки = НСтр("ru='Не удалось изменить настройки учета НДС. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			
			Возврат;
		КонецПопытки;
		
		// Синхронизация настроек ЕНВД
		
		НастройкиГоловнойОрганизации = РегистрыСведений.ПримененияЕНВД.СоздатьНаборЗаписей();
		НастройкиГоловнойОрганизации.Отбор.Организация.Установить(ГоловнаяОрганизация);
		НастройкиГоловнойОрганизации.Прочитать();
		
		НастройкиОбособленногоПодразделения = НастройкиГоловнойОрганизации;
		НастройкиОбособленногоПодразделения.Отбор.Организация.Установить(Ссылка);
		Для Каждого НастройкаОбособленногоПодразделения Из НастройкиОбособленногоПодразделения Цикл
			НастройкаОбособленногоПодразделения.Организация = Ссылка;
		КонецЦикла;
		
		Попытка
			
			НастройкиОбособленногоПодразделения.ДополнительныеСвойства.Вставить("СинхронизацияНастроек", Истина);
			НастройкиОбособленногоПодразделения.Записать();
			
		Исключение
			
			ТекстОшибки = НСтр("ru='Не удалось изменить настройки применения ЕНВД. %ОписаниеОшибки%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
			
			Возврат;
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЕстьОбособленныеПодразделенияОрганизации()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ГоловнаяОрганизация = &Ссылка
	|	И Организации.Ссылка <> Организации.ГоловнаяОрганизация";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ИзменитьИНН(СсылкаНаОбъект)
	ИНН = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ИНН");
КонецФункции

#КонецОбласти

#КонецЕсли
