#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СтруктураСозданияШтрихкодаУпаковки() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ТипУпаковки");
	Результат.Вставить("ТипШтрихкода");
	Результат.Вставить("ЗначениеШтрихкода", "");
	Результат.Вставить("ХешСумма", "");
	Результат.Вставить("Номенклатура");
	Результат.Вставить("Характеристика");
	Результат.Вставить("Упаковка");
	Результат.Вставить("Серия");
	Результат.Вставить("Количество", 0);
	Результат.Вставить("ДатаУпаковки", '00010101');
	Результат.Вставить("Ответственный");
	
	Возврат Результат;
	
КонецФункции

Функция СтруктураЗаписиСтатусаУпаковки() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ОрганизацияЕГАИС",     Неопределено);
	Результат.Вставить("АкцизнаяМарка",        Неопределено);
	Результат.Вставить("Статус",               Неопределено);
	Результат.Вставить("Справка2",             Неопределено);
	Результат.Вставить("АлкогольнаяПродукция", Неопределено);
	Результат.Вставить("Основание",            Неопределено);
	
	Возврат Результат;
	
КонецФункции

Функция НайтиШтрихкодыУпаковокПоСпискуШтрихкодов(ЗначенияШтрихкодов, ОрганизацияЕГАИС, ТипыУпаковок = Неопределено) Экспорт
	
	Результат = Новый Соответствие;
	
	Если ТипЗнч(ЗначенияШтрихкодов) = Тип("Массив") Тогда
		СписокЗначенийШтрихкодов = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ЗначенияШтрихкодов);
	Иначе
		СписокЗначенийШтрихкодов = Новый Массив;
		СписокЗначенийШтрихкодов.Добавить(ЗначенияШтрихкодов);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокЗначенийШтрихкодов", СписокЗначенийШтрихкодов);
	Запрос.УстановитьПараметр("ОрганизацияЕГАИС",         ОрганизацияЕГАИС);
	Запрос.УстановитьПараметр("УсловиеПоТипамУпаковок",   Истина);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ШтрихкодыУпаковокТоваров.Ссылка            КАК Ссылка,
	|	ШтрихкодыУпаковокТоваров.ДатаУпаковки      КАК ДатаУпаковки,
	|	ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода КАК ЗначениеШтрихкода,
	|	ШтрихкодыУпаковокТоваров.ТипУпаковки       КАК ТипУпаковки,
	|	ШтрихкодыУпаковокТоваров.ТипШтрихкода      КАК ТипШтрихкода,
	|	ШтрихкодыУпаковокТоваров.Номенклатура      КАК Номенклатура,
	|	ШтрихкодыУпаковокТоваров.Характеристика    КАК Характеристика,
	|	ШтрихкодыУпаковокТоваров.Упаковка          КАК Упаковка,
	|	ШтрихкодыУпаковокТоваров.Серия             КАК Серия
	|ПОМЕСТИТЬ ШтрихкодыУпаковокТоваров
	|ИЗ
	|	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	|ГДЕ
	|	ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода В(&СписокЗначенийШтрихкодов)
	|	И &УсловиеПоТипамУпаковок
	|;
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ШтрихкодыУпаковокТоваров.ДатаУпаковки) КАК ДатаУпаковки,
	|	ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода КАК ЗначениеШтрихкода
	|ПОМЕСТИТЬ МаксимальныеДатыУпаковки
	|ИЗ
	|	ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	|ГДЕ
	|	ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода В(&СписокЗначенийШтрихкодов)
	|СГРУППИРОВАТЬ ПО
	|	ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода
	|;
	|
	|ВЫБРАТЬ
	|	ШтрихкодыУпаковокТоваров.Ссылка            КАК Ссылка,
	|	ШтрихкодыУпаковокТоваров.ДатаУпаковки      КАК ДатаУпаковки,
	|	ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода КАК ЗначениеШтрихкода,
	|	ШтрихкодыУпаковокТоваров.ТипУпаковки       КАК ТипУпаковки,
	|	ШтрихкодыУпаковокТоваров.ТипШтрихкода      КАК ТипШтрихкода,
	|	ШтрихкодыУпаковокТоваров.Номенклатура      КАК Номенклатура,
	|	ШтрихкодыУпаковокТоваров.Характеристика    КАК Характеристика,
	|	ШтрихкодыУпаковокТоваров.Упаковка          КАК Упаковка,
	|	ШтрихкодыУпаковокТоваров.Серия             КАК Серия,
	|	ЕСТЬNULL(АкцизныеМаркиЕГАИС.Статус,               ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.ПустаяСсылка))                 КАК Статус,
	|	ЕСТЬNULL(АкцизныеМаркиЕГАИС.Справка2,             ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка))                          КАК Справка2,
	|	ЕСТЬNULL(АкцизныеМаркиЕГАИС.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)) КАК АлкогольнаяПродукция,
	|	ВЫБОР
	|		КОГДА НЕ АкцизныеМаркиЕГАИС.АлкогольнаяПродукция ЕСТЬ NULL
	|			ТОГДА АкцизныеМаркиЕГАИС.АлкогольнаяПродукция.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КодАлкогольнойПродукции
	|ИЗ
	|	ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксимальныеДатыУпаковки КАК МаксимальныеДатыУпаковки
	|		ПО МаксимальныеДатыУпаковки.ЗначениеШтрихкода = ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода
	|			И МаксимальныеДатыУпаковки.ДатаУпаковки = ШтрихкодыУпаковокТоваров.ДатаУпаковки
	|			
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АкцизныеМаркиЕГАИС КАК АкцизныеМаркиЕГАИС
	|		ПО АкцизныеМаркиЕГАИС.АкцизнаяМарка = ШтрихкодыУпаковокТоваров.Ссылка
	|			И АкцизныеМаркиЕГАИС.ОрганизацияЕГАИС = &ОрганизацияЕГАИС
	|";
	
	Если ТипыУпаковок <> Неопределено Тогда
		
		Если ТипЗнч(ТипыУпаковок) = Тип("Массив") Тогда
			СписокТиповУпаковок = ТипыУпаковок;
		Иначе
			СписокТиповУпаковок = Новый Массив;
			СписокТиповУпаковок.Добавить(ТипыУпаковок);
		КонецЕсли;
		
		Запрос.УстановитьПараметр("СписокТиповУпаковок", СписокТиповУпаковок);
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"&УсловиеПоТипамУпаковок",
			"ШтрихкодыУпаковокТоваров.ТипУпаковки В(&СписокТиповУпаковок)");
		
	КонецЕсли;
	
	ТаблицаШтрихкодовУпаковок = Запрос.Выполнить().Выгрузить();
	
	КодыАлкогольнойПродукции = Новый Массив;
	Для Каждого СтрокаТЧ Из ТаблицаШтрихкодовУпаковок Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТЧ.АлкогольнаяПродукция)
			И ЗначениеЗаполнено(СтрокаТЧ.КодАлкогольнойПродукции) Тогда
			
			КодыАлкогольнойПродукции.Добавить(СтрокаТЧ.КодАлкогольнойПродукции);
			
		ИначеЕсли Не ЗначениеЗаполнено(СтрокаТЧ.АлкогольнаяПродукция)
			И Не ЗначениеЗаполнено(СтрокаТЧ.КодАлкогольнойПродукции)
			И АкцизныеМаркиВызовСервера.ЭтоШтрихкодАкцизнойМарки(СтрокаТЧ.ЗначениеШтрихкода) Тогда
			
			СтрокаТЧ.КодАлкогольнойПродукции = АкцизныеМаркиВызовСервера.КодКлассификатораНоменклатурыЕГАИС(СтрокаТЧ.ЗначениеШтрихкода);
			КодыАлкогольнойПродукции.Добавить(СтрокаТЧ.КодАлкогольнойПродукции);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СоответствиеАлкогольнойПродукции = ИнтеграцияЕГАИС.СоответствиеАлкогольнойПродукции(КодыАлкогольнойПродукции);
	
	Для Каждого СтрокаТЧ Из ТаблицаШтрихкодовУпаковок Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТЧ.АлкогольнаяПродукция)
			И ЗначениеЗаполнено(СтрокаТЧ.КодАлкогольнойПродукции) Тогда
			
			АлкогольнаяПродукция = СоответствиеАлкогольнойПродукции[СтрокаТЧ.КодАлкогольнойПродукции];
			Если ЗначениеЗаполнено(АлкогольнаяПродукция) Тогда
				СтрокаТЧ.АлкогольнаяПродукция = АлкогольнаяПродукция;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из ТаблицаШтрихкодовУпаковок Цикл
		
		ДанныеШтрихкодаУпаковки = Новый Структура;
		ДанныеШтрихкодаУпаковки.Вставить("Ссылка"                 , СтрокаТЧ.Ссылка);
		ДанныеШтрихкодаУпаковки.Вставить("ТипУпаковки"            , СтрокаТЧ.ТипУпаковки);
		ДанныеШтрихкодаУпаковки.Вставить("ТипШтрихкода"           , СтрокаТЧ.ТипШтрихкода);
		ДанныеШтрихкодаУпаковки.Вставить("Номенклатура"           , СтрокаТЧ.Номенклатура);
		ДанныеШтрихкодаУпаковки.Вставить("Характеристика"         , СтрокаТЧ.Характеристика);
		ДанныеШтрихкодаУпаковки.Вставить("Серия"                  , СтрокаТЧ.Серия);
		ДанныеШтрихкодаУпаковки.Вставить("Статус"                 , СтрокаТЧ.Статус);
		ДанныеШтрихкодаУпаковки.Вставить("Справка2"               , СтрокаТЧ.Справка2);
		ДанныеШтрихкодаУпаковки.Вставить("АлкогольнаяПродукция"   , СтрокаТЧ.АлкогольнаяПродукция);
		ДанныеШтрихкодаУпаковки.Вставить("КодАлкогольнойПродукции", СтрокаТЧ.КодАлкогольнойПродукции);
		
		Если Результат[СтрокаТЧ.ЗначениеШтрихкода] <> Неопределено Тогда
			Результат[СтрокаТЧ.ЗначениеШтрихкода].Добавить(ДанныеШтрихкодаУпаковки);
		Иначе
			МассивУпаковок = Новый Массив;
			МассивУпаковок.Добавить(ДанныеШтрихкодаУпаковки);
			Результат.Вставить(СтрокаТЧ.ЗначениеШтрихкода, МассивУпаковок);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции


// Создает элементы справочника ШтрихкодыУпаковокТоваров.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект.ЧекЕГАИС - Чек ЕГАИС.
//  ДеревоУпаковок - ДеревоЗначений - дерево упаковок.
//  ШтрихкодыУпаковок - ТаблицаЗначений - таблица штрихкодов упаковок и их хеш-сумм.
//  РассчитыватьДанные - Булево - выполнять расчет данных о товарах и справках 2 дерева упаковок.
//  Грузополучатель - СправочникСсылка.КлассификаторОрганизацийЕГАИС - Организация ЕГАИС для записи статусов новых акцизных марок.
//
Функция СоздатьШтрихкодыУпаковок(ДеревоУпаковок, ШтрихкодыУпаковок = Неопределено, РассчитыватьДанные = Ложь, Грузополучатель = Неопределено) Экспорт
	
	КоличествоАкцизныхМарок = 0;
	
	Если ШтрихкодыУпаковок <> Неопределено Тогда
		ВозвращаемоеЗначение = Новый Структура;
		ВозвращаемоеЗначение.Вставить("ВложенныеШтрихкодыУпаковок",     Новый Массив);
		ВозвращаемоеЗначение.Вставить("ХешСумма",                       "");
	Иначе
		ВозвращаемоеЗначение = Неопределено;
	КонецЕсли;
	
	Если РассчитыватьДанные Тогда
		
		ДанныеСтроки = Новый Структура;
		ДанныеСтроки.Вставить("АлкогольнаяПродукция",    NULL);
		ДанныеСтроки.Вставить("КодАлкогольнойПродукции", NULL);
		ДанныеСтроки.Вставить("Номенклатура",            NULL);
		ДанныеСтроки.Вставить("Характеристика",          NULL);
		ДанныеСтроки.Вставить("Серия",                   NULL);
		ДанныеСтроки.Вставить("Справка2",                NULL);
		ДанныеСтроки.Вставить("ВсеСправки2Указаны",      NULL);
		
	КонецЕсли;
	
	Если ШтрихкодыУпаковок <> Неопределено Тогда
		ДанныеДляРасчетаХешСуммы = Новый СписокЗначений;
	КонецЕсли;
	
	Для Каждого СтрокаДерева Из ДеревоУпаковок.Строки Цикл
		
		Если СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
			Результат = Новый Структура;
			Результат.Вставить("ВложенныеШтрихкодыУпаковок", Новый Массив);
			Результат.Вставить("ХешСумма",                   "");
		Иначе
			Результат = СоздатьШтрихкодыУпаковок(
				СтрокаДерева,
				ШтрихкодыУпаковок, РассчитыватьДанные, Грузополучатель);
		КонецЕсли;
		
		Если СтрокаДерева.ТипУпаковки = Перечисления.ПрочиеЗоныПересчетаАлкогольнойПродукции.БутылкиБезКоробки Тогда
			Продолжить;
		КонецЕсли;
		
		Если ШтрихкодыУпаковок <> Неопределено Тогда
			
			Если Не ЗначениеЗаполнено(СтрокаДерева.ШтрихкодУпаковки) Тогда
				
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("ЗначениеШтрихкода", СтрокаДерева.ЗначениеШтрихкода);
				ПараметрыОтбора.Вставить("ХешСумма",          Результат.ХешСумма);
				НайденныеШтрихкодыУпаковок = ШтрихкодыУпаковок.НайтиСтроки(ПараметрыОтбора);
				Если НайденныеШтрихкодыУпаковок.Количество() > 0 Тогда
					
					ШтрихкодУпаковки = НайденныеШтрихкодыУпаковок[0].Ссылка;
					
				Иначе
					
					ДанныеНовойУпаковки = Справочники.ШтрихкодыУпаковокТоваров.СтруктураСозданияШтрихкодаУпаковки();
					ДанныеНовойУпаковки.ЗначениеШтрихкода = СтрокаДерева.ЗначениеШтрихкода;
					ДанныеНовойУпаковки.ДатаУпаковки      = ТекущаяДатаСеанса();
					ДанныеНовойУпаковки.ТипУпаковки       = СтрокаДерева.ТипУпаковки;
					
					ДанныеНовойУпаковки.Номенклатура   = СтрокаДерева.Номенклатура;
					ДанныеНовойУпаковки.Характеристика = СтрокаДерева.Характеристика;
					ДанныеНовойУпаковки.Серия          = СтрокаДерева.Серия;
					
					Если СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
						
						ДанныеНовойУпаковки.ТипШтрихкода = Перечисления.ТипыШтрихкодов.PDF417;
						
					ИначеЕсли СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МонотоварнаяУпаковка Тогда
						
						ДанныеНовойУпаковки.Количество = СтрокаДерева.КоличествоАкцизныхМарок;
						ДанныеНовойУпаковки.ХешСумма   = Результат.ХешСумма;
						
					ИначеЕсли СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МультитоварнаяУпаковка Тогда
						
						ДанныеНовойУпаковки.ХешСумма = Результат.ХешСумма;
						
					КонецЕсли;
					
					ШтрихкодУпаковки = Справочники.ШтрихкодыУпаковокТоваров.СоздатьШтрихкодУпаковки(ДанныеНовойУпаковки, Результат.ВложенныеШтрихкодыУпаковок);
					
					НоваяСтрока = ШтрихкодыУпаковок.Добавить();
					НоваяСтрока.ЗначениеШтрихкода = ДанныеНовойУпаковки.ЗначениеШтрихкода;
					НоваяСтрока.ХешСумма          = ДанныеНовойУпаковки.ХешСумма;
					НоваяСтрока.Ссылка            = ШтрихкодУпаковки;
					
				КонецЕсли;
				
				СтрокаДерева.ШтрихкодУпаковки = ШтрихкодУпаковки;
				
			КонецЕсли;
			
			Если СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
				ДанныеДляРасчетаХешСуммы.Добавить(СтрокаДерева.ЗначениеШтрихкода);
			Иначе
				ДанныеДляРасчетаХешСуммы.Добавить(Результат.ХешСумма);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Грузополучатель)
				И СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
				
				ДанныеЗаписиСтатуса = Справочники.ШтрихкодыУпаковокТоваров.СтруктураЗаписиСтатусаУпаковки();
				ДанныеЗаписиСтатуса.ОрганизацияЕГАИС     = Грузополучатель;
				ДанныеЗаписиСтатуса.АкцизнаяМарка        = ШтрихкодУпаковки;
				ДанныеЗаписиСтатуса.Статус               = Перечисления.СтатусыАкцизныхМарок.НеПодтверждена;
				ДанныеЗаписиСтатуса.Справка2             = СтрокаДерева.Справка2;
				ДанныеЗаписиСтатуса.АлкогольнаяПродукция = СтрокаДерева.АлкогольнаяПродукция;
				
				РегистрыСведений.АкцизныеМаркиЕГАИС.ВыполнитьЗаписьВРегистр(ДанныеЗаписиСтатуса);
				
			КонецЕсли;
			
			ВозвращаемоеЗначение.ВложенныеШтрихкодыУпаковок.Добавить(ШтрихкодУпаковки);
			
		КонецЕсли;
		
		Если СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
			КоличествоАкцизныхМарок = КоличествоАкцизныхМарок + 1;
		Иначе
			КоличествоАкцизныхМарок = КоличествоАкцизныхМарок + СтрокаДерева.КоличествоАкцизныхМарок;
		КонецЕсли;
		
		Если РассчитыватьДанные Тогда
			
			Если СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
				СтрокаДерева.ВсеСправки2Указаны = ЗначениеЗаполнено(СтрокаДерева.Справка2);
			КонецЕсли;
			
			ПроверитьДанныеСтроки(ДанныеСтроки, СтрокаДерева, "АлкогольнаяПродукция");
			ПроверитьДанныеСтроки(ДанныеСтроки, СтрокаДерева, "КодАлкогольнойПродукции");
			ПроверитьДанныеСтроки(ДанныеСтроки, СтрокаДерева, "Номенклатура");
			ПроверитьДанныеСтроки(ДанныеСтроки, СтрокаДерева, "Характеристика");
			ПроверитьДанныеСтроки(ДанныеСтроки, СтрокаДерева, "Серия");
			ПроверитьДанныеСтроки(ДанныеСтроки, СтрокаДерева, "Справка2");
			ПроверитьДанныеСтроки(ДанныеСтроки, СтрокаДерева, "ВсеСправки2Указаны", Истина);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТипЗнч(ДеревоУпаковок) = Тип("СтрокаДереваЗначений") Тогда
		
		ДеревоУпаковок.КоличествоАкцизныхМарок = КоличествоАкцизныхМарок;
		
		Если РассчитыватьДанные Тогда
			Если ДеревоУпаковок.Строки.Количество() > 0 Тогда
				ЗаполнитьЗначенияСвойств(ДеревоУпаковок, ДанныеСтроки);
			Иначе
				
				ДанныеСтроки.АлкогольнаяПродукция    = Неопределено;
				ДанныеСтроки.КодАлкогольнойПродукции = Неопределено;
				ДанныеСтроки.Номенклатура            = Неопределено;
				ДанныеСтроки.Характеристика          = Неопределено;
				ДанныеСтроки.Серия                   = Неопределено;
				ДанныеСтроки.Справка2                = Неопределено;
				ДанныеСтроки.ВсеСправки2Указаны      = Ложь;
				
				ЗаполнитьЗначенияСвойств(ДеревоУпаковок, ДанныеСтроки);
				
			КонецЕсли;
		КонецЕсли;
		
		Если ШтрихкодыУпаковок <> Неопределено
			И ДанныеДляРасчетаХешСуммы.Количество() <> 0 Тогда
			ВозвращаемоеЗначение.ХешСумма = Справочники.ШтрихкодыУпаковокТоваров.ХешСуммаСодержимогоУпаковки(ДанныеДляРасчетаХешСуммы);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ПроверитьДанныеСтроки(ДанныеСтроки, СтрокаДерева, ИмяПоля, ТолькоЗаполненность = Ложь)
	
	Значение = ДанныеСтроки[ИмяПоля];
	ЗначениеДерева = СтрокаДерева[ИмяПоля];
	
	Если Значение = NULL Тогда
		Значение = ЗначениеДерева;
	ИначеЕсли Значение <> Неопределено
		И Значение <> ЗначениеДерева Тогда
		Значение = ?(ТолькоЗаполненность, Ложь, Неопределено);
	КонецЕсли;
	
	ДанныеСтроки[ИмяПоля] = Значение;
	
КонецФункции

Функция ВложенныеШтрихкодыУпаковок(СписокШтрихкодов, ОрганизацияЕГАИС) Экспорт
	
	ДеревоУпаковок = Новый ДеревоЗначений;
	ДеревоУпаковок.Колонки.Добавить("ОрганизацияЕГАИС");
	ДеревоУпаковок.Колонки.Добавить("ИдентификаторСтроки");
	ДеревоУпаковок.Колонки.Добавить("ШтрихкодУпаковки");
	ДеревоУпаковок.Колонки.Добавить("ЗначениеШтрихкода");
	ДеревоУпаковок.Колонки.Добавить("АлкогольнаяПродукция");
	ДеревоУпаковок.Колонки.Добавить("Номенклатура");
	ДеревоУпаковок.Колонки.Добавить("Характеристика");
	ДеревоУпаковок.Колонки.Добавить("Серия");
	ДеревоУпаковок.Колонки.Добавить("Справка2");
	ДеревоУпаковок.Колонки.Добавить("ВсеСправки2Указаны");
	ДеревоУпаковок.Колонки.Добавить("ТипУпаковки");
	ДеревоУпаковок.Колонки.Добавить("ТипШтрихкода");
	ДеревоУпаковок.Колонки.Добавить("Статус");
	ДеревоУпаковок.Колонки.Добавить("КодАлкогольнойПродукции");
	ДеревоУпаковок.Колонки.Добавить("ХешСумма");
	ДеревоУпаковок.Колонки.Добавить("КоличествоАкцизныхМарок");
	
	МаркированныеТовары = Новый ТаблицаЗначений;
	МаркированныеТовары.Колонки.Добавить("ОрганизацияЕГАИС");
	МаркированныеТовары.Колонки.Добавить("ИдентификаторСтроки");
	МаркированныеТовары.Колонки.Добавить("ШтрихкодУпаковки");
	МаркированныеТовары.Колонки.Добавить("СтрокаДерева");
	МаркированныеТовары.Колонки.Добавить("УпаковкаВерхнегоУровня");
	МаркированныеТовары.Колонки.Добавить("ЗначениеШтрихкода");
	МаркированныеТовары.Колонки.Добавить("Номенклатура");
	МаркированныеТовары.Колонки.Добавить("Характеристика");
	МаркированныеТовары.Колонки.Добавить("Серия");
	МаркированныеТовары.Колонки.Добавить("Статус");
	МаркированныеТовары.Колонки.Добавить("Справка2");
	МаркированныеТовары.Колонки.Добавить("АлкогольнаяПродукция");
	МаркированныеТовары.Колонки.Добавить("КодАлкогольнойПродукции");
	
	МаркированныеТовары.Индексы.Добавить("ШтрихкодУпаковки");
	
	СоответствиеСтрокДереваУпаковок = Новый Соответствие;
	
	ТаблицаШтрихкодов = Новый ТаблицаЗначений;
	ТаблицаШтрихкодов.Колонки.Добавить("ШтрихкодУпаковки", Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	ТаблицаШтрихкодов.Колонки.Добавить("ОрганизацияЕГАИС", Новый ОписаниеТипов("СправочникСсылка.КлассификаторОрганизацийЕГАИС"));
	
	СтрокиДереваДляЗаполненияАлкогольнойПродукции = Новый Массив;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("УпаковкиВерхнегоУровня", Новый Соответствие);
	
	НуженПоискВложенныхШтрихкодов = Истина;
	НомерИтерации = 1;
	Пока НуженПоискВложенныхШтрихкодов Цикл
		
		ИмяВременнойТаблицы = "ВТВложенныеШтрихкодыИсходныеДанные";
		
		Если НомерИтерации = 1 Тогда
			
			Если ТипЗнч(СписокШтрихкодов) = Тип("СправочникСсылка.ШтрихкодыУпаковокТоваров") Тогда
				НоваяСтрока = ТаблицаШтрихкодов.Добавить();
				НоваяСтрока.ШтрихкодУпаковки = СписокШтрихкодов;
				НоваяСтрока.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
			Иначе
				Для Каждого ШтрихкодУпаковки Из СписокШтрихкодов Цикл
					НоваяСтрока = ТаблицаШтрихкодов.Добавить();
					НоваяСтрока.ШтрихкодУпаковки = ШтрихкодУпаковки;
					НоваяСтрока.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		ТекстыЗапроса = Новый СписокЗначений;
		ТекстыЗапроса.Добавить(
			СтрШаблон(
				"ВЫБРАТЬ
				|	ИсходныеДанные.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
				|	ИсходныеДанные.ШтрихкодУпаковки КАК ШтрихкодУпаковки
				|ПОМЕСТИТЬ %1
				|ИЗ
				|	&ИсходныеДанные КАК ИсходныеДанные", ИмяВременнойТаблицы));
		ТекстыЗапроса.Добавить(
			Справочники.ШтрихкодыУпаковокТоваров.ТекстЗапросаВложенныхШтрихкодов(ИмяВременнойТаблицы),
			"ВложенныеШтрихкоды");
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ИсходныеДанные", ТаблицаШтрихкодов);
		РезультатыЗапроса = ГосударственныеИнформационныеСистемы.ВыполнитьПакетЗапросов(Запрос, ТекстыЗапроса);
		Выборка = РезультатыЗапроса.ВложенныеШтрихкоды.Выбрать();
		
		ТаблицаШтрихкодов.Очистить();
		
		Пока Выборка.Следующий() Цикл
			
			Если СоответствиеСтрокДереваУпаковок[Выборка.Упаковка] = Неопределено Тогда
				СтрокаРодитель = ДеревоУпаковок.Строки.Добавить();
				ЗаполнитьСтрокуДереваУпаковок(СтрокаРодитель, Выборка, 0, МаркированныеТовары, ДополнительныеПараметры);
				СоответствиеСтрокДереваУпаковок.Вставить(Выборка.Упаковка, СтрокаРодитель);
			Иначе
				СтрокаРодитель = СоответствиеСтрокДереваУпаковок[Выборка.Упаковка];
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.УпаковкаУровень1) Тогда
				
				Если СоответствиеСтрокДереваУпаковок[Выборка.УпаковкаУровень1] = Неопределено Тогда
					СтрокаУпаковкиУровень1 = СтрокаРодитель.Строки.Добавить();
					ЗаполнитьСтрокуДереваУпаковок(СтрокаУпаковкиУровень1, Выборка, 1, МаркированныеТовары, ДополнительныеПараметры);
					СоответствиеСтрокДереваУпаковок.Вставить(Выборка.УпаковкаУровень1, СтрокаУпаковкиУровень1);
				Иначе
					СтрокаУпаковкиУровень1 = СоответствиеСтрокДереваУпаковок[Выборка.УпаковкаУровень1];
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Выборка.УпаковкаУровень2) Тогда
					
					Если СоответствиеСтрокДереваУпаковок[Выборка.УпаковкаУровень2] = Неопределено Тогда
						СтрокаУпаковкиУровень2 = СтрокаУпаковкиУровень1.Строки.Добавить();
						ЗаполнитьСтрокуДереваУпаковок(СтрокаУпаковкиУровень2, Выборка, 2, МаркированныеТовары, ДополнительныеПараметры);
						СоответствиеСтрокДереваУпаковок.Вставить(Выборка.УпаковкаУровень2, СтрокаУпаковкиУровень2);
					Иначе
						СтрокаУпаковкиУровень2 = СоответствиеСтрокДереваУпаковок[Выборка.УпаковкаУровень2];
					КонецЕсли;
					
					Если ЗначениеЗаполнено(Выборка.УпаковкаУровень3) Тогда
						
						Если СоответствиеСтрокДереваУпаковок[Выборка.УпаковкаУровень3] = Неопределено Тогда
							СтрокаУпаковкиУровень3 = СтрокаУпаковкиУровень2.Строки.Добавить();
							ЗаполнитьСтрокуДереваУпаковок(СтрокаУпаковкиУровень3, Выборка, 3, МаркированныеТовары, ДополнительныеПараметры);
							СоответствиеСтрокДереваУпаковок.Вставить(Выборка.УпаковкаУровень3, СтрокаУпаковкиУровень3);
						Иначе
							СтрокаУпаковкиУровень3 = СоответствиеСтрокДереваУпаковок[Выборка.УпаковкаУровень3];
						КонецЕсли;
						
						НоваяСтрока = ТаблицаШтрихкодов.Добавить();
						НоваяСтрока.ШтрихкодУпаковки = Выборка.УпаковкаУровень3;
						НоваяСтрока.ОрганизацияЕГАИС = Выборка.ОрганизацияЕГАИС;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		НомерИтерации = НомерИтерации + 1;
		НуженПоискВложенныхШтрихкодов = ТаблицаШтрихкодов.Количество() > 0;
		
	КонецЦикла;
	
	КодыАлкогольнойПродукции = Новый Массив;
	Для Каждого СтрокаТЧ Из МаркированныеТовары Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТЧ.АлкогольнаяПродукция)
			И ЗначениеЗаполнено(СтрокаТЧ.КодАлкогольнойПродукции) Тогда
			
			КодыАлкогольнойПродукции.Добавить(СтрокаТЧ.КодАлкогольнойПродукции);
			
		ИначеЕсли Не ЗначениеЗаполнено(СтрокаТЧ.АлкогольнаяПродукция)
			И Не ЗначениеЗаполнено(СтрокаТЧ.КодАлкогольнойПродукции)
			И АкцизныеМаркиВызовСервера.ЭтоШтрихкодАкцизнойМарки(СтрокаТЧ.ЗначениеШтрихкода) Тогда
			
			СтрокаТЧ.КодАлкогольнойПродукции = АкцизныеМаркиВызовСервера.КодКлассификатораНоменклатурыЕГАИС(СтрокаТЧ.ЗначениеШтрихкода);
			Если ЗначениеЗаполнено(СтрокаТЧ.СтрокаДерева) Тогда
				СтрокаТЧ.СтрокаДерева.КодАлкогольнойПродукции = СтрокаТЧ.КодАлкогольнойПродукции;
			КонецЕсли;
			
			КодыАлкогольнойПродукции.Добавить(СтрокаТЧ.КодАлкогольнойПродукции);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СоответствиеАлкогольнойПродукции = ИнтеграцияЕГАИС.СоответствиеАлкогольнойПродукции(КодыАлкогольнойПродукции);
	
	Для Каждого СтрокаТЧ Из МаркированныеТовары Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТЧ.АлкогольнаяПродукция)
			И ЗначениеЗаполнено(СтрокаТЧ.КодАлкогольнойПродукции) Тогда
			
			АлкогольнаяПродукция = СоответствиеАлкогольнойПродукции[СтрокаТЧ.КодАлкогольнойПродукции];
			Если ЗначениеЗаполнено(АлкогольнаяПродукция) Тогда
				СтрокаТЧ.АлкогольнаяПродукция = АлкогольнаяПродукция;
				Если ЗначениеЗаполнено(СтрокаТЧ.СтрокаДерева) Тогда
					СтрокаТЧ.СтрокаДерева.АлкогольнаяПродукция = АлкогольнаяПродукция;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Справочники.ШтрихкодыУпаковокТоваров.СоздатьШтрихкодыУпаковок(ДеревоУпаковок, Неопределено, Истина);
	
	Результат = Новый Структура;
	Результат.Вставить("ДеревоУпаковок",      ДеревоУпаковок);
	Результат.Вставить("МаркированныеТовары", МаркированныеТовары);
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаВложенныхШтрихкодов(ИмяВременнойТаблицы = "ВТВложенныеШтрихкодыИсходныеДанные") Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ИсходныеДанные.ОрганизацияЕГАИС                     КАК ОрганизацияЕГАИС,
	|	ИсходныеДанные.ШтрихкодУпаковки                     КАК Упаковка,
	|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод КАК УпаковкаУровень1
	|ПОМЕСТИТЬ ВложенныеШтрихкодыУпаковокУровень1
	|ИЗ
	|	&ВТВложенныеШтрихкодыИсходныеДанные КАК ИсходныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ШтрихкодыУпаковокТоваровВложенныеШтрихкоды
	|		ПО ИсходныеДанные.ШтрихкодУпаковки = ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Ссылка
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныеШтрихкодыУпаковокУровень1.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ВложенныеШтрихкодыУпаковокУровень1.Упаковка         КАК Упаковка,
	|	ВложенныеШтрихкодыУпаковокУровень1.УпаковкаУровень1 КАК УпаковкаУровень1,
	|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод КАК УпаковкаУровень2
	|ПОМЕСТИТЬ ВложенныеШтрихкодыУпаковокУровень2
	|ИЗ
	|	ВложенныеШтрихкодыУпаковокУровень1 КАК ВложенныеШтрихкодыУпаковокУровень1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ШтрихкодыУпаковокТоваровВложенныеШтрихкоды
	|		ПО ВложенныеШтрихкодыУпаковокУровень1.УпаковкаУровень1 = ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Ссылка
	|ИНДЕКСИРОВАТЬ ПО
	|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныеШтрихкодыУпаковокУровень2.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ВложенныеШтрихкодыУпаковокУровень2.Упаковка         КАК Упаковка,
	|	ВложенныеШтрихкодыУпаковокУровень2.УпаковкаУровень1 КАК УпаковкаУровень1,
	|	ВложенныеШтрихкодыУпаковокУровень2.УпаковкаУровень2 КАК УпаковкаУровень2,
	|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод КАК УпаковкаУровень3
	|ПОМЕСТИТЬ ВложенныеШтрихкодыУпаковокУровень3
	|ИЗ
	|	ВложенныеШтрихкодыУпаковокУровень2 КАК ВложенныеШтрихкодыУпаковокУровень2
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ШтрихкодыУпаковокТоваровВложенныеШтрихкоды
	|		ПО ВложенныеШтрихкодыУпаковокУровень2.УпаковкаУровень2 = ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Ссылка
	|ИНДЕКСИРОВАТЬ ПО
	|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеДанные.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ИсходныеДанные.Упаковка         КАК Упаковка,
	|	ИсходныеДанные.УпаковкаУровень1 КАК УпаковкаУровень1,
	|	ИсходныеДанные.УпаковкаУровень2 КАК УпаковкаУровень2,
	|	ИсходныеДанные.УпаковкаУровень3 КАК УпаковкаУровень3
	|ПОМЕСТИТЬ ДанныеУпаковок
	|ИЗ
	|	ВложенныеШтрихкодыУпаковокУровень3 КАК ИсходныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВложенныеШтрихкодыУпаковокУровень3 КАК ВложенныеШтрихкодыУпаковокУровень1
	|		ПО ИсходныеДанные.Упаковка = ВложенныеШтрихкодыУпаковокУровень1.УпаковкаУровень1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВложенныеШтрихкодыУпаковокУровень3 КАК ВложенныеШтрихкодыУпаковокУровень2
	|		ПО ИсходныеДанные.Упаковка = ВложенныеШтрихкодыУпаковокУровень2.УпаковкаУровень2
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВложенныеШтрихкодыУпаковокУровень3 КАК ВложенныеШтрихкодыУпаковокУровень3
	|		ПО ИсходныеДанные.Упаковка = ВложенныеШтрихкодыУпаковокУровень3.УпаковкаУровень3
	|ГДЕ
	|	ВложенныеШтрихкодыУпаковокУровень1.Упаковка ЕСТЬ NULL
	|	И ВложенныеШтрихкодыУпаковокУровень2.Упаковка ЕСТЬ NULL
	|	И ВложенныеШтрихкодыУпаковокУровень3.Упаковка ЕСТЬ NULL
	|;
	|
	|//#РезультатЗапроса#////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеУпаковок.ОрганизацияЕГАИС                                                         КАК ОрганизацияЕГАИС,
	|	ДанныеУпаковок.Упаковка                                                                 КАК Упаковка,
	|	""""                                                                                    КАК УпаковкаИдентификаторСтроки,
	|	ДанныеУпаковок.Упаковка.ТипШтрихкода                                                    КАК УпаковкаТипШтрихкода,
	|	ДанныеУпаковок.Упаковка.ТипУпаковки                                                     КАК УпаковкаТипУпаковки,
	|	ДанныеУпаковок.Упаковка.ЗначениеШтрихкода                                               КАК УпаковкаЗначениеШтрихкода,
	|	ДанныеУпаковок.Упаковка.Номенклатура                                                    КАК УпаковкаНоменклатура,
	|	ДанныеУпаковок.Упаковка.Характеристика                                                  КАК УпаковкаХарактеристика,
	|	ДанныеУпаковок.Упаковка.Серия                                                           КАК УпаковкаСерия,
	|	ДанныеУпаковок.Упаковка.ХешСумма                                                        КАК УпаковкаХешСумма,
	|	ЕСТЬNULL(СтатусыУпаковки.Статус,                           &ПустойСтатус)               КАК УпаковкаСтатус,
	|	ЕСТЬNULL(СтатусыУпаковки.Справка2,                         &ПустаяСправка2)             КАК УпаковкаСправка2,
	|	ЕСТЬNULL(СтатусыУпаковки.АлкогольнаяПродукция,             &ПустаяАлкогольнаяПродукция) КАК УпаковкаАлкогольнаяПродукция,
	|
	|	ДанныеУпаковок.УпаковкаУровень1                                                         КАК УпаковкаУровень1,
	|	""""                                                                                    КАК УпаковкаУровень1ИдентификаторСтроки,
	|	ДанныеУпаковок.УпаковкаУровень1.ТипШтрихкода                                            КАК УпаковкаУровень1ТипШтрихкода,
	|	ДанныеУпаковок.УпаковкаУровень1.ТипУпаковки                                             КАК УпаковкаУровень1ТипУпаковки,
	|	ДанныеУпаковок.УпаковкаУровень1.ЗначениеШтрихкода                                       КАК УпаковкаУровень1ЗначениеШтрихкода,
	|	ДанныеУпаковок.УпаковкаУровень1.Номенклатура                                            КАК УпаковкаУровень1Номенклатура,
	|	ДанныеУпаковок.УпаковкаУровень1.Характеристика                                          КАК УпаковкаУровень1Характеристика,
	|	ДанныеУпаковок.УпаковкаУровень1.Серия                                                   КАК УпаковкаУровень1Серия,
	|	ДанныеУпаковок.УпаковкаУровень1.ХешСумма                                                КАК УпаковкаУровень1ХешСумма,
	|	ЕСТЬNULL(СтатусыУпаковкиУровень1.Статус,                   &ПустойСтатус)               КАК УпаковкаУровень1Статус,
	|	ЕСТЬNULL(СтатусыУпаковкиУровень1.Справка2,                 &ПустаяСправка2)             КАК УпаковкаУровень1Справка2,
	|	ЕСТЬNULL(СтатусыУпаковкиУровень1.АлкогольнаяПродукция,     &ПустаяАлкогольнаяПродукция) КАК УпаковкаУровень1АлкогольнаяПродукция,
	|
	|	ДанныеУпаковок.УпаковкаУровень2                                                         КАК УпаковкаУровень2,
	|	""""                                                                                    КАК УпаковкаУровень2ИдентификаторСтроки,
	|	ДанныеУпаковок.УпаковкаУровень2.ТипШтрихкода                                            КАК УпаковкаУровень2ТипШтрихкода,
	|	ДанныеУпаковок.УпаковкаУровень2.ТипУпаковки                                             КАК УпаковкаУровень2ТипУпаковки,
	|	ДанныеУпаковок.УпаковкаУровень2.ЗначениеШтрихкода                                       КАК УпаковкаУровень2ЗначениеШтрихкода,
	|	ДанныеУпаковок.УпаковкаУровень2.Номенклатура                                            КАК УпаковкаУровень2Номенклатура,
	|	ДанныеУпаковок.УпаковкаУровень2.Характеристика                                          КАК УпаковкаУровень2Характеристика,
	|	ДанныеУпаковок.УпаковкаУровень2.Серия                                                   КАК УпаковкаУровень2Серия,
	|	ДанныеУпаковок.УпаковкаУровень2.ХешСумма                                                КАК УпаковкаУровень2ХешСумма,
	|	ЕСТЬNULL(СтатусыУпаковкиУровень2.Статус,                   &ПустойСтатус)               КАК УпаковкаУровень2Статус,
	|	ЕСТЬNULL(СтатусыУпаковкиУровень2.Справка2,                 &ПустаяСправка2)             КАК УпаковкаУровень2Справка2,
	|	ЕСТЬNULL(СтатусыУпаковкиУровень2.АлкогольнаяПродукция,     &ПустаяАлкогольнаяПродукция) КАК УпаковкаУровень2АлкогольнаяПродукция,
	|
	|	ДанныеУпаковок.УпаковкаУровень3                                                         КАК УпаковкаУровень3,
	|	""""                                                                                    КАК УпаковкаУровень3ИдентификаторСтроки,
	|	ДанныеУпаковок.УпаковкаУровень3.ТипШтрихкода                                            КАК УпаковкаУровень3ТипШтрихкода,
	|	ДанныеУпаковок.УпаковкаУровень3.ТипУпаковки                                             КАК УпаковкаУровень3ТипУпаковки,
	|	ДанныеУпаковок.УпаковкаУровень3.ЗначениеШтрихкода                                       КАК УпаковкаУровень3ЗначениеШтрихкода,
	|	ДанныеУпаковок.УпаковкаУровень3.Номенклатура                                            КАК УпаковкаУровень3Номенклатура,
	|	ДанныеУпаковок.УпаковкаУровень3.Характеристика                                          КАК УпаковкаУровень3Характеристика,
	|	ДанныеУпаковок.УпаковкаУровень3.Серия                                                   КАК УпаковкаУровень3Серия,
	|	ДанныеУпаковок.УпаковкаУровень3.ХешСумма                                                КАК УпаковкаУровень3ХешСумма,
	|	ЕСТЬNULL(СтатусыУпаковкиУровень3.Статус,                   &ПустойСтатус)               КАК УпаковкаУровень3Статус,
	|	ЕСТЬNULL(СтатусыУпаковкиУровень3.Справка2,                 &ПустаяСправка2)             КАК УпаковкаУровень3Справка2,
	|	ЕСТЬNULL(СтатусыУпаковкиУровень3.АлкогольнаяПродукция,     &ПустаяАлкогольнаяПродукция) КАК УпаковкаУровень3АлкогольнаяПродукция
	|ИЗ
	|	ДанныеУпаковок КАК ДанныеУпаковок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АкцизныеМаркиЕГАИС КАК СтатусыУпаковки
	|		ПО ДанныеУпаковок.Упаковка = СтатусыУпаковки.АкцизнаяМарка
	|			И (СтатусыУпаковки.ОрганизацияЕГАИС = ДанныеУпаковок.ОрганизацияЕГАИС)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АкцизныеМаркиЕГАИС КАК СтатусыУпаковкиУровень1
	|		ПО ДанныеУпаковок.УпаковкаУровень1 = СтатусыУпаковкиУровень1.АкцизнаяМарка
	|			И (СтатусыУпаковкиУровень1.ОрганизацияЕГАИС = ДанныеУпаковок.ОрганизацияЕГАИС)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АкцизныеМаркиЕГАИС КАК СтатусыУпаковкиУровень2
	|		ПО ДанныеУпаковок.УпаковкаУровень2 = СтатусыУпаковкиУровень2.АкцизнаяМарка
	|			И (СтатусыУпаковкиУровень2.ОрганизацияЕГАИС = ДанныеУпаковок.ОрганизацияЕГАИС)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АкцизныеМаркиЕГАИС КАК СтатусыУпаковкиУровень3
	|		ПО ДанныеУпаковок.УпаковкаУровень3 = СтатусыУпаковкиУровень3.АкцизнаяМарка
	|			И (СтатусыУпаковкиУровень3.ОрганизацияЕГАИС = ДанныеУпаковок.ОрганизацияЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВложенныеШтрихкодыУпаковокУровень1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВложенныеШтрихкодыУпаковокУровень2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВложенныеШтрихкодыУпаковокУровень3
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеУпаковок
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВТВложенныеШтрихкодыИсходныеДанные", ИмяВременнойТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПустойСтатус",                       "ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.ПустаяСсылка)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПустаяСправка2",                     "ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПустаяАлкогольнаяПродукция",         "ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)");
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ВложенныеШтрихкодыУпаковокПоДокументу(ВыборкаПерваяИтерация, МенеджерВременныхТаблиц) Экспорт
	
	ДеревоУпаковок = Новый ДеревоЗначений;
	ДеревоУпаковок.Колонки.Добавить("ОрганизацияЕГАИС");
	ДеревоУпаковок.Колонки.Добавить("ИдентификаторСтроки");
	ДеревоУпаковок.Колонки.Добавить("ШтрихкодУпаковки");
	ДеревоУпаковок.Колонки.Добавить("ЗначениеШтрихкода");
	ДеревоУпаковок.Колонки.Добавить("АлкогольнаяПродукция");
	ДеревоУпаковок.Колонки.Добавить("Номенклатура");
	ДеревоУпаковок.Колонки.Добавить("Характеристика");
	ДеревоУпаковок.Колонки.Добавить("Серия");
	ДеревоУпаковок.Колонки.Добавить("Справка2");
	ДеревоУпаковок.Колонки.Добавить("ВсеСправки2Указаны");
	ДеревоУпаковок.Колонки.Добавить("ТипУпаковки");
	ДеревоУпаковок.Колонки.Добавить("ТипШтрихкода");
	ДеревоУпаковок.Колонки.Добавить("Статус");
	ДеревоУпаковок.Колонки.Добавить("КодАлкогольнойПродукции");
	ДеревоУпаковок.Колонки.Добавить("ХешСумма");
	ДеревоУпаковок.Колонки.Добавить("КоличествоАкцизныхМарок");
	
	МаркированныеТовары = Новый ТаблицаЗначений;
	МаркированныеТовары.Колонки.Добавить("ОрганизацияЕГАИС");
	МаркированныеТовары.Колонки.Добавить("ИдентификаторСтроки");
	МаркированныеТовары.Колонки.Добавить("ШтрихкодУпаковки");
	МаркированныеТовары.Колонки.Добавить("СтрокаДерева");
	МаркированныеТовары.Колонки.Добавить("УпаковкаВерхнегоУровня");
	МаркированныеТовары.Колонки.Добавить("ЗначениеШтрихкода");
	МаркированныеТовары.Колонки.Добавить("Номенклатура");
	МаркированныеТовары.Колонки.Добавить("Характеристика");
	МаркированныеТовары.Колонки.Добавить("Серия");
	МаркированныеТовары.Колонки.Добавить("Статус");
	МаркированныеТовары.Колонки.Добавить("Справка2");
	МаркированныеТовары.Колонки.Добавить("АлкогольнаяПродукция");
	МаркированныеТовары.Колонки.Добавить("КодАлкогольнойПродукции");
	
	МаркированныеТовары.Индексы.Добавить("ШтрихкодУпаковки");
	
	СоответствиеСтрокДереваУпаковок = Новый Соответствие;
	
	ТаблицаШтрихкодов = Новый ТаблицаЗначений;
	ТаблицаШтрихкодов.Колонки.Добавить("ШтрихкодУпаковки",             Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	ТаблицаШтрихкодов.Колонки.Добавить("ШтрихкодРодительскойУпаковки", Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	ТаблицаШтрихкодов.Колонки.Добавить("ИдентификаторСтроки",          Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(36)));
	ТаблицаШтрихкодов.Колонки.Добавить("АлкогольнаяПродукция",         Новый ОписаниеТипов("СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС"));
	ТаблицаШтрихкодов.Колонки.Добавить("Справка2",                     Новый ОписаниеТипов("СправочникСсылка.Справки2ЕГАИС"));
	ТаблицаШтрихкодов.Колонки.Добавить("ОрганизацияЕГАИС",             Новый ОписаниеТипов("СправочникСсылка.КлассификаторОрганизацийЕГАИС"));
	
	СтрокиДереваДляЗаполненияАлкогольнойПродукции = Новый Массив;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("УпаковкиВерхнегоУровня", Новый Соответствие);
	
	НуженПоискВложенныхШтрихкодов = Истина;
	НомерИтерации = 1;
	Пока НуженПоискВложенныхШтрихкодов Цикл
		
		Если НомерИтерации = 1 Тогда
			
			Выборка = ВыборкаПерваяИтерация;
			
		Иначе
			
			ИмяВременнойТаблицы = "ВТВложенныеШтрихкодыИсходныеДанные2";
			
			ТекстыЗапроса = Новый СписокЗначений;
			ТекстыЗапроса.Добавить(
				СтрШаблон(
					"ВЫБРАТЬ
					|	ИсходныеДанные.ОрганизацияЕГАИС             КАК ОрганизацияЕГАИС,
					|	ИсходныеДанные.ИдентификаторСтроки          КАК ИдентификаторСтроки,
					|	ИсходныеДанные.АлкогольнаяПродукция         КАК АлкогольнаяПродукция,
					|	ИсходныеДанные.Справка2                     КАК Справка2,
					|	ИсходныеДанные.ШтрихкодУпаковки             КАК ШтрихкодУпаковки,
					|	ИсходныеДанные.ШтрихкодРодительскойУпаковки КАК ШтрихкодРодительскойУпаковки
					|ПОМЕСТИТЬ %1
					|ИЗ
					|	&ИсходныеДанные КАК ИсходныеДанные", ИмяВременнойТаблицы));
			
			ПараметрыФормированияТекстаЗапроса = Справочники.ШтрихкодыУпаковокТоваров.ПараметрыФормированияТекстаЗапросаВложенныхШтрихкодов();
			ПараметрыФормированияТекстаЗапроса.ДокументСсылка                  = Неопределено;
			ПараметрыФормированияТекстаЗапроса.ИспользоватьИдентификаторСтроки = Ложь;
			ПараметрыФормированияТекстаЗапроса.ИмяВременнойТаблицы             = ИмяВременнойТаблицы;
			ТекстыЗапроса.Добавить(
				Справочники.ШтрихкодыУпаковокТоваров.ТекстЗапросаВложенныхШтрихкодовПоДокументу(ПараметрыФормированияТекстаЗапроса),
				"ВложенныеШтрихкоды");
			Если НомерИтерации > 1 Тогда
				ТекстыЗапроса.Добавить(
					СтрШаблон("УНИЧТОЖИТЬ %1", ИмяВременнойТаблицы));
			КонецЕсли;
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("ИсходныеДанные", ТаблицаШтрихкодов);
			РезультатыЗапроса = ГосударственныеИнформационныеСистемы.ВыполнитьПакетЗапросов(Запрос, ТекстыЗапроса);
			Выборка = РезультатыЗапроса.ВложенныеШтрихкоды.Выбрать();
			
		КонецЕсли;
		
		ТаблицаШтрихкодов.Очистить();
		
		Пока Выборка.Следующий() Цикл
			
			Если СоответствиеСтрокДереваУпаковок[Выборка.Упаковка] = Неопределено Тогда
				СтрокаРодитель = ДеревоУпаковок.Строки.Добавить();
				ЗаполнитьСтрокуДереваУпаковок(СтрокаРодитель, Выборка, 0, МаркированныеТовары, ДополнительныеПараметры);
				СоответствиеСтрокДереваУпаковок.Вставить(Выборка.Упаковка, СтрокаРодитель);
			Иначе
				СтрокаРодитель = СоответствиеСтрокДереваУпаковок[Выборка.Упаковка];
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.УпаковкаУровень1) Тогда
				
				Если СоответствиеСтрокДереваУпаковок[Выборка.УпаковкаУровень1] = Неопределено Тогда
					СтрокаУпаковкиУровень1 = СтрокаРодитель.Строки.Добавить();
					ЗаполнитьСтрокуДереваУпаковок(СтрокаУпаковкиУровень1, Выборка, 1, МаркированныеТовары, ДополнительныеПараметры);
					СоответствиеСтрокДереваУпаковок.Вставить(Выборка.УпаковкаУровень1, СтрокаУпаковкиУровень1);
				Иначе
					СтрокаУпаковкиУровень1 = СоответствиеСтрокДереваУпаковок[Выборка.УпаковкаУровень1];
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Выборка.УпаковкаУровень2) Тогда
					
					Если СоответствиеСтрокДереваУпаковок[Выборка.УпаковкаУровень2] = Неопределено Тогда
						СтрокаУпаковкиУровень2 = СтрокаУпаковкиУровень1.Строки.Добавить();
						ЗаполнитьСтрокуДереваУпаковок(СтрокаУпаковкиУровень2, Выборка, 2, МаркированныеТовары, ДополнительныеПараметры);
						СоответствиеСтрокДереваУпаковок.Вставить(Выборка.УпаковкаУровень2, СтрокаУпаковкиУровень2);
					Иначе
						СтрокаУпаковкиУровень2 = СоответствиеСтрокДереваУпаковок[Выборка.УпаковкаУровень2];
					КонецЕсли;
					
					Если ЗначениеЗаполнено(Выборка.УпаковкаУровень3) Тогда
						
						Если СоответствиеСтрокДереваУпаковок[Выборка.УпаковкаУровень3] = Неопределено Тогда
							СтрокаУпаковкиУровень3 = СтрокаУпаковкиУровень2.Строки.Добавить();
							ЗаполнитьСтрокуДереваУпаковок(СтрокаУпаковкиУровень3, Выборка, 3, МаркированныеТовары, ДополнительныеПараметры);
							СоответствиеСтрокДереваУпаковок.Вставить(Выборка.УпаковкаУровень3, СтрокаУпаковкиУровень3);
						Иначе
							СтрокаУпаковкиУровень3 = СоответствиеСтрокДереваУпаковок[Выборка.УпаковкаУровень3];
						КонецЕсли;
						
						НоваяСтрока = ТаблицаШтрихкодов.Добавить();
						НоваяСтрока.ШтрихкодУпаковки     = Выборка.УпаковкаУровень3;
						НоваяСтрока.ИдентификаторСтроки  = Выборка.УпаковкаУровень3ИдентификаторСтроки;
						НоваяСтрока.АлкогольнаяПродукция = Выборка.УпаковкаУровень3АлкогольнаяПродукция;
						НоваяСтрока.Справка2             = Выборка.УпаковкаУровень3Справка2;
						НоваяСтрока.ОрганизацияЕГАИС     = Выборка.ОрганизацияЕГАИС;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		НомерИтерации = НомерИтерации + 1;
		НуженПоискВложенныхШтрихкодов = ТаблицаШтрихкодов.Количество() > 0;
		
	КонецЦикла;
	
	КодыАлкогольнойПродукции = Новый Массив;
	Для Каждого СтрокаТЧ Из МаркированныеТовары Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТЧ.АлкогольнаяПродукция)
			И ЗначениеЗаполнено(СтрокаТЧ.КодАлкогольнойПродукции) Тогда
			
			КодыАлкогольнойПродукции.Добавить(СтрокаТЧ.КодАлкогольнойПродукции);
			
		ИначеЕсли Не ЗначениеЗаполнено(СтрокаТЧ.АлкогольнаяПродукция)
			И Не ЗначениеЗаполнено(СтрокаТЧ.КодАлкогольнойПродукции)
			И АкцизныеМаркиВызовСервера.ЭтоШтрихкодАкцизнойМарки(СтрокаТЧ.ЗначениеШтрихкода) Тогда
			
			СтрокаТЧ.КодАлкогольнойПродукции = АкцизныеМаркиВызовСервера.КодКлассификатораНоменклатурыЕГАИС(СтрокаТЧ.ЗначениеШтрихкода);
			Если ЗначениеЗаполнено(СтрокаТЧ.СтрокаДерева) Тогда
				СтрокаТЧ.СтрокаДерева.КодАлкогольнойПродукции = СтрокаТЧ.КодАлкогольнойПродукции;
			КонецЕсли;
			
			КодыАлкогольнойПродукции.Добавить(СтрокаТЧ.КодАлкогольнойПродукции);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СоответствиеАлкогольнойПродукции = ИнтеграцияЕГАИС.СоответствиеАлкогольнойПродукции(КодыАлкогольнойПродукции);
	
	Для Каждого СтрокаТЧ Из МаркированныеТовары Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТЧ.АлкогольнаяПродукция)
			И ЗначениеЗаполнено(СтрокаТЧ.КодАлкогольнойПродукции) Тогда
			
			АлкогольнаяПродукция = СоответствиеАлкогольнойПродукции[СтрокаТЧ.КодАлкогольнойПродукции];
			Если ЗначениеЗаполнено(АлкогольнаяПродукция) Тогда
				СтрокаТЧ.АлкогольнаяПродукция = АлкогольнаяПродукция;
				Если ЗначениеЗаполнено(СтрокаТЧ.СтрокаДерева) Тогда
					СтрокаТЧ.СтрокаДерева.АлкогольнаяПродукция = АлкогольнаяПродукция;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Справочники.ШтрихкодыУпаковокТоваров.СоздатьШтрихкодыУпаковок(ДеревоУпаковок, Неопределено, Истина);
	
	Результат = Новый Структура;
	Результат.Вставить("ДеревоУпаковок",      ДеревоУпаковок);
	Результат.Вставить("МаркированныеТовары", МаркированныеТовары);
	
	Возврат Результат;
	
КонецФункции

Функция ПараметрыФормированияТекстаЗапросаВложенныхШтрихкодов() Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ДокументСсылка",                  Неопределено);
	ВозвращаемоеЗначение.Вставить("ИспользоватьИдентификаторСтроки", Ложь);
	ВозвращаемоеЗначение.Вставить("ИмяПоляОрганизацияЕГАИС",         "ОрганизацияЕГАИС");
	ВозвращаемоеЗначение.Вставить("ИмяПоляАлкогольнаяПродукция",     "АлкогольнаяПродукция");
	ВозвращаемоеЗначение.Вставить("ИмяВременнойТаблицы",             "ВТВложенныеШтрихкодыИсходныеДанные");
	ВозвращаемоеЗначение.Вставить("ЗаполнитьСправки2ИзРегистра",     Ложь);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ТекстЗапросаВложенныхШтрихкодовПоДокументу(ПараметрыФормированияТекстаЗапроса) Экспорт
	
	ДокументСсылка                  = ПараметрыФормированияТекстаЗапроса.ДокументСсылка;
	ИспользоватьИдентификаторСтроки = ПараметрыФормированияТекстаЗапроса.ИспользоватьИдентификаторСтроки;
	ИмяПоляОрганизацияЕГАИС         = ПараметрыФормированияТекстаЗапроса.ИмяПоляОрганизацияЕГАИС;
	ИмяПоляАлкогольнаяПродукция     = ПараметрыФормированияТекстаЗапроса.ИмяПоляАлкогольнаяПродукция;
	ИмяВременнойТаблицы             = ПараметрыФормированияТекстаЗапроса.ИмяВременнойТаблицы;
	ЗаполнитьСправки2ИзРегистра     = ПараметрыФормированияТекстаЗапроса.ЗаполнитьСправки2ИзРегистра;
	
	ТекстЗапроса = "";
	Если ЗначениеЗаполнено(ДокументСсылка) Тогда
		
		МетаданныеДокумента = ДокументСсылка.Метаданные();
		РеквизитыТабличнойЧастиАкцизныеМарки = МетаданныеДокумента.ТабличныеЧасти.АкцизныеМарки.Реквизиты;
		
		Если РеквизитыТабличнойЧастиАкцизныеМарки.Найти("ИдентификаторСтроки") <> Неопределено
			И ИспользоватьИдентификаторСтроки Тогда
			ЗначениеРеквизитаИдентификаторСтроки  = "ТабличнаяЧасть.ИдентификаторСтроки";
			ИспользоватьТабличнуюЧастьТовары      = Истина;
		Иначе
			ЗначениеРеквизитаИдентификаторСтроки  = "НЕОПРЕДЕЛЕНО";
			ИспользоватьТабличнуюЧастьТовары      = Ложь;
		КонецЕсли;
		
		Если РеквизитыТабличнойЧастиАкцизныеМарки.Найти("Справка2") <> Неопределено Тогда
			
			ЗначениеРеквизитаСправка2 = "ТабличнаяЧасть.Справка2";
			
			Если ИспользоватьТабличнуюЧастьТовары Тогда
				ЗначениеРеквизитаАлкогольнаяПродукция =
				"ВЫБОР КОГДА ТабличнаяЧасть.Справка2 = ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка) ТОГДА
				|	ЕСТЬNULL(Товары.%ИмяПоляАлкогольнаяПродукция%, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка))
				|ИНАЧЕ
				|	ЕСТЬNULL(ТабличнаяЧасть.Справка2.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка))
				|КОНЕЦ
				|";
			Иначе
				ЗначениеРеквизитаАлкогольнаяПродукция =
				"ВЫБОР КОГДА ТабличнаяЧасть.Справка2 = ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка) ТОГДА
				|	ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)
				|ИНАЧЕ
				|	ЕСТЬNULL(ТабличнаяЧасть.Справка2.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка))
				|КОНЕЦ
				|";
			КонецЕсли;
			
		Иначе
			
			ЗначениеРеквизитаСправка2 = "ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)";
			
			Если ИспользоватьТабличнуюЧастьТовары Тогда
				ЗначениеРеквизитаАлкогольнаяПродукция = "ЕСТЬNULL(Товары.%ИмяПоляАлкогольнаяПродукция%, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка))";
			Иначе
				ЗначениеРеквизитаАлкогольнаяПродукция = "ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)";
			КонецЕсли;
			
		КонецЕсли;
		
		Если РеквизитыТабличнойЧастиАкцизныеМарки.Найти("ШтрихкодУпаковки") <> Неопределено Тогда
			ЗначениеРеквизитаШтрихкодРодительскойУпаковки = "ТабличнаяЧасть.ШтрихкодУпаковки";
		Иначе
			ЗначениеРеквизитаШтрихкодРодительскойУпаковки = "ЗНАЧЕНИЕ(Справочник.ШтрихкодыУпаковокТоваров.ПустаяСсылка)";
		КонецЕсли;
		
		ЗначениеРеквизитаОрганизацияЕГАИС = "ТабличнаяЧасть.Ссылка." + ИмяПоляОрганизацияЕГАИС;
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	%ОрганизацияЕГАИС%             КАК ОрганизацияЕГАИС,
		|	%ИдентификаторСтроки%          КАК ИдентификаторСтроки,
		|	%АлкогольнаяПродукция%         КАК АлкогольнаяПродукция,
		|	%Справка2%                     КАК Справка2,
		|	ТабличнаяЧасть.АкцизнаяМарка   КАК ШтрихкодУпаковки,
		|	%ШтрихкодРодительскойУпаковки% КАК ШтрихкодРодительскойУпаковки
		|ПОМЕСТИТЬ ВТВложенныеШтрихкодыИсходныеДанные
		|ИЗ
		|	%ИмяТабличнойЧасти% КАК ТабличнаяЧасть
		|		%Соединения%
		|ГДЕ
		|	ТабличнаяЧасть.Ссылка = &Ссылка
		|ИНДЕКСИРОВАТЬ ПО
		|	ШтрихкодУпаковки,
		|	ШтрихкодРодительскойУпаковки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяТабличнойЧасти%",            МетаданныеДокумента.ПолноеИмя() + "." + МетаданныеДокумента.ТабличныеЧасти.АкцизныеМарки.Имя);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ОрганизацияЕГАИС%",             ЗначениеРеквизитаОрганизацияЕГАИС);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИдентификаторСтроки%",          ЗначениеРеквизитаИдентификаторСтроки);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%АлкогольнаяПродукция%",         СтрЗаменить(ЗначениеРеквизитаАлкогольнаяПродукция, "%ИмяПоляАлкогольнаяПродукция%", ИмяПоляАлкогольнаяПродукция));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%Справка2%",                     ЗначениеРеквизитаСправка2);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ШтрихкодРодительскойУпаковки%", ЗначениеРеквизитаШтрихкодРодительскойУпаковки);
		
		Если ИспользоватьТабличнуюЧастьТовары Тогда
			ТекстЗапроса = СтрЗаменить(
				ТекстЗапроса, "%Соединения%",
				СтрШаблон(
					"ЛЕВОЕ СОЕДИНЕНИЕ %1.%2 КАК Товары
					|ПО Товары.ИдентификаторСтроки = ТабличнаяЧасть.ИдентификаторСтроки
					|И Товары.ИдентификаторСтроки <> """"
					|",
					МетаданныеДокумента.ПолноеИмя(),
					МетаданныеДокумента.ТабличныеЧасти.Товары.Имя));
		Иначе
			ТекстЗапроса = СтрЗаменить(
				ТекстЗапроса, "%Соединения%", "");
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	ИсходныеДанные.ОрганизацияЕГАИС     КАК ОрганизацияЕГАИС,
	|	ИсходныеДанные.ИдентификаторСтроки  КАК ИдентификаторСтроки,
	|	ИсходныеДанные.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ИсходныеДанные.Справка2             КАК Справка2,
	|	ИсходныеДанные.ШтрихкодУпаковки     КАК Упаковка,
	|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод КАК УпаковкаУровень1,
	|	ВЫБОР КОГДА ТаблицаИдентификаторов.ИдентификаторСтроки ЕСТЬ NULL ТОГДА
	|		ИсходныеДанные.ИдентификаторСтроки
	|	ИНАЧЕ
	|		ТаблицаИдентификаторов.ИдентификаторСтроки
	|	КОНЕЦ КАК ИдентификаторСтрокиУровень1,
	|	ВЫБОР КОГДА ТаблицаИдентификаторов.АлкогольнаяПродукция ЕСТЬ NULL ТОГДА
	|		ИсходныеДанные.АлкогольнаяПродукция
	|	ИНАЧЕ
	|		ТаблицаИдентификаторов.АлкогольнаяПродукция
	|	КОНЕЦ КАК АлкогольнаяПродукцияУровень1,
	|	ВЫБОР КОГДА ТаблицаИдентификаторов.Справка2 ЕСТЬ NULL ТОГДА
	|		ИсходныеДанные.Справка2
	|	ИНАЧЕ
	|		ТаблицаИдентификаторов.Справка2
	|	КОНЕЦ КАК Справка2Уровень1
	|ПОМЕСТИТЬ ВложенныеШтрихкодыУпаковокУровень1
	|ИЗ
	|	&ВТВложенныеШтрихкодыИсходныеДанные КАК ИсходныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ШтрихкодыУпаковокТоваровВложенныеШтрихкоды
	|		ПО ИсходныеДанные.ШтрихкодУпаковки = ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВложенныеШтрихкодыИсходныеДанные КАК ТаблицаИдентификаторов
	|		ПО ТаблицаИдентификаторов.ШтрихкодУпаковки = ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод
	|ГДЕ
	|	ИсходныеДанные.ШтрихкодРодительскойУпаковки = ЗНАЧЕНИЕ(Справочник.ШтрихкодыУпаковокТоваров.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсходныеДанные.ОрганизацияЕГАИС             КАК ОрганизацияЕГАИС,
	|	НЕОПРЕДЕЛЕНО                                КАК ИдентификаторСтроки,
	|	ИсходныеДанные.АлкогольнаяПродукция         КАК АлкогольнаяПродукция,
	|	ИсходныеДанные.Справка2                     КАК Справка2,
	|	ИсходныеДанные.ШтрихкодРодительскойУпаковки КАК Упаковка,
	|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод КАК УпаковкаУровень1,
	|	ВЫБОР КОГДА ТаблицаИдентификаторов.ИдентификаторСтроки ЕСТЬ NULL ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ТаблицаИдентификаторов.ИдентификаторСтроки
	|	КОНЕЦ КАК ИдентификаторСтрокиУровень1,
	|	ВЫБОР КОГДА ТаблицаИдентификаторов.АлкогольнаяПродукция ЕСТЬ NULL ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ТаблицаИдентификаторов.АлкогольнаяПродукция
	|	КОНЕЦ КАК АлкогольнаяПродукцияУровень1,
	|	ВЫБОР КОГДА ТаблицаИдентификаторов.Справка2 ЕСТЬ NULL ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ТаблицаИдентификаторов.Справка2
	|	КОНЕЦ КАК Справка2Уровень1
	|ИЗ
	|	&ВТВложенныеШтрихкодыИсходныеДанные КАК ИсходныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ШтрихкодыУпаковокТоваровВложенныеШтрихкоды
	|		ПО ИсходныеДанные.ШтрихкодРодительскойУпаковки = ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВложенныеШтрихкодыИсходныеДанные КАК ТаблицаИдентификаторов
	|		ПО ТаблицаИдентификаторов.ШтрихкодУпаковки = ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод
	|ГДЕ
	|	ИсходныеДанные.ШтрихкодРодительскойУпаковки <> ЗНАЧЕНИЕ(Справочник.ШтрихкодыУпаковокТоваров.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныеШтрихкодыУпаковокУровень1.ОрганизацияЕГАИС             КАК ОрганизацияЕГАИС,
	|	ВложенныеШтрихкодыУпаковокУровень1.ИдентификаторСтроки          КАК ИдентификаторСтроки,
	|	ВложенныеШтрихкодыУпаковокУровень1.АлкогольнаяПродукция         КАК АлкогольнаяПродукция,
	|	ВложенныеШтрихкодыУпаковокУровень1.Справка2                     КАК Справка2,
	|	ВложенныеШтрихкодыУпаковокУровень1.Упаковка                     КАК Упаковка,
	|	ВложенныеШтрихкодыУпаковокУровень1.УпаковкаУровень1             КАК УпаковкаУровень1,
	|	ВложенныеШтрихкодыУпаковокУровень1.ИдентификаторСтрокиУровень1  КАК ИдентификаторСтрокиУровень1,
	|	ВложенныеШтрихкодыУпаковокУровень1.АлкогольнаяПродукцияУровень1 КАК АлкогольнаяПродукцияУровень1,
	|	ВложенныеШтрихкодыУпаковокУровень1.Справка2Уровень1             КАК Справка2Уровень1,
	|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод             КАК УпаковкаУровень2,
	|	ВЫБОР КОГДА ТаблицаИдентификаторов.ИдентификаторСтроки ЕСТЬ NULL ТОГДА
	|		ВложенныеШтрихкодыУпаковокУровень1.ИдентификаторСтрокиУровень1
	|	ИНАЧЕ
	|		ТаблицаИдентификаторов.ИдентификаторСтроки
	|	КОНЕЦ КАК ИдентификаторСтрокиУровень2,
	|	ВЫБОР КОГДА ТаблицаИдентификаторов.АлкогольнаяПродукция ЕСТЬ NULL ТОГДА
	|		ВложенныеШтрихкодыУпаковокУровень1.АлкогольнаяПродукцияУровень1
	|	ИНАЧЕ
	|		ТаблицаИдентификаторов.АлкогольнаяПродукция
	|	КОНЕЦ КАК АлкогольнаяПродукцияУровень2,
	|	ВЫБОР КОГДА ТаблицаИдентификаторов.Справка2 ЕСТЬ NULL ТОГДА
	|		ВложенныеШтрихкодыУпаковокУровень1.Справка2Уровень1
	|	ИНАЧЕ
	|		ТаблицаИдентификаторов.Справка2
	|	КОНЕЦ КАК Справка2Уровень2
	|ПОМЕСТИТЬ ВложенныеШтрихкодыУпаковокУровень2
	|ИЗ
	|	ВложенныеШтрихкодыУпаковокУровень1 КАК ВложенныеШтрихкодыУпаковокУровень1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ШтрихкодыУпаковокТоваровВложенныеШтрихкоды
	|		ПО ВложенныеШтрихкодыУпаковокУровень1.УпаковкаУровень1 = ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВложенныеШтрихкодыИсходныеДанные КАК ТаблицаИдентификаторов
	|		ПО ТаблицаИдентификаторов.ШтрихкодУпаковки = ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод
	|ИНДЕКСИРОВАТЬ ПО
	|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныеШтрихкодыУпаковокУровень2.ОрганизацияЕГАИС             КАК ОрганизацияЕГАИС,
	|	ВложенныеШтрихкодыУпаковокУровень2.ИдентификаторСтроки          КАК ИдентификаторСтроки,
	|	ВложенныеШтрихкодыУпаковокУровень2.АлкогольнаяПродукция         КАК АлкогольнаяПродукция,
	|	ВложенныеШтрихкодыУпаковокУровень2.Справка2                     КАК Справка2,
	|	ВложенныеШтрихкодыУпаковокУровень2.Упаковка                     КАК Упаковка,
	|	ВложенныеШтрихкодыУпаковокУровень2.УпаковкаУровень1             КАК УпаковкаУровень1,
	|	ВложенныеШтрихкодыУпаковокУровень2.ИдентификаторСтрокиУровень1  КАК ИдентификаторСтрокиУровень1,
	|	ВложенныеШтрихкодыУпаковокУровень2.АлкогольнаяПродукцияУровень1 КАК АлкогольнаяПродукцияУровень1,
	|	ВложенныеШтрихкодыУпаковокУровень2.Справка2Уровень1             КАК Справка2Уровень1,
	|	ВложенныеШтрихкодыУпаковокУровень2.УпаковкаУровень2             КАК УпаковкаУровень2,
	|	ВложенныеШтрихкодыУпаковокУровень2.ИдентификаторСтрокиУровень2  КАК ИдентификаторСтрокиУровень2,
	|	ВложенныеШтрихкодыУпаковокУровень2.АлкогольнаяПродукцияУровень2 КАК АлкогольнаяПродукцияУровень2,
	|	ВложенныеШтрихкодыУпаковокУровень2.Справка2Уровень2             КАК Справка2Уровень2,
	|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод             КАК УпаковкаУровень3,
	|	ВЫБОР КОГДА ТаблицаИдентификаторов.ИдентификаторСтроки ЕСТЬ NULL ТОГДА
	|		ВложенныеШтрихкодыУпаковокУровень2.ИдентификаторСтрокиУровень2
	|	ИНАЧЕ
	|		ТаблицаИдентификаторов.ИдентификаторСтроки
	|	КОНЕЦ КАК ИдентификаторСтрокиУровень3,
	|	ВЫБОР КОГДА ТаблицаИдентификаторов.АлкогольнаяПродукция ЕСТЬ NULL ТОГДА
	|		ВложенныеШтрихкодыУпаковокУровень2.АлкогольнаяПродукцияУровень2
	|	ИНАЧЕ
	|		ТаблицаИдентификаторов.АлкогольнаяПродукция
	|	КОНЕЦ КАК АлкогольнаяПродукцияУровень3,
	|	ВЫБОР КОГДА ТаблицаИдентификаторов.Справка2 ЕСТЬ NULL ТОГДА
	|		ВложенныеШтрихкодыУпаковокУровень2.Справка2Уровень2
	|	ИНАЧЕ
	|		ТаблицаИдентификаторов.Справка2
	|	КОНЕЦ КАК Справка2Уровень3
	|ПОМЕСТИТЬ ВложенныеШтрихкодыУпаковокУровень3
	|ИЗ
	|	ВложенныеШтрихкодыУпаковокУровень2 КАК ВложенныеШтрихкодыУпаковокУровень2
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ШтрихкодыУпаковокТоваровВложенныеШтрихкоды
	|		ПО ВложенныеШтрихкодыУпаковокУровень2.УпаковкаУровень2 = ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВложенныеШтрихкодыИсходныеДанные КАК ТаблицаИдентификаторов
	|		ПО ТаблицаИдентификаторов.ШтрихкодУпаковки = ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод
	|ИНДЕКСИРОВАТЬ ПО
	|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеДанные.ОрганизацияЕГАИС             КАК ОрганизацияЕГАИС,
	|	ИсходныеДанные.ИдентификаторСтроки          КАК ИдентификаторСтроки,
	|	ИсходныеДанные.АлкогольнаяПродукция         КАК АлкогольнаяПродукция,
	|	ИсходныеДанные.Справка2                     КАК Справка2,
	|	ИсходныеДанные.Упаковка                     КАК Упаковка,
	|	ИсходныеДанные.УпаковкаУровень1             КАК УпаковкаУровень1,
	|	ИсходныеДанные.ИдентификаторСтрокиУровень1  КАК ИдентификаторСтрокиУровень1,
	|	ИсходныеДанные.АлкогольнаяПродукцияУровень1 КАК АлкогольнаяПродукцияУровень1,
	|	ИсходныеДанные.Справка2Уровень1             КАК Справка2Уровень1,
	|	ИсходныеДанные.УпаковкаУровень2             КАК УпаковкаУровень2,
	|	ИсходныеДанные.ИдентификаторСтрокиУровень2  КАК ИдентификаторСтрокиУровень2,
	|	ИсходныеДанные.АлкогольнаяПродукцияУровень2 КАК АлкогольнаяПродукцияУровень2,
	|	ИсходныеДанные.Справка2Уровень2             КАК Справка2Уровень2,
	|	ИсходныеДанные.УпаковкаУровень3             КАК УпаковкаУровень3,
	|	ИсходныеДанные.ИдентификаторСтрокиУровень3  КАК ИдентификаторСтрокиУровень3,
	|	ИсходныеДанные.АлкогольнаяПродукцияУровень3 КАК АлкогольнаяПродукцияУровень3,
	|	ИсходныеДанные.Справка2Уровень3             КАК Справка2Уровень3
	|ПОМЕСТИТЬ ДанныеУпаковок
	|ИЗ
	|	ВложенныеШтрихкодыУпаковокУровень3 КАК ИсходныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВложенныеШтрихкодыУпаковокУровень3 КАК ВложенныеШтрихкодыУпаковокУровень1
	|		ПО ИсходныеДанные.Упаковка = ВложенныеШтрихкодыУпаковокУровень1.УпаковкаУровень1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВложенныеШтрихкодыУпаковокУровень3 КАК ВложенныеШтрихкодыУпаковокУровень2
	|		ПО ИсходныеДанные.Упаковка = ВложенныеШтрихкодыУпаковокУровень2.УпаковкаУровень2
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВложенныеШтрихкодыУпаковокУровень3 КАК ВложенныеШтрихкодыУпаковокУровень3
	|		ПО ИсходныеДанные.Упаковка = ВложенныеШтрихкодыУпаковокУровень3.УпаковкаУровень3
	|ГДЕ
	|	ВложенныеШтрихкодыУпаковокУровень1.Упаковка ЕСТЬ NULL
	|	И ВложенныеШтрихкодыУпаковокУровень2.Упаковка ЕСТЬ NULL
	|	И ВложенныеШтрихкодыУпаковокУровень3.Упаковка ЕСТЬ NULL
	|;
	|
	|//#РезультатЗапроса#////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеУпаковок.ОрганизацияЕГАИС                                                         КАК ОрганизацияЕГАИС,
	|	ДанныеУпаковок.Упаковка                                                                 КАК Упаковка,
	|	ДанныеУпаковок.ИдентификаторСтроки                                                      КАК УпаковкаИдентификаторСтроки,
	|	ВЫБОР КОГДА &ЗаполнитьСправки2ИзРегистра ТОГДА
	|		ЕСТЬNULL(СтатусыУпаковки.Справка2, &ПустаяСправка2)
	|	ИНАЧЕ
	|		ДанныеУпаковок.Справка2
	|	КОНЕЦ КАК УпаковкаСправка2,
	|	ДанныеУпаковок.Упаковка.ТипШтрихкода                                                    КАК УпаковкаТипШтрихкода,
	|	ДанныеУпаковок.Упаковка.ТипУпаковки                                                     КАК УпаковкаТипУпаковки,
	|	ДанныеУпаковок.Упаковка.ЗначениеШтрихкода                                               КАК УпаковкаЗначениеШтрихкода,
	|	ДанныеУпаковок.Упаковка.Номенклатура                                                    КАК УпаковкаНоменклатура,
	|	ДанныеУпаковок.Упаковка.Характеристика                                                  КАК УпаковкаХарактеристика,
	|	ДанныеУпаковок.Упаковка.Серия                                                           КАК УпаковкаСерия,
	|	ДанныеУпаковок.Упаковка.ХешСумма                                                        КАК УпаковкаХешСумма,
	|	ВЫБОР
	|		КОГДА &ЗаполнитьСправки2ИзРегистра ТОГДА
	|			ЕСТЬNULL(СтатусыУпаковки.Статус, &ПустойСтатус)
	|		КОГДА ЕСТЬNULL(СтатусыУпаковки.Справка2, &ПустаяСправка2) = ДанныеУпаковок.Справка2 ТОГДА
	|			ЕСТЬNULL(СтатусыУпаковки.Статус, &ПустойСтатус)
	|		ИНАЧЕ
	|			&ОшибкаЧтенияСтатуса
	|	КОНЕЦ КАК УпаковкаСтатус,
	|	ЕСТЬNULL(СтатусыУпаковки.АлкогольнаяПродукция, ДанныеУпаковок.АлкогольнаяПродукция) КАК УпаковкаАлкогольнаяПродукция,
	|
	|	ДанныеУпаковок.УпаковкаУровень1                                                         КАК УпаковкаУровень1,
	|	ДанныеУпаковок.ИдентификаторСтрокиУровень1                                              КАК УпаковкаУровень1ИдентификаторСтроки,
	|	ВЫБОР КОГДА &ЗаполнитьСправки2ИзРегистра ТОГДА
	|		ЕСТЬNULL(СтатусыУпаковкиУровень1.Справка2, &ПустаяСправка2)
	|	ИНАЧЕ
	|		ДанныеУпаковок.Справка2Уровень1
	|	КОНЕЦ КАК УпаковкаУровень1Справка2,
	|	ДанныеУпаковок.УпаковкаУровень1.ТипШтрихкода                                            КАК УпаковкаУровень1ТипШтрихкода,
	|	ДанныеУпаковок.УпаковкаУровень1.ТипУпаковки                                             КАК УпаковкаУровень1ТипУпаковки,
	|	ДанныеУпаковок.УпаковкаУровень1.ЗначениеШтрихкода                                       КАК УпаковкаУровень1ЗначениеШтрихкода,
	|	ДанныеУпаковок.УпаковкаУровень1.Номенклатура                                            КАК УпаковкаУровень1Номенклатура,
	|	ДанныеУпаковок.УпаковкаУровень1.Характеристика                                          КАК УпаковкаУровень1Характеристика,
	|	ДанныеУпаковок.УпаковкаУровень1.Серия                                                   КАК УпаковкаУровень1Серия,
	|	ДанныеУпаковок.УпаковкаУровень1.ХешСумма                                                КАК УпаковкаУровень1ХешСумма,
	|	ВЫБОР
	|		КОГДА &ЗаполнитьСправки2ИзРегистра ТОГДА
	|			ЕСТЬNULL(СтатусыУпаковкиУровень1.Статус, &ПустойСтатус)
	|		КОГДА ЕСТЬNULL(СтатусыУпаковкиУровень1.Справка2, &ПустаяСправка2) = ДанныеУпаковок.Справка2Уровень1 ТОГДА
	|			ЕСТЬNULL(СтатусыУпаковкиУровень1.Статус, &ПустойСтатус)
	|		ИНАЧЕ
	|			&ОшибкаЧтенияСтатуса
	|	КОНЕЦ КАК УпаковкаУровень1Статус,
	|	ЕСТЬNULL(СтатусыУпаковкиУровень1.АлкогольнаяПродукция, ДанныеУпаковок.АлкогольнаяПродукцияУровень1) КАК УпаковкаУровень1АлкогольнаяПродукция,
	|
	|	ДанныеУпаковок.УпаковкаУровень2                                                         КАК УпаковкаУровень2,
	|	ДанныеУпаковок.ИдентификаторСтрокиУровень2                                              КАК УпаковкаУровень2ИдентификаторСтроки,
	|	ВЫБОР КОГДА &ЗаполнитьСправки2ИзРегистра ТОГДА
	|		ЕСТЬNULL(СтатусыУпаковкиУровень2.Справка2, &ПустаяСправка2)
	|	ИНАЧЕ
	|		ДанныеУпаковок.Справка2Уровень2
	|	КОНЕЦ КАК УпаковкаУровень2Справка2,
	|	ДанныеУпаковок.УпаковкаУровень2.ТипШтрихкода                                            КАК УпаковкаУровень2ТипШтрихкода,
	|	ДанныеУпаковок.УпаковкаУровень2.ТипУпаковки                                             КАК УпаковкаУровень2ТипУпаковки,
	|	ДанныеУпаковок.УпаковкаУровень2.ЗначениеШтрихкода                                       КАК УпаковкаУровень2ЗначениеШтрихкода,
	|	ДанныеУпаковок.УпаковкаУровень2.Номенклатура                                            КАК УпаковкаУровень2Номенклатура,
	|	ДанныеУпаковок.УпаковкаУровень2.Характеристика                                          КАК УпаковкаУровень2Характеристика,
	|	ДанныеУпаковок.УпаковкаУровень2.Серия                                                   КАК УпаковкаУровень2Серия,
	|	ДанныеУпаковок.УпаковкаУровень2.ХешСумма                                                КАК УпаковкаУровень2ХешСумма,
	|	ВЫБОР
	|		КОГДА &ЗаполнитьСправки2ИзРегистра ТОГДА
	|			ЕСТЬNULL(СтатусыУпаковкиУровень2.Статус, &ПустойСтатус)
	|		КОГДА ЕСТЬNULL(СтатусыУпаковкиУровень2.Справка2, &ПустаяСправка2) = ДанныеУпаковок.Справка2Уровень2 ТОГДА
	|			ЕСТЬNULL(СтатусыУпаковкиУровень2.Статус, &ПустойСтатус)
	|		ИНАЧЕ
	|			&ОшибкаЧтенияСтатуса
	|	КОНЕЦ КАК УпаковкаУровень2Статус,
	|	ЕСТЬNULL(СтатусыУпаковкиУровень2.АлкогольнаяПродукция, ДанныеУпаковок.АлкогольнаяПродукцияУровень2) КАК УпаковкаУровень2АлкогольнаяПродукция,
	|
	|	ДанныеУпаковок.УпаковкаУровень3                                                         КАК УпаковкаУровень3,
	|	ДанныеУпаковок.ИдентификаторСтрокиУровень3                                              КАК УпаковкаУровень3ИдентификаторСтроки,
	|	ВЫБОР КОГДА &ЗаполнитьСправки2ИзРегистра ТОГДА
	|		ЕСТЬNULL(СтатусыУпаковкиУровень3.Справка2, &ПустаяСправка2)
	|	ИНАЧЕ
	|		ДанныеУпаковок.Справка2Уровень3
	|	КОНЕЦ КАК УпаковкаУровень3Справка2,
	|	ДанныеУпаковок.УпаковкаУровень3.ТипШтрихкода                                            КАК УпаковкаУровень3ТипШтрихкода,
	|	ДанныеУпаковок.УпаковкаУровень3.ТипУпаковки                                             КАК УпаковкаУровень3ТипУпаковки,
	|	ДанныеУпаковок.УпаковкаУровень3.ЗначениеШтрихкода                                       КАК УпаковкаУровень3ЗначениеШтрихкода,
	|	ДанныеУпаковок.УпаковкаУровень3.Номенклатура                                            КАК УпаковкаУровень3Номенклатура,
	|	ДанныеУпаковок.УпаковкаУровень3.Характеристика                                          КАК УпаковкаУровень3Характеристика,
	|	ДанныеУпаковок.УпаковкаУровень3.Серия                                                   КАК УпаковкаУровень3Серия,
	|	ДанныеУпаковок.УпаковкаУровень3.ХешСумма                                                КАК УпаковкаУровень3ХешСумма,
	|	ВЫБОР
	|		КОГДА &ЗаполнитьСправки2ИзРегистра ТОГДА
	|			ЕСТЬNULL(СтатусыУпаковкиУровень3.Статус, &ПустойСтатус)
	|		КОГДА ЕСТЬNULL(СтатусыУпаковкиУровень3.Справка2, &ПустаяСправка2) = ДанныеУпаковок.Справка2Уровень3 ТОГДА
	|			ЕСТЬNULL(СтатусыУпаковкиУровень3.Статус, &ПустойСтатус)
	|		ИНАЧЕ
	|			&ОшибкаЧтенияСтатуса
	|	КОНЕЦ КАК УпаковкаУровень3Статус,
	|	ЕСТЬNULL(СтатусыУпаковкиУровень3.АлкогольнаяПродукция, ДанныеУпаковок.АлкогольнаяПродукцияУровень3) КАК УпаковкаУровень3АлкогольнаяПродукция
	|ИЗ
	|	ДанныеУпаковок КАК ДанныеУпаковок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АкцизныеМаркиЕГАИС КАК СтатусыУпаковки
	|		ПО ДанныеУпаковок.Упаковка = СтатусыУпаковки.АкцизнаяМарка
	|			И (СтатусыУпаковки.ОрганизацияЕГАИС = ДанныеУпаковок.ОрганизацияЕГАИС)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АкцизныеМаркиЕГАИС КАК СтатусыУпаковкиУровень1
	|		ПО ДанныеУпаковок.УпаковкаУровень1 = СтатусыУпаковкиУровень1.АкцизнаяМарка
	|			И (СтатусыУпаковкиУровень1.ОрганизацияЕГАИС = ДанныеУпаковок.ОрганизацияЕГАИС)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АкцизныеМаркиЕГАИС КАК СтатусыУпаковкиУровень2
	|		ПО ДанныеУпаковок.УпаковкаУровень2 = СтатусыУпаковкиУровень2.АкцизнаяМарка
	|			И (СтатусыУпаковкиУровень2.ОрганизацияЕГАИС = ДанныеУпаковок.ОрганизацияЕГАИС)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АкцизныеМаркиЕГАИС КАК СтатусыУпаковкиУровень3
	|		ПО ДанныеУпаковок.УпаковкаУровень3 = СтатусыУпаковкиУровень3.АкцизнаяМарка
	|			И (СтатусыУпаковкиУровень3.ОрганизацияЕГАИС = ДанныеУпаковок.ОрганизацияЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВложенныеШтрихкодыУпаковокУровень1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВложенныеШтрихкодыУпаковокУровень2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВложенныеШтрихкодыУпаковокУровень3
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеУпаковок
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВТВложенныеШтрихкодыИсходныеДанные", ИмяВременнойТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЗаполнитьСправки2ИзРегистра",        Формат(ЗаполнитьСправки2ИзРегистра, "БЛ=ЛОЖЬ; БИ=ИСТИНА"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПустойСтатус",                       "ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.ПустаяСсылка)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОшибкаЧтенияСтатуса",                "ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.ОшибкаЧтенияСтатуса)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПустаяСправка2",                     "ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)");
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ШтрихкодыУпаковокПоДокументу(ПараметрыФормированияТекстаЗапроса) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ПараметрыФормированияТекстаЗапроса.ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстыЗапроса.Добавить(
		ТекстЗапросаВложенныхШтрихкодовПоДокументу(ПараметрыФормированияТекстаЗапроса),
		"ВложенныеШтрихкоды");
	
	РезультатыЗапроса = ГосударственныеИнформационныеСистемы.ВыполнитьПакетЗапросов(Запрос, ТекстыЗапроса);
	
	ВложенныеШтрихкодыУпаковок = ВложенныеШтрихкодыУпаковокПоДокументу(
		РезультатыЗапроса.ВложенныеШтрихкоды.Выбрать(), МенеджерВременныхТаблиц);
	
	Возврат ВложенныеШтрихкодыУпаковок;
	
КонецФункции

Функция ПолучитьСгенерироватьАкцизнуюМарку(КодАкцизнойМарки, Номенклатура, Характеристика, ЗаписьПриОбновленииИБ = ЛОЖЬ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ШтрихкодыУпаковокТоваров.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	|ГДЕ
	|	ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода = &КодАкцизнойМарки
	|;
	|";
	Запрос.УстановитьПараметр("КодАкцизнойМарки", КодАкцизнойМарки);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка
	Иначе
		СправочникОбъект = Справочники.ШтрихкодыУпаковокТоваров.СоздатьЭлемент();
		СправочникОбъект.Номенклатура = Номенклатура;
		СправочникОбъект.Характеристика = Характеристика;
		СправочникОбъект.ЗначениеШтрихкода = КодАкцизнойМарки;
		СправочникОбъект.ТипШтрихкода = Перечисления.ТипыШтрихкодов.PDF417;
		СправочникОбъект.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар;
		
		Попытка
			Если ЗаписьПриОбновленииИБ Тогда
				СправочникОбъект.ОбменДанными.Загрузка = Истина;
			КонецЕсли;
			СправочникОбъект.Записать();
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
		Возврат СправочникОбъект.Ссылка;
		
	КонецЕсли;
	
КонецФункции

Функция СгенерироватьАкцизнуюМарку(КодАкцизнойМарки, Номенклатура, Характеристика, ЗаписьПриОбновленииИБ = ЛОЖЬ) Экспорт
	
	СправочникОбъект = Справочники.ШтрихкодыУпаковокТоваров.СоздатьЭлемент();
	СправочникОбъект.Номенклатура = Номенклатура;
	СправочникОбъект.Характеристика = Характеристика;
	СправочникОбъект.ЗначениеШтрихкода = КодАкцизнойМарки;
	СправочникОбъект.ТипШтрихкода = Перечисления.ТипыШтрихкодов.PDF417;
	СправочникОбъект.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар;
	
	Попытка
		Если ЗаписьПриОбновленииИБ Тогда
			СправочникОбъект.ОбменДанными.Загрузка = Истина;
		КонецЕсли;
		СправочникОбъект.Записать();
	Исключение
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

Функция ПолучитьАкцизнуюМаркуПоКоду(КодАкцизнойМарки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ШтрихкодыУпаковокТоваров.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	|ГДЕ
	|	ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода = &КодАкцизнойМарки
	|;
	|";
	Запрос.УстановитьПараметр("КодАкцизнойМарки", КодАкцизнойМарки);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат = Неопределено;
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка
	КонецЕсли;
	Возврат Результат
КонецФункции

Функция СоздатьШтрихкодУпаковки(ДанныеШтрихкодаУпаковки, ВложенныеШтрихкоды = Неопределено, ДанныеСтатуса = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НовыйЭлемент = Справочники.ШтрихкодыУпаковокТоваров.СоздатьЭлемент();
	
	НовыйЭлемент.Номенклатура   = ДанныеШтрихкодаУпаковки.Номенклатура;
	НовыйЭлемент.Характеристика = ДанныеШтрихкодаУпаковки.Характеристика;
	
	ПараметрыУказанияСерий = ГосударственныеИнформационныеСистемыПереопределяемый.ПараметрыУказанияСерийФормыОбъекта(НовыйЭлемент, Справочники.ШтрихкодыУпаковокТоваров);
	ГосударственныеИнформационныеСистемыПереопределяемый.ЗаполнитьСтатусыУказанияСерий(НовыйЭлемент, ПараметрыУказанияСерий);
	
	НовыйЭлемент.Серия = ДанныеШтрихкодаУпаковки.Серия;
	ЗаполнитьЗначенияСвойств(НовыйЭлемент, ДанныеШтрихкодаУпаковки,,"Номенклатура, Характеристика, Серия");
	
	Если Не ЗначениеЗаполнено(НовыйЭлемент.ТипШтрихкода) Тогда
		
		ПараметрыШтрихкода = ШтрихкодыУпаковокКлиентСервер.ПараметрыШтрихкода(НовыйЭлемент.ЗначениеШтрихкода);
		Если ПараметрыШтрихкода <> Неопределено
			И ПараметрыШтрихкода.Результат <> Неопределено Тогда
			НовыйЭлемент.ТипШтрихкода = ПараметрыШтрихкода.ТипШтрихкода;
		Иначе
			ТипШтрихкодаСтрока = МенеджерОборудованияКлиентСервер.ОпределитьТипШтрихкода(НовыйЭлемент.ЗначениеШтрихкода);
			Если ТипШтрихкодаСтрока = "EAN128" Тогда
				НовыйЭлемент.ТипШтрихкода = Перечисления.ТипыШтрихкодов.GS1_128;
			Иначе
				ЗначениеПеречисления = Метаданные.Перечисления.ТипыШтрихкодов.ЗначенияПеречисления.Найти(ТипШтрихкодаСтрока);
				Если ЗначениеПеречисления <> Неопределено Тогда
					НовыйЭлемент.ТипШтрихкода = ЗначениеПеречисления;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НовыйЭлемент.ДатаУпаковки) Тогда
		НовыйЭлемент.ДатаУпаковки = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ВложенныеШтрихкоды <> Неопределено Тогда
		Для Каждого Штрихкод Из ВложенныеШтрихкоды Цикл
			СтрокаТЧ = НовыйЭлемент.ВложенныеШтрихкоды.Добавить();
			СтрокаТЧ.Штрихкод = Штрихкод;
		КонецЦикла;
	КонецЕсли;
	
	НовыйЭлемент.Записать();
	
	Возврат НовыйЭлемент.Ссылка;
	
КонецФункции

Функция ХешСуммаСодержимогоУпаковки(ДанныеДляВычисления) Экспорт
	
	Если ДанныеДляВычисления.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	ДанныеДляВычисления.СортироватьПоЗначению();
	СтрокаДляРасчетаХеша = СтрСоединить(ДанныеДляВычисления.ВыгрузитьЗначения());
	
	ХешированиеДанныхОбъект = Новый ХешированиеДанных(ХешФункция.SHA256);
	ХешированиеДанныхОбъект.Добавить(СтрокаДляРасчетаХеша);
	Возврат Base64Строка(ХешированиеДанныхОбъект.ХешСумма);
	
КонецФункции

Функция ДанныеДляВычисленияХешСуммы(ШтрихкодУпаковкиТовара) Экспорт
	
	ДанныеДляВычисления = Новый СписокЗначений;
	
	Если ТипЗнч(ШтрихкодУпаковкиТовара) = Тип("СправочникСсылка.ШтрихкодыУпаковокТоваров") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВЫБОР
		|		КОГДА ШтрихкодыУпаковокТоваров.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МаркированныйТовар)
		|			ТОГДА ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода
		|		ИНАЧЕ ШтрихкодыУпаковокТоваров.ХешСумма
		|	КОНЕЦ КАК УчитываемаяСтрока
		|ИЗ
		|	Справочник.ШтрихкодыУпаковокТоваров.ВложенныеШтрихкоды КАК ШтрихкодыУпаковокТоваровВложенныеШтрихкоды
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
		|		ПО ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Штрихкод = ШтрихкодыУпаковокТоваров.Ссылка
		|ГДЕ
		|	ШтрихкодыУпаковокТоваровВложенныеШтрихкоды.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ШтрихкодУпаковкиТовара);
		
	ИначеЕсли ТипЗнч(ШтрихкодУпаковкиТовара) = Тип("СправочникОбъект.ШтрихкодыУпаковокТоваров") Тогда
		
		МассивВложенныхШтрихкодов = Новый Массив;
		
		Для Каждого СтрокаВложенныйШтрихкод Из ШтрихкодУпаковкиТовара.ВложенныеШтрихкоды Цикл
			
			МассивВложенныхШтрихкодов.Добавить(СтрокаВложенныйШтрихкод.Штрихкод);
			
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ШтрихкодыУпаковокТоваров.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МаркированныйТовар)
		|			ТОГДА ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода
		|		ИНАЧЕ ШтрихкодыУпаковокТоваров.ХешСумма
		|	КОНЕЦ КАК УчитываемаяСтрока
		|ИЗ
		|	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
		|ГДЕ
		|	ШтрихкодыУпаковокТоваров.Ссылка В (&МассивВложенныхШтрихкодов)";
		
		Запрос.УстановитьПараметр("МассивВложенныхШтрихкодов", МассивВложенныхШтрихкодов);
		
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеДляВычисления.Добавить(Выборка.УчитываемаяСтрока);
		
	КонецЦикла;
	
	Возврат ДанныеДляВычисления;
	
КонецФункции

// Возвращает параметры для заполнения свойств, связанных с номенклатурой по вложенным штрихкодам.
// Если объект не имеет вложенных штрихкодов, тогда структура заполняется текущими реквизитами объекта.
// Для проверки по вложенным штрихкодам достаточно брать непосредственно вложенные штрихкоды, без анализа
// вложенных уже в них штрихкодов 2ого уровня, т.к. они при записи также проверются по вложенным упаковкам.
// Если во вложенных упаковках упаковки с одной позицией номенклатуры и характеристики, то
// такая упаковка считается монотоварной.
// 
// Параметры:
// 	Объект - СправочникОбъект.ШтрихкодыУпаковокТоваров - объект со свойствами и вложенными товарами
// 	       - ДанныеФормыСтруктура                      - объект формы
//
// Возвращаемое значение:
// 	Структура со свойствами:
// 	 * ТипУпаковки    - ПеречислениеСсылка.ТипыУпаковок
// 	 * Номенклатура   - ОпределяемыйТип.Номенклатура
// 	 * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры
// 	 * Упаковка       - ОпределяемыйТип.Упаковка
// 	 * Серия          - ОпределяемыйТип.СерияНоменклатуры
//
Функция ПараметрыНоменклатурыВложенныхШтрихкодов(Объект) Экспорт
	
	ПараметрыНоменклатуры = Новый Структура;
	ПараметрыНоменклатуры.Вставить("ТипУпаковки",    Объект.ТипУпаковки);
	ПараметрыНоменклатуры.Вставить("Номенклатура",   Объект.Номенклатура);
	ПараметрыНоменклатуры.Вставить("Характеристика", Объект.Характеристика);
	ПараметрыНоменклатуры.Вставить("Упаковка",       Объект.Упаковка);
	ПараметрыНоменклатуры.Вставить("Серия",          Объект.Серия);
	
	Если Объект.ВложенныеШтрихкоды.Количество() > 0 Тогда
		
		// Получаем строки 1ого уровня для выполнения запроса
		СписокШтрихкодовУпаковок = Новый Массив;
		Для каждого Строка Из Объект.ВложенныеШтрихкоды Цикл
			СписокШтрихкодовУпаковок.Добавить(Строка.Штрихкод);
		КонецЦикла;
		
		МетаданныеСправочника = Метаданные.Справочники.ШтрихкодыУпаковокТоваров;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СписокШтрихкодовУпаковок", СписокШтрихкодовУпаковок);
		Запрос.УстановитьПараметр("ПустоеЗначениеНоменклатуры",
			МетаданныеСправочника.Реквизиты.Номенклатура.Тип.ПривестиЗначение(Неопределено));
		Запрос.УстановитьПараметр("ПустоеЗначениеХарактеристики",
			МетаданныеСправочника.Реквизиты.Характеристика.Тип.ПривестиЗначение(Неопределено));
		Запрос.УстановитьПараметр("ПустоеЗначениеУпаковки",
			МетаданныеСправочника.Реквизиты.Упаковка.Тип.ПривестиЗначение(Неопределено));
		Запрос.УстановитьПараметр("ПустоеЗначениеСерии",
			МетаданныеСправочника.Реквизиты.Серия.Тип.ПривестиЗначение(Неопределено));
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МАКСИМУМ(ШтрихкодыУпаковокТоваров.Номенклатура) КАК Номенклатура,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ШтрихкодыУпаковокТоваров.Номенклатура) КАК КоличествоПозицийНоменклатуры,
		|	МАКСИМУМ(ШтрихкодыУпаковокТоваров.Характеристика) КАК Характеристика,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ШтрихкодыУпаковокТоваров.Характеристика) КАК КоличествоПозицийХарактеристик,
		|	МАКСИМУМ(ШтрихкодыУпаковокТоваров.Упаковка) КАК Упаковка,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ШтрихкодыУпаковокТоваров.Упаковка) КАК КоличествоПозицийУпаковок,
		|	МАКСИМУМ(ШтрихкодыУпаковокТоваров.Серия) КАК Серия,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ШтрихкодыУпаковокТоваров.Серия) КАК КоличествоПозицийСерий
		|ПОМЕСТИТЬ СвернутыеЗначения
		|ИЗ
		|	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
		|ГДЕ
		|	ШтрихкодыУпаковокТоваров.Ссылка В(&СписокШтрихкодовУпаковок)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА СвернутыеЗначения.КоличествоПозицийНоменклатуры = 1
		|				И СвернутыеЗначения.КоличествоПозицийХарактеристик = 1
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МонотоварнаяУпаковка)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыУпаковок.МультитоварнаяУпаковка)
		|	КОНЕЦ КАК ТипУпаковки,
		|	ВЫБОР
		|		КОГДА СвернутыеЗначения.КоличествоПозицийНоменклатуры = 1
		|			ТОГДА СвернутыеЗначения.Номенклатура
		|		ИНАЧЕ &ПустоеЗначениеНоменклатуры
		|	КОНЕЦ КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА СвернутыеЗначения.КоличествоПозицийНоменклатуры = 1
		|				И СвернутыеЗначения.КоличествоПозицийХарактеристик = 1
		|			ТОГДА СвернутыеЗначения.Характеристика
		|		ИНАЧЕ &ПустоеЗначениеХарактеристики
		|	КОНЕЦ КАК Характеристика,
		|	ВЫБОР
		|		КОГДА СвернутыеЗначения.КоличествоПозицийНоменклатуры = 1
		|				И СвернутыеЗначения.КоличествоПозицийУпаковок = 1
		|			ТОГДА СвернутыеЗначения.Упаковка
		|		ИНАЧЕ &ПустоеЗначениеУпаковки
		|	КОНЕЦ КАК Упаковка,
		|	ВЫБОР
		|		КОГДА СвернутыеЗначения.КоличествоПозицийНоменклатуры = 1
		|				И СвернутыеЗначения.КоличествоПозицийСерий = 1
		|			ТОГДА СвернутыеЗначения.Серия
		|		ИНАЧЕ &ПустоеЗначениеСерии
		|	КОНЕЦ КАК Серия
		|ИЗ
		|	СвернутыеЗначения КАК СвернутыеЗначения";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ПараметрыНоменклатуры, Выборка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПараметрыНоменклатуры;
	
КонецФункции

#Область Серии

//Имена реквизитов, от значений которых зависят параметры указания серий
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	
	Возврат ГосударственныеИнформационныеСистемыПереопределяемый.ИменаРеквизитовДляЗаполненияПараметровУказанияСерий(Метаданные.Справочники.ШтрихкодыУпаковокТоваров);
	
КонецФункции

// Возвращает параметры указания серий для товаров, указанных в документе.
//
// Параметры:
//  Объект	 - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
// 
// Возвращаемое значение:
//  Структура - состав полей задается в функции ОбработкаТабличнойЧастиКлиентСервер.ПараметрыУказанияСерий.
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	Возврат ГосударственныеИнформационныеСистемыПереопределяемый.ПараметрыУказанияСерий(Метаданные.Справочники.ШтрихкодыУпаковокТоваров, Объект);
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий
//	Параметры:
//		ПараметрыУказанияСерий - Структура - состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий
//	Возвращаемое значение:
//		Строка - текст запроса
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	Возврат ГосударственныеИнформационныеСистемыПереопределяемый.ТекстЗапросаЗаполненияСтатусовУказанияСерий(Метаданные.Справочники.ШтрихкодыУпаковокТоваров, ПараметрыУказанияСерий);

КонецФункции

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	Поля.Добавить("ЗначениеШтрихкода");
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ПустаяСтрока(Данные.ЗначениеШтрихкода) Тогда
		Представление = Данные.ЗначениеШтрихкода;
	Иначе
		Представление = НСтр("ru = 'Штрихкод не указан'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Штрихкоды упаковок
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ШтрихкодыУпаковок";
	КомандаПечати.Представление = НСтр("ru = 'Печать штрихкодов упаковок'");
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую.
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ШтрихкодыУпаковок") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		                                                     "ШтрихкодыУпаковок",
		                                                     НСтр("ru='Штрихкоды упаковок'"),
		                                                     СформироватьПечатнуюФормуШтрихкодыУпаковок(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

// Формирует табличный документ "Акцизные марки".
//
Функция СформироватьПечатнуюФормуШтрихкодыУпаковок(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ШтрихкодыУпаковокТоваров_ШтрихкодыУпаковок";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ШтрихкодыУпаковокТоваров.Ссылка КАК Ссылка,
	|	ШтрихкодыУпаковокТоваров.ТипШтрихкода КАК ТипШтрихкода,
	|	ШтрихкодыУпаковокТоваров.ТипУпаковки КАК ТипУпаковки,
	|	ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода КАК ЗначениеШтрихкода,
	|	ШтрихкодыУпаковокТоваров.Номенклатура КАК Номенклатура,
	|	ШтрихкодыУпаковокТоваров.Характеристика КАК Характеристика,
	|	ШтрихкодыУпаковокТоваров.Упаковка КАК Упаковка,
	|	ШтрихкодыУпаковокТоваров.Серия КАК Серия,
	|	ШтрихкодыУпаковокТоваров.Количество КАК Количество,
	|	ШтрихкодыУпаковокТоваров.ДатаУпаковки КАК ДатаУпаковки
	|ИЗ
	|	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	|ГДЕ
	|	ШтрихкодыУпаковокТоваров.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТипШтрихкода");
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ПараметрыМакетов = ПараметрыМакетовДляПечати();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если НЕ ЗначениеЗаполнено(Выборка.ТипШтрихкода)
			ИЛИ НЕ ЗначениеЗаполнено(Выборка.ЗначениеШтрихкода) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ Выборка.ТипШтрихкода = Перечисления.ТипыШтрихкодов.SSCC
			И НЕ Выборка.ТипШтрихкода = Перечисления.ТипыШтрихкодов.Code128
			И НЕ Выборка.ТипШтрихкода = Перечисления.ТипыШтрихкодов.GS1_128
			И НЕ Выборка.ТипШтрихкода = Перечисления.ТипыШтрихкодов.GS1_DataBarExpandedStacked
			И НЕ Выборка.ТипШтрихкода = Перечисления.ТипыШтрихкодов.PDF417 Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыШтрихкодовУпаковок = ПараметрыШтрихкодовУпаковокДляПечати();
		ЗаполнитьЗначенияСвойств(ПараметрыШтрихкодовУпаковок, Выборка);
		ДобавитьШтрихкодВТабличныйДокумент(ТабличныйДокумент, ПараметрыМакетов, ПараметрыШтрихкодовУпаковок);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПараметрыШтрихкодовУпаковокДляПечати() Экспорт
	
	МетаданныеСправочника = Метаданные.Справочники.ШтрихкодыУпаковокТоваров;
	
	Параметры = Новый Структура;
	Параметры.Вставить("ТипШтрихкода", Перечисления.ТипыШтрихкодов.ПустаяСсылка());
	Параметры.Вставить("ЗначениеШтрихкода", "");
	Параметры.Вставить("ДатаУпаковки", '00010101');
	Параметры.Вставить("Номенклатура", МетаданныеСправочника.Реквизиты.Номенклатура.Тип.ПривестиЗначение(Неопределено));
	Параметры.Вставить("Характеристика", МетаданныеСправочника.Реквизиты.Характеристика.Тип.ПривестиЗначение(Неопределено));
	Параметры.Вставить("Упаковка", МетаданныеСправочника.Реквизиты.Упаковка.Тип.ПривестиЗначение(Неопределено));
	Параметры.Вставить("Серия", МетаданныеСправочника.Реквизиты.Серия.Тип.ПривестиЗначение(Неопределено));
	Параметры.Вставить("Количество", 0);
	
	Возврат Параметры;
КонецФункции

Функция ПараметрыМакетовДляПечати() Экспорт
	Параметры = Новый Структура;
	Параметры.Вставить("Макет_SSCC",    УправлениеПечатью.МакетПечатнойФормы("Справочник.ШтрихкодыУпаковокТоваров.SSCC"));
	Параметры.Вставить("Макет_Code128", УправлениеПечатью.МакетПечатнойФормы("Справочник.ШтрихкодыУпаковокТоваров.Code128"));
	Параметры.Вставить("Макет_GS1_128", УправлениеПечатью.МакетПечатнойФормы("Справочник.ШтрихкодыУпаковокТоваров.GS1_128"));
	Параметры.Вставить("Макет_DataBar", УправлениеПечатью.МакетПечатнойФормы("Справочник.ШтрихкодыУпаковокТоваров.GS1DataBarExpandedStacked"));
	Параметры.Вставить("Макет_PDF417",  УправлениеПечатью.МакетПечатнойФормы("Справочник.ШтрихкодыУпаковокТоваров.PDF417"));
	
	Эталон = УправлениеПечатью.МакетПечатнойФормы("Справочник.ШтрихкодыУпаковокТоваров.Эталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	Параметры.Вставить("КоличествоМиллиметровВПикселе", КоличествоМиллиметровВПикселе);
	
	Возврат Параметры;
	
КонецФункции

Процедура ДобавитьШтрихкодВТабличныйДокумент(ТабличныйДокумент, ПараметрыМакетовДляПечати, ПараметрыШтрихкодовУпаковокДляПечати) Экспорт
	
	ТипШтрихкодаУпаковки = ПараметрыШтрихкодовУпаковокДляПечати.ТипШтрихкода;
	ЗначениеШтрихкода    = ПараметрыШтрихкодовУпаковокДляПечати.ЗначениеШтрихкода;
	ДатаУпаковки         = ПараметрыШтрихкодовУпаковокДляПечати.ДатаУпаковки;
	Номенклатура         = ПараметрыШтрихкодовУпаковокДляПечати.Номенклатура;
	Характеристика       = ПараметрыШтрихкодовУпаковокДляПечати.Характеристика;
	Упаковка             = ПараметрыШтрихкодовУпаковокДляПечати.Упаковка;
	Серия                = ПараметрыШтрихкодовУпаковокДляПечати.Серия;
	Количество           = ПараметрыШтрихкодовУпаковокДляПечати.Количество;
	
	Если Не ЗначениеЗаполнено(ТипШтрихкодаУпаковки)
		Или Не ЗначениеЗаполнено(ЗначениеШтрихкода) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОбласти   = ВозможныеПараметрыОбластиПечатиШтрихкодовПоТипуШтрихкода(ТипШтрихкодаУпаковки);
	ПараметрыОбласти.Вставить("ДатаУпаковки",   ДатаУпаковки);
	ПараметрыОбласти.Вставить("Номенклатура",   Номенклатура);
	ПараметрыОбласти.Вставить("Характеристика", Характеристика);
	ПараметрыОбласти.Вставить("Упаковка",       Упаковка);
	ПараметрыОбласти.Вставить("Серия",          Серия);
	ПараметрыОбласти.Вставить("Количество",     Количество);
	
	ШтрихкодДляКомпоненты = ЗначениеШтрихкода;
	
	ОтображатьТекст    = Истина;
	Если ТипШтрихкодаУпаковки = Перечисления.ТипыШтрихкодов.SSCC Тогда
		
		Макет   = ПараметрыМакетовДляПечати.Макет_SSCC;
		ТипКода = 2;
		Область = Макет.ПолучитьОбласть(Макет.ОбластьПечати.Имя);
		
		ПараметрыШтрихкода = ШтрихкодыУпаковокКлиентСервер.ПараметрыШтрихкодаSSCC(ЗначениеШтрихкода);
		Если Не ПараметрыШтрихкода.Результат = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ПараметрыОбласти, ПараметрыШтрихкода.Результат);
		КонецЕсли;
		
	ИначеЕсли ТипШтрихкодаУпаковки = Перечисления.ТипыШтрихкодов.Code128 Тогда
		
		Макет   = ПараметрыМакетовДляПечати.Макет_Code128;
		ТипКода = 4;
		Область = Макет.ПолучитьОбласть(Макет.ОбластьПечати.Имя);
		
		ПараметрыШтрихкода = ШтрихкодыУпаковокКлиентСервер.ПараметрыШтрихкодаCode128(ЗначениеШтрихкода);
		Если Не ПараметрыШтрихкода.Результат = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ПараметрыОбласти, ПараметрыШтрихкода.Результат);
		КонецЕсли;
		
	ИначеЕсли ТипШтрихкодаУпаковки = Перечисления.ТипыШтрихкодов.GS1_128 Тогда
		
		Макет   = ПараметрыМакетовДляПечати.Макет_GS1_128;
		ТипКода = 2;
		Область = Макет.ПолучитьОбласть(Макет.ОбластьПечати.Имя);
		
		Если СтрНайти(ЗначениеШтрихкода, "(") Тогда
			ПараметрыШтрихкода = ШтрихкодыУпаковокКлиентСервер.ПараметрыШтрихкодаGS1СоСкобками(ЗначениеШтрихкода);
		Иначе
			ПараметрыШтрихкода = ШтрихкодыУпаковокКлиентСервер.ПараметрыШтрихкодаGS1(ЗначениеШтрихкода);
		КонецЕсли;
		Если Не ПараметрыШтрихкода.Результат = Неопределено Тогда
			
			Для каждого СвойстваПараметра Из ПараметрыШтрихкода.Результат Цикл
				ИмяПараметра = "ИдентификаторПрименения_" + СвойстваПараметра.КлючИдентификатора;
				ПараметрыОбласти.Вставить(ИмяПараметра, СвойстваПараметра.Значение);
			КонецЦикла;
			
			ШтрихкодДляКомпоненты = ШтрихкодыУпаковокКлиентСервер.ШтрихкодGS1(
				ПараметрыШтрихкода.Результат,
				Истина,
				ШтрихкодыУпаковокКлиентСервер.СимволОкончанияСтрокиПеременнойДлины());
			
		КонецЕсли;
		
	ИначеЕсли ТипШтрихкодаУпаковки = Перечисления.ТипыШтрихкодов.GS1_DataBarExpandedStacked Тогда
		
		Макет   = ПараметрыМакетовДляПечати.Макет_DataBar;
		ТипКода = 17;
		Область = Макет.ПолучитьОбласть(Макет.ОбластьПечати.Имя);
		
		Если СтрНайти(ЗначениеШтрихкода, "(") Тогда
			ПараметрыШтрихкода = ШтрихкодыУпаковокКлиентСервер.ПараметрыШтрихкодаGS1СоСкобками(ЗначениеШтрихкода);
		Иначе
			ПараметрыШтрихкода = ШтрихкодыУпаковокКлиентСервер.ПараметрыШтрихкодаGS1(ЗначениеШтрихкода);
		КонецЕсли;
		Если Не ПараметрыШтрихкода.Результат = Неопределено Тогда
			
			Для каждого СвойстваПараметра Из ПараметрыШтрихкода.Результат Цикл
				ПараметрыОбласти.Вставить(СвойстваПараметра.ИмяИдентификатора, СвойстваПараметра.Значение);
			КонецЦикла;
			
			ШтрихкодДляКомпоненты = ШтрихкодыУпаковокКлиентСервер.ШтрихкодGS1(
				ПараметрыШтрихкода.Результат,
				Истина,
				ШтрихкодыУпаковокКлиентСервер.СимволОкончанияСтрокиПеременнойДлины());
			
		КонецЕсли;
		
	ИначеЕсли ТипШтрихкодаУпаковки = Перечисления.ТипыШтрихкодов.PDF417 Тогда
		
		Макет   = ПараметрыМакетовДляПечати.Макет_PDF417;
		ТипКода = 6;
		Область = Макет.ПолучитьОбласть(Макет.ОбластьПечати.Имя);
		ОтображатьТекст = Ложь;
		
	КонецЕсли;
	
	ПараметрыОбласти.ПредставлениеНоменклатуры = ИнтеграцияЕГАИСПереопределяемый.ПредставлениеНоменклатуры(
		Номенклатура,
		Характеристика,
		Упаковка);
	
	ЗаполнитьЗначенияСвойств(Область.Параметры, ПараметрыОбласти);
	
	Рисунок = Область.Рисунки.ШтрихкодПечать;
	
	КоличествоМиллиметровВПикселе = ПараметрыМакетовДляПечати.КоличествоМиллиметровВПикселе;
	
	ПараметрыШтрихкода = Новый Структура;
	ПараметрыШтрихкода.Вставить("Ширина",          Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
	ПараметрыШтрихкода.Вставить("Высота",          Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
	ПараметрыШтрихкода.Вставить("Штрихкод",        СокрЛП(ШтрихкодДляКомпоненты));
	ПараметрыШтрихкода.Вставить("ТипКода",         ТипКода);
	ПараметрыШтрихкода.Вставить("ОтображатьТекст", ОтображатьТекст);
	
	Если ТипШтрихкодаУпаковки = Перечисления.ТипыШтрихкодов.GS1_DataBarExpandedStacked Тогда
		ПараметрыШтрихкода.Вставить("GS1DatabarКоличествоСтрок", 2);
	КонецЕсли;
	
	Рисунок.Картинка = МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(ПараметрыШтрихкода);
	
	ТабличныйДокумент.Вывести(Область);
	ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
КонецПроцедуры

Функция ВозможныеПараметрыОбластиПечатиШтрихкодовПоТипуШтрихкода(ТипШтрихкодаУпаковки)
	
	ПараметрыОбласти = Новый Структура;
	
	ПараметрыОбласти.Вставить("ПредставлениеНоменклатуры", "");
	
	//Если ТипШтрихкодаУпаковки = Перечисления.ТипыШтрихкодов.Code128 Тогда
	//	ПараметрыОбласти.Вставить("ИдентификаторОрганизации", "");
	//	ПараметрыОбласти.Вставить("ДатаМаркировки", '00010101');
	//	ПараметрыОбласти.Вставить("НомерПоПорядку", 0);
	//ИначеЕсли ТипШтрихкодаУпаковки = Перечисления.ТипыШтрихкодов.GS1_128
	//	ИЛИ ТипШтрихкодаУпаковки = Перечисления.ТипыШтрихкодов.GS1_DataBarExpandedStacked Тогда
	//	
	//	СвойстваИдентификаторов = ШтрихкодыУпаковокКлиентСерверПовтИсп.СвойстваКлючейИдентификаторовПрименения();
	//	Для каждого КлючИЗначение Из СвойстваИдентификаторов Цикл
	//		
	//		ИмяПараметра = КлючИЗначение.Значение.ИмяИдентификатора;
	//		ТипПараметра = КлючИЗначение.Значение.БазовыйТипДанных;
	//		ЗначениеПоУмолчанию = ТипПараметра.ПривестиЗначение(Неопределено);
	//		ПараметрыОбласти.Вставить(ИмяПараметра, ЗначениеПоУмолчанию);
	//		
	//	КонецЦикла; 
	//	
	//ИначеЕсли ТипШтрихкодаУпаковки = Перечисления.ТипыШтрихкодов.SSCC Тогда
	//	ПараметрыОбласти.Вставить("ЦифраРасширения", 0);
	//	ПараметрыОбласти.Вставить("ПрефиксКомпанииGS1", 0);
	//	ПараметрыОбласти.Вставить("СерийныйНомерSSCC", 0);
	//	ПараметрыОбласти.Вставить("КонтрольноеЧисло", 0);
	//ИначеЕсли ТипШтрихкодаУпаковки = Перечисления.ТипыШтрихкодов.PDF417 Тогда
	//	ПараметрыОбласти.Вставить("СерияНомерМарки", "");
	//КонецЕсли;
	КолонкиПечати = ШтрихкодыУпаковокКлиентСерверПовтИсп.ТипыКолонокПечатиПоТипуШтрихкода(ТипШтрихкодаУпаковки);
	Для каждого ИдентификаторИтипКолонки Из КолонкиПечати Цикл
		ИдентификаторКолонки = ИдентификаторИтипКолонки.Ключ;
		ТипКолонки = ИдентификаторИтипКолонки.Значение;
		ЗначениеПоУмолчанию = ТипКолонки.ПривестиЗначение(Неопределено);
		
		Если ТипШтрихкодаУпаковки = Перечисления.ТипыШтрихкодов.GS1_128
			ИЛИ ТипШтрихкодаУпаковки = Перечисления.ТипыШтрихкодов.GS1_DataBarExpandedStacked Тогда
			ИмяКолонки = "ИдентификаторПрименения_" + ИдентификаторКолонки;
		Иначе
			ИмяКолонки = ИдентификаторКолонки;
		КонецЕсли;
		
		ПараметрыОбласти.Вставить(ИмяКолонки, ЗначениеПоУмолчанию);
	КонецЦикла;
	
	Возврат ПараметрыОбласти;
	
КонецФункции

#КонецОбласти

Процедура ЗаполнитьСтрокуДереваУпаковок(СтрокаДерева, Выборка, Уровень, МаркированныеТовары, ДополнительныеПараметры)
	
	ТекстУровень = ?(Уровень = 0, "", "Уровень" + Уровень);
	
	СтрокаДерева.ОрганизацияЕГАИС        = Выборка.ОрганизацияЕГАИС;
	СтрокаДерева.ШтрихкодУпаковки        = Выборка["Упаковка" + ТекстУровень];
	СтрокаДерева.ИдентификаторСтроки     = Выборка["Упаковка" + ТекстУровень + "ИдентификаторСтроки"];
	СтрокаДерева.ТипУпаковки             = Выборка["Упаковка" + ТекстУровень + "ТипУпаковки"];
	СтрокаДерева.ТипШтрихкода            = Выборка["Упаковка" + ТекстУровень + "ТипШтрихкода"];
	СтрокаДерева.ЗначениеШтрихкода       = Выборка["Упаковка" + ТекстУровень + "ЗначениеШтрихкода"];
	СтрокаДерева.Номенклатура            = Выборка["Упаковка" + ТекстУровень + "Номенклатура"];
	СтрокаДерева.Характеристика          = Выборка["Упаковка" + ТекстУровень + "Характеристика"];
	СтрокаДерева.Серия                   = Выборка["Упаковка" + ТекстУровень + "Серия"];
	СтрокаДерева.ХешСумма                = Выборка["Упаковка" + ТекстУровень + "ХешСумма"];
	СтрокаДерева.Статус                  = Выборка["Упаковка" + ТекстУровень + "Статус"];
	СтрокаДерева.Справка2                = Выборка["Упаковка" + ТекстУровень + "Справка2"];
	СтрокаДерева.АлкогольнаяПродукция    = Выборка["Упаковка" + ТекстУровень + "АлкогольнаяПродукция"];
	
	Если СтрокаДерева.ТипУпаковки = Перечисления.ТипыУпаковок.МаркированныйТовар Тогда
		
		Если Не ЗначениеЗаполнено(СтрокаДерева.АлкогольнаяПродукция)
			И Не ЗначениеЗаполнено(СтрокаДерева.КодАлкогольнойПродукции)
			И АкцизныеМаркиВызовСервера.ЭтоШтрихкодАкцизнойМарки(СтрокаДерева.ЗначениеШтрихкода) Тогда
			СтрокаДерева.КодАлкогольнойПродукции = АкцизныеМаркиВызовСервера.КодКлассификатораНоменклатурыЕГАИС(СтрокаДерева.ЗначениеШтрихкода);
		КонецЕсли;
		
		СтрокаТаблицы = МаркированныеТовары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаДерева);
		
		СтрокаТаблицы.СтрокаДерева           = СтрокаДерева;
		СтрокаТаблицы.УпаковкаВерхнегоУровня = СтрокаДереваВерхнегоУровня(СтрокаДерева, ДополнительныеПараметры.УпаковкиВерхнегоУровня);
		
	КонецЕсли;
	
КонецПроцедуры

Функция СтрокаДереваВерхнегоУровня(СтрокаДерева, УпаковкиВерхнегоУровня)
	
	УпаковкаВерхнегоУровня = УпаковкиВерхнегоУровня.Получить(СтрокаДерева);
	Если УпаковкаВерхнегоУровня <> Неопределено Тогда
		Возврат УпаковкаВерхнегоУровня;
	КонецЕсли;
	
	Если СтрокаДерева.Родитель <> Неопределено Тогда
		
		УпаковкаВерхнегоУровня = СтрокаДереваВерхнегоУровня(СтрокаДерева.Родитель, УпаковкиВерхнегоУровня);
		
		УпаковкиВерхнегоУровня.Вставить(СтрокаДерева, УпаковкаВерхнегоУровня);
		
		Возврат УпаковкаВерхнегоУровня;
		
	Иначе
		
		Возврат СтрокаДерева;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли
