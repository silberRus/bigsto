
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Номенклатура = Параметры.Номенклатура;
	ЗаполнитьСписок();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_СоглашенияСПоставщиками" Тогда
		ЗаполнитьСписок()
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.СписокДействиеПредставление Тогда
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		Гиперссылка = ТекущиеДанные.Действие;
		
		Если Гиперссылка = "СоглашенияВыбрать"
			Или Гиперссылка = "СписокСоглашенийПоХарактеристикам" Тогда
				
			ОтборСоглашений = Новый Структура;
			ОтборСоглашений.Вставить("Статус", ПредопределенноеЗначение("Перечисление.СтатусыСоглашенийСПоставщиками.Действует"));
			ОтборСоглашений.Вставить("ХозяйственнаяОперация", ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ОказаниеАгентскихУслуг"));
			ОтборСоглашений.Вставить("Партнер", ТекущиеДанные.Принципал);
			
			МассивКонтрагентовДляОтбора = Новый Массив;
			МассивКонтрагентовДляОтбора.Добавить(ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
			МассивКонтрагентовДляОтбора.Добавить(ТекущиеДанные.Контрагент);
			
			ОтборСоглашений.Вставить("Контрагент", Новый ФиксированныйМассив(МассивКонтрагентовДляОтбора));
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Отбор", ОтборСоглашений);                         
			ПараметрыФормы.Вставить("ВключитьОтборПоАгенсткойУслуге", Гиперссылка = "СписокСоглашенийПоХарактеристикам");
			
			ПараметрыСоздания = Новый Структура;
			ПараметрыСоздания.Вставить("АгентскаяУслугаНоменклатура", Номенклатура);
			ПараметрыСоздания.Вставить("АгентскаяУслугаХарактеристика", ТекущиеДанные.Характеристика);
			ПараметрыФормы.Вставить("ДополнительныеПараметры", ПараметрыСоздания);
			
			ПараметрыПереходаПоГиперссылке = Новый Структура;
			ПараметрыПереходаПоГиперссылке.Вставить("ИмяФормы", "Справочник.СоглашенияСПоставщиками.ФормаСписка");
			ПараметрыПереходаПоГиперссылке.Вставить("ПараметрыФормы",ПараметрыФормы);
			
		ИначеЕсли Гиперссылка = "СоглашенияСоздать" Тогда
			
			ЗначенияЗаполненияСоглашений = Новый Структура;
			ЗначенияЗаполненияСоглашений.Вставить("Партнер", ТекущиеДанные.Принципал);
			ЗначенияЗаполненияСоглашений.Вставить("Статус",  ПредопределенноеЗначение("Перечисление.СтатусыСоглашенийСПоставщиками.Действует"));
			ЗначенияЗаполненияСоглашений.Вставить("ХозяйственнаяОперация", ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ОказаниеАгентскихУслуг"));
			ЗначенияЗаполненияСоглашений.Вставить("Контрагент", ТекущиеДанные.Контрагент);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполненияСоглашений);
			
			ПараметрыСоздания = Новый Структура;
			ПараметрыСоздания.Вставить("АгентскаяУслугаНоменклатура", Номенклатура);
			ПараметрыСоздания.Вставить("АгентскаяУслугаХарактеристика", ТекущиеДанные.Характеристика);
			ПараметрыФормы.Вставить("ДополнительныеПараметры", ПараметрыСоздания);
			
			ПараметрыПереходаПоГиперссылке = Новый Структура;
			ПараметрыПереходаПоГиперссылке.Вставить("ИмяФормы", "Справочник.СоглашенияСПоставщиками.ФормаОбъекта");
			ПараметрыПереходаПоГиперссылке.Вставить("ПараметрыФормы",ПараметрыФормы);
			
		Иначе
			ТекстИсключения = НСтр("ru = 'Неопределено действие перехода по гиперссылке.'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		ОткрытьФорму(ПараметрыПереходаПоГиперссылке.ИмяФормы,
			ПараметрыПереходаПоГиперссылке.ПараметрыФормы, , ЭтаФорма.УникальныйИдентификатор, , , ,
			ЭтаФорма.РежимОткрытияОкна);
			
	ИначеЕсли Поле = Элементы.СписокПринципал Тогда
			
		ПоказатьЗначение(,Элементы.Список.ТекущиеДанные.Принципал);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФукнции

&НаСервере
Процедура ЗаполнитьСписок()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СпрНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)
	|			ТОГДА СпрНоменклатура.Ссылка
	|		КОГДА СпрНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры)
	|			ТОГДА СпрНоменклатура.ВидНоменклатуры
	|		КОГДА СпрНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры)
	|			ТОГДА СпрНоменклатура.ВладелецХарактеристик
	|	КОНЕЦ КАК Ссылка
	|ПОМЕСТИТЬ ВладелецХарактеристик
	|ИЗ
	|	Справочник.Номенклатура КАК СпрНоменклатура
	|ГДЕ
	|	СпрНоменклатура.Ссылка = &Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Ссылка КАК Характеристика,
	|	ХарактеристикиНоменклатуры.Принципал КАК Принципал,
	|	ХарактеристикиНоменклатуры.Контрагент КАК Контрагент,
	|	МАКСИМУМ(НЕ СоглашенияСПоставщиками.Ссылка ЕСТЬ NULL) КАК ЕстьСоглашения,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ ТЧАгентскиеУслуги.Ссылка ЕСТЬ NULL
	|					ИЛИ НЕ СоглашенияСПоставщиками.ИспользоватьУказанныеАгентскиеУслуги
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоСоглашений,
	|	МАКСИМУМ(НЕ ТЧАгентскиеУслуги.Ссылка ЕСТЬ NULL
	|			ИЛИ НЕ ЕСТЬNULL(СоглашенияСПоставщиками.ИспользоватьУказанныеАгентскиеУслуги, ИСТИНА)) КАК ЕстьСоглашениеПоХарактеристики
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСПоставщиками КАК СоглашенияСПоставщиками
	|		ПО ХарактеристикиНоменклатуры.Принципал = СоглашенияСПоставщиками.Партнер
	|			И (ХарактеристикиНоменклатуры.Контрагент = СоглашенияСПоставщиками.Контрагент
	|				ИЛИ СоглашенияСПоставщиками.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|			И (СоглашенияСПоставщиками.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийСПоставщиками.Действует))
	|			И (СоглашенияСПоставщиками.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОказаниеАгентскихУслуг))
	|			И (СоглашенияСПоставщиками.ДатаНачалаДействия = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ СоглашенияСПоставщиками.ДатаНачалаДействия  <= &ТекущаяДата)
	|			И (СоглашенияСПоставщиками.ДатаОкончанияДействия = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ СоглашенияСПоставщиками.ДатаОкончанияДействия >= &ТекущаяДата)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСПоставщиками.АгентскиеУслуги КАК ТЧАгентскиеУслуги
	|		ПО (СоглашенияСПоставщиками.Ссылка = ТЧАгентскиеУслуги.Ссылка)
	|			И (ТЧАгентскиеУслуги.Номенклатура = &Номенклатура)
	|			И (ТЧАгентскиеУслуги.Характеристика = ХарактеристикиНоменклатуры.Ссылка)
	|ГДЕ
	|	ХарактеристикиНоменклатуры.Владелец В
	|			(ВЫБРАТЬ
	|				ВладелецХарактеристик.Ссылка
	|			ИЗ
	|				ВладелецХарактеристик)
	|	И НЕ ХарактеристикиНоменклатуры.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	ХарактеристикиНоменклатуры.Ссылка,
	|	ХарактеристикиНоменклатуры.Принципал,
	|	ХарактеристикиНоменклатуры.Контрагент
	|
	|УПОРЯДОЧИТЬ ПО
	|	ХарактеристикиНоменклатуры.Представление";
	
	Запрос.УстановитьПараметр("Номенклатура", Параметры.Номенклатура);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());

	Соглашения = Запрос.Выполнить().Выгрузить();
	
	Список.Очистить();
	
	Для Каждого СтрТабл из Соглашения Цикл
		
		НоваяСтрока = Список.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрТабл);
		
		Если Не СтрТабл.ЕстьСоглашения Тогда
			ГиперссылкаСоглашения = "СоглашенияСоздать";
			ЗаголовокГиперссылки = НСтр("ru = 'Создать соглашение'");
		ИначеЕсли Не СтрТабл.ЕстьСоглашениеПоХарактеристики Тогда
			ГиперссылкаСоглашения = "СоглашенияВыбрать";
			ЗаголовокГиперссылки = НСтр("ru = 'Выбрать в существующем соглашении'");
		Иначе
			СтрокаСоглашений = СтрокаСЧислом(";%1 соглашении;;%1 соглашениях;%1 соглашениях;%1 соглашениях",
												СтрТабл.КоличествоСоглашений,
												ВидЧисловогоЗначения.Количественное,
												"L=ru");
			
			ЗаголовокГиперссылки = НСтр("ru = 'Выбрано в %КоличествоСоглашений%'");
			ЗаголовокГиперссылки = СтрЗаменить(ЗаголовокГиперссылки, "%КоличествоСоглашений%", СтрокаСоглашений);
			
			ГиперссылкаСоглашения = "СписокСоглашенийПоХарактеристикам";
		КонецЕсли;
		
		НоваяСтрока.Действие = ГиперссылкаСоглашения;
		НоваяСтрока.ДействиеПредставление =  ЗаголовокГиперссылки;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
