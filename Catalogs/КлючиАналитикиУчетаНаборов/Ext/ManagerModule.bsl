#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура устанавливает пометку на удаление для найденных элементов справочника.
//
// Параметры:
//	СтруктураПараметров - Структура - Параметры выбора элементов справочника
//	ПометкаУдаления - Булево - Признак установки пометки на удаление
//
Процедура УстановитьПометкуУдаления(СтруктураПараметров, ПометкаУдаления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапроса = "ВЫБРАТЬ
		|	Таблица.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаНаборов КАК Таблица
		|ГДЕ
		|	Таблица.ПометкаУдаления <> &ПометкаУдаления
		|";
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПометкаУдаления", ПометкаУдаления);
	
	Если СтруктураПараметров.Свойство("НоменклатураНабора") Тогда
		Запрос.УстановитьПараметр("НоменклатураНабора", СтруктураПараметров.НоменклатураНабора);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.НоменклатураНабора = &НоменклатураНабора";
	КонецЕсли;
	Если СтруктураПараметров.Свойство("ХарактеристикаНабора") Тогда
		Запрос.УстановитьПараметр("ХарактеристикаНабора", СтруктураПараметров.ХарактеристикаНабора);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.ХарактеристикаНабора = &ХарактеристикаНабора";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Выборка.Ссылка.ПолучитьОбъект().УстановитьПометкуУдаления(ПометкаУдаления);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаменаДублейКлючейАналитики

Процедура ЗаменитьДублиКлючейАналитики() Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
		|	ДанныеСправочника.Ссылка КАК Ссылка,
		|	ДанныеСправочника.ПометкаУдаления КАК ПометкаУдаления,
		|	Аналитика.КлючАналитики КАК КлючАналитики
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаНаборов КАК ДанныеСправочника
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.АналитикаУчетаНаборов КАК ДанныеРегистра
		|	ПО
		|		ДанныеСправочника.Ссылка = ДанныеРегистра.КлючАналитики
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		РегистрСведений.АналитикаУчетаНаборов КАК Аналитика
		|	ПО
		|		ДанныеСправочника.НоменклатураНабора     = Аналитика.НоменклатураНабора
		|		И ДанныеСправочника.ХарактеристикаНабора = Аналитика.ХарактеристикаНабора
		|ГДЕ
		|	ДанныеРегистра.КлючАналитики ЕСТЬ NULL
		|");
	
	// Сформируем соответствие ключей аналитики.
	СоответствиеАналитик = Новый Соответствие;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СоответствиеАналитик.Вставить(Выборка.Ссылка, Выборка.КлючАналитики);
			
			Если Не Выборка.ПометкаУдаления Тогда
				СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
				Попытка
					СправочникОбъект.УстановитьПометкуУдаления(Истина, Ложь);
				Исключение
					ВызватьИсключение;
				КонецПопытки;
			КонецЕсли;
			
		КонецЦикла;
		ОбщегоНазначенияУТ.ЗаменитьСсылки(СоответствиеАналитик);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли