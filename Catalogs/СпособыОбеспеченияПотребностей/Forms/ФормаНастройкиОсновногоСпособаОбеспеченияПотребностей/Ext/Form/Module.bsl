&НаКлиенте
Перем ВыполняетсяЗакрытие;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ПрочитатьОсновнойСпособОбеспеченияПотребностей();

	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли; 

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И ЗавершениеРаботы Тогда
		Отказ = Истина;
		ТекстПредупреждения = НСтр("ru = 'Данные были изменены. Все изменения будут потеряны.'");
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект), НСтр("ru = 'Данные изменены. Сохранить?'"),РежимДиалогаВопрос.ДаНетОтмена);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    ОтветНаВопрос = РезультатВопроса;
    
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		ВыполняетсяЗакрытие = Истина;
        ЗаписатьОсновнойСпособОбеспеченияПотребностей();
		Закрыть();
    ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
        ВыполняетсяЗакрытие = Истина;
		Модифицированность = Ложь;
		Закрыть();
    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить()

	ЗаписатьОсновнойСпособОбеспеченияПотребностей();
	Оповестить("ЗакрытиеФормыНастройкиОсновногоСпособа",, ЭтаФорма);
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьВыполнить()
	
	ЗаписатьОсновнойСпособОбеспеченияПотребностей();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура ЗаписатьОсновнойСпособОбеспеченияПотребностей()
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Константы.ОсновнойСпособОбеспеченияПотребностей КАК ОсновнойСпособОбеспеченияПотребностей
		|ИЗ
		|	Константы КАК Константы
		|");
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если ОсновнойСпособОбеспеченияПотребностей <> Выборка.ОсновнойСпособОбеспеченияПотребностей Тогда
			Константы.ОсновнойСпособОбеспеченияПотребностей.Установить(ОсновнойСпособОбеспеченияПотребностей);
		КонецЕсли;
	КонецЕсли;
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьОсновнойСпособОбеспеченияПотребностей()
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Константы.ОсновнойСпособОбеспеченияПотребностей КАК ОсновнойСпособОбеспеченияПотребностей
		|ИЗ
		|	Константы КАК Константы
		|");
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ОсновнойСпособОбеспеченияПотребностей  = Выборка.ОсновнойСпособОбеспеченияПотребностей;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#Область Инициализация

ВыполняетсяЗакрытие = Ложь;

#КонецОбласти

