#Область СлужебныйПрограммныйИнтерфейс

// Добавляет в форму настроек обмена с сайтом реквизиты
// необходимые для выгрузки товаров и загрузки заказов.
//
// Параметры:
//  Форма - управляемаяФорма - форму узла плана обмена "Обмен с сайтом".
//
Процедура ДобавитьРеквизитыФормыНастройки(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	ТипБулево = Новый ОписаниеТипов("Булево");
	ТипДата = Новый ОписаниеТипов("Дата");
	ТипСтрока = Новый ОписаниеТипов("Строка");
	
	ТипСклад = Новый ОписаниеТипов("СправочникСсылка.Склады");
	ТипСоглашение = Новый ОписаниеТипов("СправочникСсылка.СоглашенияСКлиентами");
	ТипОрганизация = Новый ОписаниеТипов("СправочникСсылка.Организации");
	ТипПользователи = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
	ТипПричина = Новый ОписаниеТипов("СправочникСсылка.ПричиныОтменыЗаказовКлиентов");
	ТипГруппаДоступа = Новый ОписаниеТипов("СправочникСсылка.ГруппыДоступаПартнеров");
	ТипВидНоменклатуры = Новый ОписаниеТипов("СправочникСсылка.ВидыНоменклатуры");
	ТипЕдиницаИзмерения = Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения");
	ТипГруппаНоменклатуры = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	
	ТипСведения = Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения");
	
	ТипСоответствиеСтатусов = Новый ОписаниеТипов("ТаблицаЗначений");
	ТипСтатусЗаказаВБазе = Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыЗаказовКлиентов");
	ТипСпособИдентификации = Новый ОписаниеТипов("ПеречислениеСсылка.СпособыИдентификацииКонтрагентов");
	
	РеквизитформаОткрыта = Новый РеквизитФормы("ВыгружатьФайлы", ТипБулево);
	
	РеквизитВыгружатьПрочиеФайлы = Новый РеквизитФормы("ВыгружатьФайлы", ТипБулево,,НСтр("ru = 'Прочие файлы'"),Истина);
	РеквизитДатаНаСайте = Новый РеквизитФормы("ДополнительныйРеквизитЗаказаКлиентаДатаЗаказаНаСайте",
		ТипСведения,,НСтр("ru = 'Дата заказа на сайте'"),Истина);
	РеквизитНомерНаСайте = Новый РеквизитФормы("ДополнительныйРеквизитЗаказаКлиентаНомерЗаказаНаСайте",
		ТипСведения,,НСтр("ru = 'Номер заказа на сайте'"),Истина);
	РеквизитСоглашение = Новый РеквизитФормы("Соглашение", ТипСоглашение,,"Соглашение",Истина);
	РеквизитОрганизация = Новый РеквизитФормы("Организация", ТипОрганизация,,"Организация",Истина);
	РеквизитСклад = Новый РеквизитФормы("Склад", ТипСклад,,"Склад",Истина);
	РеквизитМенеджер = Новый РеквизитФормы("Менеджер", ТипПользователи,,НСтр("ru = 'Менеджер'"),Истина);
	РеквизитПричинаОтмены = Новый РеквизитФормы("ПричинаОтменыЗаказа", ТипПричина,,НСтр("ru = 'Причина отмены заказа'"),Истина);
	РеквизитСпособИдентификации = Новый РеквизитФормы("СпособИдентификацииКонтрагентов", ТипСпособИдентификации,,,Истина);
	РеквизитВидНоменклатурыТовар = Новый РеквизитФормы("ВидНоменклатурыТовар", ТипВидНоменклатуры,,НСтр("ru = 'Вид номенклатуры товар'"),Истина);
	РеквизитВидНоменклатурыУслуга = Новый РеквизитФормы("ВидНоменклатурыУслуга", ТипВидНоменклатуры,,НСтр("ru = 'Вид номенклатуры услуга'"),Истина);
	РеквизитЕдиницаИзмерения = Новый РеквизитФормы("ЕдиницаИзмеренияНовойНоменклатуры", ТипЕдиницаИзмерения,,НСтр("ru = 'Единица измерения'"),Истина);
	РеквизитГруппаНоменклатуры = Новый РеквизитФормы("ГруппаДляНовойНоменклатуры", ТипГруппаНоменклатуры,,НСтр("ru = 'Группа номенклатуры'"),Истина);
	
	РеквизитСоздаватьПартнеровДляНовыхКонтрагентов = Новый РеквизитФормы("СоздаватьПартнеровДляНовыхКонтрагентов",
		ТипБулево, ,НСтр("ru = 'Создавать партнеров для новых контрагентов'"),Истина);
	РеквизитГруппаДоступа = Новый РеквизитФормы("ГруппаДоступаПартнеров", ТипГруппаДоступа,, НСтр("ru = 'Група доступа партнеров'"), Истина);
	
	
	// Служебные реквизит, не отображаемый на форме.
	РеквизитИспользуютсяГруппыДоступаПартнеров = Новый РеквизитФормы("ИспользуютсяГруппыДоступаПартнеров", ТипБулево);
	РеквизитИспользоватьПартнеровКакКонтрагентов = Новый РеквизитФормы("ИспользоватьПартнеровКакКонтрагентов", ТипБулево);
	
	
	РеквизитСоответствиеСтатусов = Новый РеквизитФормы("СоответствиеСтатусовЗаказов", ТипСоответствиеСтатусов, ,
		НСтр("ru = 'Соответствие статусов заказов'"), Истина);
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(РеквизитСоответствиеСтатусов);
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);

	РеквизитСтатусЗаказаВБазе = Новый РеквизитФормы("СтатусЗаказаВБазе", ТипСтатусЗаказаВБазе,
		"СоответствиеСтатусовЗаказов", НСтр("ru = 'Статус заказа в базе'"), Истина);
	РеквизитСтатусЗаказаНаСайте = Новый РеквизитФормы("СтатусЗаказаНаСайте", ТипСтрока,
		"СоответствиеСтатусовЗаказов", НСтр("ru = 'Статус заказа на сайте'"), Истина);
		
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(РеквизитВыгружатьПрочиеФайлы);
	ДобавляемыеРеквизиты.Добавить(РеквизитДатаНаСайте);
	ДобавляемыеРеквизиты.Добавить(РеквизитНомерНаСайте);
	ДобавляемыеРеквизиты.Добавить(РеквизитСоглашение);
	ДобавляемыеРеквизиты.Добавить(РеквизитОрганизация);
	ДобавляемыеРеквизиты.Добавить(РеквизитСклад);
	ДобавляемыеРеквизиты.Добавить(РеквизитМенеджер);
	ДобавляемыеРеквизиты.Добавить(РеквизитПричинаОтмены);
	ДобавляемыеРеквизиты.Добавить(РеквизитСтатусЗаказаВБазе);
	ДобавляемыеРеквизиты.Добавить(РеквизитСтатусЗаказаНаСайте);
	ДобавляемыеРеквизиты.Добавить(РеквизитГруппаДоступа);
	ДобавляемыеРеквизиты.Добавить(РеквизитИспользуютсяГруппыДоступаПартнеров);
	ДобавляемыеРеквизиты.Добавить(РеквизитИспользоватьПартнеровКакКонтрагентов);
	ДобавляемыеРеквизиты.Добавить(РеквизитСоздаватьПартнеровДляНовыхКонтрагентов);
	ДобавляемыеРеквизиты.Добавить(РеквизитСпособИдентификации);
	ДобавляемыеРеквизиты.Добавить(РеквизитВидНоменклатурыТовар);
	ДобавляемыеРеквизиты.Добавить(РеквизитВидНоменклатурыУслуга);
	ДобавляемыеРеквизиты.Добавить(РеквизитЕдиницаИзмерения);
	ДобавляемыеРеквизиты.Добавить(РеквизитГруппаНоменклатуры);

	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);

	// Закладка Выгрузка товаров.
	ЭлементВыгружатьПрочиеФайлы = Элементы.Добавить("ВыгружатьФайлы", Тип("ПолеФормы"), Элементы.ГруппаВыгружатьФайлы);
	ЭлементВыгружатьПрочиеФайлы.ПутьКДанным = "ВыгружатьФайлы";
	ЭлементВыгружатьПрочиеФайлы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	ЭлементВыгружатьПрочиеФайлы.Вид = ВидПоляФормы.ПолеФлажка;
	
	// Закладка Обмен заказами.
	
	// Группа дата и номер заказа.
	ГруппаДатаНомерЗаказа = Элементы.Добавить("ГруппаДатаНомерЗаказа", Тип("ГруппаФормы"), Элементы.ГруппаЗаказыОсновныеНастройки);
	ГруппаДатаНомерЗаказа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаДатаНомерЗаказа.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	ЗаголовокШаблон = НСтр("ru = 'Дополнительные реквизиты для поиска документов ""%1""'");
	ЗаголовокГруппы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокШаблон, НСтр("ru = 'Заказ клиент'"));
	ГруппаДатаНомерЗаказа.Заголовок = ЗаголовокГруппы;
	ГруппаДатаНомерЗаказа.ОтображатьЗаголовок = Истина;
	ГруппаДатаНомерЗаказа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	ЭлементДатаЗаказа = Элементы.Добавить("ДатаЗаказаНаСайте", Тип("ПолеФормы"), ГруппаДатаНомерЗаказа);
	ЭлементДатаЗаказа.ПутьКДанным = "ДополнительныйРеквизитЗаказаКлиентаДатаЗаказаНаСайте";
	ЭлементДатаЗаказа.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементДатаЗаказа.АвтоОтметкаНезаполненного = Истина;
		
	ПараметрыДатаНомерЗаказа = Новый Массив;
	ПараметрыДатаНомерЗаказа.Добавить(Новый ПараметрВыбора("Отбор.НаборСвойств",
		Справочники.НаборыДополнительныхРеквизитовИСведений.Документ_ЗаказКлиента));
	ПараметрыДатаНомерЗаказа.Добавить(Новый ПараметрВыбора("Отбор.ЭтоДополнительноеСведение", Ложь));
	
	ПараметрыВыбораДатаНомерЗаказа = Новый ФиксированныйМассив(ПараметрыДатаНомерЗаказа);
	ЭлементДатаЗаказа.ПараметрыВыбора = ПараметрыВыбораДатаНомерЗаказа;
	
	ЭлементНомерЗаказа = Элементы.Добавить("НомерЗаказаНаСайте", Тип("ПолеФормы"), ГруппаДатаНомерЗаказа);
	ЭлементНомерЗаказа.ПутьКДанным = "ДополнительныйРеквизитЗаказаКлиентаНомерЗаказаНаСайте";
	ЭлементНомерЗаказа.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементНомерЗаказа.АвтоОтметкаНезаполненного = Истина;
	ЭлементНомерЗаказа.ПараметрыВыбора = ПараметрыВыбораДатаНомерЗаказа;
	
	// Группа параметры Заказа.
	ГруппаПараметрыЗаказа = Элементы.Добавить("ГруппаПараметрыЗаказа", Тип("ГруппаФормы"), Элементы.ГруппаЗаказыОсновныеНастройки);
	ГруппаПараметрыЗаказа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПараметрыЗаказа.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	ЗаголовокШаблон = НСтр("ru = 'Реквизиты для создания документов ""%1""'");
	ЗаголовокГруппы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокШаблон, НСтр("ru = 'Заказ клиент'"));
	ГруппаПараметрыЗаказа.Заголовок = ЗаголовокГруппы;
	ГруппаПараметрыЗаказа.ОтображатьЗаголовок = Истина;
	ГруппаПараметрыЗаказа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;

	// Группа ГруппаСоглашениеОрганизация
	ГруппаСоглашениеОрганизация = Элементы.Добавить("ГруппаСоглашениеОрганизация", Тип("ГруппаФормы"), ГруппаПараметрыЗаказа);
	ГруппаСоглашениеОрганизация.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСоглашениеОрганизация.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСоглашениеОрганизация.ОтображатьЗаголовок = Ложь;
	ГруппаСоглашениеОрганизация.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	ЭлементСоглашение = Элементы.Добавить("Соглашение", Тип("ПолеФормы"), ГруппаСоглашениеОрганизация);
	ЭлементСоглашение.ПутьКДанным = "Соглашение";
	ЭлементСоглашение.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементСоглашение.УстановитьДействие("ПриИзменении", "Подключаемый_ПолеВВодаПриИзменнии");
	ЭлементСоглашение.АвтоОтметкаНезаполненного = Истина;
	ЭлементСоглашение.ПараметрыВыбора = ПараметрыВыбораСоглашения();

	ЭлементОрганизация = Элементы.Добавить("Организация", Тип("ПолеФормы"), ГруппаСоглашениеОрганизация);
	ЭлементОрганизация.ПутьКДанным = "Организация";
	ЭлементОрганизация.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементОрганизация.АвтоОтметкаНезаполненного = Истина;
	
	// Группа ГруппаСкладМенеджер
	ГруппаСкладМенеджер = Элементы.Добавить("ГруппаСкладМенеджер", Тип("ГруппаФормы"), ГруппаПараметрыЗаказа);
	ГруппаСкладМенеджер.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкладМенеджер.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСкладМенеджер.ОтображатьЗаголовок = Ложь;
	ГруппаСкладМенеджер.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;

	
	ЭлементСклад = Элементы.Добавить("Склад", Тип("ПолеФормы"), ГруппаСкладМенеджер);
	ЭлементСклад.ПутьКДанным = "Склад";
	ЭлементСклад.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементСклад.АвтоОтметкаНезаполненного = Истина;
	ЭлементСклад.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов");
	
	ЭлементМенеджер = Элементы.Добавить("Менеджер", Тип("ПолеФормы"), ГруппаСкладМенеджер);
	ЭлементМенеджер.ПутьКДанным = "Менеджер";
	ЭлементМенеджер.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементМенеджер.АвтоОтметкаНезаполненного = Ложь;
	
	// Группа Параметры поиска
	ГруппаПараметрыПоиска = Элементы.Добавить("ГруппаПараметрыПоиска", Тип("ГруппаФормы"), Элементы.ГруппаЗаказыОсновныеНастройки);
	ГруппаПараметрыПоиска.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПараметрыПоиска.ОтображатьЗаголовок = Истина;
	ГруппаПараметрыПоиска.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	ЗаголовокШаблон = НСтр("ru = 'Способ поиска существующих элементов справочника ""%1""'");
	ЗаголовокГруппы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокШаблон, НСтр("ru = 'Контрагенты'"));
	ГруппаПараметрыПоиска.Заголовок = ЗаголовокГруппы;
	ГруппаПараметрыПоиска.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаПараметрыПоиска.Объединенная = Истина;
	
	ЭлементСпособИдентификации = Элементы.Добавить("СпособИдентификацииКонтрагентов", Тип("ПолеФормы"), ГруппаПараметрыПоиска);
	ЭлементСпособИдентификации.ПутьКДанным = "СпособИдентификацииКонтрагентов";
	ЭлементСпособИдентификации.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементСпособИдентификации.Вид = ВидПоляФормы.ПолеПереключателя;
	ЭлементСпособИдентификации.СписокВыбора.Добавить(Перечисления.СпособыИдентификацииКонтрагентов.Наименование, "Наименование");
	ЭлементСпособИдентификации.СписокВыбора.Добавить(Перечисления.СпособыИдентификацииКонтрагентов.ИННКПП, "ИНН/КПП");
	
	ЭлементСоздаватьПартнеровДляНовыхКонтрагентов = Элементы.Добавить("СоздаватьПартнеровДляНовыхКонтрагентов",
		Тип("ПолеФормы"), ГруппаПараметрыПоиска);
	ЭлементСоздаватьПартнеровДляНовыхКонтрагентов.ПутьКДанным = "СоздаватьПартнеровДляНовыхКонтрагентов";
	ЭлементСоздаватьПартнеровДляНовыхКонтрагентов.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	ЭлементСоздаватьПартнеровДляНовыхКонтрагентов.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементСоздаватьПартнеровДляНовыхКонтрагентов.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	ТекстПодсказки = Новый ФорматированнаяСтрока(НСтр("ru = 'При получении информации о новых контрагентах также будут создаваться новые партнеры.'")
	, ,ЦветаСтиля.ПоясняющийТекст);
	ЭлементСоздаватьПартнеровДляНовыхКонтрагентов.РасширеннаяПодсказка.Заголовок = ТекстПодсказки;
	ЭлементСоздаватьПартнеровДляНовыхКонтрагентов.РасширеннаяПодсказка.АвтоМаксимальнаяШирина = Ложь;
	ЭлементСоздаватьПартнеровДляНовыхКонтрагентов.УстановитьДействие("ПриИзменении", "Подключаемый_ПолеФлажкаПриИзменнии");
	
	ЭлементГруппаДоступаПартнеров = Элементы.Добавить("ГруппаДоступаПартнеров", Тип("ПолеФормы"), ГруппаПараметрыПоиска);
	ЭлементГруппаДоступаПартнеров.ПутьКДанным = "ГруппаДоступаПартнеров";
	ЭлементГруппаДоступаПартнеров.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементГруппаДоступаПартнеров.АвтоОтметкаНезаполненного = Истина;
	
	// Группа ПараметрыНовыхЭлементов
	ГруппаПараметрыНовыхЭлементов = Элементы.Добавить("ГруппаПараметрыНовыхЭлементов", Тип("ГруппаФормы"), Элементы.ГруппаЗаказыОсновныеНастройки);
	ГруппаПараметрыНовыхЭлементов.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПараметрыНовыхЭлементов.ОтображатьЗаголовок = Истина;
	ГруппаПараметрыНовыхЭлементов.Отображение = ОтображениеОбычнойГруппы.ОбычноеВыделение;
	ЗаголовокШаблон = НСтр("ru = 'Реквизиты для создания новых элементов справочника ""%1""'");
	ЗаголовокГруппы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокШаблон, НСтр("ru = 'Номенклатура'"));
	ГруппаПараметрыНовыхЭлементов.Заголовок = ЗаголовокГруппы;
	ГруппаПараметрыНовыхЭлементов.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	// Группа ПараметрыНовыхЭлементов
	ГруппаВидыНоменклатуры = Элементы.Добавить("ГруппаВидыНоменклатуры", Тип("ГруппаФормы"), ГруппаПараметрыНовыхЭлементов);
	ГруппаВидыНоменклатуры.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаВидыНоменклатуры.ОтображатьЗаголовок = Ложь;
	ГруппаВидыНоменклатуры.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	ЭлементВидНоменклатурыТовар = Элементы.Добавить("ВидНоменклатурыТовар", Тип("ПолеФормы"), ГруппаВидыНоменклатуры);
	ЭлементВидНоменклатурыТовар.ПутьКДанным = "ВидНоменклатурыТовар";
	ЭлементВидНоменклатурыТовар.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементВидНоменклатурыТовар.АвтоОтметкаНезаполненного = Истина;
	
	ЭлементВидНоменклатурыУслуга = Элементы.Добавить("ВидНоменклатурыУслуга", Тип("ПолеФормы"), ГруппаВидыНоменклатуры);
	ЭлементВидНоменклатурыУслуга.ПутьКДанным = "ВидНоменклатурыУслуга";
	ЭлементВидНоменклатурыУслуга.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементВидНоменклатурыУслуга.АвтоОтметкаНезаполненного = Истина;

	// Группа Единицы
	ГруппаЕдиницы = Элементы.Добавить("ГруппаЕдиницы", Тип("ГруппаФормы"), ГруппаПараметрыНовыхЭлементов);
	ГруппаЕдиницы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаЕдиницы.ОтображатьЗаголовок = Ложь;
	ГруппаЕдиницы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	ЭлементЕдиницаИзмерения = Элементы.Добавить("ЕдиницаИзмеренияНовойНоменклатуры", Тип("ПолеФормы"), ГруппаЕдиницы);
	ЭлементЕдиницаИзмерения.ПутьКДанным = "ЕдиницаИзмеренияНовойНоменклатуры";
	ЭлементЕдиницаИзмерения.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементЕдиницаИзмерения.АвтоОтметкаНезаполненного = Истина;
	
	ЭлементГруппаНоменклатуры = Элементы.Добавить("ГруппаДляНовойНоменклатуры", Тип("ПолеФормы"), ГруппаЕдиницы);
	ЭлементГруппаНоменклатуры.ПутьКДанным = "ГруппаДляНовойНоменклатуры";
	ЭлементГруппаНоменклатуры.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементГруппаНоменклатуры.АвтоОтметкаНезаполненного = Ложь;

	// Группа дополнительные настройки
	ГруппаПричиныОтменаЗаказа = Элементы.Добавить("ГруппаПричиныОтменаЗаказа", Тип("ГруппаФормы"), Элементы.ГруппаЗаказыДополнительно);
	ГруппаПричиныОтменаЗаказа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПричиныОтменаЗаказа.ОтображатьЗаголовок = Ложь;
	ГруппаПричиныОтменаЗаказа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	ЭлементПричинаОтменыЗаказа = Элементы.Добавить("ПричинаОтменыЗаказа", Тип("ПолеФормы"), ГруппаПричиныОтменаЗаказа);
	ЭлементПричинаОтменыЗаказа.ПутьКДанным = "ПричинаОтменыЗаказа";
	ЭлементПричинаОтменыЗаказа.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементПричинаОтменыЗаказа.АвтоОтметкаНезаполненного = Истина;
	
	// Группа соответсвие статустов заказов
	ГруппаСоответствиеСтатусов = Элементы.Добавить("ГруппаСоответствиеСтатусов", Тип("ГруппаФормы"), Элементы.ГруппаЗаказыДополнительно);
	ГруппаСоответствиеСтатусов.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСоответствиеСтатусов.ОтображатьЗаголовок = Ложь;
	ГруппаСоответствиеСтатусов.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	ЭлементСоответствиеСтатусовЗаказов = Элементы.Добавить("СоответствиеСтатусовЗаказов", Тип("ТаблицаФормы"), ГруппаСоответствиеСтатусов);
	ЭлементСоответствиеСтатусовЗаказов.ПутьКДанным = "СоответствиеСтатусовЗаказов";
	ЭлементСоответствиеСтатусовЗаказов.Отображение = ОтображениеТаблицы.Список;
	ЭлементСоответствиеСтатусовЗаказов.УстановитьДействие("ПередОкончаниемРедактирования", "Подключаемый_ТаблицаФормыПередОкончаниемРедактирования");
	
	ЭлементСтатусЗаказаВБазе = Элементы.Добавить("СтатусЗаказаВБазе", Тип("ПолеФормы"), ЭлементСоответствиеСтатусовЗаказов);
	ЭлементСтатусЗаказаВБазе.ПутьКДанным = "СоответствиеСтатусовЗаказов.СтатусЗаказаВБазе";
	ЭлементСтатусЗаказаВБазе.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементСтатусЗаказаВБазе.АвтоОтметкаНезаполненного = Истина;
	
	ЭлементСтатусЗаказаНаСайте = Элементы.Добавить("СтатусЗаказаНаСайте", Тип("ПолеФормы"), ЭлементСоответствиеСтатусовЗаказов);
	ЭлементСтатусЗаказаНаСайте.ПутьКДанным = "СоответствиеСтатусовЗаказов.СтатусЗаказаНаСайте";
	ЭлементСтатусЗаказаНаСайте.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементСтатусЗаказаНаСайте.АвтоОтметкаНезаполненного = Истина;
КонецПроцедуры

// Устанавливает условное оформление формы узла плана обмена
//
// Параметры
//  Форма - управляемая форма - форма узла плана обмена "Обмен с сайтом".
//
Процедура УстановитьУсловноеОформление(Форма) Экспорт
	
	Элемент = Форма.УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ГруппаДоступаПартнеров.Имя);
	
	ГруппаОтбораИ = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбораИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ГруппаОтбораИли = ГруппаОтбораИ.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбораИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

	ОтборЭлемента = ГруппаОтбораИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользуютсяГруппыДоступаПартнеров");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ГруппаОтбораИ2 = ГруппаОтбораИли.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбораИ2.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбораИ2.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользоватьПартнеровКакКонтрагентов");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаОтбораИ2.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СоздаватьПартнеровДляНовыхКонтрагентов");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаОтбораИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ГруппаДоступаПартнеров");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	ОбязательныеПоля = Новый Соответствие;
	// Имя элементы формы, имя реквизита формы
	ОбязательныеПоля.Вставить("НомерЗаказаНаСайте","ДополнительныйРеквизитЗаказаКлиентаНомерЗаказаНаСайте");
	ОбязательныеПоля.Вставить("ДатаЗаказаНаСайте","ДополнительныйРеквизитЗаказаКлиентаДатаЗаказаНаСайте");
	ОбязательныеПоля.Вставить("Соглашение","Соглашение");
	ОбязательныеПоля.Вставить("Организация","Организация");
	ОбязательныеПоля.Вставить("Склад", "Склад");
	ОбязательныеПоля.Вставить("ВидНоменклатурыТовар","ВидНоменклатурыТовар");
	ОбязательныеПоля.Вставить("ВидНоменклатурыУслуга","ВидНоменклатурыУслуга");
	ОбязательныеПоля.Вставить("ЕдиницаИзмеренияНовойНоменклатуры","ЕдиницаИзмеренияНовойНоменклатуры");
	ОбязательныеПоля.Вставить("ПричинаОтменыЗаказа","ПричинаОтменыЗаказа");
	
	Для Каждого КлючЗначение Из ОбязательныеПоля Цикл
		
		// Отметка не заполненного пустого поля формы узла обмена.
		Элемент = Форма.УсловноеОформление.Элементы.Добавить();
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(КлючЗначение.Ключ);
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(КлючЗначение.Значение);
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
		Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
		
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает параметр компоновки
//
// Параметры:
//  ПараметрыКомпоновки  - ПараметрыСхемыКомпоновкиДанных - в них будет добавлен новый параметр.
//  ИмяПараметра         - Строка - имя параметра компоновки данных.
//  ЗначениеПараметра    - Произвольный - значение параметра компоновки данных.
//
Процедура УстановитьПараметрКомпоновки(ПараметрыКомпоновки, ИмяПараметра, ЗначениеПараметра) Экспорт
	
	ЭлементПараметра = ПараметрыКомпоновки.Добавить();
	ЭлементПараметра.Имя = ИмяПараметра;
	ЭлементПараметра.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
	ЭлементПараметра.Значение = ЗначениеПараметра;
	
КонецПроцедуры

// Заполнение Реквизитов Адреса по строке XTDO
//  СкладАдрес - Структура, в которой необходимо представить адресную информацию о складе
//        Поля структуры: ПочтовыйИндекс, Страна, Регион, Район, НаселенныйПункт, Город,Улица, Дом, Корпус, Квартира
//  ЗначениеПолей - Значение полей адреса
Процедура ЗаполнитьСтруктуруПолейАдресаПоЗначениюПолей(СкладАдрес, ЗначениеПолей) Экспорт
	
	ДанныеАдреса = ЗначениеПолей.Состав;
	
	// Индекс просто ставим
	СкладАдрес.ПочтовыйИндекс = Формат(Обработки.РасширенныйВводКонтактнойИнформации.ПочтовыйИндексАдреса(ДанныеАдреса), "ЧГ=");
	
	// Страна по наименованию
	НаименованиеСтраны = СокрЛП(ДанныеАдреса.Страна);
	Если ПустаяСтрока(НаименованиеСтраны) Тогда
		СкладАдрес.Страна = "Россия";
	Иначе
		СкладАдрес.Страна = НаименованиеСтраны;
	КонецЕсли;
	
	// Синтетический "Населенный пункт" получаем как представление
	НаселенныйПункт = Обработки.РасширенныйВводКонтактнойИнформации.ПредставлениеНаселенногоПункта(ДанныеАдреса);
	
	СкладАдрес.НаселенныйПункт         = НаселенныйПункт;
	
	СкладАдрес.Город  = ДанныеАдреса.Состав.Город;
	СкладАдрес.Район  = ДанныеАдреса.Состав.ВнутригРайон;
	СкладАдрес.Регион = ДанныеАдреса.Состав.СубъектРФ;
	
	
	// Синтетическую "Улицу" получаем как представление
	Улица = Обработки.РасширенныйВводКонтактнойИнформации.ПредставлениеУлицы(ДанныеАдреса);
	СкладАдрес.Улица       = Улица;
	
	// Дом, строение, помещение
	ЗданияИПомещения = Обработки.РасширенныйВводКонтактнойИнформации.ЗданияИПомещенияАдреса(ДанныеАдреса);
	
	// Первые два здания выделяем отдельно, остальное в списке
	ТаблицаДанных = ЗданияИПомещения.Здания;
	
	// Вид = 1 - признак дома, владения. Вид = 2, доп строение
	СтрокаДома = ТаблицаДанных.Найти(1, "Вид");
	Если СтрокаДома<>Неопределено Тогда
		СкладАдрес.Дом     = СтрокаДома.Значение;
		ТаблицаДанных.Удалить(СтрокаДома);
	КонецЕсли;
	
	СтрокаДома = ТаблицаДанных.Найти(2, "Вид");
	Если СтрокаДома<>Неопределено Тогда
		СкладАдрес.Корпус    = СтрокаДома.Значение;
		ТаблицаДанных.Удалить(СтрокаДома);
	КонецЕсли;
	
КонецПроцедуры

// Ищет номенклатуру по артикулу.
//
// Параметры:
//  Артикул     - Строка.
//
// Возвращаемое значение:
//   СправочникСсылка.Номенклатура, Неопределено - ссылка на найденный элемент справочника Номенклатура, Неорпределено, если не найдено.
//
Функция НоменклатураПоАртикулу(Артикул) Экспорт
	
	Если ЗначениеЗаполнено(Артикул) Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Номенклатура.Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Артикул = &Артикул
		|	И НЕ Номенклатура.ЭтоГруппа");
			
		Запрос.УстановитьПараметр("Артикул", Артикул);
		
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Возврат Результат.Выгрузить()[0][0];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Ищет номенклатуру по наименованию и виду номенлатуры.
//
// Параметры:
//  Наименование     - Строка - наименование, используемое для поиска.
//  ВидНоменклатуры  - СправочникСсылка.ВидыНоменлатуры - вид номенлатуры, по которому выполняется поиск
//
// Возвращаемое значение:
//   СправочникСсылка.Номенклатура, Неопределено - ссылка на найденный элемент справочника Номенклатура, Неорпределено, если не найдено.
//
Функция НоменклатураПоНаименованиюИВиду(Наименование, ВидНоменклатуры) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Номенклатура.Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Наименование = &Наименование
	|	И Номенклатура.ВидНоменклатуры = &ВидНоменклатуры");
	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("ВидНоменклатуры", ВидНоменклатуры);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Результат.Выгрузить()[0][0];
	
КонецФункции

// Ищет номенлатуру по уникальному идентификатору
//
// Параметры:
//  Номенклатура                      - СправочникСсылка.Номенлкатура - найденная номенклатура будет помещена в данный параметр.
//  ДанныеНоменклатуры                - Структура - содержит данные о номенклатуре, в частности свойство ИД, по которому будет выполняться поиск.
//  ПрикладныеПараметры               - Структура - содержит параметры узла обмена с сайтом.
//  УникальныйИдентификаторКорректен  - Булево - признак того, что уникальный идентификатор в данных номенклатуры был коррекнтым
//
// Возвращаемое значение:
//   Булево   - признак того, что номенклатура успешно найдена
//
Функция УспешноНайденТоварПоСсылке(Номенклатура, ДанныеНоменклатуры, ПрикладныеПараметры, УникальныйИдентификаторКорректен) Экспорт
	
	Попытка
		
		УникальныйИдентификаторНоменклатуры = Новый УникальныйИдентификатор(ДанныеНоменклатуры.Ид);
		
		Номенклатура = Справочники.Номенклатура.ПолучитьСсылку(УникальныйИдентификаторНоменклатуры);
		
		Если Не Номенклатура = Справочники.Номенклатура.ПустаяСсылка()
			И Номенклатура.ПолучитьОбъект() <> Неопределено Тогда
			
			Возврат Истина;
			
		КонецЕсли;
		
	Исключение
		
		УникальныйИдентификаторКорректен = Ложь;
		
	КонецПопытки;
	
	Возврат Ложь;
	
КонецФункции

// Заполняет дополнительные реквизиты документа "Заказ клиента" соотвествующие номеру и дате заказа с сайта
//
// Параметры:
//  ДокументОбъект      - ДокументОбъект.ЗаказКлиента - документ, дополнетельные реквзиты которого необходимо обновить
//  РеквизитыЗаказа     - Структура - содержит информацию о данных заказа, полученных с сайта.
//  ПрикладныеПараметры - Структура - содержит информацию настройках обмена, выполненных в форме узла.
//
Процедура ЗаполнитьНомерИДатуЗаказаДополнительныеРеквизиты(ДокументОбъект, РеквизитыЗаказа, ПрикладныеПараметры) Экспорт

	Если ЗначениеЗаполнено(ПрикладныеПараметры.ДополнительныйРеквизитЗаказаКлиентаДатаЗаказаНаСайте) Тогда
		НайденныеСтроки = ДокументОбъект.ДополнительныеРеквизиты.НайтиСтроки(Новый Структура("Свойство", ПрикладныеПараметры.ДополнительныйРеквизитЗаказаКлиентаДатаЗаказаНаСайте));
		Если НайденныеСтроки.Количество() = 0 Тогда
			СтрокаСвойства = ДокументОбъект.ДополнительныеРеквизиты.Добавить();
		Иначе
			СтрокаСвойства = НайденныеСтроки[0];
		КонецЕсли;
		СтрокаСвойства.Свойство = ПрикладныеПараметры.ДополнительныйРеквизитЗаказаКлиентаДатаЗаказаНаСайте;
		СтрокаСвойства.Значение = РеквизитыЗаказа.Дата;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПрикладныеПараметры.ДополнительныйРеквизитЗаказаКлиентаНомерЗаказаНаСайте) Тогда
		НайденныеСтроки = ДокументОбъект.ДополнительныеРеквизиты.НайтиСтроки(Новый Структура("Свойство", ПрикладныеПараметры.ДополнительныйРеквизитЗаказаКлиентаНомерЗаказаНаСайте));
		Если НайденныеСтроки.Количество() = 0 Тогда
			СтрокаСвойства = ДокументОбъект.ДополнительныеРеквизиты.Добавить();
		Иначе
			СтрокаСвойства = НайденныеСтроки[0];
		КонецЕсли;
		СтрокаСвойства.Свойство = ПрикладныеПараметры.ДополнительныйРеквизитЗаказаКлиентаНомерЗаказаНаСайте;
		СтрокаСвойства.Значение = РеквизитыЗаказа.Номер;
	КонецЕсли;

КонецПроцедуры

// Заполняет массив реквизитов формы узла обмена с сайтами, отвечающих за настройки выгрузки товаров.
//
// Возвращаемое значение:
//   Массиив  - массив реквизитов формы узла обмена с сайтами, отвечающих за настройки выгрузки товаров.
//
Функция РеквизитыОбменаТоварами() Экспорт

	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить("ВыгружатьФайлы");
	МассивРеквизитов.Добавить("СписокТочекСамовывоза");

	Возврат МассивРеквизитов;

КонецФункции

// Заполняет массив реквизитов формы узла обмена с сайтами, отвечающих за настройки выгрузки товаров.
//
// Возвращаемое значение:
//   Массиив  - массив реквизитов формы узла обмена с сайтами, отвечающих за настройки обмена заказами.
//
Функция РеквизитыОбменаЗаказами() Экспорт
	
	МассивРеквизитов = Новый Массив;
	
	МассивРеквизитов.Добавить("ДополнительныйРеквизитЗаказаКлиентаДатаЗаказаНаСайте");
	МассивРеквизитов.Добавить("ДополнительныйРеквизитЗаказаКлиентаНомерЗаказаНаСайте");
	МассивРеквизитов.Добавить("СпособИдентификацииКонтрагентов");
	МассивРеквизитов.Добавить("ВидНоменклатурыТовар");
	МассивРеквизитов.Добавить("ВидНоменклатурыУслуга");
	МассивРеквизитов.Добавить("ЕдиницаИзмеренияНовойНоменклатуры");
	МассивРеквизитов.Добавить("ГруппаДляНовойНоменклатуры");
	МассивРеквизитов.Добавить("Соглашение");
	МассивРеквизитов.Добавить("Склад");
	МассивРеквизитов.Добавить("Организация");
	МассивРеквизитов.Добавить("Менеджер");
	МассивРеквизитов.Добавить("ПричинаОтменыЗаказа");
	МассивРеквизитов.Добавить("СоответствиеСтатусовЗаказов");
	МассивРеквизитов.Добавить("СоздаватьПартнеровДляНовыхКонтрагентов");
	МассивРеквизитов.Добавить("ГруппаДоступаПартнеров");
	
	Возврат МассивРеквизитов;
	
КонецФункции

// Создает нового контрагента
//
// Параметры:
//  ДанныеКонтрагента        - Структура - содержит данные покупателя с сайта.
//  ПрикладныеПараметры      - Структура - содержит информацию настройках обмена, выполненных в форме узла.
//  ОписаниеОшибки           - Строка - в нее записывается текст ошибки, которая может возникнуть при создании нового контрагента.
//  СтруктураСвойстваЗаказа  - Структура - содержит свойства заказа с сайта.
//
// Возвращаемое значение:
//   СправочникСсылка.Контрагент   - ссылка на вновь созданного контрагента
//
Функция СоздатьКонтрагента(ДанныеКонтрагента, ПрикладныеПараметры, ОписаниеОшибки, СтруктураСвойстваЗаказа) Экспорт
	
	НовыйКонтрагент                     = Справочники.Контрагенты.СоздатьЭлемент(); 
	
	ЗаполнитьЗначенияСвойств(НовыйКонтрагент, ДанныеКонтрагента);
	
	НовыйКонтрагент.ЮрФизЛицо          = ?(ДанныеКонтрагента.ЮрЛицо, Перечисления.ЮрФизЛицо.ЮрЛицо, Перечисления.ЮрФизЛицо.ФизЛицо);
	НовыйКонтрагент.Наименование       = ДанныеКонтрагента.Наименование;
	НовыйКонтрагент.НаименованиеПолное = ?(ДанныеКонтрагента.ЮрЛицо, ДанныеКонтрагента.ОфициальноеНаименование, ДанныеКонтрагента.ПолноеНаименование);
	
	Для Каждого СтрокаИнформации Из ДанныеКонтрагента.ИнформацияПоКонтрагенту Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаИнформации.Представление) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ВРег(СтрокаИнформации.Вид) = ВРег(НСтр("ru = 'Фактический адрес'")) Тогда
			
			ДобавитьКонтактнуюИнформацию(НовыйКонтрагент,
			                             Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента,
			                             Перечисления.ТипыКонтактнойИнформации.Адрес,
			                             СтрокаИнформации.Представление);
			
		ИначеЕсли ВРег(СтрокаИнформации.Вид) =  ВРег(НСтр("ru = 'Юридический адрес'")) Тогда
			
			ДобавитьКонтактнуюИнформацию(НовыйКонтрагент,
			                             Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента,
			                             Перечисления.ТипыКонтактнойИнформации.Адрес,
			                             СтрокаИнформации.Представление);
			
		ИначеЕсли ВРег(СтрокаИнформации.Вид) =  ВРег(НСтр("ru = 'ТелефонРабочий'")) Тогда
			
			ДобавитьКонтактнуюИнформацию(НовыйКонтрагент,
			                             Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента,
			                             Перечисления.ТипыКонтактнойИнформации.Телефон,
			                             СтрокаИнформации.Представление);
			
		ИначеЕсли ВРег(СтрокаИнформации.Вид) =  ВРег(НСтр("ru = 'Почта'")) Тогда
			
			ДобавитьКонтактнуюИнформацию(НовыйКонтрагент,
			                             Справочники.ВидыКонтактнойИнформации.EmailКонтрагента,
			                             Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты,
			                             СтрокаИнформации.Представление);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов")
		ИЛИ ПрикладныеПараметры.СоздаватьПартнеровДляНовыхКонтрагентов Тогда
		
		НовыйКонтрагент.Партнер = ВновьСозданныйПартнер(ДанныеКонтрагента,
		                                                ПрикладныеПараметры);
		
	Иначе
		
		НовыйКонтрагент.Партнер = Справочники.Партнеры.НеизвестныйПартнер;
		
	КонецЕсли;
		
	НовыйКонтрагент.Записать();
	
	Если ДанныеКонтрагента.Свойство("РасчетныеСчета") И ДанныеКонтрагента.РасчетныеСчета.Количество() > 0 Тогда
		
		ВалютаРеглУчета = Константы.ВалютаРегламентированногоУчета.Получить();
		
		Для Каждого РасчетныйСчет Из ДанныеКонтрагента.РасчетныеСчета Цикл
			
			ДанныеБанка = Новый Структура;
			ДанныеБанка.Вставить("SWIFT","");
			ДанныеБанка.Вставить("Адрес","");
			ДанныеБанка.Вставить("БИК","");
			ДанныеБанка.Вставить("Город","");
			ДанныеБанка.Вставить("КоррСчет","");
			ДанныеБанка.Вставить("Наименование","");
			ЗаполнитьЗначенияСвойств(ДанныеБанка, РасчетныйСчет.ДанныеБанка);
			
			Если ПустаяСтрока(РасчетныйСчет.НомерСчета)
				Или ПустаяСтрока(ДанныеБанка.БИК)
				Или ПустаяСтрока(ДанныеБанка.КоррСчет) Тогда
				Продолжить;
			КонецЕсли;
			
			НовыйСчет = Справочники.БанковскиеСчетаКонтрагентов.СоздатьЭлемент();
			НовыйСчет.НомерСчета            = РасчетныйСчет.НомерСчета;
			НовыйСчет.Владелец              = НовыйКонтрагент.Ссылка;
			НовыйСчет.ВалютаДенежныхСредств = ВалютаРеглУчета;
			
			Запрос = Новый Запрос;
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	КлассификаторБанков.Ссылка
			|ИЗ
			|	Справочник.КлассификаторБанков КАК КлассификаторБанков
			|ГДЕ
			|	КлассификаторБанков.Код = &БИК
			|	ИЛИ КлассификаторБанков.КоррСчет = &КоррСчет";
			
			Запрос.УстановитьПараметр("БИК", ДанныеБанка.БИК);
			Запрос.УстановитьПараметр("КоррСчет", ДанныеБанка.КоррСчет);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				НовыйСчет.Банк = Выборка.Ссылка;
			Иначе
				
				Если ПустаяСтрока(ДанныеБанка.КоррСчет)
					Или ПустаяСтрока(ДанныеБанка.Наименование) Тогда
					Продолжить;
				КонецЕсли;
				
				НовыйСчет.РучноеИзменениеРеквизитовБанка = Истина;
				НовыйСчет.НаименованиеБанка = ДанныеБанка.Наименование;
				НовыйСчет.ГородБанка        = ДанныеБанка.Город;
				НовыйСчет.КоррСчетБанка     = ДанныеБанка.КоррСчет;
				НовыйСчет.СВИФТБанка        = ДанныеБанка.SWIFT;
				НовыйСчет.АдресБанка        = ДанныеБанка.Адрес;
				
			КонецЕсли;
			
			НовыйСчет.Записать();
		
		КонецЦикла;
		
	КонецЕсли;
	
	СтруктураСвойстваЗаказа.Партнер = НовыйКонтрагент.Партнер;
	
	Возврат НовыйКонтрагент.Ссылка;
	
КонецФункции

// Заполняет табличную часть "Товары" документа "Заказ клиента".
//
// Параметры:
//  Документ            - ДокументОбъект.ЗаказКлиента - документ, чья табличная часть "Товары" будет заполнена.
//  ТаблицаТоваровСайт  - ТаблицаЗначений - таблица заказанных товаров по данным с сайта.
//  ПрикладныеПараметры - Структура - содержит информацию настройках обмена, выполненных в форме узла.
//
Процедура ЗаполнитьТабличнуюЧастьДокумента(Документ, ТаблицаТоваровСайт, ПрикладныеПараметры) Экспорт
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Документ);
	СтруктураПересчетаЦены  = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаЦеныСкидкиВПродажахВТЧ(Документ, 
		Документ.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию"));
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры"));
	СтруктураДействий.Вставить("ПроверитьЗаполнитьСклад", Новый Структура("Склад, СкладГруппа", Документ.Склад, Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ЗаполнитьСодержание", Новый Структура("ЗаполнятьДляВсехУслуг", Истина));
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьЦенуСкидкуПоСуммеВПродажах", СтруктураПересчетаЦены);
	
	ВариантыОбеспечения = ПродажиСервер.ВариантыОбеспеченияПоУмолчанию(Документ.Соглашение, Документ.Статус);
	ПараметрыДействия   = ОбеспечениеКлиентСервер.ПараметрыДействияПроверитьЗаполнитьОбеспечение(ВариантыОбеспечения,
		Документ.ЖелаемаяДатаОтгрузки);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьОбеспечениеВДокументеПродажи", ПараметрыДействия);
	
	СтруктураДействийНабор = Новый Структура;
	СтруктураДействийНабор.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействийНабор.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействийНабор.Вставить("ПересчитатьСумму");
		
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	Документ.Товары.Очистить();
	ТаблицаТоварыДокумент = Документ.Товары.Выгрузить();
	ТаблицаТоварыДокумент.Колонки.Добавить("ТипНоменклатуры", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыНоменклатуры"));
	
	//Параметры, используемые для набора
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Дата",       Документ.Дата);
	ДополнительныеПараметры.Вставить("Соглашение", Документ.Соглашение);
	ДополнительныеПараметры.Вставить("Валюта",     Документ.Валюта);
		
	Для Каждого ТекСтрока Из ТаблицаТоваровСайт Цикл
		
		ЭтоНабор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекСтрока.Номенклатура, "ТипНоменклатуры") = 
			Перечисления.ТипыНоменклатуры.Набор;
			
		Если ЭтоНабор Тогда
			ТоварыНабор = Документ.Товары.ВыгрузитьКолонки();	
				
			ПараметрыКомплектующих = ПодборТоваровКлиентСервер.ПараметрыТовара();
			ЗаполнитьЗначенияСвойств(ПараметрыКомплектующих, ТекСтрока);
			ПараметрыКомплектующих.КоличествоУпаковок   = ПараметрыКомплектующих.Количество;
			ПараметрыКомплектующих.НоменклатураНабора   = ТекСтрока.Номенклатура;
			ПараметрыКомплектующих.ХарактеристикаНабора = ТекСтрока.Характеристика;
			ПараметрыКомплектующих.Вставить("ВариантКомплектацииНоменклатуры", Неопределено);
			
			ДополнительныеПараметры.Вставить("Цена", ТекСтрока.Цена);
			
			ПодобранныеТовары = НаборыВызовСервера.Комплектующие(ПараметрыКомплектующих, ДополнительныеПараметры);
			Для Каждого ПодобранныйТовар Из ПодобранныеТовары Цикл
				
				НоваяСтрока = ТоварыНабор.Добавить();
				НоваяСтрока.СтавкаНДС = СтавкаНДСПоЗначениюПриЗагрузке(ТекСтрока.СтавкаНДС);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ПодобранныйТовар);
				
				ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействийНабор, Неопределено);
				
			КонецЦикла;
						
			КоэффициентПересчета = ТекСтрока.Сумма / ТоварыНабор.Итог("Сумма");
			
			//Перенесем товары из набора в табличную часть
			ПогрешностьСтоимости = ТекСтрока.Сумма;
			ПогрешностьНДС       = ?(Значениезаполнено(ТекСтрока.СуммаНДС), ТекСтрока.СуммаНДС, 0);
			Для Каждого СтрНабора из ТоварыНабор Цикл
				НоваяСтрока = ТаблицаТоварыДокумент.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрНабора);
				Если КоэффициентПересчета <> 1 Тогда
					НоваяСтрока.Сумма = НоваяСтрока.Сумма * КоэффициентПересчета;
					НоваяСтрока.СуммаНДС = НоваяСтрока.СуммаНДС * КоэффициентПересчета;
				КонецЕсли;
				
				ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, КэшированныеЗначения);
				
				ПогрешностьСтоимости = ПогрешностьСтоимости - НоваяСтрока.Сумма;
				ПогрешностьНДС       = ПогрешностьНДС - НоваяСтрока.СуммаНДС;
			КонецЦикла;
			
			//Погрешность отнесем на стоимость последнего товара в наборе
			Если ПогрешностьСтоимости <> 0 или ПогрешностьНДС <> 0 Тогда
				НоваяСтрока.Сумма     = НоваяСтрока.Сумма + ПогрешностьСтоимости;  	
				НоваяСтрока.СуммаНДС  = НоваяСтрока.СуммаНДС + ПогрешностьНДС;
				НоваяСтрока.СуммаСНДС = НоваяСтрока.Сумма + ?(Документ.ЦенаВключаетНДС, 0, НоваяСтрока.СуммаНДС);
			КонецЕсли;
		Иначе
			
			НоваяСтрока = ТаблицаТоварыДокумент.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
			НоваяСтрока.СтавкаНДС = СтавкаНДСПоЗначениюПриЗагрузке(ТекСтрока.СтавкаНДС);
			НоваяСтрока.КоличествоУпаковок = ТекСтрока.Количество;
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, КэшированныеЗначения);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Документ.Товары.Загрузить(ТаблицаТоварыДокумент);
		
КонецПроцедуры

// Ищет валюту по коду валюту, переданному с сайта.
// Если валюту найти не удалось, получает валюту регламентированного учета.
//
// Параметры:
//  Валюта           - СправочникСсылка.Валюта - в данный параметр устанавливается найденная валюта
//  КодВалютыСтрока  - Строка - код валюты, по которому выполняется поиск.
//
Процедура ОбработатьВалютуXML(Валюта, КодВалютыСтрока) Экспорт
	
	Валюта = Справочники.Валюты.НайтиПоНаименованию(КодВалютыСтрока);
	
	Если НЕ ЗначениеЗаполнено(Валюта) Тогда
		Валюта = Константы.ВалютаРегламентированногоУчета.Получить();
	КонецЕсли;
	
КонецПроцедуры

// Получает значение ставки НДС по строковому значению, переданному с сайта.
//
// Параметры:
//  СтавкаНалогаСтрока - Строка - строковое представление ставки НДС
//
// Возвращаемое значение:
//   ПеречислениеСсылка.СтавкиНДС   - найденное по строковому описанию значение ставки НДС
//
Функция СтавкаНДСПоЗначениюПриЗагрузке(СтавкаНалогаСтрока) Экспорт
	
	Если СтавкаНалогаСтрока = "0" Тогда
		СтавкаНДС = Перечисления.СтавкиНДС.НДС0;
	ИначеЕсли СтавкаНалогаСтрока = "10" Тогда
		СтавкаНДС = Перечисления.СтавкиНДС.НДС10;
	ИначеЕсли СтавкаНалогаСтрока = "10/110" Тогда
		СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110;
	ИначеЕсли СтавкаНалогаСтрока = "18" Тогда
		СтавкаНДС = Перечисления.СтавкиНДС.НДС18;
	ИначеЕсли СтавкаНалогаСтрока = "18/118" Тогда
		СтавкаНДС =  Перечисления.СтавкиНДС.НДС18_118;
	Иначе
		СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
	КонецЕсли;
	
	Возврат СтавкаНДС;
		
КонецФункции

// Возвращает пустую таблицу значений, в которыю будут добавляться дополнительные поля отбора
//
Функция ДобавляемыеПоляОтбора() Экспорт
	
	ПоляОтбора = Новый ТаблицаЗначений;
	
	ПоляОтбора.Колонки.Добавить("Наименование");
	ПоляОтбора.Колонки.Добавить("Синоним");
	ПоляОтбора.Колонки.Добавить("ТипЗначения");
	ПоляОтбора.Колонки.Добавить("Отбор");
	
	Возврат ПоляОтбора;
	
КонецФункции

// Возращает массив структур по которым происходит отбор соглашения
//
Функция ПараметрыОтбораСоглашения() Экспорт
	
	ОтборСоглашения = Новый Массив;
	ОтборСоглашения.Добавить(Новый Структура("ИмяПоля, ЗначениеОтбора", "Типовое", Истина));
	ОтборСоглашения.Добавить(Новый Структура("ИмяПоля, ЗначениеОтбора","ДоступноВнешнимПользователям", Истина));
	ОтборСоглашения.Добавить(Новый Структура("ИмяПоля, ЗначениеОтбора","Статус",
		Перечисления.СтатусыСоглашенийСКлиентами.Действует));
	ОтборСоглашения.Добавить(Новый Структура("ИмяПоля, ЗначениеОтбора","ХозяйственнаяОперация",
		Перечисления.ХозяйственныеОперации.РеализацияКлиенту));
		
	Возврат ОтборСоглашения;
		
КонецФункции

// Возращает массив структур по которым происходит отбор сегмена номенклатуры
//
Функция ПараметрыОтбораСегмента() Экспорт
	
	ЗначенияОтбора = Новый Массив;
	ЗначенияОтбора.Добавить(Перечисления.СпособыФормированияСегментов.ФормироватьВручную);
	ЗначенияОтбора.Добавить(Перечисления.СпособыФормированияСегментов.ПериодическиОбновлять);
	
	ОтборСегмента = Новый Массив;
	ОтборСегмента.Добавить(Новый Структура("ИмяПоля, ЗначениеОтбора", "СпособФормирования", ЗначенияОтбора));
	
	Возврат ОтборСегмента;

КонецФункции

Процедура ФормаУзлаОбменаСоглашениеПриИзмененииНаСервере(Форма, ЭтоЧтение = Ложь) Экспорт
	
	ДанныеСоглашения = Новый Структура("Склад, ЭтоГруппаСкладов", Неопределено, Ложь);
	Если ЗначениеЗаполнено(Форма.Соглашение) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	СоглашенияСКлиентами.Склад,
		|	ЕСТЬNULL(Склады.ЭтоГруппа, ЛОЖЬ) КАК ЭтоГруппаСкладов,
		|	СоглашенияСКлиентами.Менеджер
		|ИЗ
		|	Справочник.СоглашенияСКлиентами КАК СоглашенияСКлиентами
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
		|		ПО СоглашенияСКлиентами.Склад = Склады.Ссылка
		|ГДЕ
		|	СоглашенияСКлиентами.Ссылка = &Соглашение";
		
		Запрос.УстановитьПараметр("Соглашение", Форма.Соглашение);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			ЗаполнитьЗначенияСвойств(ДанныеСоглашения, Выборка);
			Если НЕ ЭтоЧтение И НЕ ЗначениеЗаполнено(Форма.Менеджер) Тогда
				Форма.Менеджер = Выборка.Менеджер;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СамообслуживаниеСервер.УправлениеЭлементомФормыСклад(Форма, ДанныеСоглашения, Форма.Элементы.Склад)
	
КонецПроцедуры

Процедура ПриСозданииНаСервереФормаСоздатьСайт(Форма) Экспорт
	
	// Устанавливается тип значения поля ВидЦены
	СоглашениеТип = Новый ОписаниеТипов("СправочникСсылка.СоглашенияСКлиентами");
	Форма.Элементы.ВидЦен.ОграничениеТипа.ПривестиЗначение(СоглашениеТип);
	Форма.Элементы.ВидЦен.ВыбиратьТип = Ложь;
	Форма.Элементы.ВидЦен.Заголовок = НСтр("ru = 'Соглашение'");
	Форма.Элементы.ВидЦен.ПараметрыВыбора = ПараметрыВыбораСоглашения();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СоглашенияСКлиентами.Ссылка КАК Соглашение
	|ИЗ
	|	Справочник.СоглашенияСКлиентами КАК СоглашенияСКлиентами
	|ГДЕ
	|	СоглашенияСКлиентами.СегментПартнеров = &СегментПартнеров
	|	И СоглашенияСКлиентами.Статус = &Статус
	|	И СоглашенияСКлиентами.Типовое = &Типовое
	|	И СоглашенияСКлиентами.ДоступноВнешнимПользователям = &ДоступноВнешнимПользователям
	|	И СоглашенияСКлиентами.ХозяйственнаяОперация = &ХозяйственнаяОперация";
	
	Запрос.УстановитьПараметр("СегментПартнеров", Справочники.СегментыПартнеров.ПустаяСсылка());
	Запрос.УстановитьПараметр("Статус", Перечисления.СтатусыСоглашенийСКлиентами.Действует);
	Запрос.УстановитьПараметр("Типовое", Истина);
	Запрос.УстановитьПараметр("ДоступноВнешнимПользователям", Истина);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.РеализацияКлиенту);
	
	Запрос.Выполнить();
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		Форма.ВидЦен = Выборка.Соглашение;
	Иначе
		Форма.ВидЦен = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
	КонецЕсли;
	
	
КонецПроцедуры

Функция МассивДопустимыхРасширенийИзображений() Экспорт

	МассивДопустимыхРасширенийИзображений = Новый Массив;
	МассивДопустимыхРасширенийИзображений.Добавить("gif");
	МассивДопустимыхРасширенийИзображений.Добавить("jpg");
	МассивДопустимыхРасширенийИзображений.Добавить("jpeg");
	МассивДопустимыхРасширенийИзображений.Добавить("bmp");
	МассивДопустимыхРасширенийИзображений.Добавить("png");
	
	Возврат МассивДопустимыхРасширенийИзображений;
	
КонецФункции

// Возвращает значение реквизита, который был добавлен на форму узла плана обмена
// добавленный реквизит формы не является реквизитом плана обмена
//Параметры
//	Узел - ПланОбмена.ОбменССайтом.Ссылка
//	ИмяРеквизита - имя реквизита, добавленного на форму
//Возвращаемое значение
// Значение реквизита, которое было выбрано в форме узла
//
Функция РеквизитПрикладногоРешения(Узел, ИмяРеквизита) Экспорт
	
	СохраненныеПараметрыПрикладногоРешения = Узел.ПараметрыПрикладногоРешения.Получить();
	Если Не ТипЗнч(СохраненныеПараметрыПрикладногоРешения) = Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыПрикладногоРешения = Неопределено;
	Если Не СохраненныеПараметрыПрикладногоРешения.Свойство("ПараметрыПрикладногоРешения", ПараметрыПрикладногоРешения) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрПрикладногоРешения = Неопределено;
	Если Не ПараметрыПрикладногоРешения.Свойство(ИмяРеквизита,ПараметрПрикладногоРешения) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПараметрПрикладногоРешения;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыВыбораСоглашения()
	
	ПараметрыСоглашения = Новый Массив;
	ПараметрыСоглашения.Добавить(Новый ПараметрВыбора("Отбор.СегментПартнеров",Справочники.СегментыПартнеров.ПустаяСсылка()));
	ПараметрыСоглашения.Добавить(Новый ПараметрВыбора("Отбор.ДоступноВнешнимПользователям",Истина));
	ПараметрыСоглашения.Добавить(Новый ПараметрВыбора("Отбор.Типовое",Истина));
	ПараметрыСоглашения.Добавить(Новый ПараметрВыбора("Отбор.Статус",Перечисления.СтатусыСоглашенийСКлиентами.Действует));
	ПараметрыСоглашения.Добавить(Новый ПараметрВыбора("Отбор.ХозяйственнаяОперация",Перечисления.ХозяйственныеОперации.РеализацияКлиенту));
	ПараметрыВыбораСоглашения = Новый ФиксированныйМассив(ПараметрыСоглашения);
	
	Возврат ПараметрыВыбораСоглашения;
	
КонецФункции

Функция ВновьСозданныйПартнер(ДанныеКонтрагента , Параметры)
	
	ПартнерОбъект = Справочники.Партнеры.СоздатьЭлемент();
	ПартнерОбъект.Клиент             = Истина;
	ПартнерОбъект.ЮрФизЛицо          = ?(ДанныеКонтрагента.Юрлицо, Перечисления.КомпанияЧастноеЛицо.Компания, Перечисления.КомпанияЧастноеЛицо.ЧастноеЛицо);
	ПартнерОбъект.ДатаРегистрации    = ТекущаяДатаСеанса();
	ПартнерОбъект.Комментарий        = НСтр("ru = 'Создан автоматически при импорте заказов с сайта'");
	ПартнерОбъект.Наименование       = ДанныеКонтрагента.Наименование;
	ПартнерОбъект.НаименованиеПолное =?(ДанныеКонтрагента.ЮрЛицо, ДанныеКонтрагента.ОфициальноеНаименование, ДанныеКонтрагента.ПолноеНаименование);
	ПартнерОбъект.ОсновнойМенеджер   = Параметры.Менеджер;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГруппыДоступаПартнеров") Тогда
		ПартнерОбъект.ГруппаДоступа = Параметры.ГруппаДоступаПартнеров;
	КонецЕсли;
	
	Для Каждого СтрокаИнформации Из ДанныеКонтрагента.ИнформацияПоКонтрагенту Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаИнформации.Представление) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ВРег(СтрокаИнформации.Вид) = ВРег(НСтр("ru = 'Фактический адрес'")) Тогда
			
			ДобавитьКонтактнуюИнформацию(ПартнерОбъект,
			                             Справочники.ВидыКонтактнойИнформации.АдресПартнера,
			                             Перечисления.ТипыКонтактнойИнформации.Адрес,
			                             СтрокаИнформации.Представление);
			
		ИначеЕсли ВРег(СтрокаИнформации.Вид) =  ВРег(НСтр("ru = 'ТелефонРабочий'")) Тогда
			
			ДобавитьКонтактнуюИнформацию(ПартнерОбъект,
			                             Справочники.ВидыКонтактнойИнформации.ТелефонПартнера,
			                             Перечисления.ТипыКонтактнойИнформации.Телефон,
			                             СтрокаИнформации.Представление);
			
		ИначеЕсли ВРег(СтрокаИнформации.Вид) =  ВРег(НСтр("ru = 'Почта'")) Тогда
			
			ДобавитьКонтактнуюИнформацию(ПартнерОбъект,
			                             Справочники.ВидыКонтактнойИнформации.EmailПартнера,
			                             Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты,
			                             СтрокаИнформации.Представление);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПартнерОбъект.Записать();
	
	Возврат ПартнерОбъект.Ссылка;
	
КонецФункции

Процедура ДобавитьКонтактнуюИнформацию(Объект, ВидКИ, ТипКИ, Представление)
	
	ЗначенияПолей = УправлениеКонтактнойИнформациейСлужебныйВызовСервера.КонтактнаяИнформацияXMLПоПредставлению(Представление, ВидКИ);
	УправлениеКонтактнойИнформацией.ЗаписатьКонтактнуюИнформацию(Объект, ЗначенияПолей, ВидКИ, ТипКИ);
	
КонецПроцедуры

#КонецОбласти

