////////////////////////////////////////////////////////////////////////////////
// ИнтеграцияСЯндексКассойКлиент: механизм интеграции с сервисом Яндекс.Касса.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область Команды

// Открывает форму с платежной ссылкой на страницу оплаты через Яндекс.Кассу
//
// Параметры:
// см. описание параметров общих команд 
Процедура ОткрытьФормуПолученияПлатежнойСсылки(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт 
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина,
		"ОбщийМодуль.ИнтеграцияСЯндексКассойКлиент.ОткрытьФормуПолученияПлатежнойСсылки");
	
	////////////////////////////////////////////////////////////////////////////////
	
	Команда = "ОткрытьФормуПлатежнойСсылки";
	ПараметрыКоманды = Новый Структура();
	ПараметрыКоманды.Вставить("ПараметрКоманды", ПараметрКоманды);
	ПараметрыКоманды.Вставить("ПараметрыВыполненияКоманды", ПараметрыВыполненияКоманды);
	
	ИнтеграцияСЯндексКассойСлужебныйКлиент.ВыполнитьКомандуСПроверкойПодключенияИПП(Команда, ПараметрыКоманды);
	
КонецПроцедуры

// Отправляет запрос получения операций по Яндекс.Кассе на сервер 1С, а после получения ответа вызывает оповещение о завершении 
// указанное в параметре ОповещениеОЗавершении, в которое возвращает адрес хранилища в котором содержится массив структур описывающих операции
//
// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения - Оповещение о завершении получения операций по Яндекс.Кассе
//  НастройкаЯндексКассы - СправочникСсылка.НастройкиЯндексКассы - Текущая настройка Яндекс.Кассы, если не указана то, будут обработаны все действительные настройки;
//  ПериодЗапроса - СтандартныйПериод, Структура - Период за который будут выбираться операции по Яндекс.Кассе.
//    * ДатаНачала - Дата - Начало периода запроса, если не указан, дата начала будет определена автоматически.
//    * ДатаОкончания - Дата - Окончание периода запроса, если не указан, дата окончания будет равна текущей дате.
//  ТолькоСДоговором - Булево, Неопределено - позволяет указать для каких настроек следует загружать операции:
//                                            * Неопределено - будут загружены и операции по схемам "С договором" и "Без договора"
//                                            * Истина - будут загружены операции по схеме "С договором"
//                                            * Ложь - будут загружены операции по схеме "Без договора"
//                                            Если указан параметр НастройкаЯндексКассы этот параметр не учитывается
//
Процедура ПолучитьОперацииПоЯндексКассе(ОповещениеОЗавершении,
										НастройкаЯндексКассы = Неопределено,
										ПериодЗапроса = Неопределено,
										ТолькоСДоговором = Неопределено) Экспорт
										
	ДатаНачала    = Неопределено;
	ДатаОкончания = Неопределено;
	Если ТипЗнч(ПериодЗапроса) = Тип("СтандартныйПериод") Тогда 
		ДатаНачала    = ПериодЗапроса.ДатаНачала;
		ДатаОкончания = ПериодЗапроса.ДатаОкончания;
	ИначеЕсли ТипЗнч(ПериодЗапроса) = Тип("Структура") Тогда 
		ПериодЗапроса.Свойство("ДатаНачала",    ДатаНачала);
		ПериодЗапроса.Свойство("ДатаОкончания", ДатаОкончания);
	КонецЕсли;
	
	ТекущаяДатаСеанса = ОбщегоНазначенияКлиент.ДатаСеанса();
	Если Не ДатаНачала = Неопределено Тогда 
		Если ДатаНачала > ТекущаяДатаСеанса ИЛИ ДатаОкончания > КонецДня(ТекущаяДатаСеанса) Тогда
			СообщениеТекст = НСтр("ru = 'Период запроса указан неверно'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеТекст, , "Период");
			Возврат;
		КонецЕсли;
		
		Если Не ДатаОкончания = Неопределено Тогда
			Если ДатаНачала > ДатаОкончания Тогда 
				СообщениеТекст = НСтр("ru = 'Период запроса указан неверно'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеТекст, , "Период");
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	Если Не ДатаОкончания = Неопределено Тогда
		Если КонецДня(ДатаОкончания) = КонецДня(ТекущаяДатаСеанса) Тогда
			ДатаОкончания = ТекущаяДатаСеанса;
		Иначе
			ДатаОкончания = КонецДня(ДатаОкончания);
		КонецЕсли;
	КонецЕсли;
	
	Команда = "ПолучитьОперацииПоЯндексКассе";
	ПараметрыКоманды = Новый Структура();
	ПараметрыКоманды.Вставить("ОповещениеОЗавершении",	ОповещениеОЗавершении);
	ПараметрыКоманды.Вставить("НастройкаЯндексКассы",	НастройкаЯндексКассы);
	ПараметрыКоманды.Вставить("ДатаНачала",				ДатаНачала);
	ПараметрыКоманды.Вставить("ДатаОкончания",			ДатаОкончания);
	ПараметрыКоманды.Вставить("ТолькоСДоговором",		ТолькоСДоговором);
	
	ИнтеграцияСЯндексКассойСлужебныйКлиент.ВыполнитьКомандуСПроверкойПодключенияИПП(Команда, ПараметрыКоманды);
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти

