
#Область ПрограммныйИнтерфейс

// МЕТОД НЕ ПОДДЕРЖИВАЕТСЯ.
// Возвращает описание данных логического хранилища.
//
// Параметры:
//  ИдентификаторХранилища - Строка - идентификатор логического хранилища.
//  ИдентификаторДанных    - Строка - идентификатор данных хранилища.
// 
// Возвращаемое значение:
//   - Вызывается исключение.
//
Функция Описание(ИдентификаторХранилища, ИдентификаторДанных) Экспорт
	
	ВызватьИсключение Словарь().МетодНеПоддерживается;
    
КонецФункции

// МЕТОД НЕ ПОДДЕРЖИВАЕТСЯ.
// Возвращает данные логического хранилища.
//
// Параметры:
//  ОписаниеДанных - Структура - описание данных хранилища.
// 
// Возвращаемое значение:
//   - Вызывается исключение.
//
Функция Данные(ОписаниеДанных) Экспорт
    
	ВызватьИсключение Словарь().МетодНеПоддерживается;
	
КонецФункции

// Записывает данные в логическое хранилище.
// Выполняет действия:
// - сохраняет файл данных в файловом хранилище
// - планирует задание очереди заданий на обработки файла
// - возвращается идентификатор задания в ответ
//
// Возвращаемое значение:
//   - Строка(36) - идентификатор задания очереди заданий.
//
Функция Загрузить(ОписаниеДанных) Экспорт
    
    Ответ = ОтветПоУмолчанию();
    Если ОписаниеДанных.Данные = Тип("Строка") Тогда
        ПолноеИмя = Неопределено;
        Данные = ОписаниеДанных.Данные;
    Иначе
        ПолноеИмя = ОписаниеДанных.Данные;
        Данные = Неопределено;
	КонецЕсли;
	
    УстановитьПривилегированныйРежим(Истина);
    ИдентификаторФайла = РегистрыСведений.ФайлыОбластейДанных.Загрузить(ОписаниеДанных.ИмяФайла, Данные, ПолноеИмя);
    
	ПараметрыЗапуска = Новый Массив();
	ПараметрыЗапуска.Добавить(Строка(ИдентификаторФайла));
    
    ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ОбластьДанных", ОбщегоНазначения.ЗначениеРазделителяСеанса());
	ПараметрыЗадания.Вставить("Использование", Истина);
	ПараметрыЗадания.Вставить("ЭксклюзивноеВыполнение", Истина);
	ПараметрыЗадания.Вставить("ИмяМетода", "ПоставляемыеДанныеОбластейДанных.ОбработатьДанные");
	ПараметрыЗадания.Вставить("Параметры", ПараметрыЗапуска);
	ПараметрыЗадания.Вставить("Ключ", Строка(ИдентификаторФайла));
	ПараметрыЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", 0);
	ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", 0);
	Задание = ОчередьЗаданий.ДобавитьЗадание(ПараметрыЗадания);
    ИдентификаторЗадания = Задание.УникальныйИдентификатор();
    РегистрыСведений.СвойстваЗаданий.Установить(Задание);
    
    Ответ.Вставить("storage", ОчередьЗаданийВнешнийИнтерфейс.ИдентификаторХранилища());
    Ответ.Вставить("id", Строка(Задание.УникальныйИдентификатор()));
    
    Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// См. ОчередьЗаданийПереопределяемый.ПриОпределенииПсевдонимовОбработчиков.
//
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить("ПоставляемыеДанныеОбластейДанных.ОбработатьДанные");
	
КонецПроцедуры                                                       

// Обработчик задания очереди заданий.
//
// Параметры:
//  ИдентификаторФайла - Строка(36) - идентификатор файла для обработки.
//
Процедура ОбработатьДанные(ИдентификаторФайла) Экспорт
    
    Словарь = Словарь();
    ИдентификаторЗадания = ОчередьЗаданийВнешнийИнтерфейс.ИдентификаторЗадания(ИдентификаторФайла);
    ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
    КодыСостояний = РегистрыСведений.СвойстваЗаданий.КодыСостояний();
    
    Попытка
        ОписаниеФайла = РегистрыСведений.ФайлыОбластейДанных.ОписаниеФайла(ИдентификаторФайла);
        Если ЗначениеЗаполнено(ОписаниеФайла.ПолноеИмя) Тогда
            ПотокДанных = ФайловыеПотоки.ОткрытьДляЧтения(ОписаниеФайла.ПолноеИмя);
        Иначе
            ПотокДанных = ОписаниеФайла.Данные.ОткрытьПотокДляЧтения();
        КонецЕсли; 
        Архиватор = Новый ЧтениеZipФайла(ПотокДанных);
        Попытка
            Архиватор.ИзвлечьВсе(ИмяВременногоФайла);
        Исключение
            ОчередьЗаданийВнешнийИнтерфейс.УстановитьОшибкуДанных(ИдентификаторЗадания, Словарь.ФайлПоврежден);
            Возврат;
        КонецПопытки;
        ПоискМанифеста = НайтиФайлы(ИмяВременногоФайла, ПоставляемыеДанныеОбластейДанныхПовтИсп.ИмяФайлаМанифеста(), Истина);
        Если ПоискМанифеста.Количество() = 0 Тогда
            ОчередьЗаданийВнешнийИнтерфейс.УстановитьОшибкуДанных(ИдентификаторЗадания, Словарь.МанифестНеЗадан);
            Возврат;
        КонецЕсли;
        Попытка
            ЧтениеJSON = Новый ЧтениеJSON;
            ЧтениеJSON.ОткрытьФайл(ПоискМанифеста[0].ПолноеИмя);
            Манифест = ПрочитатьJSON(ЧтениеJSON);
            ЧтениеJSON.Закрыть();
        Исключение
            ОчередьЗаданийВнешнийИнтерфейс.УстановитьОшибкуДанных(ИдентификаторЗадания, Словарь.МанифестНеВерногоФормата);
            Возврат;
        КонецПопытки;
        Порядок = ПоставляемыеДанныеОбластейДанныхПовтИсп.ИмяПоляСоставаДанных();
        Если Не Манифест.Свойство(Порядок) Тогда
            ОчередьЗаданийВнешнийИнтерфейс.УстановитьОшибкуДанных(ИдентификаторЗадания, 
                СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Словарь.ОтсутствуетСвойствоМанифеста, Порядок));
            Возврат;
        КонецЕсли;
        Результаты = Новый Массив;
        Для Каждого Элемент Из Манифест[Порядок] Цикл
            ПоискФайла = НайтиФайлы(ИмяВременногоФайла, Элемент.file);
            Если Элемент.Свойство("version") Тогда
                Версия = Элемент.version;
            Иначе 
                Версия = "";
            КонецЕсли; 
            Если ПоискФайла.Количество() = 0 Тогда
                ОчередьЗаданийВнешнийИнтерфейс.УстановитьОшибкуДанных(ИдентификаторЗадания, 
                    СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Словарь.ФайлНеНайден, Элемент.file));
                Возврат;
            КонецЕсли; 
            ПотокДанных = ФайловыеПотоки.ОткрытьДляЧтения(ПоискФайла[0].ПолноеИмя);
            Обработчик = Элемент.handler;
            ДанныеОбработаны = Ложь;
            КодВозвратаОбработчика = КодыСостояний.Выполнено;
            ОписаниеОшибкиОбработчика = "";
        	// _Демо начало примера
            Если Метаданные.ОбщиеМодули.Найти("_ДемоПоставляемыеДанныеОбластейДанных") <> Неопределено Тогда
                Модуль_ДемоПоставляемыеДанныеОбластейДанных = ТехнологияСервисаИнтеграцияСБСП.ОбщийМодуль("_ДемоПоставляемыеДанныеОбластейДанных");
                Модуль_ДемоПоставляемыеДанныеОбластейДанных.ОбработатьПолученныеДанныеОбласти(
                    ПотокДанных, Обработчик, ДанныеОбработаны, КодВозвратаОбработчика, ОписаниеОшибкиОбработчика);
            КонецЕсли;
        	// _Демо конец примера
            ПоставляемыеДанныеОбластейДанныхПереопределяемый.ОбработатьПолученныеДанныеОбласти(
                ПотокДанных, Обработчик, ДанныеОбработаны, КодВозвратаОбработчика, ОписаниеОшибкиОбработчика);
                
            РезультатОбработчика = Новый Структура;
            РезультатОбработчика.Вставить("file", Элемент.file);
            РезультатОбработчика.Вставить("version", Версия);
            РезультатОбработчика.Вставить("handler", Обработчик);
            РезультатОбработчика.Вставить("response", КодВозвратаОбработчика);
            РезультатОбработчика.Вставить("error", Не ДанныеОбработаны);
            РезультатОбработчика.Вставить("message", ОписаниеОшибкиОбработчика);
            Результаты.Добавить(РезультатОбработчика);
            
            Если Не ДанныеОбработаны Тогда
                ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
                    Словарь.ОбработчикНеНайден, Обработчик); 
            КонецЕсли; 
        КонецЦикла;
    Исключение
        ОчередьЗаданийВнешнийИнтерфейс.УстановитьВнутреннююОшибку(
            ИдентификаторЗадания, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
        ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
    КонецПопытки;

    Свойства = РегистрыСведений.СвойстваЗаданий.НовыйСвойстваЗадания();
    Свойства.КодСостояния = КодыСостояний.Выполнено;
    Свойства.Результат = ДанныеJSON(Результаты);
    
    РегистрыСведений.СвойстваЗаданий.Установить(ИдентификаторЗадания, Свойства);
    
КонецПроцедуры

// См. ВыгрузкаЗагрузкаДанныхПереопределяемый.ПриЗаполненииТиповИсключаемыхИзВыгрузкиЗагрузки.
Процедура ПриЗаполненииТиповИсключаемыхИзВыгрузкиЗагрузки(Типы) Экспорт
	
	Типы.Добавить(Метаданные.РегистрыСведений.ФайлыОбластейДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОтветПоУмолчанию()
	
    КодыСостояний = ПоставляемыеДанныеОбластейДанныхПовтИсп.КодыСостояний();
    ОсновнойРаздел = Новый Структура;
    ОсновнойРаздел.Вставить("response", КодыСостояний.Выполнено);
    ОсновнойРаздел.Вставить("error", Ложь);
    ОсновнойРаздел.Вставить("message", "");
    ОтветПоУмолчанию = Новый Структура;
    ОтветПоУмолчанию.Вставить("general", ОсновнойРаздел);
    
    Возврат ОтветПоУмолчанию;
    
КонецФункции

Функция ДанныеJSON(Данные)
    
    ЗаписьJSON = Новый ЗаписьJSON;
    ЗаписьJSON.УстановитьСтроку();
    ЗаписатьJSON(ЗаписьJSON, Данные);
    
    Возврат ЗаписьJSON.Закрыть();
    
КонецФункции

Функция Словарь()
	
	Возврат ПоставляемыеДанныеОбластейДанныхПовтИсп.СловарьПодсистемы();
	
КонецФункции

#КонецОбласти 

