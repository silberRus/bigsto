
#Область ПрограммныйИнтерфейс

// Возвращает описание данных логического хранилища.
//
// Параметры:
//  ИдентификаторХранилища - Строка - идентификатор логического хранилища.
//  ИдентификаторДанных    - Строка - идентификатор данных хранилища.
// 
// Возвращаемое значение:
//   - Структура - описание состояния задания очереди.
//       - ИмяФайла - Строка - имя файла описания состояния задания {УникальныйИдентификатор}.json.
//       - Размер - Число - размер файла в байтах.
//       - Данные - ДвоичныеДанные - двоичные данные файла описания задания.
//
Функция Описание(ИдентификаторХранилища, ИдентификаторДанных) Экспорт
    
    ДвоичныеДанныеОписанияЗадания = ДвоичныеДанныеОписанияЗадания(ИдентификаторДанных);
    Если ДвоичныеДанныеОписанияЗадания = Неопределено Тогда
        ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Словарь().ЗаданиеНеНайдено, ИдентификаторДанных);
    КонецЕсли;
    
    Описание = Новый Структура;
    Описание.Вставить("ИмяФайла", ИдентификаторДанных + ".json");
    Описание.Вставить("Размер", ДвоичныеДанныеОписанияЗадания.Размер());
    Описание.Вставить("Данные", ДвоичныеДанныеОписанияЗадания);
    
    Возврат Описание;
	
КонецФункции

// Возвращает данные логического хранилища.
//
// Параметры:
//  ОписаниеДанных - Структура - описание данных хранилища.
// 
// Возвращаемое значение:
//   - Вызывается исключение.
//
Функция Данные(ОписаниеДанных) Экспорт
    
	Возврат ОписаниеДанных.Данные;
	
КонецФункции

// МЕТОД НЕ ПОДДЕРЖИВАЕТСЯ. 
// Записывает данные в логическое хранилище.
//
// Возвращаемое значение:
//   - Вызывается исключение.
//
Функция Загрузить(ОписаниеДанных) Экспорт
	
    ВызватьИсключение Словарь().МетодНеПоддерживается;
    
КонецФункции

// Возвращает идентификатор хранилища в виде строки.
// 
// Возвращаемое значение:
//   - Строка - идентификатор хранилища. 
//
Функция ИдентификаторХранилища() Экспорт
	
	Возврат "jobs";
	
КонецФункции
 
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// См. ВыгрузкаЗагрузкаДанныхПереопределяемый.ПриЗаполненииТиповИсключаемыхИзВыгрузкиЗагрузки.
//
Процедура ПриЗаполненииТиповИсключаемыхИзВыгрузкиЗагрузки(Типы) Экспорт
    
    Типы.Добавить(Метаданные.РегистрыСведений.СвойстваЗаданий);
    
КонецПроцедуры

// Обработчик подписки на событие ОчередьЗаданийПередУдалением.
//
// Параметры:
//  Источник - СправочникОбъект.ОчередьЗаданий, СправочникОбъект.ОчередьЗаданийОбластейДанных - удаляемый объект.
//  Отказ    - Булево - признак отказа от удаления.
//
Процедура ОчередьЗаданийПередУдалением(Источник, Отказ) Экспорт

    Если Источник.ОбменДанными.Загрузка Тогда
        Возврат;
    КонецЕсли;
    
    РегистрыСведений.СвойстваЗаданий.ОчиститьСвойствоЗадание(Источник.Ссылка);
    
КонецПроцедуры

// Обработчик подписки на событие ОчередьЗаданийПриЗаписи.
//
// Параметры:
//  Источник - СправочникОбъект.ОчередьЗаданий, СправочникОбъект.ОчередьЗаданийОбластейДанных - записываемый объект.
//  Отказ    - Булево - признак отказа от записи объекта.
//
Процедура ОчередьЗаданийПриЗаписи(Источник, Отказ) Экспорт
    
    Если Источник.ОбменДанными.Загрузка Тогда
        Возврат;
    КонецЕсли;
    
    СвойстваЗадания = РегистрыСведений.СвойстваЗаданий.НовыйСвойстваЗадания();
    Если Источник.СостояниеЗадания = Перечисления.СостоянияЗаданий.Запланировано Или 
        Источник.СостояниеЗадания = Перечисления.СостоянияЗаданий.Выполняется Тогда
        СвойстваЗадания.КодСостояния = РегистрыСведений.СвойстваЗаданий.КодыСостояний().Ожидание;
        РегистрыСведений.СвойстваЗаданий.Установить(Источник.Ссылка, СвойстваЗадания);
    КонецЕсли; 
    
    Если Источник.ПометкаУдаления Тогда
        РегистрыСведений.СвойстваЗаданий.ОчиститьСвойствоЗадание(Источник.Ссылка);
    КонецЕсли; 
    
КонецПроцедуры

// Устанавливает в свойствах задания состояние ошибки данных.
// Используется при ошибках в логике обработки или при неправильных составах данных для обработки.
//
// Параметры:
//  ИдентификаторЗадания - УниверсальныйИдентификатор - уникальный идентификатор ссылки задания.
//  СообщениеОбОшибке    - Строка - сообщение об ошибке для добавления в свойства задания.
//
Процедура УстановитьОшибкуДанных(ИдентификаторЗадания, СообщениеОбОшибке) Экспорт
	
    Свойства = РегистрыСведений.СвойстваЗаданий.НовыйСвойстваЗадания();
    Свойства.КодСостояния = ОчередьЗаданийВнешнийИнтерфейсПовтИсп.КодыСостояний().ОшибкаДанных;
    Свойства.СообщениеОбОшибке = СообщениеОбОшибке;
    РегистрыСведений.СвойстваЗаданий.Установить(ИдентификаторЗадания, Свойства);
	
КонецПроцедуры

// Устанавливает в свойствах задания состояние внутренней ошибки.
//
// Параметры:
//  ИдентификаторЗадания - УниверсальныйИдентификатор - уникальный идентификатор ссылки задания.
//  СообщениеОбОшибке    - Строка - сообщение об ошибке для добавления в свойства задания.
//
Процедура УстановитьВнутреннююОшибку(ИдентификаторЗадания, СообщениеОбОшибке) Экспорт
	
    Свойства = РегистрыСведений.СвойстваЗаданий.НовыйСвойстваЗадания();
    Свойства.КодСостояния = ОчередьЗаданийВнешнийИнтерфейсПовтИсп.КодыСостояний().ВнутренняяОшибка;
    Свойства.СообщениеОбОшибке = СообщениеОбОшибке;
    РегистрыСведений.СвойстваЗаданий.Установить(ИдентификаторЗадания, Свойства);
	
КонецПроцедуры

// Возвращает уникальный идентификатор выполняющегося задания по ключу.
//
// Параметры:
//  Ключ - Строка - ключ задания для поиска выполняющегося задания.
// 
// Возвращаемое значение:
//   - 
//
Функция ИдентификаторЗадания(Ключ) Экспорт
	
    Отбор = Новый Структура;
    Отбор.Вставить("Ключ", Ключ);
    Отбор.Вставить("СостояниеЗадания", Перечисления.СостоянияЗаданий.Выполняется);
    ПоискЗаданий = ОчередьЗаданий.ПолучитьЗадания(Отбор);
    Если ПоискЗаданий.Количество() = 0 Тогда
        ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Словарь().ЗаданиеПоКлючуНеНайдено, Ключ); 
    КонецЕсли;
    
    Возврат ПоискЗаданий[0].Идентификатор.УникальныйИдентификатор();
	
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция ОписаниеСостоянияЗадания(ИдентификаторЗадания)
    
    Идентификатор = Новый УникальныйИдентификатор(ИдентификаторЗадания);
    Отбор = Новый Структура("Идентификатор", Идентификатор);
    
    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ
        |   СвойстваЗаданий.КодСостояния КАК response,
        |   СвойстваЗаданий.Ошибка КАК error,
        |   СвойстваЗаданий.СообщениеОбОшибке КАК message,
        |   СвойстваЗаданий.Результат КАК result
        |ИЗ
        |   РегистрСведений.СвойстваЗаданий КАК СвойстваЗаданий
        |ГДЕ
        |   СвойстваЗаданий.ИдентификаторЗадания = &ИдентификаторЗадания";
    Запрос.УстановитьПараметр("ИдентификаторЗадания", Идентификатор);
	УстановитьПривилегированныйРежим(Истина);
    Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
    Если Результат.Пустой() Тогда
        Возврат Неопределено;
    Иначе
        Данные = Результат.Выгрузить()[0];
        Описание = НовыйОписаниеСостоянияЗадания();
        ЗаполнитьЗначенияСвойств(Описание.general, Данные);
        ЗаполнитьЗначенияСвойств(Описание, Данные);
        Если ЗначениеЗаполнено(Данные.result) Тогда
            Результат = СокрЛП(Данные.result); 
            Если (Лев(Результат, 1) = "{" И Прав(Результат, 1) = "}") 
             Или (Лев(Результат, 1) = "[" И Прав(Результат, 1) = "]") Тогда
                // Попытаемся превратить результат в структуру, 
                // чтобы потом обратно сериализовать в JSON.
                Попытка
                    ЧтениеJSON = Новый ЧтениеJSON;
                    ЧтениеJSON.УстановитьСтроку(Результат);
                    Результат = ПрочитатьJSON(ЧтениеJSON);
                    ЧтениеJSON.Закрыть();
                    Описание.result = Результат;
                Исключение
                    // Не удалось десериализовать данные из строки.
                    // В этом случае в результате будет возвращен текст из результата.
                    // Исключение проглатывается запланировано. 
                КонецПопытки; 
            КонецЕсли;
        Иначе
            Описание.result = Неопределено;    
        КонецЕсли;
        УдалитьПустойРезультат(Описание);
        Возврат Описание
    КонецЕсли; 
    
КонецФункции

Функция ДвоичныеДанныеОписанияЗадания(ИдентификаторЗадания)
    
    ОписаниеСостояния = ОписаниеСостоянияЗадания(ИдентификаторЗадания);
    Если ОписаниеСостояния = Неопределено Тогда
        Возврат Неопределено;    
    КонецЕсли; 
    ОписаниеСостоянияЗадания = ДанныеJSON(ОписаниеСостояния);
    ДвоичныеДанныеОписанияЗадания = ПолучитьДвоичныеДанныеИзСтроки(ОписаниеСостоянияЗадания);
    
    Возврат ДвоичныеДанныеОписанияЗадания;
    
КонецФункции    

Функция ДанныеJSON(Данные)
    
    ЗаписьJSON = Новый ЗаписьJSON;
    ЗаписьJSON.УстановитьСтроку();
    ЗаписатьJSON(ЗаписьJSON, Данные);
    
    Возврат ЗаписьJSON.Закрыть();
    
КонецФункции

Функция НовыйОписаниеСостоянияЗадания()

    ОсновнаяСекция = Новый Структура;
	ОсновнаяСекция.Вставить("response", 0);
	ОсновнаяСекция.Вставить("error", Ложь);
	ОсновнаяСекция.Вставить("message", "");
    
	ОписаниеСостояния = Новый Структура;
    ОписаниеСостояния.Вставить("general", ОсновнаяСекция);
    ОписаниеСостояния.Вставить("result");
	
	Возврат ОписаниеСостояния;

КонецФункции

Функция Словарь()
    
    Возврат ОчередьЗаданийВнешнийИнтерфейсПовтИсп.СловарьПодсистемы();
    
КонецФункции

Процедура УдалитьПустойРезультат(Ответ)
	
    Если Ответ.Свойство("result") И Не ЗначениеЗаполнено(Ответ.result) Тогда
        Ответ.Удалить("result");
    КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 
