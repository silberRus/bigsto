
#Область ПрограммныйИнтерфейс

Функция ДанныеОформленияДокументовПоПриоритетам(ДокументСсылка) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СтатусыОформленияДокументовЕГАИС.Документ,
	|	СтатусыОформленияДокументовЕГАИС.СтатусОформления КАК Статус
	|ИЗ
	|	РегистрСведений.СтатусыОформленияДокументовЕГАИС КАК СтатусыОформленияДокументовЕГАИС
	|ГДЕ
	|	СтатусыОформленияДокументовЕГАИС.Основание = &Основание
	|");
	Запрос.УстановитьПараметр("Основание", ДокументСсылка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеПоСтатусам = Запрос.Выполнить().Выгрузить();
	ДокументыПоОснованию = ИнтеграцияЕГАИСВызовСервера.ДокументыПоОснованию(ДокументСсылка);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	СтатусыОформления = Новый Структура;
	Для Каждого СтрокаТЧ Из ДанныеПоСтатусам Цикл
		СтатусыОформления.Вставить(СтрокаТЧ.Документ.Метаданные().Имя, СтрокаТЧ.Статус);
	КонецЦикла;
	
	ТипДокумента = ТипЗнч(ДокументСсылка);
	
	ОформлениеДокументовПоПриоритетам = Новый Соответствие;
	Для Каждого КлючИЗначение Из СтатусыОформления Цикл
		
		Если КлючИЗначение.Ключ = Метаданные.Документы.АктСписанияЕГАИС.Имя Тогда
			
			Если Метаданные.ОпределяемыеТипы.ОснованиеАктаСписанияЕГАИС.Тип.СодержитТип(ТипДокумента) Тогда
				Данные = ИнтеграцияЕГАИС.ДанныеДокументаЕГАИС(Метаданные.Документы.АктСписанияЕГАИС, ДокументыПоОснованию, СтатусыОформления);
				ДобавитьВРезультат(ОформлениеДокументовПоПриоритетам, 1, Данные);
			КонецЕсли;
			
		КонецЕсли;
		
		Если КлючИЗначение.Ключ = Метаданные.Документы.АктПостановкиНаБалансЕГАИС.Имя Тогда
			
			Если Метаданные.ОпределяемыеТипы.ОснованиеАктаПостановкиНаБалансЕГАИС.Тип.СодержитТип(ТипДокумента) Тогда
				Данные = ИнтеграцияЕГАИС.ДанныеДокументаЕГАИС(Метаданные.Документы.АктПостановкиНаБалансЕГАИС, ДокументыПоОснованию, СтатусыОформления);
				ДобавитьВРезультат(ОформлениеДокументовПоПриоритетам, 2, Данные);
			КонецЕсли;
			
		КонецЕсли;
		
		Если КлючИЗначение.Ключ = Метаданные.Документы.ТТНИсходящаяЕГАИС.Имя
			Или КлючИЗначение.Ключ = Метаданные.Документы.ТТНВходящаяЕГАИС.Имя Тогда
			
			Если Метаданные.ОпределяемыеТипы.ОснованиеТТНИсходящаяЕГАИС.Тип.СодержитТип(ТипДокумента) Тогда
				Данные = ИнтеграцияЕГАИС.ДанныеДокументаЕГАИС(Метаданные.Документы.ТТНИсходящаяЕГАИС, ДокументыПоОснованию, СтатусыОформления);
				ДобавитьВРезультат(ОформлениеДокументовПоПриоритетам, 3, Данные);
			КонецЕсли;
			Если Метаданные.ОпределяемыеТипы.ОснованиеТТНВходящаяЕГАИС.Тип.СодержитТип(ТипДокумента) Тогда
				Данные = ИнтеграцияЕГАИС.ДанныеДокументаЕГАИС(Метаданные.Документы.ТТНВходящаяЕГАИС, ДокументыПоОснованию, СтатусыОформления);
				ДобавитьВРезультат(ОформлениеДокументовПоПриоритетам, 4, Данные);
			КонецЕсли;
			
		КонецЕсли;
		
		Если КлючИЗначение.Ключ = Метаданные.Документы.ЧекЕГАИС.Имя
			Или КлючИЗначение.Ключ = Метаданные.Документы.ЧекЕГАИСВозврат.Имя Тогда
			
			Если Метаданные.ОпределяемыеТипы.ОснованиеЧекаЕГАИС.Тип.СодержитТип(ТипДокумента) Тогда
				Данные = ИнтеграцияЕГАИС.ДанныеДокументаЕГАИС(Метаданные.Документы.ЧекЕГАИС, ДокументыПоОснованию, СтатусыОформления);
				ДобавитьВРезультат(ОформлениеДокументовПоПриоритетам, 1, Данные);
			КонецЕсли;
			Если Метаданные.ОпределяемыеТипы.ОснованиеЧекаЕГАИСВозврат.Тип.СодержитТип(ТипДокумента) Тогда
				Данные = ИнтеграцияЕГАИС.ДанныеДокументаЕГАИС(Метаданные.Документы.ЧекЕГАИСВозврат, ДокументыПоОснованию, СтатусыОформления);
				ДобавитьВРезультат(ОформлениеДокументовПоПриоритетам, 6, Данные);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОформлениеДокументовПоПриоритетам;
	
КонецФункции

Процедура СформироватьТекстДокументаЕГАИС(Форма, ОчищатьВместоИзмененияВидимости = Ложь) Экспорт
	
	ОформлениеДокументовПоПриоритетам = ДанныеОформленияДокументовПоПриоритетам(
		Форма.Объект.Ссылка);
	
	ЕстьАлкогольнаяПродукция = ОформлениеДокументовПоПриоритетам.Количество() > 0;
	
	Если Не ОчищатьВместоИзмененияВидимости Тогда
		Форма.Элементы.ТекстДокументаЕГАИС.Видимость = ЕстьАлкогольнаяПродукция;
	Иначе
		Форма.ТекстДокументаЕГАИС = "";
	КонецЕсли;
	
	Если Не ЕстьАлкогольнаяПродукция Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
		И ЗначениеЗаполнено(Форма.Объект.УдалитьПодписьЧекаЕГАИС) Тогда
		Форма.ТекстДокументаЕГАИС = Новый ФорматированнаяСтрока(НСтр("ru = 'Данные переданы в ЕГАИС'"));
		Возврат;
	КонецЕсли;
	
	ФорматированныеСтроки = Новый Массив;
	Для Каждого КлючИЗначение Из ОформлениеДокументовПоПриоритетам Цикл
		Данные = КлючИЗначение.Значение;
		
		ЕстьДокументыСЧастичнымОформлением = Ложь;
		Для Каждого ДанныеДокументаЕГАИС Из Данные Цикл
			Если ДанныеДокументаЕГАИС.МассивДокументов.Количество() > 0 Тогда
				ЕстьДокументыСЧастичнымОформлением = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ДанныеДокументаЕГАИС Из Данные Цикл
			
			Если ЕстьДокументыСЧастичнымОформлением Тогда
				Если ДанныеДокументаЕГАИС.МассивДокументов.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если ФорматированныеСтроки.Количество() <> 0 Тогда
				Если Данные.Количество() > 1 Тогда
					ФорматированныеСтроки.Добавить(" " + НСтр("ru = 'или'") + " ");
				Иначе
					ФорматированныеСтроки.Добавить("," + " ");
				КонецЕсли;
			КонецЕсли;
			
			ФорматированныеСтроки.Добавить(ДанныеДокументаЕГАИС.Представление);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Форма.ТекстДокументаЕГАИС = Новый ФорматированнаяСтрока(ФорматированныеСтроки);
	
КонецПроцедуры

Функция ТребуетсяОформлениеДокументовЕГАИС(Основание, МетаданныеДокументаЕГАИС) Экспорт
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ВнутреннееПотреблениеТоваров") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.АктСписанияЕГАИС Тогда
			
			Возврат ЕстьАлкогольнаяПродукция(Основание, "Товары");
			
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.ЧекЕГАИС Тогда
			
			ЗапросПоСкладу = Новый Запрос(
			"ВЫБРАТЬ
			|	Документ.Склад.ТипСклада КАК ТипСклада
			|ИЗ
			|	Документ.ВнутреннееПотреблениеТоваров КАК Документ
			|ГДЕ
			|	Документ.Ссылка = &Основание");
			
			ЗапросПоСкладу.УстановитьПараметр("Основание", Основание);
			
			ВыборкаПоСкладу = ЗапросПоСкладу.Выполнить().Выбрать();
			ВыборкаПоСкладу.Следующий();
			
			Возврат ЕстьМаркируемаяАлкогольнаяПродукция(Основание, "Товары")
				И ВыборкаПоСкладу.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин;;
			
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.ЧекЕГАИСВозврат Тогда
			
			Возврат ЕстьМаркируемаяАлкогольнаяПродукция(Основание, "Товары");
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.АктСписанияЕГАИС Тогда
			
			ТребуетсяОформление = ЕстьНеМаркируемаяАлкогольнаяПродукция(Основание, "Товары");
			
			Если Константы.ДатаНачалаРегистрацииРозничныхПродажВЕГАИСВСельскойМестности.Получить() >= ТекущаяДатаСеанса()
				И Не ТребуетсяОформление Тогда
				ТребуетсяОформление = ЕстьМаркируемаяАлкогольнаяПродукция(Основание, "Товары");
			КонецЕсли;
			
			Возврат ТребуетсяОформление;
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.СписаниеНедостачТоваров") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.АктСписанияЕГАИС Тогда
			
			Возврат ЕстьАлкогольнаяПродукция(Основание, "Товары");
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ОприходованиеИзлишковТоваров") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.АктПостановкиНаБалансЕГАИС Тогда
			
			Возврат ЕстьАлкогольнаяПродукция(Основание, "Товары");
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПересортицаТоваров") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.АктПостановкиНаБалансЕГАИС Тогда
			
			Возврат ЕстьАлкогольнаяПродукция(Основание, "Товары", "НоменклатураОприходование");
			
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.АктСписанияЕГАИС Тогда
			
			Возврат ЕстьАлкогольнаяПродукция(Основание, "Товары", "Номенклатура");
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНИсходящаяЕГАИС Тогда
			
			Возврат ЕстьАлкогольнаяПродукция(Основание, "Товары");
			
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНВходящаяЕГАИС Тогда
			
			Возврат Ложь;
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНИсходящаяЕГАИС Тогда
			
			Возврат ЕстьАлкогольнаяПродукция(Основание, "Товары");
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНИсходящаяЕГАИС Тогда
			
			Возврат ЕстьАлкогольнаяПродукция(Основание, "Товары");
			
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНВходящаяЕГАИС Тогда
			
			Возврат Ложь;
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНИсходящаяЕГАИС Тогда
			
			Возврат ЕстьАлкогольнаяПродукция(Основание, "Товары");
			
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНВходящаяЕГАИС Тогда
			
			Возврат Ложь;
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ЧекЕГАИС Тогда
			
			ЗапросПоСкладу = Новый Запрос(
			"ВЫБРАТЬ
			|	Документ.Склад.ТипСклада КАК ТипСклада
			|ИЗ
			|	Документ.РеализацияТоваровУслуг КАК Документ
			|ГДЕ
			|	Документ.Ссылка = &Основание");
			
			ЗапросПоСкладу.УстановитьПараметр("Основание", Основание);
			
			ВыборкаПоСкладу = ЗапросПоСкладу.Выполнить().Выбрать();
			ВыборкаПоСкладу.Следующий();
			
			Возврат ЕстьМаркируемаяАлкогольнаяПродукция(Основание, "Товары")
				И ВыборкаПоСкладу.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин;
				
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.ТТНИсходящаяЕГАИС Тогда
			
			ЗапросПоСкладу = Новый Запрос(
			"ВЫБРАТЬ
			|	Документ.Склад.ТипСклада КАК ТипСклада
			|ИЗ
			|	Документ.РеализацияТоваровУслуг КАК Документ
			|ГДЕ
			|	Документ.Ссылка = &Основание");
			
			ЗапросПоСкладу.УстановитьПараметр("Основание", Основание);
			
			ВыборкаПоСкладу = ЗапросПоСкладу.Выполнить().Выбрать();
			ВыборкаПоСкладу.Следующий();
			
			Возврат ЕстьАлкогольнаяПродукция(Основание, "Товары")
				И ВыборкаПоСкладу.ТипСклада = Перечисления.ТипыСкладов.ОптовыйСклад;
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.ЧекЕГАИСВозврат Тогда
			
			ЗапросПоСкладу = Новый Запрос(
			"ВЫБРАТЬ
			|	Документ.Склад.ТипСклада КАК ТипСклада
			|ИЗ
			|	Документ.ВозвратТоваровОтКлиента КАК Документ
			|ГДЕ
			|	Документ.Ссылка = &Основание");
			
			ЗапросПоСкладу.УстановитьПараметр("Основание", Основание);
			
			ВыборкаПоСкладу = ЗапросПоСкладу.Выполнить().Выбрать();
			ВыборкаПоСкладу.Следующий();
			
			Возврат ЕстьМаркируемаяАлкогольнаяПродукция(Основание, "Товары")
				И ВыборкаПоСкладу.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин;
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.СборкаТоваров") Тогда
		
		Номенклатура = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание, "Номенклатура");
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТаблицаНоменклатура.АлкогольнаяПродукция                                КАК ЕстьАлкогольнаяПродукция,
		|	ЕСТЬNULL(ТаблицаНоменклатура.ВидАлкогольнойПродукции.Маркируемый, ЛОЖЬ) КАК ЕстьМаркируемаяАлкогольнаяПродукция
		|ИЗ
		|	Справочник.Номенклатура КАК ТаблицаНоменклатура
		|ГДЕ
		|	Не ТаблицаНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
		|	И ТаблицаНоменклатура.Ссылка = &Номенклатура");
		
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		Если МетаданныеДокументаЕГАИС = Метаданные.Документы.АктСписанияЕГАИС Тогда
			
			Возврат Выборка.Количество() > 0 И Выборка.ЕстьАлкогольнаяПродукция;
			
		ИначеЕсли МетаданныеДокументаЕГАИС = Метаданные.Документы.ЧекЕГАИС Тогда
			
			ЗапросПоСкладу = Новый Запрос(
			"ВЫБРАТЬ
			|	Документ.Склад.ТипСклада КАК ТипСклада
			|ИЗ
			|	Документ.СборкаТоваров КАК Документ
			|ГДЕ
			|	Документ.Ссылка = &Основание");
			
			ЗапросПоСкладу.УстановитьПараметр("Основание", Основание);
			
			ВыборкаПоСкладу = ЗапросПоСкладу.Выполнить().Выбрать();
			ВыборкаПоСкладу.Следующий();
			
			Возврат Выборка.Количество() > 0 И Выборка.ЕстьМаркируемаяАлкогольнаяПродукция
				И ВыборкаПоСкладу.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин;
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		
		Возврат ЕстьАлкогольнаяПродукция(Основание, "Товары");
		
	КонецЕсли;
	
	ВызватьИсключение НСтр("ru = 'Не реализована процедура ""ИнтеграцияЕГАИСУТ.ТребуетсяОформлениеДокументовЕГАИС""'");
	
КонецФункции

// Копирует связь между номенклатурой и алкогольной продукцией
// для новой позиции номенклатуры
//
// Параметры:
//  Источник						 - СправочникСсылка.Номенклатура - Номенклатура для которой существует связь.
//  Приемник						 - СправочникСсылка.Номенклатура - Номенклатура, для которой копируется связь.
//  УстановитьСвязиДляХарактеристик	 - Булево - Требуется ли задать связь для характеристик номенклатуры-приемника
//
Процедура КопироватьСвязьСКлассификаторомАлкогольнойПродукцииЕГАИС(Источник, Приемник, УстановитьСвязиДляХарактеристик) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТХарактеристики
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|ГДЕ
	|	ХарактеристикиНоменклатуры.Владелец = &НоменклатураПриемник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ЕСТЬNULL(ВТХарактеристики.Ссылка, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТХарактеристики КАК ВТХарактеристики
	|		ПО (&УстановитьСвязиДляХарактеристик)
	|ГДЕ
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура = &НоменклатураИсточник";
	
	Запрос.УстановитьПараметр("НоменклатураИсточник", Источник);
	Запрос.УстановитьПараметр("НоменклатураПриемник", Приемник);
	Запрос.УстановитьПараметр("УстановитьСвязиДляХарактеристик", УстановитьСвязиДляХарактеристик);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаборЗаписей = РегистрыСведений.СоответствиеНоменклатурыЕГАИС.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Номенклатура.Установить(Приемник);
	
	Пока Выборка.Следующий() Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Номенклатура         = Приемник;
		НоваяЗапись.Характеристика       = Выборка.Характеристика;
		НоваяЗапись.АлкогольнаяПродукция = Выборка.АлкогольнаяПродукция;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Заполняет реквизит "Серия" в товарной табличной части
//
// Параметры:
// 		ТабличнаяЧасть - ДанныеФормыКоллекция, ТабличнаяЧасть - Товарная табличная часть документа
// 		ВыделенныеСтроки - Массив - Массив выделенных строк
// 		ПараметрыЗаполнения - Структура - Структура параметров заполнения
// 			Обязательные поля:
// 				"ОрганизацияЕГАИС" - СправочникСсылка.КлассификаторОрганизацийЕГАИС - Организация
// 				"ЗаполнятьБезЗапросаСправок" - Булево - Принудительное заполнение серий без запроса справок 1
// 			Необязательные поля:
// 				"ПоляЗаполнения" - Строка - Строка с перечислением заполняемых полей в таблице. Значение по умолчанию: "Серия".
// 				"КолонкиПоЗначению" - Структура - Структура для передачи в качестве параметра "КолонкиПоЗначению" в функцию "ОбщегоНазначенияУТ.ВыгрузитьТаблицуЗначений"
// 				"ДругиеИменаКолонок" - Структура - Структура для передачи в качестве параметра "НовыеИменаКолонок" в функцию "ОбщегоНазначенияУТ.ВыгрузитьТаблицуЗначений"
// 		СтруктураДействий - Структура - Стандартная структура действий со строками для вызова "ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ"
// 		
// Возвращаемое значение:
//   Структура - Структура с результатом выполнения функции.
//
Функция ЗаполнитьСгенерироватьСерии(ТабличнаяЧасть, ВыделенныеСтроки = Неопределено, ПараметрыЗаполнения = Неопределено, СтруктураДействий = Неопределено) Экспорт
	
	// Инициализация результирующей структуры
	Результат = Новый Структура("АдресМассиваЗапросов, ЗаполнениеЗавершено, СписокОшибок",
		Неопределено,
		Ложь,
		Неопределено);
		
	ДополнительныеКолонки = "";
	
	// Получение структуры параметров по умолчанию
	Параметры = Новый Структура(
		"ПоляЗаполнения, КолонкиПоЗначению, ДругиеИменаКолонок",
		"Серия", Новый Структура, Новый Структура);
	ОбщегоНазначенияУТКлиентСервер.ДополнитьСтруктуру(Параметры, ПараметрыЗаполнения, Истина);
	
	// Получение выгрузки по табличной части
	Таблица = ОбщегоНазначенияУТ.ВыгрузитьТаблицуЗначений(
		ТабличнаяЧасть,
		ВыделенныеСтроки,
		"НомерСтроки, Номенклатура, Характеристика, АлкогольнаяПродукция, Серия, Справка2" + ДополнительныеКолонки,
		Параметры.КолонкиПоЗначению,
		Параметры.ДругиеИменаКолонок);
	
	СтрокиБезНоменклатуры = Таблица.НайтиСтроки(Новый Структура("Номенклатура", Справочники.Номенклатура.ПустаяСсылка()));
	Для каждого СтрокаБезНоменклатуры Из СтрокиБезНоменклатуры Цикл
		
		ТекстСообщения = НСтр("ru = 'Не указана номенклатура в строке %НомерСтроки%.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", СтрокаБезНоменклатуры.НомерСтроки);
		
		Результат.ЗаполнениеЗавершено = Ложь;
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Результат.СписокОшибок,
			"Объект.Товары.Номенклатура",
			ТекстСообщения, 
			Неопределено);
		
	КонецЦикла;
		
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Получение запроса
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", Таблица);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.НомерСтроки КАК НомерСтроки,
		|	Таблица.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Серия КАК Серия,
		|	Таблица.Справка2 КАК Справка2
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	&Таблица КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтТовары.Номенклатура КАК Номенклатура,
		|	ЕСТЬNULL(ВладельцыСерии.АвтоматическиГенерироватьСерии, ВидыНоменклатуры.АвтоматическиГенерироватьСерии) КАК АвтоматическиГенерироватьСерии,
		|	ЕСТЬNULL(ВладельцыСерии.ИспользоватьДатуПроизводстваСерии, ВидыНоменклатуры.ИспользоватьДатуПроизводстваСерии) КАК ИспользоватьДатуПроизводстваСерии,
		|	ЕСТЬNULL(ВладельцыСерии.ИспользоватьПроизводителяЕГАИССерии, ВидыНоменклатуры.ИспользоватьПроизводителяЕГАИССерии) КАК ИспользоватьПроизводителяЕГАИССерии,
		|	ЕСТЬNULL(ВладельцыСерии.ИспользоватьСправку2ЕГАИССерии, ВидыНоменклатуры.ИспользоватьСправку2ЕГАИССерии) КАК ИспользоватьСправку2ЕГАИССерии,
		|	ЕСТЬNULL(ВладельцыСерии.ИспользоватьСрокГодностиСерии, ВидыНоменклатуры.ИспользоватьСрокГодностиСерии) КАК ИспользоватьСрокГодностиСерии,
		|	ЕСТЬNULL(ВладельцыСерии.Ссылка, ВидыНоменклатуры.Ссылка) КАК ВидНоменклатуры
		|ПОМЕСТИТЬ ВтВидыНоменклатуры
		|ИЗ
		|	ВтТовары КАК ВтТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО ВтТовары.Номенклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВладельцыСерии
		|		ПО ВтТовары.Номенклатура.ВладелецСерий = ВладельцыСерии.Ссылка
		|ГДЕ
		|	ЕСТЬNULL(ВладельцыСерии.АвтоматическиГенерироватьСерии, ВидыНоменклатуры.АвтоматическиГенерироватьСерии) = ИСТИНА
		|ИНДЕКСИРОВАТЬ ПО
		|	ВтТовары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтТовары.НомерСтроки КАК НомерСтроки,
		|	ВтТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	ВтТовары.Номенклатура КАК Номенклатура,
		|	ВтТовары.Характеристика КАК Характеристика,
		|	ВтТовары.Серия КАК Серия,
		|	ВтТовары.Справка2 КАК Справка2,
		|	Справки2ЕГАИС.НомерСправки1 КАК НомерСправки1,
		|	ЕСТЬNULL(Справки1ЕГАИС.Ссылка,ЗНАЧЕНИЕ(Справочник.Справки1ЕГАИС.ПустаяСсылка)) КАК Справка1,
		|	Справки1ЕГАИС.ДатаРозлива КАК Справка1ДатаРозлива,
		|	КлассификаторАлкогольнойПродукцииЕГАИС.Производитель КАК Производитель,
		|	СпрНоменклатура.СрокГодности КАК СрокГодности,
		|	СпрНоменклатура.ЕдиницаИзмеренияСрокаГодности КАК ЕдиницаИзмеренияСрокаГодности,
		|	ВтВидыНоменклатуры.ИспользоватьДатуПроизводстваСерии КАК ИспользоватьДатуПроизводстваСерии,
		|	ВтВидыНоменклатуры.ИспользоватьПроизводителяЕГАИССерии КАК ИспользоватьПроизводителяЕГАИССерии,
		|	ВтВидыНоменклатуры.ИспользоватьСправку2ЕГАИССерии КАК ИспользоватьСправку2ЕГАИССерии,
		|	ВтВидыНоменклатуры.ИспользоватьСрокГодностиСерии КАК ИспользоватьСрокГодностиСерии
		|ПОМЕСТИТЬ ТаблицаТоварыПолная
		|ИЗ
		|	ВтТовары КАК ВтТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Справки2ЕГАИС КАК Справки2ЕГАИС
		|		ПО ВтТовары.Справка2 = Справки2ЕГАИС.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
		|		ПО ВтТовары.АлкогольнаяПродукция = КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО ВтТовары.Номенклатура = СпрНоменклатура.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтВидыНоменклатуры КАК ВтВидыНоменклатуры
		|		ПО ВтТовары.Номенклатура = ВтВидыНоменклатуры.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Справки1ЕГАИС КАК Справки1ЕГАИС
		|		ПО Справки2ЕГАИС.НомерСправки1 = Справки1ЕГАИС.РегистрационныйНомер
		|ИНДЕКСИРОВАТЬ ПО
		|	ВтТовары.Номенклатура,
		|	ВтТовары.Характеристика,
		|	ВтТовары.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВтТовары.Номенклатура КАК Номенклатура
		|ИЗ
		|	ВтТовары КАК ВтТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО ВтТовары.Номенклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВладельцыСерии
		|		ПО ВтТовары.Номенклатура.ВладелецСерий = ВладельцыСерии.Ссылка
		|ГДЕ
		|	ЕСТЬNULL(ВладельцыСерии.АвтоматическиГенерироватьСерии, ВидыНоменклатуры.АвтоматическиГенерироватьСерии) = ЛОЖЬ
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаТоварыПолная.НомерСправки1 КАК НомерСправки1
		|ИЗ
		|	ТаблицаТоварыПолная КАК ТаблицаТоварыПолная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтВидыНоменклатуры КАК ВтВидыНоменклатуры
		|		ПО ТаблицаТоварыПолная.Номенклатура = ВтВидыНоменклатуры.Номенклатура
		|ГДЕ
		|	ТаблицаТоварыПолная.Справка1 = ЗНАЧЕНИЕ(Справочник.Справки1ЕГАИС.ПустаяСсылка)
		|	И (ВтВидыНоменклатуры.ИспользоватьДатуПроизводстваСерии = ИСТИНА
		|			ИЛИ ВтВидыНоменклатуры.ИспользоватьСрокГодностиСерии = ИСТИНА)
		|";
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		Если НЕ РезультатЗапроса[РезультатЗапроса.Количество()-1].Пустой() И НЕ ПараметрыЗаполнения.ЗаполнятьБезЗапросаСправок Тогда
			Выборка = РезультатЗапроса[РезультатЗапроса.Количество()-1].Выбрать();
			
			МассивЗапросовСправок1 = Новый Массив;
			Пока Выборка.Следующий() Цикл
				
				ПараметрыЗапроса = Новый Структура;
				ПараметрыЗапроса.Вставить("Операция"         , Перечисления.ВидыДокументовЕГАИС.ЗапросСправки1);
				ПараметрыЗапроса.Вставить("ИмяПараметра"     , "");
				ПараметрыЗапроса.Вставить("ЗначениеПараметра", Выборка.НомерСправки1);
				ПараметрыЗапроса.Вставить("ОрганизацияЕГАИС" , ПараметрыЗаполнения.ОрганизацияЕГАИС);
				
				МассивЗапросовСправок1.Добавить(ПараметрыЗапроса);
				
			КонецЦикла;
			Результат.АдресМассиваЗапросов = ПоместитьВоВременноеХранилище(МассивЗапросовСправок1);
			Результат.ЗаполнениеЗавершено = Ложь;
			Возврат Результат
		КонецЕсли;
		
		Если НЕ РезультатЗапроса[РезультатЗапроса.Количество()-2].Пустой() Тогда
			Выборка = РезультатЗапроса[РезультатЗапроса.Количество()-2].Выбрать();
			
			Пока Выборка.Следующий() Цикл
				ТекстСообщения = НСтр("ru = 'Для номенклатуры %Номенклатура% автоматическая генерация серий не настроена.'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номенклатура%", Выборка.Номенклатура);
				
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Результат.СписокОшибок,
					"Объект.Товары.Номенклатура",
					ТекстСообщения, 
					Неопределено);
				
			КонецЦикла;
			Результат.ЗаполнениеЗавершено = Ложь;
		КонецЕсли;
	
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ВтВидыНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ТаблицаТоварыПолная.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	ТаблицаТоварыПолная.Номенклатура КАК Номенклатура,
		|	ТаблицаТоварыПолная.Характеристика КАК Характеристика,
		|	ТаблицаТоварыПолная.Справка2 КАК Справка2,
		|	ВЫРАЗИТЬ(ТаблицаТоварыПолная.Справка1 КАК Справочник.Справки1ЕГАИС).ДатаРозлива КАК Справка1ДатаРозлива,
		|	ТаблицаТоварыПолная.СрокГодности КАК СрокГодностиНоменклатуры,
		|	ТаблицаТоварыПолная.ЕдиницаИзмеренияСрокаГодности КАК ЕдиницаИзмеренияСрокаГодности
		|ИЗ
		|	ТаблицаТоварыПолная КАК ТаблицаТоварыПолная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтВидыНоменклатуры КАК ВтВидыНоменклатуры
		|		ПО ТаблицаТоварыПолная.Номенклатура = ВтВидыНоменклатуры.Номенклатура
		|ГДЕ
		|	ВтВидыНоменклатуры.ИспользоватьДатуПроизводстваСерии = ИСТИНА
		|	И ТаблицаТоварыПолная.Справка1 <> ЗНАЧЕНИЕ(Справочник.Справки1ЕГАИС.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВтВидыНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ТаблицаТоварыПолная.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	ТаблицаТоварыПолная.Номенклатура КАК Номенклатура,
		|	ТаблицаТоварыПолная.Характеристика КАК Характеристика,
		|	ТаблицаТоварыПолная.Справка2 КАК Справка2,
		|	ВЫРАЗИТЬ(ТаблицаТоварыПолная.Справка1 КАК Справочник.Справки1ЕГАИС).ДатаРозлива КАК Справка1ДатаРозлива,
		|	ТаблицаТоварыПолная.СрокГодности КАК СрокГодностиНоменклатуры,
		|	ТаблицаТоварыПолная.ЕдиницаИзмеренияСрокаГодности КАК ЕдиницаИзмеренияСрокаГодности
		|ИЗ
		|	ТаблицаТоварыПолная КАК ТаблицаТоварыПолная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтВидыНоменклатуры КАК ВтВидыНоменклатуры
		|		ПО ТаблицаТоварыПолная.Номенклатура = ВтВидыНоменклатуры.Номенклатура
		|ГДЕ
		|	ВтВидыНоменклатуры.ИспользоватьСрокГодностиСерии = ИСТИНА
		|	И ТаблицаТоварыПолная.Справка1 <> ЗНАЧЕНИЕ(Справочник.Справки1ЕГАИС.ПустаяСсылка)
		|";
	
		ТаблицаСрокиГодности = Запрос.Выполнить().Выгрузить();
		ТаблицаСрокиГодности.Колонки.Добавить("ГоденДо", Новый ОписаниеТипов("Дата"));
		Для Каждого ТекСтрока Из ТаблицаСрокиГодности Цикл
			Если ЗначениеЗаполнено(ТекСтрока.Справка1ДатаРозлива) Тогда
				ТекСтрока.ГоденДо = НоменклатураКлиентСервер.ГоденДоПоДатеПроизводства(ТекСтрока.Справка1ДатаРозлива,
					ТекСтрока.СрокГодностиНоменклатуры,
					ТекСтрока.ЕдиницаИзмеренияСрокаГодности);
			КонецЕсли;
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТаблицаСрокиГодности", ТаблицаСрокиГодности);
		Запрос.УстановитьПараметр("ПустаяДата", Дата(1, 1, 1));
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ТаблицаСрокиГодности.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ТаблицаСрокиГодности.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	ТаблицаСрокиГодности.Номенклатура КАК Номенклатура,
		|	ТаблицаСрокиГодности.Характеристика КАК Характеристика,
		|	ТаблицаСрокиГодности.Справка2 КАК Справка2,
		|	ТаблицаСрокиГодности.Справка1ДатаРозлива КАК Справка1ДатаРозлива,
		|	ТаблицаСрокиГодности.СрокГодностиНоменклатуры КАК СрокГодностиНоменклатуры,
		|	ТаблицаСрокиГодности.ГоденДо КАК ГоденДо,
		|	ТаблицаСрокиГодности.ЕдиницаИзмеренияСрокаГодности КАК ЕдиницаИзмеренияСрокаГодности
		|ПОМЕСТИТЬ
		|	ВТГоденДо
		|ИЗ
		|	&ТаблицаСрокиГодности КАК ТаблицаСрокиГодности
		|ИНДЕКСИРОВАТЬ ПО
		|	ТаблицаСрокиГодности.Номенклатура,
		|	ТаблицаСрокиГодности.Характеристика,
		|	ТаблицаСрокиГодности.АлкогольнаяПродукция,
		|	ТаблицаСрокиГодности.Справка2
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтТовары.НомерСтроки КАК НомерСтроки,
		|	ВтВидыНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ВтТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	ВтТовары.Номенклатура КАК Номенклатура,
		|	ВтТовары.Характеристика КАК Характеристика,
		|	ВтТовары.Серия КАК Серия,
		|	ВтТовары.Справка2 КАК Справка2,
		|	ВЫБОР
		|		КОГДА ВтТовары.Справка1ДатаРозлива = ЗНАЧЕНИЕ(Справочник.Справки1ЕГАИС.ПустаяСсылка) ТОГДА
		|			ВТГоденДо.Справка1ДатаРозлива
		|		ИНАЧЕ
		|			ВтТовары.Справка1ДатаРозлива
		|	КОНЕЦ КАК Справка1ДатаРозлива,
		|	ВТГоденДо.ГоденДо КАК ГоденДо,
		|	ВтТовары.Производитель КАК Производитель,
		|	ВтТовары.ИспользоватьДатуПроизводстваСерии КАК ИспользоватьДатуПроизводстваСерии,
		|	ВтТовары.ИспользоватьПроизводителяЕГАИССерии КАК ИспользоватьПроизводителяЕГАИССерии,
		|	ВтТовары.ИспользоватьСправку2ЕГАИССерии КАК ИспользоватьСправку2ЕГАИССерии,
		|	ВтТовары.ИспользоватьСрокГодностиСерии КАК ИспользоватьСрокГодностиСерии
		|ПОМЕСТИТЬ
		|	ВтТоварыКонечная
		|ИЗ
		|	ТаблицаТоварыПолная КАК ВтТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтВидыНоменклатуры КАК ВтВидыНоменклатуры
		|			ПО ВтТовары.Номенклатура = ВтВидыНоменклатуры.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТГоденДо КАК ВТГоденДо
		|		ПО ВтТовары.Номенклатура = ВТГоденДо.Номенклатура
		|			И ВтТовары.Характеристика = ВТГоденДо.Характеристика
		|			И ВтТовары.АлкогольнаяПродукция = ВТГоденДо.АлкогольнаяПродукция
		|			И ВтТовары.Справка2 = ВТГоденДо.Справка2
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтТовары.НомерСтроки КАК НомерСтроки,
		|	ВтТовары.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ВтТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	ВтТовары.Номенклатура КАК Номенклатура,
		|	ВтТовары.Характеристика КАК Характеристика,
		|	СерииНоменклатуры.Ссылка КАК Серия,
		|	ВтТовары.Справка2 КАК Справка2,
		|	ВтТовары.Справка1ДатаРозлива КАК Справка1ДатаРозлива,
		|	ВтТовары.ГоденДо КАК ГоденДо,
		|	ВтТовары.Производитель КАК Производитель,
		|	ВтТовары.ИспользоватьДатуПроизводстваСерии КАК ИспользоватьДатуПроизводстваСерии,
		|	ВтТовары.ИспользоватьПроизводителяЕГАИССерии КАК ИспользоватьПроизводителяЕГАИССерии,
		|	ВтТовары.ИспользоватьСправку2ЕГАИССерии КАК ИспользоватьСправку2ЕГАИССерии,
		|	ВтТовары.ИспользоватьСрокГодностиСерии КАК ИспользоватьСрокГодностиСерии
		|ИЗ
		|	ВтТоварыКонечная КАК ВтТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
		|			ПО СерииНоменклатуры.ВидНоменклатуры = ВтТовары.ВидНоменклатуры
		|			И (ВЫБОР
		|				КОГДА ВтТовары.ИспользоватьДатуПроизводстваСерии = ИСТИНА ТОГДА
		|					СерииНоменклатуры.ДатаПроизводства = ВтТовары.Справка1ДатаРозлива
		|				ИНАЧЕ
		|					ИСТИНА
		|				КОНЕЦ)
		|			И (ВЫБОР
		|				КОГДА ВтТовары.ИспользоватьПроизводителяЕГАИССерии = ИСТИНА ТОГДА
		|					СерииНоменклатуры.ПроизводительЕГАИС = ВтТовары.Производитель
		|				ИНАЧЕ
		|					ИСТИНА
		|				КОНЕЦ)
		|			И (ВЫБОР
		|				КОГДА ВтТовары.ИспользоватьСправку2ЕГАИССерии = ИСТИНА ТОГДА
		|					СерииНоменклатуры.Справка2ЕГАИС = ВтТовары.Справка2
		|				ИНАЧЕ
		|					ИСТИНА
		|				КОНЕЦ)
		|			И (ВЫБОР
		|				КОГДА ВтТовары.ИспользоватьСрокГодностиСерии = ИСТИНА ТОГДА
		|					СерииНоменклатуры.ГоденДо = ВтТовары.ГоденДо
		|				ИНАЧЕ
		|					ИСТИНА
		|				КОНЕЦ)
		|ГДЕ
		|	ВЫБОР
		|		КОГДА ВтТовары.ИспользоватьДатуПроизводстваСерии = ИСТИНА ТОГДА
		|			ВтТовары.Справка1ДатаРозлива <> &ПустаяДата
		|		ИНАЧЕ
		|			ИСТИНА
		|		КОНЕЦ
		|	И (ВЫБОР
		|		КОГДА ВтТовары.ИспользоватьСрокГодностиСерии = ИСТИНА ТОГДА
		|			ВтТовары.ГоденДо <> &ПустаяДата
		|		ИНАЧЕ
		|			ИСТИНА
		|		КОНЕЦ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВтТовары.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ВтТовары.Справка2 КАК Справка2,
		|	ВтТовары.Справка1ДатаРозлива КАК Справка1ДатаРозлива,
		|	ВтТовары.ГоденДо КАК ГоденДо,
		|	ВтТовары.Производитель КАК Производитель,
		|	ВтТовары.ИспользоватьДатуПроизводстваСерии КАК ИспользоватьДатуПроизводстваСерии,
		|	ВтТовары.ИспользоватьПроизводителяЕГАИССерии КАК ИспользоватьПроизводителяЕГАИССерии,
		|	ВтТовары.ИспользоватьСправку2ЕГАИССерии КАК ИспользоватьСправку2ЕГАИССерии,
		|	ВтТовары.ИспользоватьСрокГодностиСерии КАК ИспользоватьСрокГодностиСерии
		|ИЗ
		|	ВтТоварыКонечная КАК ВтТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
		|			ПО СерииНоменклатуры.ВидНоменклатуры = ВтТовары.ВидНоменклатуры
		|			И (ВЫБОР
		|				КОГДА ВтТовары.ИспользоватьДатуПроизводстваСерии = ИСТИНА ТОГДА
		|					СерииНоменклатуры.ДатаПроизводства = ВтТовары.Справка1ДатаРозлива
		|				ИНАЧЕ
		|					ИСТИНА
		|				КОНЕЦ)
		|			И (ВЫБОР
		|				КОГДА ВтТовары.ИспользоватьПроизводителяЕГАИССерии = ИСТИНА ТОГДА
		|					СерииНоменклатуры.ПроизводительЕГАИС = ВтТовары.Производитель
		|				ИНАЧЕ
		|					ИСТИНА
		|				КОНЕЦ)
		|			И (ВЫБОР
		|				КОГДА ВтТовары.ИспользоватьСправку2ЕГАИССерии = ИСТИНА ТОГДА
		|					СерииНоменклатуры.Справка2ЕГАИС = ВтТовары.Справка2
		|				ИНАЧЕ
		|					ИСТИНА
		|				КОНЕЦ)
		|			И (ВЫБОР
		|				КОГДА ВтТовары.ИспользоватьСрокГодностиСерии = ИСТИНА ТОГДА
		|					СерииНоменклатуры.ГоденДо = ВтТовары.ГоденДо
		|				ИНАЧЕ
		|					ИСТИНА
		|				КОНЕЦ)
		|ГДЕ
		|	СерииНоменклатуры.Ссылка ЕСТЬ NULL
		|	И ВЫБОР
		|		КОГДА ВтТовары.ИспользоватьДатуПроизводстваСерии = ИСТИНА ТОГДА
		|			ВтТовары.Справка1ДатаРозлива <> &ПустаяДата
		|		ИНАЧЕ
		|			ИСТИНА
		|		КОНЕЦ
		|	И (ВЫБОР
		|		КОГДА ВтТовары.ИспользоватьСрокГодностиСерии = ИСТИНА ТОГДА
		|			ВтТовары.ГоденДо <> &ПустаяДата
		|		ИНАЧЕ
		|			ИСТИНА
		|		КОНЕЦ)
		|;
		|";
		
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаГенерацияСерий = ПакетРезультатов[ПакетРезультатов.Количество()-1].Выгрузить();
	ТаблицаГенерацияСерий.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	ТаблицаГенерацияСерий.Индексы.Добавить("ВидНоменклатуры, Справка2, Справка1ДатаРозлива, ГоденДо, Производитель");
	Для Каждого ТекСтрока Из ТаблицаГенерацияСерий Цикл
		НоваяСерия = Справочники.СерииНоменклатуры.СоздатьЭлемент();
		НоваяСерия.Заполнить(ТекСтрока);
		Попытка
			НоваяСерия.Записать();
			ТекСтрока.Серия = НоваяСерия.Ссылка;
			Результат.ЗаполнениеЗавершено = Истина;
		Исключение
			Результат.ЗаполнениеЗавершено = Ложь;
			
			ТекстСообщения = НСтр("ru = 'Не удалось сгенерировать серию по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
										УровеньЖурналаРегистрации.Ошибка,
										Метаданные.Справочники.СерииНоменклатуры,
										НоваяСерия.Ссылка,
										ТекстСообщения);
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Результат.СписокОшибок,
				"Объект.Товары.Серия",
				ТекстСообщения, 
				Неопределено);
		КонецПопытки;
	КонецЦикла;
	
	ВыборкаЗаполнениеСерий = ПакетРезультатов[ПакетРезультатов.Количество()-2].Выбрать();
	Пока ВыборкаЗаполнениеСерий.Следующий() Цикл
		СтрокаТовары = ТабличнаяЧасть[ВыборкаЗаполнениеСерий.НомерСтроки-1];
		Если ЗначениеЗаполнено(ВыборкаЗаполнениеСерий.Серия) Тогда
			СтрокаТовары.Серия = ВыборкаЗаполнениеСерий.Серия;
			Результат.ЗаполнениеЗавершено = Истина;
		Иначе
			ПараметрыПоиска = Новый Структура("ВидНоменклатуры", ВыборкаЗаполнениеСерий.ВидНоменклатуры);
			Если ВыборкаЗаполнениеСерий.ИспользоватьДатуПроизводстваСерии Тогда
				ПараметрыПоиска.Вставить("Справка1ДатаРозлива", ВыборкаЗаполнениеСерий.Справка1ДатаРозлива);
			КонецЕсли;
			Если ВыборкаЗаполнениеСерий.ИспользоватьПроизводителяЕГАИССерии Тогда
				ПараметрыПоиска.Вставить("Производитель", ВыборкаЗаполнениеСерий.Производитель);
			КонецЕсли;
			Если ВыборкаЗаполнениеСерий.ИспользоватьСправку2ЕГАИССерии Тогда
				ПараметрыПоиска.Вставить("Справка2", ВыборкаЗаполнениеСерий.Справка2);
			КонецЕсли;
			Если ВыборкаЗаполнениеСерий.ИспользоватьСрокГодностиСерии Тогда
				ПараметрыПоиска.Вставить("ГоденДо", ВыборкаЗаполнениеСерий.ГоденДо);
			КонецЕсли;
			НайденныеСтроки = ТаблицаГенерацияСерий.НайтиСтроки(ПараметрыПоиска);
			Если НайденныеСтроки.Количество() > 0 Тогда
				СтрокаТовары.Серия = НайденныеСтроки[0].Серия;
				Результат.ЗаполнениеЗавершено = Истина;
			Иначе
				Результат.ЗаполнениеЗавершено = Ложь;
			КонецЕсли;
		КонецЕсли;
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТовары, СтруктураДействий, Неопределено);
	КонецЦикла;
	
	Возврат Результат
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Расхождения

// Функция проверяет наличие расхождений между ТТН и товарами накладной.
//
// Параметры:
//  ТоварноТранспортнаяНакладнаяЕГАИС - ДокументСсылка.ТТНВходящаяЕГАИС - проверяемая ТТН,
//  ДокументСсылка - ДокументСсылка.ПриобретениеТоваровУслуг - проверяемое поступление товаров.
//
// Возвращаемое значение:
//  Булево - Истина, если есть расхождения, иначе - Ложь.
//
Функция ЕстьРасхожденияМеждуДокументомПриобретениеТоваровУслугИТТНЕГАИС(ТоварноТранспортнаяНакладнаяЕГАИС, ДокументСсылка) Экспорт
	
	//++ НЕ ЕГАИС
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия,
	|	СУММА(ТабличнаяЧасть.Количество)    КАК Количество
	|ПОМЕСТИТЬ ВтТоварыТТН
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ТТНСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Сопоставлено.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия,
	|	СУММА(ТабличнаяЧасть.Количество
	|	* ВЫБОР
	|		КОГДА Сопоставлено.АлкогольнаяПродукция.ТипПродукции = ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная) ТОГДА
	|			ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ОбъемДАЛ, 0)
	|		ИНАЧЕ
	|			1
	|	КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ ВтТоварыПоступления
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Серии КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыТТН КАК Сопоставлено
	|		ПО ТабличнаяЧасть.Номенклатура = Сопоставлено.Номенклатура
	|			И ТабличнаяЧасть.Характеристика = Сопоставлено.Характеристика
	|			И ТабличнаяЧасть.Серия = Сопоставлено.Серия
	|			И Сопоставлено.Серия <> &ПустаяСерия
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ПоступлениеТоваров
	|	И ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(Сопоставлено.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)),
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Сопоставлено.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия,
	|	СУММА(ТабличнаяЧасть.Количество
	|	* ВЫБОР
	|		КОГДА Сопоставлено.АлкогольнаяПродукция.ТипПродукции = ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная) ТОГДА
	|			ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ОбъемДАЛ, 0)
	|		ИНАЧЕ
	|			1
	|	КОНЕЦ) КАК Количество
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыТТН КАК Сопоставлено
	|		ПО ТабличнаяЧасть.Номенклатура = Сопоставлено.Номенклатура
	|		И ТабличнаяЧасть.Характеристика = Сопоставлено.Характеристика
	|		И ТабличнаяЧасть.Серия = Сопоставлено.Серия
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ПоступлениеТоваров
	|	И ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(Сопоставлено.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)),
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтТоварыТТН.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ВтТоварыПоступления.Количество   КАК Количество
	|ИЗ
	|	ВтТоварыПоступления КАК ВтТоварыПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыТТН КАК ВтТоварыТТН
	|		ПО ВтТоварыПоступления.АлкогольнаяПродукция = ВтТоварыТТН.АлкогольнаяПродукция
	|			И ВтТоварыПоступления.Номенклатура = ВтТоварыТТН.Номенклатура
	|			И ВтТоварыПоступления.Характеристика = ВтТоварыТТН.Характеристика
	|			И ВтТоварыПоступления.Серия = ВтТоварыТТН.Серия
	|ГДЕ
	|	(ВтТоварыПоступления.Количество > ЕСТЬNULL(ВтТоварыТТН.Количество, 0)
	|			ИЛИ ВтТоварыТТН.АлкогольнаяПродукция = ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка))");
	
	Запрос.УстановитьПараметр("ТТНСсылка",          ТоварноТранспортнаяНакладнаяЕГАИС);
	Запрос.УстановитьПараметр("ПоступлениеТоваров", ДокументСсылка);
	Запрос.УстановитьПараметр("ПустаяСерия", 
		ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	//-- НЕ ЕГАИС
	
	Возврат Ложь;
	
КонецФункции

// Функция проверяет наличие расхождений между ТТН и товарами перемещения.
//
// Параметры:
//  ТоварноТранспортнаяНакладнаяЕГАИС - ДокументСсылка.ТТНВходящаяЕГАИС - проверяемая ТТН,
//  ДокументСсылка - ДокументСсылка.ПеремещениеТоваров - проверяемое поступление товаров.
//
// Возвращаемое значение:
//  Булево - Истина, если есть расхождения, иначе - Ложь.
//
Функция ЕстьРасхожденияМеждуДокументомПеремещениеТоваровИТТНЕГАИС(ТоварноТранспортнаяНакладнаяЕГАИС, ДокументСсылка) Экспорт
	
	//++ НЕ ЕГАИС
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия,
	|	СУММА(ТабличнаяЧасть.Количество)    КАК Количество
	|ПОМЕСТИТЬ ВтТоварыТТН
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ТТНСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Сопоставлено.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия,
	|	СУММА(ТабличнаяЧасть.Количество
	|	* ВЫБОР
	|		КОГДА Сопоставлено.АлкогольнаяПродукция.ТипПродукции = ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная) ТОГДА
	|			ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ОбъемДАЛ, 0)
	|		ИНАЧЕ
	|			1
	|	КОНЕЦ) КАК Количество
	|ПОМЕСТИТЬ ВтТоварыПоступления
	|ИЗ
	|	Документ.ПеремещениеТоваров.Серии КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыТТН КАК Сопоставлено
	|		ПО ТабличнаяЧасть.Номенклатура = Сопоставлено.Номенклатура
	|			И ТабличнаяЧасть.Характеристика = Сопоставлено.Характеристика
	|			И ТабличнаяЧасть.Серия = Сопоставлено.Серия
	|			И Сопоставлено.Серия <> &ПустаяСерия
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ПеремещениеТоваров
	|	И ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(Сопоставлено.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)),
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Сопоставлено.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия,
	|	СУММА(ТабличнаяЧасть.Количество
	|	* ВЫБОР
	|		КОГДА Сопоставлено.АлкогольнаяПродукция.ТипПродукции = ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная) ТОГДА
	|			ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ОбъемДАЛ, 0)
	|		ИНАЧЕ
	|			1
	|	КОНЕЦ) КАК Количество
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыТТН КАК Сопоставлено
	|		ПО ТабличнаяЧасть.Номенклатура = Сопоставлено.Номенклатура
	|		И ТабличнаяЧасть.Характеристика = Сопоставлено.Характеристика
	|		И ТабличнаяЧасть.Серия = Сопоставлено.Серия
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ПеремещениеТоваров
	|	И ТабличнаяЧасть.Номенклатура.АлкогольнаяПродукция
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(Сопоставлено.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)),
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтТоварыТТН.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ВтТоварыПоступления.Количество   КАК Количество
	|ИЗ
	|	ВтТоварыПоступления КАК ВтТоварыПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТоварыТТН КАК ВтТоварыТТН
	|		ПО ВтТоварыПоступления.АлкогольнаяПродукция = ВтТоварыТТН.АлкогольнаяПродукция
	|			И ВтТоварыПоступления.Номенклатура = ВтТоварыТТН.Номенклатура
	|			И ВтТоварыПоступления.Характеристика = ВтТоварыТТН.Характеристика
	|			И ВтТоварыПоступления.Серия = ВтТоварыТТН.Серия
	|ГДЕ
	|	(ВтТоварыПоступления.Количество > ЕСТЬNULL(ВтТоварыТТН.Количество, 0)
	|			ИЛИ ВтТоварыТТН.АлкогольнаяПродукция = ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка))");
	
	
	Запрос.УстановитьПараметр("ТТНСсылка",          ТоварноТранспортнаяНакладнаяЕГАИС);
	Запрос.УстановитьПараметр("ПеремещениеТоваров", ДокументСсылка);
	Запрос.УстановитьПараметр("ПустаяСерия",
		ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	//-- НЕ ЕГАИС
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область РасчетСтатусаОформления

Функция СтруктураРеквизитыРасчетаСтатусаОформления()
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Проведен");
	Реквизиты.Вставить("Дата");
	Реквизиты.Вставить("Номер");
	Реквизиты.Вставить("Ответственный");
	Реквизиты.Вставить("ТорговыйОбъект");
	Реквизиты.Вставить("Контрагент");
	
	Возврат Реквизиты;
	
КонецФункции

#Область АктПостановкиНаБалансЕГАИС

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусАктаПостановкиНаБаланс
//
Процедура РассчитатьСтатусОформленияАктПостановкиНаБалансЕГАИС(Источник, Отказ) Экспорт
	
	ЗначенияРеквизитов = СтруктураРеквизитыРасчетаСтатусаОформления();
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.АктПостановкиНаБалансЕГАИС")
		Или ТипЗнч(Источник) = Тип("ДокументСсылка.АктПостановкиНаБалансЕГАИС") Тогда
		
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.АктПостановкиНаБалансЕГАИС") Тогда
			ИсточникСсылка = Источник.ДокументОснование;
		Иначе
			ИсточникСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "ДокументОснование");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИсточникСсылка) Тогда
			
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Таблица.Проведен      КАК Проведен,
			|	Таблица.Дата          КАК Дата,
			|	Таблица.Номер         КАК Номер,
			|	Таблица.Ответственный КАК Ответственный,
			|	Таблица.Склад         КАК ТорговыйОбъект,
			|	Таблица.Организация   КАК Контрагент
			|ИЗ
			|	" + ИсточникСсылка.Метаданные().ПолноеИмя() + " КАК Таблица
			|ГДЕ
			|	Таблица.Ссылка = &Ссылка");
			
			Запрос.УстановитьПараметр("Ссылка", ИсточникСсылка);
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			
			ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Выборка);
			
		КонецЕсли;
		
	Иначе
		
		ИсточникСсылка = Источник.Ссылка;
		
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Источник);
		
		ЗначенияРеквизитов.Контрагент     = Источник.Организация;
		ЗначенияРеквизитов.ТорговыйОбъект = Источник.Склад;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИсточникСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьТребуется = ТребуетсяОформлениеДокументовЕГАИС(
		ИсточникСсылка,
		Метаданные.Документы.АктПостановкиНаБалансЕГАИС);
	
	ИнтеграцияЕГАИС.РассчитатьСтатусОформления(
		Метаданные.Документы.АктПостановкиНаБалансЕГАИС,
		ИсточникСсылка, ЗначенияРеквизитов, ЗаписьТребуется);
	
КонецПроцедуры

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.ЗапросСтатусаОформленияАктаПостановкиНаБаланс
//
Функция ЗапросСтатусаОформленияАктПостановкиНаБалансЕГАИС(ДокументОснование) Экспорт
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОприходованиеИзлишковТоваров") Тогда
		
		Возврат ЗапросСтатусаОформленияАктПостановкиНаБалансЕГАИСНаОснованииОприходованияИзлишковТовара(ДокументОснование);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПересортицаТоваров") Тогда
	
		Возврат ЗапросСтатусаОформленияАктПостановкиНаБалансЕГАИСНаОснованииПересортицыТоваров(ДокументОснование);
		
	КонецЕсли;
	
КонецФункции

Функция ЗапросСтатусаОформленияАктПостановкиНаБалансЕГАИСНаОснованииОприходованияИзлишковТовара(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.АктПостановкиНаБалансЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ОприходованиеИзлишковТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                КАК План,
	|	ОформленныеТовары.Количество     КАК Факт
	|ИЗ
	|	Документ.АктПостановкиНаБалансЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План)    КАК План,
	|	СУММА(ТоварыКОформлению.Факт)    КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросСтатусаОформленияАктПостановкиНаБалансЕГАИСНаОснованииПересортицыТоваров(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.АктПостановкиНаБалансЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ПересортицаТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                КАК План,
	|	ОформленныеТовары.Количество     КАК Факт
	|ИЗ
	|	Документ.АктПостановкиНаБалансЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План)    КАК План,
	|	СУММА(ТоварыКОформлению.Факт)    КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#Область АктСписанияЕГАИС

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусАктаПостановкиНаБаланс
//
Процедура РассчитатьСтатусОформленияАктСписанияЕГАИС(Источник, Отказ) Экспорт
	
	ЗначенияРеквизитов = СтруктураРеквизитыРасчетаСтатусаОформления();
	ЗаписьДокументаОснования = Ложь;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.АктСписанияЕГАИС")
		Или ТипЗнч(Источник) = Тип("ДокументСсылка.АктСписанияЕГАИС") Тогда
		
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.АктСписанияЕГАИС") Тогда
			ИсточникСсылка = Источник.ДокументОснование;
		Иначе
			ИсточникСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "ДокументОснование");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИсточникСсылка) Тогда
			
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Таблица.Проведен      КАК Проведен,
			|	Таблица.Дата          КАК Дата,
			|	Таблица.Номер         КАК Номер,
			|	Таблица.Ответственный КАК Ответственный,
			|	Таблица.Склад         КАК ТорговыйОбъект,
			|	Таблица.Организация   КАК Контрагент
			|ИЗ
			|	" + ИсточникСсылка.Метаданные().ПолноеИмя() + " КАК Таблица
			|ГДЕ
			|	Таблица.Ссылка = &Ссылка");
			
			Запрос.УстановитьПараметр("Ссылка", ИсточникСсылка);
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			
			ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Выборка);
			
		КонецЕсли;
		
	Иначе
		
		ЗаписьДокументаОснования = Истина;
		
		ИсточникСсылка = Источник.Ссылка;
		
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Источник);
		
		ЗначенияРеквизитов.Контрагент     = Источник.Организация;
		ЗначенияРеквизитов.ТорговыйОбъект = Источник.Склад;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИсточникСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьТребуется = ТребуетсяОформлениеДокументовЕГАИС(
		ИсточникСсылка,
		Метаданные.Документы.АктСписанияЕГАИС);
	
	ИнтеграцияЕГАИС.РассчитатьСтатусОформления(
		Метаданные.Документы.АктСписанияЕГАИС,
		ИсточникСсылка, ЗначенияРеквизитов, ЗаписьТребуется);
	
	Если Не ЗаписьДокументаОснования Тогда
		
		ЗаписьТребуется = ТребуетсяОформлениеДокументовЕГАИС(
			ИсточникСсылка,
			Метаданные.Документы.ЧекЕГАИС);
		
		ИнтеграцияЕГАИС.РассчитатьСтатусОформления(
			Метаданные.Документы.ЧекЕГАИС,
			ИсточникСсылка, ЗначенияРеквизитов, ЗаписьТребуется);
		
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.ЗапросСтатусаОформленияАктаПостановкиНаБаланс
//
Функция ЗапросСтатусаОформленияАктСписанияЕГАИС(ДокументОснование) Экспорт
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВнутреннееПотреблениеТоваров") Тогда
		
		Возврат ЗапросСтатусаОформленияАктСписанияЕГАИСЧекЕГАИСНаОснованииВнутреннееПотреблениеТоваров(ДокументОснование);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СписаниеНедостачТоваров") Тогда
		
		Возврат ЗапросСтатусаОформленияАктСписанияЕГАИСНаОснованииСписаниеНедостачТоваров(ДокументОснование);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПересортицаТоваров") Тогда
		
		Возврат ЗапросСтатусаОформленияАктСписанияЕГАИСНаОснованииПересортицаТоваров(ДокументОснование);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		
		Возврат ЗапросСтатусаОформленияАктСписанияЕГАИСНаОснованииОтчетОРозничныхПродажах(ДокументОснование);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СборкаТоваров") Тогда
		
		Возврат ЗапросСтатусаОформленияАктСписанияЕГАИСНаОснованииСборкаТоваров(ДокументОснование);
		
	КонецЕсли;
	
КонецФункции

Функция ЗапросСтатусаОформленияАктСписанияЕГАИСЧекЕГАИСНаОснованииВнутреннееПотреблениеТоваров(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыАктСписанияЕГАИС
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыЧекЕГАИС
	|ИЗ
	|	Документ.ЧекЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусыЧекЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ВнутреннееПотреблениеТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотреблениеТоваров.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТабличнаяЧасть.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТабличнаяЧасть.Характеристика
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И ТаблицаСерии.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ИЗ
	|	Документ.ВнутреннееПотреблениеТоваров.Серии КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыАктСписанияЕГАИС КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.ЧекЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыЧекЕГАИС КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросСтатусаОформленияАктСписанияЕГАИСНаОснованииСписаниеНедостачТоваров(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.СписаниеНедостачТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросСтатусаОформленияАктСписанияЕГАИСНаОснованииПересортицаТоваров(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыАктСписанияЕГАИС
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК План,
	|	0                                                          КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ПересортицаТоваров.Товары КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыАктСписанияЕГАИС КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросСтатусаОформленияАктСписанияЕГАИСНаОснованииОтчетОРозничныхПродажах(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК План,
	|	0                                                          КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТабличнаяЧасть.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТабличнаяЧасть.Характеристика
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И НЕ СправочникНоменклатура.ВидАлкогольнойПродукции.Маркируемый
	|	И ТаблицаСерии.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК План,
	|	0                                                          КАК Факт
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.Серии КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И НЕ СправочникНоменклатура.ВидАлкогольнойПродукции.Маркируемый
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросСтатусаОформленияАктСписанияЕГАИСНаОснованииСборкаТоваров(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыАктСписанияЕГАИС
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыЧекЕГАИС
	|ИЗ
	|	Документ.ЧекЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусыЧекЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК План,
	|	0                                                          КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.СборкаТоваров КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И ТабличнаяЧасть.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров)
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыАктСписанияЕГАИС КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.ЧекЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыЧекЕГАИС КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#Область ВозвратИзРегистра2ЕГАИС

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусАктаПостановкиНаБаланс
//
Процедура РассчитатьСтатусОформленияВозвратИзРегистра2ЕГАИС(Источник, Отказ) Экспорт
	
	ЗначенияРеквизитов = СтруктураРеквизитыРасчетаСтатусаОформления();
	ЗаписьТребуется = Ложь;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ВозвратИзРегистра2ЕГАИС")
		Или ТипЗнч(Источник) = Тип("ДокументСсылка.ВозвратИзРегистра2ЕГАИС") Тогда
		
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.ВозвратИзРегистра2ЕГАИС") Тогда
			ИсточникСсылка = Источник.ДокументОснование;
		Иначе
			ИсточникСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "ДокументОснование");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИсточникСсылка)
			И ТипЗнч(ИсточникСсылка) = Тип("ДокументСсылка.ТТНИсходящаяЕГАИС") Тогда
			
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Таблица.Проведен                        КАК Проведен,
			|	Таблица.Дата                            КАК Дата,
			|	Таблица.Номер                           КАК Номер,
			|	Таблица.Ответственный                   КАК Ответственный,
			|	Таблица.Грузоотправитель                КАК Грузоотправитель,
			|	Таблица.Грузоотправитель.ТорговыйОбъект КАК ТорговыйОбъект,
			|	Таблица.Грузоотправитель.Контрагент     КАК Контрагент
			|ИЗ
			|	Документ.ТТНИсходящаяЕГАИС КАК Таблица
			|ГДЕ
			|	Таблица.Ссылка = &Ссылка");
			
			Запрос.УстановитьПараметр("Ссылка", ИсточникСсылка);
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			
			ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Выборка);
			
			ИспользоватьРегистр2 = ИнтеграцияЕГАИСПереопределяемый.ИспользоватьРегистр2(Выборка.Грузоотправитель);
			
			ЗаписьТребуется = ИспользоватьРегистр2;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ТТНИсходящаяЕГАИС") Тогда
		
		ИсточникСсылка = Источник.Ссылка;
		
		ИспользоватьРегистр2 = ИнтеграцияЕГАИСПереопределяемый.ИспользоватьРегистр2(Источник.Грузоотправитель);
		
		ЗаписьТребуется = ИспользоватьРегистр2;
		
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Источник);
		
		РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Грузоотправитель, "ТорговыйОбъект, Контрагент");
		
		ЗначенияРеквизитов.Контрагент     = РеквизитыОбъекта.Контрагент;
		ЗначенияРеквизитов.ТорговыйОбъект = РеквизитыОбъекта.ТорговыйОбъект;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИсточникСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЕГАИС.РассчитатьСтатусОформления(
		Метаданные.Документы.ВозвратИзРегистра2ЕГАИС,
		ИсточникСсылка, ЗначенияРеквизитов, ЗаписьТребуется);
	
КонецПроцедуры

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.ЗапросСтатусаОформленияАктаПостановкиНаБаланс
//
Функция ЗапросСтатусаОформленияВозвратИзРегистра2ЕГАИС(ДокументОснование) Экспорт
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ТТНИсходящаяЕГАИС") Тогда
		
		Возврат ЗапросСтатусаОформленияВозвратИзРегистра2ЕГАИСНаОснованииТТНИсходящаяЕГАИС(ДокументОснование);
		
	КонецЕсли;
	
КонецФункции

Функция ЗапросСтатусаОформленияВозвратИзРегистра2ЕГАИСНаОснованииТТНИсходящаяЕГАИС(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ВозвратИзРегистра2ЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Товары.Количество           КАК План,
	|	0                           КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.ВозвратИзРегистра2ЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.АлкогольнаяПродукция = &ПустаяСсылка) КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#Область ПередачаВРегистр2ЕГАИС

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусАктаПостановкиНаБаланс
//
Процедура РассчитатьСтатусОформленияПередачаВРегистр2ЕГАИС(Источник, Отказ) Экспорт
	
	ЗначенияРеквизитов = СтруктураРеквизитыРасчетаСтатусаОформления();
	ЗаписьТребуется    = Ложь;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ПередачаВРегистр2ЕГАИС")
		Или ТипЗнч(Источник) = Тип("ДокументСсылка.ПередачаВРегистр2ЕГАИС") Тогда
		
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.ПередачаВРегистр2ЕГАИС") Тогда
			ИсточникСсылка = Источник.ДокументОснование;
		Иначе
			ИсточникСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "ДокументОснование");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИсточникСсылка)
			И ТипЗнч(ИсточникСсылка) = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
			
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Таблица.Проведен                       КАК Проведен,
			|	Таблица.Дата                           КАК Дата,
			|	Таблица.Номер                          КАК Номер,
			|	Таблица.Ответственный                  КАК Ответственный,
			|	Таблица.Грузополучатель                КАК Грузополучатель,
			|	Таблица.Грузополучатель.ТорговыйОбъект КАК ТорговыйОбъект,
			|	Таблица.Грузополучатель.Контрагент     КАК Контрагент,
			|	СтатусыДокументовЕГАИС.Статус          КАК Статус
			|ИЗ
			|	Документ.ТТНВходящаяЕГАИС КАК Таблица
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
			|		ПО СтатусыДокументовЕГАИС.Документ = Таблица.Ссылка
			|ГДЕ
			|	Таблица.Ссылка = &Ссылка");
			
			Запрос.УстановитьПараметр("Ссылка", ИсточникСсылка);
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			
			ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Выборка);
			
			ЗаписьТребуется = ИнтеграцияЕГАИСПереопределяемый.ИспользоватьРегистр2(Выборка.Грузополучатель)
			                И (    Выборка.Статус = Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.Подтвержден
			                   Или Выборка.Статус = Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.ПодтвержденСРасхождениями);
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ТТНВходящаяЕГАИС") Тогда
		
		ИсточникСсылка = Источник.Ссылка;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СтатусыДокументовЕГАИС.Статус КАК Статус
		|ИЗ
		|	РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
		|ГДЕ
		|	СтатусыДокументовЕГАИС.Документ = &Ссылка
		|");
		
		Запрос.УстановитьПараметр("Ссылка", ИсточникСсылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		ЗаписьТребуется = ИнтеграцияЕГАИСПереопределяемый.ИспользоватьРегистр2(Источник.Грузополучатель)
		                И (    Выборка.Статус = Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.Подтвержден
		                   Или Выборка.Статус = Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.ПодтвержденСРасхождениями);
		
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Источник);
		
		ЗначенияРеквизитов.Контрагент     = Источник.Организация;
		ЗначенияРеквизитов.ТорговыйОбъект = Источник.ТорговыйОбъект;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИсточникСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЕГАИС.РассчитатьСтатусОформления(
		Метаданные.Документы.ПередачаВРегистр2ЕГАИС,
		ИсточникСсылка, ЗначенияРеквизитов, ЗаписьТребуется);
	
КонецПроцедуры

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.ЗапросСтатусаОформленияАктаПостановкиНаБаланс
//
Функция ЗапросСтатусаОформленияПередачаВРегистр2ЕГАИС(ДокументОснование) Экспорт
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
		
		Возврат ЗапросСтатусаОформленияПередачаВРегистр2ЕГАИСНаОснованииТТНВходящаяЕГАИС(ДокументОснование);
		
	КонецЕсли;
	
КонецФункции

Функция ЗапросСтатусаОформленияПередачаВРегистр2ЕГАИСНаОснованииТТНВходящаяЕГАИС(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ПередачаВРегистр2ЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Товары.КоличествоФакт       КАК План,
	|	0                           КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	0                                      КАК План,
	|	ВЫБОР
	|		КОГДА ОформленныеТовары.АлкогольнаяПродукция.ТипПродукции = ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ОформленныеТовары.Номенклатура.ОбъемДАЛ, 1) <> 0
	|						ТОГДА ОформленныеТовары.Количество * ЕСТЬNULL(ОформленныеТовары.Номенклатура.ОбъемДАЛ, 1)
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ОформленныеТовары.Количество
	|	КОНЕЦ           КАК Факт
	|ИЗ
	|	Документ.ПередачаВРегистр2ЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.АлкогольнаяПродукция = &ПустаяСсылка) КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#Область ТТНИсходящаяЕГАИС

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусОформленияТТНИсходящаяЕГАИС
//
Процедура РассчитатьСтатусОформленияТТНИсходящаяЕГАИС(Источник, Отказ) Экспорт
	
	ЗначенияРеквизитов = СтруктураРеквизитыРасчетаСтатусаОформления();
	ЗаписьДокументаОснования = Ложь;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ТТНИсходящаяЕГАИС")
		Или ТипЗнч(Источник) = Тип("ДокументСсылка.ТТНИсходящаяЕГАИС") Тогда
		
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.ТТНИсходящаяЕГАИС") Тогда
			ИсточникСсылка = Источник.ДокументОснование;
		Иначе
			ИсточникСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "ДокументОснование");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИсточникСсылка) Тогда
			
			Если ТипЗнч(ИсточникСсылка) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
				Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Таблица.Проведен      КАК Проведен,
				|	Таблица.Дата          КАК Дата,
				|	Таблица.Номер         КАК Номер,
				|	Таблица.Менеджер      КАК Ответственный,
				|	Таблица.Склад         КАК ТорговыйОбъект,
				|	Таблица.Организация   КАК Контрагент
				|ИЗ
				|	Документ.ПередачаТоваровМеждуОрганизациями КАК Таблица
				|ГДЕ
				|	Таблица.Ссылка = &Ссылка");
			ИначеЕсли ТипЗнч(ИсточникСсылка) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
				Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Таблица.Проведен      КАК Проведен,
				|	Таблица.Дата          КАК Дата,
				|	Таблица.Номер         КАК Номер,
				|	Таблица.Менеджер      КАК Ответственный,
				|	Таблица.Склад         КАК ТорговыйОбъект,
				|	Таблица.Организация   КАК Контрагент
				|ИЗ
				|	Документ.ВозвратТоваровМеждуОрганизациями КАК Таблица
				|ГДЕ
				|	Таблица.Ссылка = &Ссылка");
			ИначеЕсли ТипЗнч(ИсточникСсылка) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
				Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Таблица.Проведен         КАК Проведен,
				|	Таблица.Дата             КАК Дата,
				|	Таблица.Номер            КАК Номер,
				|	Таблица.Ответственный    КАК Ответственный,
				|	Таблица.СкладОтправитель КАК ТорговыйОбъект,
				|	Таблица.Организация      КАК Контрагент
				|ИЗ
				|	Документ.ПеремещениеТоваров КАК Таблица
				|ГДЕ
				|	Таблица.Ссылка = &Ссылка");
			ИначеЕсли ТипЗнч(ИсточникСсылка) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
				Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Таблица.Проведен    КАК Проведен,
				|	Таблица.Дата        КАК Дата,
				|	Таблица.Номер       КАК Номер,
				|	Таблица.Менеджер    КАК Ответственный,
				|	Таблица.Склад       КАК ТорговыйОбъект,
				|	Таблица.Организация КАК Контрагент
				|ИЗ
				|	Документ.ВозвратТоваровПоставщику КАК Таблица
				|ГДЕ
				|	Таблица.Ссылка = &Ссылка");
			ИначеЕсли ТипЗнч(ИсточникСсылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
				Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Таблица.Проведен    КАК Проведен,
				|	Таблица.Дата        КАК Дата,
				|	Таблица.Номер       КАК Номер,
				|	Таблица.Менеджер    КАК Ответственный,
				|	Таблица.Склад       КАК ТорговыйОбъект,
				|	Таблица.Организация КАК Контрагент
				|ИЗ
				|	Документ.РеализацияТоваровУслуг КАК Таблица
				|ГДЕ
				|	Таблица.Ссылка = &Ссылка");
			Иначе
				ВызватьИсключение НСтр("ru = 'Внутренняя ошибка: РассчитатьСтатусОформленияТТНИсходящаяЕГАИС'");
			КонецЕсли;
			
			Запрос.УстановитьПараметр("Ссылка", ИсточникСсылка);
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			
			ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Выборка);
			
		КонецЕсли;
		
	Иначе
		
		ИсточникСсылка  = Источник.Ссылка;
		ЗаписьДокументаОснования = Истина;
		
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Источник);
		
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.ПередачаТоваровМеждуОрганизациями") Тогда
			ЗначенияРеквизитов.Ответственный  = Источник.Менеджер;
			ЗначенияРеквизитов.ТорговыйОбъект = Источник.Склад;
			ЗначенияРеквизитов.Контрагент     = Источник.Организация;
		ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ВозвратТоваровМеждуОрганизациями") Тогда
			ЗначенияРеквизитов.Ответственный  = Источник.Менеджер;
			ЗначенияРеквизитов.ТорговыйОбъект = Источник.Склад;
			ЗначенияРеквизитов.Контрагент     = Источник.Организация;
		ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ПеремещениеТоваров") Тогда
			ЗначенияРеквизитов.Ответственный  = Источник.Ответственный;
			ЗначенияРеквизитов.ТорговыйОбъект = Источник.СкладОтправитель;
			ЗначенияРеквизитов.Контрагент     = Источник.Организация;
		ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ВозвратТоваровПоставщику") Тогда
			ЗначенияРеквизитов.Ответственный  = Источник.Менеджер;
			ЗначенияРеквизитов.ТорговыйОбъект = Источник.Склад;
			ЗначенияРеквизитов.Контрагент     = Источник.Организация;
		ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда
			ЗначенияРеквизитов.Ответственный  = Источник.Менеджер;
			ЗначенияРеквизитов.ТорговыйОбъект = Источник.Склад;
			ЗначенияРеквизитов.Контрагент     = Источник.Организация;
		Иначе
			ВызватьИсключение НСтр("ru = 'Внутренняя ошибка: РассчитатьСтатусОформленияТТНИсходящаяЕГАИС'");
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИсточникСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьТребуется = ТребуетсяОформлениеДокументовЕГАИС(
		ИсточникСсылка,
		Метаданные.Документы.ТТНИсходящаяЕГАИС);
	
	ИнтеграцияЕГАИС.РассчитатьСтатусОформления(
		Метаданные.Документы.ТТНИсходящаяЕГАИС,
		ИсточникСсылка, ЗначенияРеквизитов, ЗаписьТребуется);
		
	Если Не ЗаписьДокументаОснования Тогда
		
		ЗаписьТребуется = ТребуетсяОформлениеДокументовЕГАИС(
			ИсточникСсылка,
			Метаданные.Документы.ЧекЕГАИС);
		
		ИнтеграцияЕГАИС.РассчитатьСтатусОформления(
			Метаданные.Документы.ЧекЕГАИС,
			ИсточникСсылка, ЗначенияРеквизитов, ЗаписьТребуется);
		
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.ЗапросСтатусаОформленияТТНИсходящаяЕГАИС
//
Функция ЗапросСтатусаОформленияТТНИсходящаяЕГАИС(ДокументОснование) Экспорт
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		
		Возврат ЗапросСтатусаОформленияТТНИсходящаяЕГАИСНаОснованииВозвратТоваровПоставщику(ДокументОснование);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		Возврат ЗапросСтатусаОформленияТТНИсходящаяЕГАИСНаОснованииРеализацииТоваров(ДокументОснование);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		
		Возврат ЗапросСтатусаОформленияТТНИсходящаяЕГАИСНаОснованииПеремещениеТоваров(ДокументОснование);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
		
		Возврат ЗапросСтатусаОформленияТТНИсходящаяЕГАИСНаОснованииВозвратТоваровМеждуОрганизациями(ДокументОснование);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
		
		Возврат ЗапросСтатусаОформленияТТНИсходящаяЕГАИСНаОснованииПередачаТоваровМеждуОрганизациями(ДокументОснование);
		
	КонецЕсли;
	
КонецФункции

Функция ЗапросСтатусаОформленияТТНИсходящаяЕГАИСНаОснованииВозвратТоваровПоставщику(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Товары КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровПоставщику.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТабличнаяЧасть.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТабличнаяЧасть.Характеристика
	|
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И ТаблицаСерии.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Серии КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                КАК План,
	|	ОформленныеТовары.Количество     КАК Факт
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросСтатусаОформленияТТНИсходящаяЕГАИСНаОснованииПеремещениеТоваров(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК План,
	|	0                                                          КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТабличнаяЧасть.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТабличнаяЧасть.Характеристика
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И ТаблицаСерии.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК План,
	|	0                                                          КАК Факт
	|ИЗ
	|	Документ.ПеремещениеТоваров.Серии КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросСтатусаОформленияТТНИсходящаяЕГАИСНаОснованииВозвратТоваровМеждуОрганизациями(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросСтатусаОформленияТТНИсходящаяЕГАИСНаОснованииПередачаТоваровМеждуОрганизациями(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями.Товары КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                КАК План,
	|	ОформленныеТовары.Количество     КАК Факт
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросСтатусаОформленияТТНИсходящаяЕГАИСНаОснованииРеализацииТоваров(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТабличнаяЧасть.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТабличнаяЧасть.Характеристика
	|
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И ТаблицаСерии.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Серии КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                КАК План,
	|	ОформленныеТовары.Количество     КАК Факт
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                КАК План,
	|	ОформленныеТовары.Количество     КАК Факт
	|ИЗ
	|	Документ.ЧекЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#Область ТТНВходящаяЕГАИС

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусОформленияТТНВходящаяЕГАИС
//
Процедура РассчитатьСтатусОформленияТТНВходящаяЕГАИС(Источник, Отказ) Экспорт
	
	ЗначенияРеквизитов = СтруктураРеквизитыРасчетаСтатусаОформления();
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ТТНВходящаяЕГАИС")
		Или ТипЗнч(Источник) = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
		
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.ТТНВходящаяЕГАИС") Тогда
			ИсточникСсылка = Источник.ДокументОснование;
		Иначе
			ИсточникСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "ДокументОснование");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИсточникСсылка) Тогда
			
			Если ТипЗнч(ИсточникСсылка) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
				Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Таблица.Проведен              КАК Проведен,
				|	Таблица.Дата                  КАК Дата,
				|	Таблица.Номер                 КАК Номер,
				|	Таблица.Менеджер              КАК Ответственный,
				|	Таблица.Склад                 КАК ТорговыйОбъект,
				|	Таблица.ОрганизацияПолучатель КАК Контрагент
				|ИЗ
				|	Документ.ПередачаТоваровМеждуОрганизациями КАК Таблица
				|ГДЕ
				|	Таблица.Ссылка = &Ссылка");
			ИначеЕсли ТипЗнч(ИсточникСсылка) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
				Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Таблица.Проведен              КАК Проведен,
				|	Таблица.Дата                  КАК Дата,
				|	Таблица.Номер                 КАК Номер,
				|	Таблица.Менеджер              КАК Ответственный,
				|	Таблица.Склад                 КАК ТорговыйОбъект,
				|	Таблица.ОрганизацияПолучатель КАК Контрагент
				|ИЗ
				|	Документ.ВозвратТоваровМеждуОрганизациями КАК Таблица
				|ГДЕ
				|	Таблица.Ссылка = &Ссылка");
			ИначеЕсли ТипЗнч(ИсточникСсылка) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
				Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Таблица.Проведен              КАК Проведен,
				|	Таблица.Дата                  КАК Дата,
				|	Таблица.Номер                 КАК Номер,
				|	Таблица.Ответственный         КАК Ответственный,
				|	Таблица.СкладПолучатель       КАК ТорговыйОбъект,
				|	Таблица.ОрганизацияПолучатель КАК Контрагент
				|ИЗ
				|	Документ.ПеремещениеТоваров КАК Таблица
				|ГДЕ
				|	Таблица.Ссылка = &Ссылка");
			ИначеЕсли ТипЗнч(ИсточникСсылка) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
				Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Таблица.Проведен    КАК Проведен,
				|	Таблица.Дата        КАК Дата,
				|	Таблица.Номер       КАК Номер,
				|	Таблица.Менеджер    КАК Ответственный,
				|	Таблица.Склад       КАК ТорговыйОбъект,
				|	Таблица.Организация КАК Контрагент
				|ИЗ
				|	Документ.ПриобретениеТоваровУслуг КАК Таблица
				|ГДЕ
				|	Таблица.Ссылка = &Ссылка");
			Иначе
				ВызватьИсключение НСтр("ru = 'Внутренняя ошибка: РассчитатьСтатусОформленияТТНВходящаяЕГАИС'");
			КонецЕсли;
			
			Запрос.УстановитьПараметр("Ссылка", ИсточникСсылка);
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			
			ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Выборка);
			
		КонецЕсли;
		
	Иначе
		
		ИсточникСсылка = Источник.Ссылка;
		
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Источник);
		
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.ПередачаТоваровМеждуОрганизациями") Тогда
			ЗначенияРеквизитов.Ответственный  = Источник.Менеджер;
			ЗначенияРеквизитов.ТорговыйОбъект = Источник.Склад;
			ЗначенияРеквизитов.Контрагент     = Источник.ОрганизацияПолучатель;
		ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ВозвратТоваровМеждуОрганизациями") Тогда
			ЗначенияРеквизитов.Ответственный  = Источник.Менеджер;
			ЗначенияРеквизитов.ТорговыйОбъект = Источник.Склад;
			ЗначенияРеквизитов.Контрагент     = Источник.ОрганизацияПолучатель;
		ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ПеремещениеТоваров") Тогда
			ЗначенияРеквизитов.Ответственный  = Источник.Ответственный;
			ЗначенияРеквизитов.ТорговыйОбъект = Источник.СкладПолучатель;
			ЗначенияРеквизитов.Контрагент     = Источник.ОрганизацияПолучатель;
		ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ПриобретениеТоваровУслуг") Тогда
			ЗначенияРеквизитов.Ответственный  = Источник.Менеджер;
			ЗначенияРеквизитов.ТорговыйОбъект = Источник.Склад;
			ЗначенияРеквизитов.Контрагент     = Источник.Организация;
		Иначе
			ВызватьИсключение НСтр("ru = 'Внутренняя ошибка: РассчитатьСтатусОформленияТТНВходящаяЕГАИС'");
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИсточникСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьТребуется = ТребуетсяОформлениеДокументовЕГАИС(
		ИсточникСсылка,
		Метаданные.Документы.ТТНВходящаяЕГАИС);
	
	ИнтеграцияЕГАИС.РассчитатьСтатусОформления(
		Метаданные.Документы.ТТНВходящаяЕГАИС,
		ИсточникСсылка, ЗначенияРеквизитов, ЗаписьТребуется);
	
КонецПроцедуры

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.ЗапросСтатусаОформленияТТНВходящаяЕГАИС
//
Функция ЗапросСтатусаОформленияТТНВходящаяЕГАИС(ДокументОснование) Экспорт
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		
		Возврат ЗапросСтатусаОформленияТТНВходящаяЕГАИСНаОснованииПриобретениеТоваровУслуг(ДокументОснование);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		
		Возврат ЗапросСтатусаОформленияТТНВходящаяЕГАИСНаОснованииПеремещениеТоваров(ДокументОснование);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
		
		Возврат ЗапросСтатусаОформленияТТНВходящаяЕГАИСНаОснованииВозвратТоваровМеждуОрганизациями(ДокументОснование);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
		
		Возврат ЗапросСтатусаОформленияТТНВходящаяЕГАИСНаОснованииПередачаТоваровМеждуОрганизациями(ДокументОснование);
		
	КонецЕсли;
	
КонецФункции

Функция ЗапросСтатусаОформленияТТНВходящаяЕГАИСНаОснованииПриобретениеТоваровУслуг(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК План,
	|	0                                                          КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТабличнаяЧасть.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТабличнаяЧасть.Характеристика
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И ТаблицаСерии.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Серии КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросСтатусаОформленияТТНВходящаяЕГАИСНаОснованииПеремещениеТоваров(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК План,
	|	0                                                          КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТабличнаяЧасть.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТабличнаяЧасть.Характеристика
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И ТаблицаСерии.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ИЗ
	|	Документ.ПеремещениеТоваров.Серии КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросСтатусаОформленияТТНВходящаяЕГАИСНаОснованииВозвратТоваровМеждуОрганизациями(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК План,
	|	0                                                          КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями.Товары КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросСтатусаОформленияТТНВходящаяЕГАИСНаОснованииПередачаТоваровМеждуОрганизациями(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#Область ЧекЕГАИС

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусАктаПостановкиНаБаланс
//
Процедура РассчитатьСтатусОформленияЧекЕГАИС(Источник, Отказ) Экспорт
	
	ЗначенияРеквизитов = СтруктураРеквизитыРасчетаСтатусаОформления();
	ЗаписьДокументаОснования = Ложь;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ЧекЕГАИС")
		Или ТипЗнч(Источник) = Тип("ДокументСсылка.ЧекЕГАИС") Тогда
		
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.ЧекЕГАИС") Тогда
			ИсточникСсылка = Источник.ДокументОснование;
		Иначе
			ИсточникСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "ДокументОснование");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИсточникСсылка) Тогда
			
			Если ТипЗнч(ИсточникСсылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
				
				Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Таблица.Проведен      КАК Проведен,
				|	Таблица.Дата          КАК Дата,
				|	Таблица.Номер         КАК Номер,
				|	Таблица.Менеджер      КАК Ответственный,
				|	Таблица.Склад         КАК ТорговыйОбъект,
				|	Таблица.Организация   КАК Контрагент
				|ИЗ
				|	Документ.РеализацияТоваровУслуг КАК Таблица
				|ГДЕ
				|	Таблица.Ссылка = &Ссылка");
				
			Иначе
				
				Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Таблица.Проведен      КАК Проведен,
				|	Таблица.Дата          КАК Дата,
				|	Таблица.Номер         КАК Номер,
				|	Таблица.Ответственный КАК Ответственный,
				|	Таблица.Склад         КАК ТорговыйОбъект,
				|	Таблица.Организация   КАК Контрагент
				|ИЗ
				|	" + ИсточникСсылка.Метаданные().ПолноеИмя() + " КАК Таблица
				|ГДЕ
				|	Таблица.Ссылка = &Ссылка");
				
			КонецЕсли;
			
			Запрос.УстановитьПараметр("Ссылка", ИсточникСсылка);
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			
			ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Выборка);
			
		КонецЕсли;
		
	Иначе
		
		ЗаписьДокументаОснования = Истина;
		
		ИсточникСсылка = Источник.Ссылка;
		
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Источник);
		
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда
			ЗначенияРеквизитов.Ответственный  = Источник.Менеджер;
			ЗначенияРеквизитов.ТорговыйОбъект = Источник.Склад;
			ЗначенияРеквизитов.Контрагент     = Источник.Организация;
		Иначе
			ЗначенияРеквизитов.Ответственный  = Источник.Ответственный;
			ЗначенияРеквизитов.ТорговыйОбъект = Источник.Склад;
			ЗначенияРеквизитов.Контрагент     = Источник.Организация;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИсточникСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьТребуется = ТребуетсяОформлениеДокументовЕГАИС(
		ИсточникСсылка,
		Метаданные.Документы.ЧекЕГАИС);
	
	ИнтеграцияЕГАИС.РассчитатьСтатусОформления(
		Метаданные.Документы.ЧекЕГАИС,
		ИсточникСсылка, ЗначенияРеквизитов, ЗаписьТребуется);
	
	Если Не ЗаписьДокументаОснования Тогда
		
		ЗаписьТребуется = ТребуетсяОформлениеДокументовЕГАИС(
			ИсточникСсылка,
			Метаданные.Документы.АктСписанияЕГАИС);
		
		ИнтеграцияЕГАИС.РассчитатьСтатусОформления(
			Метаданные.Документы.АктСписанияЕГАИС,
			ИсточникСсылка, ЗначенияРеквизитов, ЗаписьТребуется);
			
		ЗаписьТребуется = ТребуетсяОформлениеДокументовЕГАИС(
			ИсточникСсылка,
			Метаданные.Документы.ТТНИсходящаяЕГАИС);
		
		ИнтеграцияЕГАИС.РассчитатьСтатусОформления(
			Метаданные.Документы.ТТНИсходящаяЕГАИС,
			ИсточникСсылка, ЗначенияРеквизитов, ЗаписьТребуется);
		
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.ЗапросСтатусаОформленияАктаПостановкиНаБаланс
//
Функция ЗапросСтатусаОформленияЧекЕГАИС(ДокументОснование) Экспорт
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		Возврат ЗапросСтатусаОформленияЧекЕГАИСНаОснованииРеализацияТоваровУслуг(ДокументОснование);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СборкаТоваров") Тогда
		
		Возврат ЗапросСтатусаОформленияЧекЕГАИСНаОснованииСборкаТоваров(ДокументОснование);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВнутреннееПотреблениеТоваров") Тогда
		
		Возврат ЗапросСтатусаОформленияАктСписанияЕГАИСЧекЕГАИСНаОснованииВнутреннееПотреблениеТоваров(ДокументОснование);
		
	КонецЕсли;
	
КонецФункции

Функция ЗапросСтатусаОформленияЧекЕГАИСНаОснованииРеализацияТоваровУслуг(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ЧекЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТабличнаяЧасть.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТабличнаяЧасть.Характеристика
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И ТаблицаСерии.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Серии КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                КАК План,
	|	ОформленныеТовары.Количество     КАК Факт
	|ИЗ
	|	Документ.ЧекЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросСтатусаОформленияЧекЕГАИСНаОснованииСборкаТоваров(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыЧекЕГАИС
	|ИЗ
	|	Документ.ЧекЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыАктСписанияЕГАИС
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусыАктСписанияЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.СборкаТоваров КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И ТабличнаяЧасть.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров)
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И СправочникНоменклатура.ВидАлкогольнойПродукции.Маркируемый
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.ЧекЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыЧекЕГАИС КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыАктСписанияЕГАИС КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#Область ЧекЕГАИСВозврат

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусАктаПостановкиНаБаланс
//
Процедура РассчитатьСтатусОформленияЧекЕГАИСВозврат(Источник, Отказ) Экспорт
	
	ЗначенияРеквизитов = СтруктураРеквизитыРасчетаСтатусаОформления();
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ЧекЕГАИСВозврат")
		Или ТипЗнч(Источник) = Тип("ДокументСсылка.ЧекЕГАИСВозврат") Тогда
		
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.ЧекЕГАИСВозврат") Тогда
			ИсточникСсылка = Источник.ДокументОснование;
		Иначе
			ИсточникСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "ДокументОснование");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИсточникСсылка) Тогда
			
			Если ТипЗнч(ИсточникСсылка) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
				
				Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Таблица.Проведен      КАК Проведен,
				|	Таблица.Дата          КАК Дата,
				|	Таблица.Номер         КАК Номер,
				|	Таблица.Менеджер      КАК Ответственный,
				|	Таблица.Склад         КАК ТорговыйОбъект,
				|	Таблица.Организация   КАК Контрагент
				|ИЗ
				|	Документ.ВозвратТоваровОтКлиента КАК Таблица
				|ГДЕ
				|	Таблица.Ссылка = &Ссылка");
				
			Иначе
				
				Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	Таблица.Проведен      КАК Проведен,
				|	Таблица.Дата          КАК Дата,
				|	Таблица.Номер         КАК Номер,
				|	Таблица.Ответственный КАК Ответственный,
				|	Таблица.Склад         КАК ТорговыйОбъект,
				|	Таблица.Организация   КАК Контрагент
				|ИЗ
				|	" + ИсточникСсылка.Метаданные().ПолноеИмя() + " КАК Таблица
				|ГДЕ
				|	Таблица.Ссылка = &Ссылка");
				
			КонецЕсли;
			
			Запрос.УстановитьПараметр("Ссылка", ИсточникСсылка);
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			
			ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Выборка);
			
		КонецЕсли;
		
	Иначе
		
		ИсточникСсылка = Источник.Ссылка;
		
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Источник);
		
		Если ТипЗнч(Источник) = Тип("ДокументОбъект.ВозвратТоваровОтКлиента") Тогда
			ЗначенияРеквизитов.Ответственный  = Источник.Менеджер;
			ЗначенияРеквизитов.ТорговыйОбъект = Источник.Склад;
			ЗначенияРеквизитов.Контрагент     = Источник.Организация;
		Иначе
			ЗначенияРеквизитов.Ответственный  = Источник.Ответственный;
			ЗначенияРеквизитов.ТорговыйОбъект = Источник.Склад;
			ЗначенияРеквизитов.Контрагент     = Источник.Организация;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИсточникСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьТребуется = ТребуетсяОформлениеДокументовЕГАИС(
		ИсточникСсылка,
		Метаданные.Документы.ЧекЕГАИСВозврат);
	
	ИнтеграцияЕГАИС.РассчитатьСтатусОформления(
		Метаданные.Документы.ЧекЕГАИСВозврат,
		ИсточникСсылка, ЗначенияРеквизитов, ЗаписьТребуется);
	
КонецПроцедуры

// См. процедуру ИнтеграцияЕГАИСПереопределяемый.ЗапросСтатусаОформленияАктаПостановкиНаБаланс
//
Функция ЗапросСтатусаОформленияЧекЕГАИСВозврат(ДокументОснование) Экспорт
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
		
		Возврат ЗапросСтатусаОформленияЧекЕГАИСВозвратНаОснованииВозвратТоваровОтКлиента(ДокументОснование);
		
	КонецЕсли;
	
КонецФункции

Функция ЗапросСтатусаОформленияЧекЕГАИСВозвратНаОснованииВозвратТоваровОтКлиента(ДокументОснование)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ЧекЕГАИСВозврат КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.Товары КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТабличнаяЧасть.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТабличнаяЧасть.Характеристика
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И ТаблицаСерии.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия          КАК Серия,
	|	ТабличнаяЧасть.Количество     КАК План,
	|	0                             КАК Факт
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.Серии КАК ТабличнаяЧасть
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура   КАК Номенклатура,
	|	ОформленныеТовары.Характеристика КАК Характеристика,
	|	ОформленныеТовары.Серия          КАК Серия,
	|	0                                      КАК План,
	|	ОформленныеТовары.Количество           КАК Факт
	|ИЗ
	|	Документ.ЧекЕГАИСВозврат.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура   КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия          КАК Серия,
	|	СУММА(ТоварыКОформлению.План) КАК План,
	|	СУММА(ТоварыКОформлению.Факт) КАК Факт
	|ПОМЕСТИТЬ Результат
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт > 0 И Т.План > 0)                КАК ЕстьОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.Факт >= 0 И Т.План > Т.Факт)          КАК ЕстьНеОформленныеТовары,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План <= Т.Факт)                       КАК ЕстьПолностьюОформленныеТовары,
	|	ЛОЖЬ                                                                                   КАК ТребуетсяСопоставлениеНоменклатуры,
	|	1 В (ВЫБРАТЬ ПЕРВЫЕ 1 1 Из Результат КАК Т ГДЕ Т.План < Т.Факт)                        КАК ЕстьОшибкиОформления
	|");
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ОбработкаЗаполнения

#Область АктПостановкиНаБалансЕГАИС

Процедура ОбработкаЗаполненияАктаПостановкиНаБалансЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОприходованиеИзлишковТоваров") Тогда
		
		ЗаполнитьАктПостановкиНаБалансНаОснованииОприходованияИзлишковТовара(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПересортицаТоваров") Тогда
	
		ЗаполнитьАктПостановкиНаБалансНаОснованииПересортицыТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("АкцизныеМаркиВРегистр3") Тогда
		
		ДокументОбъект.ОрганизацияЕГАИС = ДанныеЗаполнения.ОрганизацияЕГАИС;
		ДокументОбъект.ВидДокумента     = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр3;
		
		ТаблицаТовары = Новый ТаблицаЗначений;
		ТаблицаТовары.Колонки.Добавить("АлкогольнаяПродукция", Новый ОписаниеТипов("СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС"));
		ТаблицаТовары.Колонки.Добавить("Справка2",             Новый ОписаниеТипов("СправочникСсылка.Справки2ЕГАИС"));
		
		Для Каждого ДанныеАкцизнойМарки Из ДанныеЗаполнения.АкцизныеМарки Цикл
			
			НоваяСтрока = ДокументОбъект.АкцизныеМарки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеАкцизнойМарки);
			НоваяСтрока.Количество = 1;
			
			НоваяСтрокаТовары = ТаблицаТовары.Добавить();
			НоваяСтрокаТовары.АлкогольнаяПродукция = ДанныеАкцизнойМарки.АлкогольнаяПродукция;
			НоваяСтрокаТовары.Справка2             = ДанныеАкцизнойМарки.Справка2;
			
		КонецЦикла;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Т.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	Т.Справка2 КАК Справка2,
		|	1 КАК Количество
		|ПОМЕСТИТЬ втТовары
		|ИЗ
		|	&Товары КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.АлкогольнаяПродукция,
		|	Т.Справка2 КАК Справка2,
		|	СУММА(Т.Количество) КАК Количество
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	втТовары КАК Т
		|СГРУППИРОВАТЬ ПО
		|	Т.АлкогольнаяПродукция,
		|	Т.Справка2
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	ТабличнаяЧасть.Справка2 КАК Справка2,
		|	МАКСИМУМ(СоответствиеНоменклатурыЕГАИС.Номенклатура) КАК Номенклатура,
		|	МАКСИМУМ(СоответствиеНоменклатурыЕГАИС.Характеристика) КАК Характеристика
		|ПОМЕСТИТЬ СопоставленыеПозиции
		|ИЗ
		|	Товары КАК ТабличнаяЧасть
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
		|		ПО СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция = ТабличнаяЧасть.АлкогольнаяПродукция
		|			И СоответствиеНоменклатурыЕГАИС.Справка2 = ТабличнаяЧасть.Справка2
		|ГДЕ
		|	Не СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция ЕСТЬ NULL
		|СГРУППИРОВАТЬ ПО
		|	ТабличнаяЧасть.АлкогольнаяПродукция,
		|	ТабличнаяЧасть.Справка2
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеНоменклатурыЕГАИС.Номенклатура) = 1
		|		И КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеНоменклатурыЕГАИС.Характеристика) < 2
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СопоставленыеПозиции.Номенклатура   КАК Номенклатура,
		|	СопоставленыеПозиции.Характеристика КАК Характеристика,
		|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	ТабличнаяЧасть.Справка2             КАК Справка2,
		|	ТабличнаяЧасть.Количество           КАК Количество,
		|	ТабличнаяЧасть.Количество           КАК КоличествоУпаковок
		|ИЗ
		|	Товары КАК ТабличнаяЧасть
		|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
		|		ПО ТабличнаяЧасть.АлкогольнаяПродукция = СопоставленыеПозиции.АлкогольнаяПродукция
		|			И ТабличнаяЧасть.Справка2 = СопоставленыеПозиции.Справка2
		|			И НЕ СопоставленыеПозиции.АлкогольнаяПродукция ЕСТЬ NULL
		|");
		Запрос.УстановитьПараметр("Товары", ТаблицаТовары);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ДокументОбъект.Товары.Добавить(), Выборка);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьАктПостановкиНаБалансНаОснованииОприходованияИзлишковТовара(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка          КАК ДокументОснование,
	|	НЕ Таблица.Проведен     КАК ЕстьОшибкиПроведен,
	|	Таблица.Склад           КАК Склад,
	|	Таблица.Склад.ТипСклада КАК ТипСклада,
	|	Таблица.Организация     КАК Организация,
	|	Таблица.Ответственный   КАК Ответственный
	|ИЗ
	|	Документ.ОприходованиеИзлишковТоваров КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.АктПостановкиНаБалансЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ОприходованиеИзлишковТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество
	|ИЗ
	|	Документ.АктПостановкиНаБалансЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура              КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика            КАК Характеристика,
	|	ТоварыКОформлению.Серия                     КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция   КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)         КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)         КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|	ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ЭтаСсылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы", Документы.АктПостановкиНаБалансЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыШапки = РезультатЗапроса[0].Выбрать();
	РеквизитыШапки.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыШапки.ДокументОснование,
			,
			РеквизитыШапки.ЕстьОшибкиПроведен,
			,
			);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыШапки);
			
			ДокументОбъект.ОрганизацияЕГАИС = ОрганизацияЕГАИСПоОрганизацииИСкладу(РеквизитыШапки.Организация, РеквизитыШапки.Склад);
			Если РеквизитыШапки.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин Тогда
				ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр2;
			Иначе
				ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр1;
			КонецЕсли;
			
			ДокументОбъект.ПричинаПостановкиНаБаланс = Перечисления.ПричиныПостановкиНаБалансЕГАИС.Излишки;
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса[РезультатЗапроса.Количество() - 1], ДанныеЗаполнения, Истина)
	
КонецПроцедуры

Процедура ЗаполнитьАктПостановкиНаБалансНаОснованииПересортицыТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка          КАК ДокументОснование,
	|	НЕ Таблица.Проведен     КАК ЕстьОшибкиПроведен,
	|	Таблица.Склад           КАК Склад,
	|	Таблица.Склад.ТипСклада КАК ТипСклада,
	|	Таблица.Организация     КАК Организация,
	|	Таблица.Ответственный   КАК Ответственный
	|ИЗ
	|	Документ.ПересортицаТоваров КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.АктПостановкиНаБалансЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ПересортицаТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.НоменклатураОприходование = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество
	|ИЗ
	|	Документ.АктПостановкиНаБалансЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура              КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика            КАК Характеристика,
	|	ТоварыКОформлению.Серия                     КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция      КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)         КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество) КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|	ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 2
	|	АктСписанияЕГАИС.Ссылка КАК АктСписанияЕГАИС
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК АктСписанияЕГАИС
	|ГДЕ
	|	АктСписанияЕГАИС.ДокументОснование = &ДокументОснование
	|	И АктСписанияЕГАИС.ПричинаСписания = ЗНАЧЕНИЕ(Перечисление.ПричиныСписанийЕГАИС.Пересортица)
	|");
	
	Запрос.УстановитьПараметр("ЭтаСсылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы", Документы.АктПостановкиНаБалансЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыШапки = РезультатЗапроса[0].Выбрать();
	РеквизитыШапки.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыШапки.ДокументОснование,
			,
			РеквизитыШапки.ЕстьОшибкиПроведен,
			,
			);
		
		ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыШапки);
		
		ДокументОбъект.ОрганизацияЕГАИС = ОрганизацияЕГАИСПоОрганизацииИСкладу(РеквизитыШапки.Организация, РеквизитыШапки.Склад);
		Если РеквизитыШапки.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин Тогда
			ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр2;
		Иначе
			ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр1;
			
			ДокументОбъект.ПричинаПостановкиНаБаланс = Перечисления.ПричиныПостановкиНаБалансЕГАИС.Пересортица;
			
			ВыборкаАктыСписания = РезультатЗапроса[РезультатЗапроса.Количество() - 1].Выбрать();
			Если ВыборкаАктыСписания.Количество() = 1 Тогда
				ВыборкаАктыСписания.Следующий();
				ДокументОбъект.АктСписанияЕГАИС = ВыборкаАктыСписания.АктСписанияЕГАИС;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса[РезультатЗапроса.Количество() - 2], ДанныеЗаполнения, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область АктСписанияЕГАИС

Процедура ОбработкаЗаполненияАктаСписанияЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВнутреннееПотреблениеТоваров") Тогда
		
		ЗаполнитьАктСписанияЕГАИСНаОснованииВнутреннегоПотребленияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СписаниеНедостачТоваров") Тогда
		
		ЗаполнитьАктСписанияЕГАИСНаОснованииСписанияНедостачТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПересортицаТоваров") Тогда
		
		ЗаполнитьАктСписанияЕГАИСНаОснованииПересортицыТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		
		ЗаполнитьАктСписанияЕГАИСНаОснованииОтчетаОРозничныхПродажах(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СборкаТоваров") Тогда
		
		ЗаполнитьАктСписанияЕГАИСНаОснованииСборкиТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьАктСписанияЕГАИСНаОснованииВнутреннегоПотребленияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка          КАК ДокументОснование,
	|	НЕ Таблица.Проведен     КАК ЕстьОшибкиПроведен,
	|	Таблица.Склад           КАК Склад,
	|	Таблица.Склад.ТипСклада КАК ТипСклада,
	|	Таблица.Организация     КАК Организация,
	|	Таблица.Ответственный   КАК Ответственный
	|ИЗ
	|	Документ.ВнутреннееПотреблениеТоваров КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыАктСписанияЕГАИС
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусыАктСписанияЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыЧекЕГАИС
	|ИЗ
	|	Документ.ЧекЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусыЧекЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ВнутреннееПотреблениеТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотреблениеТоваров.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТабличнаяЧасть.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТабличнаяЧасть.Характеристика
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И ТаблицаСерии.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|ИЗ
	|	Документ.ВнутреннееПотреблениеТоваров.Серии КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыАктСписанияЕГАИС КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество
	|ИЗ
	|	Документ.ЧекЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыЧекЕГАИС КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура            КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика          КАК Характеристика,
	|	ТоварыКОформлению.Серия                   КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)       КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)       КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ЭтаСсылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусыАктСписанияЕГАИС", Документы.АктСписанияЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("КонечныеСтатусыЧекЕГАИС",         Документы.ЧекЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыДокумента = РезультатЗапроса[0].Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
			
			ДокументОбъект.ОрганизацияЕГАИС = ОрганизацияЕГАИСПоОрганизацииИСкладу(РеквизитыДокумента.Организация, РеквизитыДокумента.Склад);
			Если РеквизитыДокумента.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин Тогда
				ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра2;
			Иначе
				ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра1;
			КонецЕсли;
			
			ДокументОбъект.ПричинаСписания = Неопределено;
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса[РезультатЗапроса.Количество() - 1], ДанныеЗаполнения, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьАктСписанияЕГАИСНаОснованииСписанияНедостачТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка          КАК ДокументОснование,
	|	НЕ Таблица.Проведен     КАК ЕстьОшибкиПроведен,
	|	Таблица.Склад           КАК Склад,
	|	Таблица.Склад.ТипСклада КАК ТипСклада,
	|	Таблица.Организация     КАК Организация,
	|	Таблица.Ответственный   КАК Ответственный
	|ИЗ
	|	Документ.СписаниеНедостачТоваров КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.СписаниеНедостачТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура              КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика            КАК Характеристика,
	|	ТоварыКОформлению.Серия                     КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция   КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)         КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество) КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|	ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ЭтаСсылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы", Документы.АктСписанияЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыДокумента = РезультатЗапроса[0].Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
			
			ДокументОбъект.ОрганизацияЕГАИС = ОрганизацияЕГАИСПоОрганизацииИСкладу(РеквизитыДокумента.Организация, РеквизитыДокумента.Склад);
			Если РеквизитыДокумента.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин Тогда
				ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра2;
			Иначе
				ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра1;
			КонецЕсли;
			
			ДокументОбъект.ПричинаСписания = Перечисления.ПричиныСписанийЕГАИС.Недостача;
			
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса[РезультатЗапроса.Количество() - 1], ДанныеЗаполнения, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьАктСписанияЕГАИСНаОснованииПересортицыТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка          КАК ДокументОснование,
	|	НЕ Таблица.Проведен     КАК ЕстьОшибкиПроведен,
	|	Таблица.Склад           КАК Склад,
	|	Таблица.Склад.ТипСклада КАК ТипСклада,
	|	Таблица.Организация     КАК Организация,
	|	Таблица.Ответственный   КАК Ответственный
	|ИЗ
	|	Документ.ПересортицаТоваров КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыАктСписанияЕГАИС
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ПересортицаТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыАктСписанияЕГАИС КАК Т)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура            КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика          КАК Характеристика,
	|	ТоварыКОформлению.Серия                   КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)       КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)       КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|	ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ЭтаСсылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы", Документы.АктСписанияЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыДокумента = РезультатЗапроса[0].Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
			
			ДокументОбъект.ОрганизацияЕГАИС = ОрганизацияЕГАИСПоОрганизацииИСкладу(РеквизитыДокумента.Организация, РеквизитыДокумента.Склад);
			Если РеквизитыДокумента.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин Тогда
				ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра2;
			Иначе
				ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра1;
			КонецЕсли;
			
			ДокументОбъект.ПричинаСписания = Перечисления.ПричиныСписанийЕГАИС.Пересортица;
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса[РезультатЗапроса.Количество() - 1], ДанныеЗаполнения, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьАктСписанияЕГАИСНаОснованииОтчетаОРозничныхПродажах(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка          КАК ДокументОснование,
	|	Таблица.Дата            КАК Дата,
	|	НЕ Таблица.Проведен     КАК ЕстьОшибкиПроведен,
	|	Таблица.Склад           КАК Склад,
	|	Таблица.Склад.ТипСклада КАК ТипСклада,
	|	Таблица.Организация     КАК Организация,
	|	Таблица.Валюта          КАК Валюта,
	|	Таблица.Ответственный   КАК Ответственный
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	РезультатЗапроса = Запрос.Выполнить();
	
	РеквизитыДокумента = РезультатЗапроса.Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
			
			ДокументОбъект.ОрганизацияЕГАИС = ОрганизацияЕГАИСПоОрганизацииИСкладу(РеквизитыДокумента.Организация, РеквизитыДокумента.Склад);
			Если РеквизитыДокумента.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин Тогда
				ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра2;
			Иначе
				ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра1;
			КонецЕсли;
			
			ДокументОбъект.ПричинаСписания = Перечисления.ПричиныСписанийЕГАИС.Реализация;
		
	КонецЕсли;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ втКурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОснования,
	|		Валюта = &ВалютаРеглУчета ИЛИ Валюта = &ВалютаОснования) КАК КурсыВалютСрезПоследних
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура                 КАК Номенклатура,
	|	ТаблицаТовары.Характеристика               КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаСерии.Ссылка ЕСТЬ NULL
	|			ТОГДА ТаблицаТовары.Серия
	|		ИНАЧЕ
	|			ТаблицаСерии.Серия
	|	КОНЕЦ                                      КАК Серия,
	|	СУММА(ВЫБОР
	|		КОГДА ТаблицаСерии.Количество ЕСТЬ NULL
	|			ТОГДА ТаблицаТовары.Количество
	|		ИНАЧЕ
	|			ТаблицаСерии.Количество
	|	КОНЕЦ)                                     КАК Количество,
	|	ВЫРАЗИТЬ(СУММА(ВЫБОР
	|		КОГДА ТаблицаСерии.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|				КОГДА Шапка.ЦенаВключаетНДС ТОГДА ТаблицаТовары.Сумма
	|				ИНАЧЕ ТаблицаТовары.Сумма + ТаблицаТовары.СуммаНДС
	|			КОНЕЦ
	|		КОГДА ТаблицаТовары.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ
	|			ТаблицаСерии.Количество * ВЫБОР 
	|				КОГДА Шапка.ЦенаВключаетНДС ТОГДА ТаблицаТовары.Сумма
	|				ИНАЧЕ ТаблицаТовары.Сумма + ТаблицаТовары.СуммаНДС
	|			КОНЕЦ / ТаблицаТовары.Количество
	|	КОНЕЦ * ВЫБОР
	|		КОГДА &ВалютаОснования = &ВалютаРеглУчета
	|			ТОГДА 1
	|		ИНАЧЕ
	|			ЕСТЬNULL(КурсВалютыОснования.Курс, 1) * ЕСТЬNULL(КурсВалютыРегл.Кратность, 1)
	|		    / (ЕСТЬNULL(КурсВалютыРегл.Курс, 1) * ЕСТЬNULL(КурсВалютыОснования.Кратность, 1))
	|	КОНЕЦ) КАК ЧИСЛО(15, 2))                   КАК Сумма
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК Шапка
	|		ПО ТаблицаТовары.Ссылка = Шапка.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.Серии КАК ТаблицаСерии
	|		ПО ТаблицаТовары.Ссылка = ТаблицаСерии.Ссылка
	|			И ТаблицаТовары.Номенклатура = ТаблицаСерии.Номенклатура
	|			И ТаблицаТовары.Характеристика = ТаблицаСерии.Характеристика
	|			И &УсловиеПоПомещению
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыОснования
	|		ПО КурсВалютыОснования.Валюта = &ВалютаОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыРегл
	|		ПО КурсВалютыРегл.Валюта = &ВалютаРеглУчета
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И Не СправочникНоменклатура.ВидАлкогольнойПродукции.Маркируемый
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаСерии.Ссылка ЕСТЬ NULL
	|			ТОГДА ТаблицаТовары.Серия
	|		ИНАЧЕ
	|			ТаблицаСерии.Серия
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура       КАК Номенклатура,
	|	ОформленныеТовары.Характеристика     КАК Характеристика,
	|	ОформленныеТовары.Серия              КАК Серия,
	|	-СУММА(ОформленныеТовары.Количество) КАК Количество,
	|	-СУММА(ОформленныеТовары.Сумма)      КАК Сумма
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|СГРУППИРОВАТЬ ПО
	|	ОформленныеТовары.Номенклатура,
	|	ОформленныеТовары.Характеристика,
	|	ОформленныеТовары.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура            КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика          КАК Характеристика,
	|	ТоварыКОформлению.Серия                   КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)       КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)       КАК КоличествоУпаковок,
	|	СУММА(ТоварыКОформлению.Сумма)            КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА СУММА(ТоварыКОформлению.Количество) = 0 ТОГДА 0
	|		ИНАЧЕ СУММА(ТоварыКОформлению.Сумма) / СУММА(ТоварыКОформлению.Количество)
	|	КОНЕЦ КАК ЧИСЛО(15, 2))                   КАК Цена
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|	ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|";
	
	Если СкладыСервер.ИспользоватьСкладскиеПомещения(РеквизитыДокумента.Склад, РеквизитыДокумента.Дата) Тогда
		УсловиеПоПомещению = "ТаблицаТовары.Помещение = ТаблицаСерии.Помещение";
	Иначе
		УсловиеПоПомещению = "ИСТИНА";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоПомещению", УсловиеПоПомещению);
	
	Запрос.УстановитьПараметр("ВалютаРеглУчета",   Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ДатаОснования",     РеквизитыДокумента.Дата);
	Запрос.УстановитьПараметр("ВалютаОснования",   РеквизитыДокумента.Валюта);
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.АктСписанияЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия",       ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса, ДанныеЗаполнения, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьАктСписанияЕГАИСНаОснованииСборкиТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка          КАК ДокументОснование,
	|	НЕ Таблица.Проведен     КАК ЕстьОшибкиПроведен,
	|	Таблица.Склад           КАК Склад,
	|	Таблица.Склад.ТипСклада КАК ТипСклада,
	|	Таблица.Организация     КАК Организация,
	|	Таблица.Ответственный   КАК Ответственный
	|ИЗ
	|	Документ.СборкаТоваров КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыАктСписанияЕГАИС
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусыАктСписанияЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыЧекЕГАИС
	|ИЗ
	|	Документ.ЧекЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусыЧекЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура                            КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                          КАК Характеристика,
	|	ЕСТЬNULL(ТЧСерии.Серия,ТабличнаяЧасть.Серия)           КАК Серия,
	|	ЕСТЬNULL(ТЧСерии.Количество,ТабличнаяЧасть.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.СборкаТоваров КАК ТабличнаяЧасть
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СборкаТоваров.Серии  КАК ТЧСерии
	|		ПО ТабличнаяЧасть.Номенклатура = ТЧСерии.Номенклатура
	|		И ТабличнаяЧасть.Характеристика = ТЧСерии.Характеристика
	|		И ТабличнаяЧасть.Ссылка = ТЧСерии.Ссылка
	|		
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И ТабличнаяЧасть.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыАктСписанияЕГАИС КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество
	|ИЗ
	|	Документ.ЧекЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыЧекЕГАИС КАК Т)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура            КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика          КАК Характеристика,
	|	ТоварыКОформлению.Серия                   КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)       КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)       КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|	ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ЭтаСсылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусыАктСписанияЕГАИС", Документы.АктСписанияЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("КонечныеСтатусыЧекЕГАИС",         Документы.ЧекЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыДокумента = РезультатЗапроса[0].Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
			
			ДокументОбъект.ОрганизацияЕГАИС = ОрганизацияЕГАИСПоОрганизацииИСкладу(РеквизитыДокумента.Организация, РеквизитыДокумента.Склад);
			Если РеквизитыДокумента.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин Тогда
				ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра2;
			Иначе
				ДокументОбъект.ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра1;
			КонецЕсли;
			
			ДокументОбъект.ПричинаСписания = Перечисления.ПричиныСписанийЕГАИС.Реализация;
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса[РезультатЗапроса.Количество() - 1], ДанныеЗаполнения, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ТТНИсходящаяЕГАИС

Процедура ОбработкаЗаполненияТТНИсходящейЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		
		ЗаполнитьТТНИсходящуюЕГАИСНаОснованииВозвратаТоваровПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		ЗаполнитьТТНИсходящуюЕГАИСНаОснованииРеализацииТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		
		ЗаполнитьТТНИсходящуюЕГАИСНаОснованииПеремещенияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
		
		ЗаполнитьТТНИсходящуюЕГАИСНаОснованииВозвратаТоваровМеждуОрганизациями(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
		
		ЗаполнитьТТНИсходящуюЕГАИСНаОснованииПередачиТоваровМеждуОрганизациями(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТТНИсходящуюЕГАИСНаОснованииВозвратаТоваровПоставщику(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументОснование.Ссылка                КАК ДокументОснование,
	|	ДокументОснование.Номер                 КАК НомерТТН,
	|	ДокументОснование.Дата                  КАК ДатаТТН,
	|	ДокументОснование.Дата                  КАК ДатаОтгрузки,
	|	НЕ ДокументОснование.Проведен           КАК ЕстьОшибкиПроведен,
	|	ДокументОснование.Организация           КАК Поставщик,
	|	ДокументОснование.Менеджер              КАК Ответственный,
	|	ДокументОснование.Контрагент            КАК Грузополучатель,
	|	ДокументОснование.Организация           КАК Грузоотправитель,
	|	ДокументОснование.Склад                 КАК Склад,
	|	ДокументОснование.Партнер               КАК Партнер,
	|	ДокументОснование.СпособДоставки        КАК ТипДоставки,
	|	ДокументОснование.Валюта                КАК Валюта
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ДокументОснование
	|ГДЕ
	|	ДокументОснование.Ссылка = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		
		ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийТТНИсходящейЕГАИС.ВозвратПоставщику;
		
		ДокументОбъект.Поставщик        = ОрганизацияЕГАИСПоОрганизацииИСкладу(РеквизитыДокумента.Поставщик, РеквизитыДокумента.Склад);
		ДокументОбъект.Грузоотправитель = ДокументОбъект.Поставщик;
		ДокументОбъект.Грузополучатель  = ОрганизацияЕГАИСПоПартнеруИКонтрагенту(
			РеквизитыДокумента.Партнер,
			РеквизитыДокумента.Грузополучатель);
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ втКурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&ДатаОснования,
	|			Валюта = &ВалютаРеглУчета
	|				ИЛИ Валюта = &ВалютаОснования) КАК КурсыВалютСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	Товары.Серия                                       КАК Серия,
	|	СУММА(Товары.Количество)                           КАК Количество,
	|	СУММА(ВЫРАЗИТЬ(Товары.СуммаСНДС * ВЫБОР
	|					КОГДА &ВалютаОснования <> &ВалютаРеглУчета
	|						ТОГДА ВЫБОР
	|								КОГДА ЕСТЬNULL(КурсВалютыОснования.Кратность, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыОснования.Курс, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыРегл.Кратность, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыРегл.Курс, 0) > 0
	|									ТОГДА КурсВалютыОснования.Курс * КурсВалютыРегл.Кратность / (КурсВалютыРегл.Курс * КурсВалютыОснования.Кратность)
	|								ИНАЧЕ 0
	|							КОНЕЦ
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|			КАК ЧИСЛО(15, 2))) КАК Сумма
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыОснования
	|		ПО (КурсВалютыОснования.Валюта = &ВалютаОснования)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыРегл
	|		ПО (КурсВалютыРегл.Валюта = &ВалютаРеглУчета)
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура                                КАК Номенклатура,
	|	Серии.Характеристика                              КАК Характеристика,
	|	Серии.Серия                                       КАК Серия,
	|	СУММА(Серии.Количество)                           КАК Количество
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Серии КАК Серии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Серии.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Серии.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|СГРУППИРОВАТЬ ПО
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	ЕСТЬNULL(Серии.Серия,Товары.Серия)                 КАК Серия,
	|	ЕСТЬNULL(Серии.Количество, Товары.Количество)      КАК Количество,
	|	ВЫРАЗИТЬ(Товары.Сумма * (ВЫБОР
	|		КОГДА
	|			ЕСТЬNULL(Серии.Количество, 0) <> 0
	|			И Товары.Количество <> 0
	|		ТОГДА
	|			Серии.Количество / Товары.Количество
	|		ИНАЧЕ
	|			1
	|	КОНЕЦ) КАК ЧИСЛО(15, 2)) КАК Сумма
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Серии КАК Серии
	|			ПО Серии.Номенклатура = Товары.Номенклатура
	|			И Серии.Характеристика = Товары.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура          КАК Номенклатура,
	|	ТоварыСерии.Характеристика        КАК Характеристика,
	|	ТоварыСерии.Серия                 КАК Серия,
	|	ТоварыСерии.Количество            КАК Количество,
	|	ТоварыСерии.Сумма                 КАК Сумма
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество,
	|	-Сумма                                  КАК Сумма
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура            КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика          КАК Характеристика,
	|	ТоварыКОформлению.Серия                   КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)       КАК КоличествоУпаковок,
	|	СУММА(ТоварыКОформлению.Количество)       КАК Количество,
	|	СУММА(ТоварыКОформлению.Сумма)            КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА  СУММА(ТоварыКОформлению.Количество) = 0
	|				ТОГДА 0
	|			ИНАЧЕ СУММА(ТоварыКОформлению.Сумма) / СУММА(ТоварыКОформлению.Количество) КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Цена
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ВалютаРеглУчета",   Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ДатаОснования",     РеквизитыДокумента.ДатаТТН);
	Запрос.УстановитьПараметр("ВалютаОснования",   РеквизитыДокумента.Валюта);
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ТТНИсходящаяЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, Запрос.Выполнить(), ДанныеЗаполнения, Истина)
	
КонецПроцедуры

Процедура ЗаполнитьТТНИсходящуюЕГАИСНаОснованииПеремещенияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПеремещениеТоваров.Ссылка                КАК ДокументОснование,
	|	ПеремещениеТоваров.Номер                 КАК НомерТТН,
	|	ПеремещениеТоваров.Дата                  КАК ДатаТТН,
	|	ПеремещениеТоваров.Дата                  КАК ДатаОтгрузки,
	|	НЕ ПеремещениеТоваров.Проведен           КАК ЕстьОшибкиПроведен,
	|	
	|	ПеремещениеТоваров.Организация           КАК Организация,
	|	ПеремещениеТоваров.ОрганизацияПолучатель КАК ОрганизацияПолучатель,
	|	
	|	ПеремещениеТоваров.СкладОтправитель      КАК СкладОтправитель,
	|	ПеремещениеТоваров.СкладПолучатель       КАК СкладПолучатель,
	|	
	|	ПеремещениеТоваров.Ответственный         КАК Ответственный,
	|	ПеремещениеТоваров.СпособДоставки        КАК ТипДоставки,
	|	ПеремещениеТоваров.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ПеремещениеТоваров.ВидЦены               КАК ВидЦены,
	|	ПеремещениеТоваров.ВидЦены.ВалютаЦены    КАК ВалютаЦены
	|ИЗ
	|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	|ГДЕ
	|	ПеремещениеТоваров.Ссылка = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		
		ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийТТНИсходящейЕГАИС.РасходнаяНакладная;
		
		Поставщик = ОрганизацияЕГАИСПоОрганизацииИСкладу(
			РеквизитыДокумента.Организация,
			РеквизитыДокумента.СкладОтправитель);
		
		Если РеквизитыДокумента.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеТоваровМеждуФилиалами Тогда
			
			Если РеквизитыДокумента.Организация = РеквизитыДокумента.ОрганизацияПолучатель Тогда
				
				ТекстОшибки = НСтр("ru='В %1 одинаковые организация и организация-получатель - %2. Ввод на основании невозможен.'");
				ТекстОшибки = СтрШаблон(ТекстОшибки, ДанныеЗаполнения, РеквизитыДокумента.Грузоотправитель);
				
				ВызватьИсключение ТекстОшибки;
				
			КонецЕсли;
			
			ДокументОбъект.Поставщик        = Поставщик;
			ДокументОбъект.Грузоотправитель = Поставщик;
			ДокументОбъект.Грузополучатель  = ОрганизацияЕГАИСПоОрганизацииИСкладу(
				РеквизитыДокумента.ОрганизацияПолучатель,
				РеквизитыДокумента.СкладПолучатель);
			
		ИначеЕсли РеквизитыДокумента.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеТоваров Тогда
			
			ДокументОбъект.Поставщик        = Поставщик;
			ДокументОбъект.Грузоотправитель = Поставщик;
			ДокументОбъект.Грузополучатель  = ОрганизацияЕГАИСПоОрганизацииИСкладу(
				РеквизитыДокумента.Организация,
				РеквизитыДокумента.СкладПолучатель);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ втКурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&ДатаОснования,
	|			Валюта = &ВалютаРеглУчета
	|				ИЛИ Валюта = &ВалютаВидаЦены) КАК КурсыВалютСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТабличнаяЧасть.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТабличнаяЧасть.Характеристика
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И ТаблицаСерии.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|ИЗ
	|	Документ.ПеремещениеТоваров.Серии КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т);
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АлкогольнаяПродукцияДокумента.Номенклатура КАК Номенклатура,
	|	АлкогольнаяПродукцияДокумента.Характеристика КАК Характеристика,
	|	АлкогольнаяПродукцияДокумента.Серия КАК Серия,
	|	СРЕДНЕЕ(ЕстьNULL(ЦеныНоменклатурыСрезПоследних.Цена,0)/ЕстьNULL(&ТекстЗапросаКоэффициентУпаковкиЦеныНоменклатуры,1)
	|	* ВЫБОР
	|		КОГДА &ВалютаРеглУчета <> ЦеныНоменклатурыСрезПоследних.Валюта
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(КурсыВалютыЦены.Кратность, 0) > 0
	|						И ЕСТЬNULL(КурсыВалютыЦены.Курс, 0) > 0
	|						И ЕСТЬNULL(КурсыВалюты.Кратность, 0) > 0
	|						И ЕСТЬNULL(КурсыВалюты.Курс, 0) > 0
	|					ТОГДА 
	|						(КурсыВалютыЦены.Курс * КурсыВалюты.Кратность)
	|						/ (КурсыВалюты.Курс * КурсыВалютыЦены.Кратность)
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 1
	|	КОНЕЦ) КАК Цена
	|ПОМЕСТИТЬ ТоварыКОформлениюЦены
	|ИЗ
	|	ТоварыКОформлению КАК АлкогольнаяПродукцияДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|				&ДатаОснования,
	|				ВидЦены = &ВидЦены
	|					И (Номенклатура, Характеристика) В
	|						(ВЫБРАТЬ
	|							АлкогольнаяПродукцияДокумента.Номенклатура,
	|							АлкогольнаяПродукцияДокумента.Характеристика
	|						ИЗ
	|							ТоварыКОформлению КАК АлкогольнаяПродукцияДокумента)) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО АлкогольнаяПродукцияДокумента.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|			И АлкогольнаяПродукцияДокумента.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсыВалютыЦены
	|		ПО (КурсыВалютыЦены.Валюта = &ВалютаВидаЦены)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсыВалюты
	|		ПО (КурсыВалюты.Валюта = &ВалютаРеглУчета)
	|СГРУППИРОВАТЬ ПО
	|	АлкогольнаяПродукцияДокумента.Номенклатура,
	|	АлкогольнаяПродукцияДокумента.Характеристика,
	|	АлкогольнаяПродукцияДокумента.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура         КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика       КАК Характеристика,
	|	ТоварыКОформлению.Серия                КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ЕСТЬNULL(ТоварыКОформлениюЦеныФинСерии.Цена,ТоварыКОформлениюЦены.Цена) КАК Цена,
	|	СУММА(ТоварыКОформлению.Количество)    КАК КоличествоУпаковок,
	|	СУММА(ТоварыКОформлению.Количество)    КАК Количество,
	|	СУММА(ЕСТЬNULL(ТоварыКОформлениюЦеныФинСерии.Цена,ЕСТЬNULL(ТоварыКОформлениюЦены.Цена,0))*ТоварыКОформлению.Количество) КАК Сумма
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыКОформлениюЦены КАК ТоварыКОформлениюЦеныФинСерии
	|		ПО ТоварыКОформлениюЦеныФинСерии.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И ТоварыКОформлениюЦеныФинСерии.Характеристика = ТоварыКОформлению.Характеристика
	|			И ТоварыКОформлениюЦеныФинСерии.Серия = ТоварыКОформлению.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыКОформлениюЦены КАК ТоварыКОформлениюЦены
	|		ПО ТоварыКОформлениюЦены.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И ТоварыКОформлениюЦены.Характеристика = ТоварыКОформлению.Характеристика
	|			И ТоварыКОформлениюЦены.Серия = &ПустаяСерия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция,
	|	ЕСТЬNULL(ТоварыКОформлениюЦеныФинСерии.Цена,ТоварыКОформлениюЦены.Цена)
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ЭтаСсылка",       ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("ДатаОснования",   РеквизитыДокумента.ДатаТТН);
	Запрос.УстановитьПараметр("ВидЦены",         РеквизитыДокумента.ВидЦены);
	Запрос.УстановитьПараметр("ВалютаРеглУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаВидаЦены",  РеквизитыДокумента.ВалютаЦены);
	Запрос.УстановитьПараметр("КонечныеСтатусы", Документы.ТТНИсходящаяЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ТекстЗапросаКоэффициентУпаковкиЦеныНоменклатуры",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ЦеныНоменклатурыСрезПоследних.Упаковка",
			"ЦеныНоменклатурыСрезПоследних.Номенклатура"));
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, Запрос.Выполнить(), ДанныеЗаполнения, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьТТНИсходящуюЕГАИСНаОснованииВозвратаТоваровМеждуОрганизациями(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументОснование.Ссылка                КАК ДокументОснование,
	|	ДокументОснование.Номер                 КАК НомерТТН,
	|	ДокументОснование.Дата                  КАК ДатаТТН,
	|	ДокументОснование.Дата                  КАК ДатаОтгрузки,
	|	НЕ ДокументОснование.Проведен           КАК ЕстьОшибкиПроведен,
	|	ДокументОснование.Организация           КАК Поставщик,
	|	ДокументОснование.Менеджер              КАК Ответственный,
	|	ДокументОснование.ОрганизацияПолучатель КАК Грузополучатель,
	|	ДокументОснование.Организация           КАК Грузоотправитель,
	|	ДокументОснование.Склад                 КАК Склад,
	|	ДокументОснование.Валюта                КАК Валюта
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями КАК ДокументОснование
	|ГДЕ
	|	ДокументОснование.Ссылка = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		
		ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийТТНИсходящейЕГАИС.ВозвратПоставщику;
		
		ДокументОбъект.Поставщик        = ОрганизацияЕГАИСПоОрганизацииИСкладу(РеквизитыДокумента.Поставщик, РеквизитыДокумента.Склад);
		ДокументОбъект.Грузоотправитель = ДокументОбъект.Поставщик;
		ДокументОбъект.Грузополучатель  = ОрганизацияЕГАИСПоОрганизацииИСкладу(РеквизитыДокумента.Грузополучатель, РеквизитыДокумента.Склад);
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ втКурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&ДатаОснования,
	|			Валюта = &ВалютаРеглУчета
	|				ИЛИ Валюта = &ВалютаОснования) КАК КурсыВалютСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	Товары.Серия                                       КАК Серия,
	|	СУММА(Товары.Количество)                           КАК Количество,
	|	СУММА(ВЫРАЗИТЬ(Товары.СуммаСНДС * ВЫБОР
	|					КОГДА &ВалютаОснования <> &ВалютаРеглУчета
	|						ТОГДА ВЫБОР
	|								КОГДА ЕСТЬNULL(КурсВалютыОснования.Кратность, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыОснования.Курс, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыРегл.Кратность, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыРегл.Курс, 0) > 0
	|									ТОГДА КурсВалютыОснования.Курс * КурсВалютыРегл.Кратность / (КурсВалютыРегл.Курс * КурсВалютыОснования.Кратность)
	|								ИНАЧЕ 0
	|							КОНЕЦ
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|			КАК ЧИСЛО(15, 2))) КАК Сумма
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыОснования
	|		ПО (КурсВалютыОснования.Валюта = &ВалютаОснования)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыРегл
	|		ПО (КурсВалютыРегл.Валюта = &ВалютаРеглУчета)
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура          КАК Номенклатура,
	|	ТоварыСерии.Характеристика        КАК Характеристика,
	|	ТоварыСерии.Серия                 КАК Серия,
	|	ТоварыСерии.Количество            КАК Количество,
	|	ТоварыСерии.Сумма                 КАК Сумма
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество,
	|	-Сумма                                  КАК Сумма
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура            КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика          КАК Характеристика,
	|	ТоварыКОформлению.Серия                   КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)       КАК КоличествоУпаковок,
	|	СУММА(ТоварыКОформлению.Количество)       КАК Количество,
	|	СУММА(ТоварыКОформлению.Сумма)            КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА  СУММА(ТоварыКОформлению.Количество) = 0
	|				ТОГДА 0
	|			ИНАЧЕ СУММА(ТоварыКОформлению.Сумма) / СУММА(ТоварыКОформлению.Количество) КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Цена
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ВалютаРеглУчета",   Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ДатаОснования",     РеквизитыДокумента.ДатаТТН);
	Запрос.УстановитьПараметр("ВалютаОснования",   РеквизитыДокумента.Валюта);
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ТТНИсходящаяЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, Запрос.Выполнить(), ДанныеЗаполнения, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьТТНИсходящуюЕГАИСНаОснованииПередачиТоваровМеждуОрганизациями(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументОснование.Ссылка                КАК ДокументОснование,
	|	ДокументОснование.Номер                 КАК НомерТТН,
	|	ДокументОснование.Дата                  КАК ДатаТТН,
	|	ДокументОснование.Дата                  КАК ДатаОтгрузки,
	|	НЕ ДокументОснование.Проведен           КАК ЕстьОшибкиПроведен,
	|	ДокументОснование.Организация           КАК Поставщик,
	|	ДокументОснование.Менеджер              КАК Ответственный,
	|	ДокументОснование.ОрганизацияПолучатель КАК Грузополучатель,
	|	ДокументОснование.Организация           КАК Грузоотправитель,
	|	ДокументОснование.Склад                 КАК Склад,
	|	ДокументОснование.Валюта                КАК Валюта
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями КАК ДокументОснование
	|ГДЕ
	|	ДокументОснование.Ссылка = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		
		ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийТТНИсходящейЕГАИС.РасходнаяНакладная;
		
		ДокументОбъект.Поставщик        = ОрганизацияЕГАИСПоОрганизацииИСкладу(РеквизитыДокумента.Поставщик, РеквизитыДокумента.Склад);
		ДокументОбъект.Грузоотправитель = ДокументОбъект.Поставщик;
		ДокументОбъект.Грузополучатель  = ОрганизацияЕГАИСПоОрганизацииИСкладу(РеквизитыДокумента.Грузополучатель, РеквизитыДокумента.Склад);
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ втКурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&ДатаОснования,
	|			Валюта = &ВалютаРеглУчета
	|				ИЛИ Валюта = &ВалютаОснования) КАК КурсыВалютСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	Товары.Серия                                       КАК Серия,
	|	СУММА(Товары.Количество)                           КАК Количество,
	|	СУММА(ВЫРАЗИТЬ(Товары.СуммаСНДС * ВЫБОР
	|					КОГДА &ВалютаОснования <> &ВалютаРеглУчета
	|						ТОГДА ВЫБОР
	|								КОГДА ЕСТЬNULL(КурсВалютыОснования.Кратность, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыОснования.Курс, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыРегл.Кратность, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыРегл.Курс, 0) > 0
	|									ТОГДА КурсВалютыОснования.Курс * КурсВалютыРегл.Кратность / (КурсВалютыРегл.Курс * КурсВалютыОснования.Кратность)
	|								ИНАЧЕ 0
	|							КОНЕЦ
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|			КАК ЧИСЛО(15, 2))) КАК Сумма
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыОснования
	|		ПО (КурсВалютыОснования.Валюта = &ВалютаОснования)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыРегл
	|		ПО (КурсВалютыРегл.Валюта = &ВалютаРеглУчета)
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура          КАК Номенклатура,
	|	ТоварыСерии.Характеристика        КАК Характеристика,
	|	ТоварыСерии.Серия                 КАК Серия,
	|	ТоварыСерии.Количество            КАК Количество,
	|	ТоварыСерии.Сумма                 КАК Сумма
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество,
	|	-Сумма                                  КАК Сумма
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура            КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика          КАК Характеристика,
	|	ТоварыКОформлению.Серия                   КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)       КАК КоличествоУпаковок,
	|	СУММА(ТоварыКОформлению.Количество)       КАК Количество,
	|	СУММА(ТоварыКОформлению.Сумма)            КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА  СУММА(ТоварыКОформлению.Количество) = 0
	|				ТОГДА 0
	|			ИНАЧЕ СУММА(ТоварыКОформлению.Сумма) / СУММА(ТоварыКОформлению.Количество) КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Цена
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ВалютаРеглУчета",   Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ДатаОснования",     РеквизитыДокумента.ДатаТТН);
	Запрос.УстановитьПараметр("ВалютаОснования",   РеквизитыДокумента.Валюта);
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ТТНИсходящаяЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, Запрос.Выполнить(), ДанныеЗаполнения, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьТТНИсходящуюЕГАИСНаОснованииРеализацииТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДокументОснование.Ссылка                КАК ДокументОснование,
	|	ДокументОснование.Номер                 КАК НомерТТН,
	|	ДокументОснование.Дата                  КАК ДатаТТН,
	|	ДокументОснование.Дата                  КАК ДатаОтгрузки,
	|	НЕ ДокументОснование.Проведен           КАК ЕстьОшибкиПроведен,
	|	ДокументОснование.Организация           КАК Поставщик,
	|	ДокументОснование.Менеджер              КАК Ответственный,
	|	ДокументОснование.Контрагент            КАК Грузополучатель,
	|	ДокументОснование.Организация           КАК Грузоотправитель,
	|	ДокументОснование.Склад                 КАК Склад,
	|	ДокументОснование.Партнер               КАК Партнер,
	|	ДокументОснование.СпособДоставки        КАК ТипДоставки,
	|	ДокументОснование.Валюта                КАК Валюта
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДокументОснование
	|ГДЕ
	|	ДокументОснование.Ссылка = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		
		ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийТТНИсходящейЕГАИС.РасходнаяНакладная;
		
		ДокументОбъект.Поставщик        = ОрганизацияЕГАИСПоОрганизацииИСкладу(РеквизитыДокумента.Поставщик, РеквизитыДокумента.Склад);
		ДокументОбъект.Грузоотправитель = ДокументОбъект.Поставщик;
		ДокументОбъект.Грузополучатель  = ОрганизацияЕГАИСПоПартнеруИКонтрагенту(
			РеквизитыДокумента.Партнер,
			РеквизитыДокумента.Грузополучатель);
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ втКурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&ДатаОснования,
	|			Валюта = &ВалютаРеглУчета
	|				ИЛИ Валюта = &ВалютаОснования) КАК КурсыВалютСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	Товары.Серия                                       КАК Серия,
	|	СУММА(Товары.Количество)                           КАК Количество,
	|	СУММА(ВЫРАЗИТЬ(Товары.СуммаСНДС * ВЫБОР
	|					КОГДА &ВалютаОснования <> &ВалютаРеглУчета
	|						ТОГДА ВЫБОР
	|								КОГДА ЕСТЬNULL(КурсВалютыОснования.Кратность, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыОснования.Курс, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыРегл.Кратность, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыРегл.Курс, 0) > 0
	|									ТОГДА КурсВалютыОснования.Курс * КурсВалютыРегл.Кратность / (КурсВалютыРегл.Курс * КурсВалютыОснования.Кратность)
	|								ИНАЧЕ 0
	|							КОНЕЦ
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|			КАК ЧИСЛО(15, 2))) КАК Сумма
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыОснования
	|		ПО (КурсВалютыОснования.Валюта = &ВалютаОснования)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыРегл
	|		ПО (КурсВалютыРегл.Валюта = &ВалютаРеглУчета)
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура                                КАК Номенклатура,
	|	Серии.Характеристика                              КАК Характеристика,
	|	Серии.Серия                                       КАК Серия,
	|	СУММА(Серии.Количество)                           КАК Количество
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Серии КАК Серии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Серии.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Серии.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|СГРУППИРОВАТЬ ПО
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	ЕСТЬNULL(Серии.Серия,Товары.Серия)                 КАК Серия,
	|	ЕСТЬNULL(Серии.Количество, Товары.Количество)      КАК Количество,
	|	ВЫРАЗИТЬ(Товары.Сумма * (ВЫБОР
	|		КОГДА
	|			ЕСТЬNULL(Серии.Количество, 0) <> 0
	|			И Товары.Количество <> 0
	|		ТОГДА
	|			Серии.Количество / Товары.Количество
	|		ИНАЧЕ
	|			1
	|	КОНЕЦ) КАК ЧИСЛО(15, 2)) КАК Сумма
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Серии КАК Серии
	|			ПО Серии.Номенклатура = Товары.Номенклатура
	|			И Серии.Характеристика = Товары.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура          КАК Номенклатура,
	|	ТоварыСерии.Характеристика        КАК Характеристика,
	|	ТоварыСерии.Серия                 КАК Серия,
	|	ТоварыСерии.Количество            КАК Количество,
	|	ТоварыСерии.Сумма                 КАК Сумма
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество,
	|	-Сумма                                  КАК Сумма
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура            КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика          КАК Характеристика,
	|	ТоварыКОформлению.Серия                   КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)       КАК КоличествоУпаковок,
	|	СУММА(ТоварыКОформлению.Количество)       КАК Количество,
	|	СУММА(ТоварыКОформлению.Сумма)            КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА  СУММА(ТоварыКОформлению.Количество) = 0
	|				ТОГДА 0
	|			ИНАЧЕ СУММА(ТоварыКОформлению.Сумма) / СУММА(ТоварыКОформлению.Количество) КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Цена
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ВалютаРеглУчета",   Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ДатаОснования",     РеквизитыДокумента.ДатаТТН);
	Запрос.УстановитьПараметр("ВалютаОснования",   РеквизитыДокумента.Валюта);
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ТТНИсходящаяЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, Запрос.Выполнить(), ДанныеЗаполнения, Истина)
	
КонецПроцедуры

#КонецОбласти

#Область ВозвратИзРегистра2ЕГАИС

Процедура ОбработкаЗаполненияВозвратаИзРегистра2ЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ТТНИсходящаяЕГАИС") Тогда
		
		ЗаполнитьВозвратИзРегистра2ЕГАИСНаОснованииТТНИсходящейЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьВозвратИзРегистра2ЕГАИСНаОснованииТТНИсходящейЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка           КАК ДокументОснование,
	|	Ложь                     КАК ЕстьОшибкиПроведен,
	|	Таблица.Грузоотправитель КАК ОрганизацияЕГАИС,
	|	Таблица.Ответственный    КАК Ответственный
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ВозвратИзРегистра2ЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	ТабличнаяЧасть.Количество                                  КАК Количество,
	|	ТабличнаяЧасть.Количество                                  КАК КоличествоУпаковок,
	|	ТабличнаяЧасть.АлкогольнаяПродукция                        КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2                                    КАК Справка2
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Справка2 = ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)
	|	И ТабличнаяЧасть.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	ОформленныеТовары.Упаковка              КАК Упаковка,
	|	-ОформленныеТовары.Количество           КАК Количество,
	|	-ОформленныеТовары.КоличествоУпаковок   КАК КоличествоУпаковок,
	|	ОформленныеТовары.АлкогольнаяПродукция  КАК АлкогольнаяПродукция,
	|	ОформленныеТовары.Справка2              КАК Справка2
	|ИЗ
	|	Документ.ВозвратИзРегистра2ЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура              КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика            КАК Характеристика,
	|	ТоварыКОформлению.Серия                     КАК Серия,
	|	ТоварыКОформлению.Упаковка                  КАК Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция      КАК АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2                  КАК Справка2,
	|	СУММА(ТоварыКОформлению.Количество)         КАК Количество,
	|	СУММА(ТоварыКОформлению.КоличествоУпаковок) КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	ТоварыКОформлению.Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ВозвратИзРегистра2ЕГАИС.КонечныеСтатусы());
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыДокумента = РезультатЗапроса[0].Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса[РезультатЗапроса.Количество() - 1], ДанныеЗаполнения, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ПередачаВРегистр2ЕГАИС

Процедура ОбработкаЗаполненияПередачиВРегистр2ЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
		
		ЗаполнитьПередачуВРегистр2ЕГАИСНаОснованииТТНВходящейЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПередачуВРегистр2ЕГАИСНаОснованииТТНВходящейЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК ДокументОснование,
	|	НЕ Таблица.Проведен КАК ЕстьОшибкиПроведен,
	|	Таблица.Грузополучатель КАК ОрганизацияЕГАИС,
	|	Таблица.Ответственный КАК Ответственный
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ПередачаВРегистр2ЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО (СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка)
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И НЕ СтатусыДокументовЕГАИС.Статус В (&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.АлкогольнаяПродукция.ТипПродукции = ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ОбъемДАЛ, 1) <> 0
	|						ТОГДА ВЫБОР
	|								КОГДА ТабличнаяЧасть.Ссылка.ЕстьРасхождения
	|									ТОГДА ТабличнаяЧасть.КоличествоФакт / ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ОбъемДАЛ, 1)
	|								ИНАЧЕ ТабличнаяЧасть.Количество / ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ОбъемДАЛ, 1)
	|							КОНЕЦ
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТабличнаяЧасть.Ссылка.ЕстьРасхождения
	|					ТОГДА ТабличнаяЧасть.КоличествоФакт
	|				ИНАЧЕ ТабличнаяЧасть.Количество
	|			КОНЕЦ
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.АлкогольнаяПродукция.ТипПродукции = ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ОбъемДАЛ, 1) <> 0
	|						ТОГДА ВЫБОР
	|								КОГДА ТабличнаяЧасть.Ссылка.ЕстьРасхождения
	|									ТОГДА ТабличнаяЧасть.КоличествоФакт / ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ОбъемДАЛ, 1)
	|								ИНАЧЕ ТабличнаяЧасть.Количество / ЕСТЬNULL(ТабличнаяЧасть.Номенклатура.ОбъемДАЛ, 1)
	|							КОНЕЦ
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТабличнаяЧасть.Ссылка.ЕстьРасхождения
	|					ТОГДА ТабличнаяЧасть.КоличествоФакт
	|				ИНАЧЕ ТабличнаяЧасть.Количество
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Справка2 КАК Справка2
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Справка2 <> ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)
	|	И ТабличнаяЧасть.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура,
	|	ОформленныеТовары.Характеристика,
	|	ОформленныеТовары.Серия,
	|	ОформленныеТовары.Упаковка,
	|	-ОформленныеТовары.Количество,
	|	-ОформленныеТовары.КоличествоУпаковок,
	|	ОформленныеТовары.АлкогольнаяПродукция,
	|	ОформленныеТовары.Справка2
	|ИЗ
	|	Документ.ПередачаВРегистр2ЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				Т.Ссылка
	|			ИЗ
	|				ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия КАК Серия,
	|	ТоварыКОформлению.Упаковка КАК Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2 КАК Справка2,
	|	СУММА(ТоварыКОформлению.Количество) КАК Количество,
	|	СУММА(ТоварыКОформлению.КоличествоУпаковок) КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ВтОстатки
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	ТоварыКОформлению.Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АкцизныеМарки.Справка2 КАК Справка2,
	|	КОЛИЧЕСТВО(АкцизныеМарки.АкцизнаяМарка) КАК Количество
	|ПОМЕСТИТЬ АкцизныеМарки
	|ИЗ
	|	РегистрСведений.АкцизныеМаркиЕГАИС КАК АкцизныеМарки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОстатки КАК втОстатки
	|		ПО (втОстатки.Справка2 = АкцизныеМарки.Справка2)
	|ГДЕ
	|	АкцизныеМарки.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.ВНаличии), ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.КПостановкеНаБаланс))
	|
	|СГРУППИРОВАТЬ ПО
	|	АкцизныеМарки.Справка2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	ТоварыКОформлению.Серия КАК Серия,
	|	ТоварыКОформлению.Упаковка КАК Упаковка,
	|	ТоварыКОформлению.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТоварыКОформлению.Справка2 КАК Справка2,
	|	ТоварыКОформлению.Количество - ЕСТЬNULL(АкцизныеМарки.Количество, 0) КАК Количество,
	|	ТоварыКОформлению.КоличествоУпаковок - ЕСТЬNULL(АкцизныеМарки.Количество, 0) КАК КоличествоУпаковок
	|ИЗ
	|	ВтОстатки КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ АкцизныеМарки КАК АкцизныеМарки
	|		ПО ТоварыКОформлению.Справка2 = АкцизныеМарки.Справка2
	|ГДЕ
	|	ТоварыКОформлению.Количество - ЕСТЬNULL(АкцизныеМарки.Количество, 0) > 0");
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ПередачаВРегистр2ЕГАИС.КонечныеСтатусы());
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыДокумента = РезультатЗапроса[0].Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса[РезультатЗапроса.Количество() - 1], ДанныеЗаполнения, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ЧекЕГАИС

Процедура ОбработкаЗаполненияЧекаЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		ЗаполнитьЧекЕГАИСНаОснованииРеализацииТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СборкаТоваров") Тогда
		
		ЗаполнитьЧекЕГАИСНаОснованииСборкиТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВнутреннееПотреблениеТоваров") Тогда
		
		ЗаполнитьЧекЕГАИСНаОснованииВнутреннегоПотребленияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьЧекЕГАИСНаОснованииРеализацииТоваровУслуг(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка      КАК ДокументОснование,
	|	НЕ Таблица.Проведен КАК ЕстьОшибкиПроведен,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Склад       КАК Склад,
	|	Таблица.Менеджер    КАК Ответственный,
	|	Таблица.Валюта      КАК Валюта,
	|	Таблица.Дата        КАК Дата,             // Дата товарной накладной, на основании которой осуществлена розничная продажа алкогольной продукции по безналичному расчету.
	|	Таблица.Номер       КАК НомерСмены,       // Номер товарной накладной, на основании которой осуществлена розничная продажа алкогольной продукции по безналичному расчету.
	|	Таблица.Номер       КАК НомерЧекаККМ,     // Номер товарной накладной, на основании которой осуществлена розничная продажа алкогольной продукции по безналичному расчету.
	|	""""                КАК СерийныйНомерККМ  // Номер счета на безналичную оплату алкогольной продукции.
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
			
			ДокументОбъект.ВидОперации  = Перечисления.ВидыОперацийЧекаЕГАИС.РеализацияЮридическомуЛицуСБезналичнойОплатой;
			ДокументОбъект.НомерСмены   = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(РеквизитыДокумента.НомерСмены);
			ДокументОбъект.НомерЧекаККМ = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(РеквизитыДокумента.НомерЧекаККМ);
			
			ДокументОбъект.ОрганизацияЕГАИС = ОрганизацияЕГАИСПоОрганизацииИСкладу(
				РеквизитыДокумента.Организация,
				РеквизитыДокумента.Склад);
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ втКурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&ДатаОснования,
	|			Валюта = &ВалютаРеглУчета
	|				ИЛИ Валюта = &ВалютаОснования) КАК КурсыВалютСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	Товары.Серия                                       КАК Серия,
	|	СУММА(Товары.Количество)                           КАК Количество,
	|	СУММА(ВЫРАЗИТЬ(Товары.СуммаСНДС * ВЫБОР
	|					КОГДА &ВалютаОснования <> &ВалютаРеглУчета
	|						ТОГДА ВЫБОР
	|								КОГДА ЕСТЬNULL(КурсВалютыОснования.Кратность, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыОснования.Курс, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыРегл.Кратность, 0) > 0
	|										И ЕСТЬNULL(КурсВалютыРегл.Курс, 0) > 0
	|									ТОГДА КурсВалютыОснования.Курс * КурсВалютыРегл.Кратность / (КурсВалютыРегл.Курс * КурсВалютыОснования.Кратность)
	|								ИНАЧЕ 0
	|							КОНЕЦ
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|			КАК ЧИСЛО(15, 2))) КАК Сумма
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыОснования
	|		ПО (КурсВалютыОснования.Валюта = &ВалютаОснования)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыРегл
	|		ПО (КурсВалютыРегл.Валюта = &ВалютаРеглУчета)
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура                                КАК Номенклатура,
	|	Серии.Характеристика                              КАК Характеристика,
	|	Серии.Серия                                       КАК Серия,
	|	СУММА(Серии.Количество)                           КАК Количество
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Серии КАК Серии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Серии.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	Серии.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|СГРУППИРОВАТЬ ПО
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура                                КАК Номенклатура,
	|	Товары.Характеристика                              КАК Характеристика,
	|	ЕСТЬNULL(Серии.Серия,Товары.Серия)                 КАК Серия,
	|	ЕСТЬNULL(Серии.Количество, Товары.Количество)      КАК Количество,
	|	ВЫРАЗИТЬ(Товары.Сумма * (ВЫБОР
	|		КОГДА
	|			ЕСТЬNULL(Серии.Количество, 0) <> 0
	|			И Товары.Количество <> 0
	|		ТОГДА
	|			Серии.Количество / Товары.Количество
	|		ИНАЧЕ
	|			1
	|	КОНЕЦ) КАК ЧИСЛО(15, 2)) КАК Сумма
	|ПОМЕСТИТЬ ТоварыСерии
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Серии КАК Серии
	|			ПО Серии.Номенклатура = Товары.Номенклатура
	|			И Серии.Характеристика = Товары.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСерии.Номенклатура          КАК Номенклатура,
	|	ТоварыСерии.Характеристика        КАК Характеристика,
	|	ТоварыСерии.Серия                 КАК Серия,
	|	ТоварыСерии.Количество            КАК Количество,
	|	ТоварыСерии.Сумма                 КАК Сумма
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	ТоварыСерии КАК ТоварыСерии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество,
	|	-Сумма                                  КАК Сумма
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура            КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика          КАК Характеристика,
	|	ТоварыКОформлению.Серия                   КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)       КАК КоличествоУпаковок,
	|	СУММА(ТоварыКОформлению.Количество)       КАК Количество,
	|	СУММА(ТоварыКОформлению.Сумма)            КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА  СУММА(ТоварыКОформлению.Количество) = 0
	|				ТОГДА 0
	|			ИНАЧЕ СУММА(ТоварыКОформлению.Сумма) / СУММА(ТоварыКОформлению.Количество) КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Цена
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ЧекЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ВалютаРеглУчета",   Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ДатаОснования",     РеквизитыДокумента.Дата);
	Запрос.УстановитьПараметр("ВалютаОснования",   РеквизитыДокумента.Валюта);
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, Запрос.Выполнить(), ДанныеЗаполнения, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьЧекЕГАИСНаОснованииСборкиТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка        КАК ДокументОснование,
	|	НЕ Таблица.Проведен   КАК ЕстьОшибкиПроведен,
	|	Таблица.Организация   КАК Организация,
	|	Таблица.Склад         КАК Склад,
	|	Таблица.Ответственный КАК Ответственный,
	|	Таблица.Дата          КАК Дата,             // Дата документа, на основании которого осуществлено вскрытие тары.
	|	Таблица.Номер         КАК НомерСмены,       // Номер документа, на основании которого осуществлено вскрытие тары.
	|	Таблица.Номер         КАК НомерЧекаККМ,     // Номер документа, на основании которого осуществлено вскрытие тары.
	|	""""                  КАК СерийныйНомерККМ
	|ИЗ
	|	Документ.СборкаТоваров КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыЧекЕГАИС
	|ИЗ
	|	Документ.ЧекЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусыЧекЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыАктСписанияЕГАИС
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусыАктСписанияЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.СборкаТоваров КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ВидАлкогольнойПродукции.Маркируемый
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И ТабличнаяЧасть.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазборкаТоваров)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество
	|ИЗ
	|	Документ.ЧекЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыЧекЕГАИС КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыАктСписанияЕГАИС КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура            КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика          КАК Характеристика,
	|	ТоварыКОформлению.Серия                   КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)       КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)       КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ЭтаСсылка",                       ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование",               ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусыЧекЕГАИС",         Документы.ЧекЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("КонечныеСтатусыАктСписанияЕГАИС", Документы.АктСписанияЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыДокумента = РезультатЗапроса[0].Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
			
			ДокументОбъект.ВидОперации  = Перечисления.ВидыОперацийЧекаЕГАИС.ВскрытиеТары;
			ДокументОбъект.НомерСмены   = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(РеквизитыДокумента.НомерСмены);
			ДокументОбъект.НомерЧекаККМ = "";
			
			ДокументОбъект.ОрганизацияЕГАИС = ОрганизацияЕГАИСПоОрганизацииИСкладу(
				РеквизитыДокумента.Организация,
				РеквизитыДокумента.Склад);
		
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса[РезультатЗапроса.Количество() - 1], ДанныеЗаполнения, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьЧекЕГАИСНаОснованииВнутреннегоПотребленияТоваров(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка        КАК ДокументОснование,
	|	НЕ Таблица.Проведен   КАК ЕстьОшибкиПроведен,
	|	Таблица.Склад         КАК Склад,
	|	Таблица.Организация   КАК Организация,
	|	Таблица.Ответственный КАК Ответственный,
	|	Таблица.Дата          КАК Дата,             // Дата документа, на основании которого осуществлено вскрытие тары.
	|	Таблица.Номер         КАК НомерСмены,       // Номер документа, на основании которого осуществлено вскрытие тары.
	|	Таблица.Номер         КАК НомерЧекаККМ,     // Номер документа, на основании которого осуществлено вскрытие тары.
	|	""""                  КАК СерийныйНомерККМ
	|ИЗ
	|	Документ.ВнутреннееПотреблениеТоваров КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыАктСписанияЕГАИС
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусыАктСписанияЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументыЧекЕГАИС
	|ИЗ
	|	Документ.ЧекЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусыЧекЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ВнутреннееПотреблениеТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотреблениеТоваров.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТабличнаяЧасть.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТабличнаяЧасть.Характеристика	
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И ТаблицаСерии.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура                                КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                              КАК Характеристика,
	|	ТабличнаяЧасть.Серия                                       КАК Серия,
	|	ТабличнаяЧасть.Количество                                  КАК Количество
	|ИЗ
	|	Документ.ВнутреннееПотреблениеТоваров.Серии КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыАктСписанияЕГАИС КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура          КАК Номенклатура,
	|	ОформленныеТовары.Характеристика        КАК Характеристика,
	|	ОформленныеТовары.Серия                 КАК Серия,
	|	-ОформленныеТовары.Количество           КАК Количество
	|ИЗ
	|	Документ.ЧекЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументыЧекЕГАИС КАК Т)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТабличнаяЧасть.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТабличнаяЧасть.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Номенклатура,
	|	ТабличнаяЧасть.Характеристика,
	|	ТабличнаяЧасть.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура            КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика          КАК Характеристика,
	|	ТоварыКОформлению.Серия                   КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)       КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)       КАК КоличествоУпаковок
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|");
	
	Запрос.УстановитьПараметр("ЭтаСсылка",                       ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование",               ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусыАктСписанияЕГАИС", Документы.АктСписанияЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("КонечныеСтатусыЧекЕГАИС",         Документы.ЧекЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыДокумента = РезультатЗапроса[0].Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
			
			ДокументОбъект.ВидОперации  = Перечисления.ВидыОперацийЧекаЕГАИС.ВскрытиеТары;
			ДокументОбъект.НомерСмены   = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(РеквизитыДокумента.НомерСмены);
			ДокументОбъект.НомерЧекаККМ = "";
			
			ДокументОбъект.ОрганизацияЕГАИС = ОрганизацияЕГАИСПоОрганизацииИСкладу(
				РеквизитыДокумента.Организация,
				РеквизитыДокумента.Склад);
			
	КонецЕсли;
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса[РезультатЗапроса.Количество() - 1], ДанныеЗаполнения, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ЧекЕГАИСВозврат

Процедура ОбработкаЗаполненияЧекаЕГАИСВозврат(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
		
		ЗаполнитьЧекЕГАИСВозвратНаОснованииВозвратаТоваровОтКлиента(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьЧекЕГАИСВозвратНаОснованииВозвратаТоваровОтКлиента(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.Ссылка                   КАК ДокументОснование,
	|	НЕ Таблица.Проведен              КАК ЕстьОшибкиПроведен,
	|	Таблица.Организация              КАК Организация,
	|	Таблица.Склад                    КАК Склад,
	|	Таблица.Валюта                   КАК Валюта,
	|	Таблица.Менеджер                 КАК Ответственный,
	|	Таблица.ДокументРеализации.Дата  КАК Дата,             // Дата товарной накладной, на основании которой осуществлена розничная продажа алкогольной продукции по безналичному расчету
	|	Таблица.ДокументРеализации.Номер КАК НомерСмены,       // Номер товарной накладной, на основании которой осуществлена розничная продажа алкогольной продукции по безналичному расчету
	|	Таблица.ДокументРеализации.Номер КАК НомерЧекаККМ,
	|	""""                             КАК СерийныйНомерККМ  // Номер счета на безналичную оплату алкогольной продукции
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ДокументОснование
	|");
	
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	РезультатЗапроса = Запрос.Выполнить();
	
	РеквизитыДокумента = РезультатЗапроса.Выбрать();
	РеквизитыДокумента.Следующий();
	
	ЭтоПерезаполнение = ЗначениеЗаполнено(ДокументОбъект.Ссылка);
	
	Если Не ЭтоПерезаполнение Тогда
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыДокумента.ДокументОснование,
			,
			РеквизитыДокумента.ЕстьОшибкиПроведен,,);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыДокумента);
			
			ДокументОбъект.НомерСмены = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(РеквизитыДокумента.НомерСмены);
			ДокументОбъект.НомерЧекаККМ = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(РеквизитыДокумента.НомерЧекаККМ);
			
			ДокументОбъект.ОрганизацияЕГАИС = ОрганизацияЕГАИСПоОрганизацииИСкладу(
				РеквизитыДокумента.Организация,
				РеквизитыДокумента.Склад);
		
	КонецЕсли;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ втКурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОснования,
	|		Валюта = &ВалютаРеглУчета ИЛИ Валюта = &ВалютаОснования) КАК КурсыВалютСрезПоследних
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.ЧекЕГАИСВозврат КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И СтатусыДокументовЕГАИС.Статус Не В(&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура                 КАК Номенклатура,
	|	ТаблицаТовары.Характеристика               КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаСерии.Ссылка ЕСТЬ NULL
	|			ТОГДА ТаблицаТовары.Серия
	|		ИНАЧЕ
	|			ТаблицаСерии.Серия
	|	КОНЕЦ                                      КАК Серия,
	|	СУММА(ВЫБОР
	|		КОГДА ТаблицаСерии.Количество ЕСТЬ NULL
	|			ТОГДА ТаблицаТовары.Количество
	|		ИНАЧЕ
	|			ТаблицаСерии.Количество
	|	КОНЕЦ)                                     КАК Количество,
	|	ВЫРАЗИТЬ(СУММА(ВЫБОР
	|		КОГДА ТаблицаСерии.Ссылка ЕСТЬ NULL
	|			ТОГДА ТаблицаТовары.СуммаСНДС
	|		КОГДА ТаблицаТовары.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ
	|			ТаблицаСерии.Количество * ТаблицаТовары.СуммаСНДС / ТаблицаТовары.Количество
	|	КОНЕЦ * ВЫБОР
	|		КОГДА &ВалютаОснования = &ВалютаРеглУчета
	|			ТОГДА 1
	|		ИНАЧЕ
	|			ЕСТЬNULL(КурсВалютыОснования.Курс, 1) * ЕСТЬNULL(КурсВалютыРегл.Кратность, 1)
	|		    / (ЕСТЬNULL(КурсВалютыРегл.Курс, 1) * ЕСТЬNULL(КурсВалютыОснования.Кратность, 1))
	|	КОНЕЦ) КАК ЧИСЛО(15, 2))                   КАК Сумма
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТаблицаТовары.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТаблицаТовары.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТаблицаТовары.Характеристика
	|			И ТаблицаСерии.НоменклатураОприходование = ТаблицаТовары.НоменклатураОприходование
	|			И ТаблицаСерии.ХарактеристикаОприходование = ТаблицаТовары.ХарактеристикаОприходование
	|			И ТаблицаСерии.Назначение = ТаблицаТовары.Назначение
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыОснования
	|		ПО КурсВалютыОснования.Валюта = &ВалютаОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыРегл
	|		ПО КурсВалютыРегл.Валюта = &ВалютаРеглУчета
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.ВидАлкогольнойПродукции.Маркируемый
	|	И Не СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаСерии.Ссылка ЕСТЬ NULL
	|			ТОГДА ТаблицаТовары.Серия
	|		ИНАЧЕ
	|			ТаблицаСерии.Серия
	|	КОНЕЦ
	|
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура       КАК Номенклатура,
	|	ОформленныеТовары.Характеристика     КАК Характеристика,
	|	ОформленныеТовары.Серия              КАК Серия,
	|	-СУММА(ОформленныеТовары.Количество) КАК Количество,
	|	-СУММА(ОформленныеТовары.Сумма)      КАК Сумма
	|ИЗ
	|	Документ.ЧекЕГАИСВозврат.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В (ВЫБРАТЬ Т.Ссылка Из ОформленныеДокументы КАК Т)
	|СГРУППИРОВАТЬ ПО
	|	ОформленныеТовары.Номенклатура,
	|	ОформленныеТовары.Характеристика,
	|	ОформленныеТовары.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) КАК АлкогольнаяПродукция,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Серия КАК Серия
	|ПОМЕСТИТЬ СопоставленыеПозиции
	|ИЗ
	|	ТоварыКОформлению КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = ТаблицаТовары.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС.Характеристика = ТаблицаТовары.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТаблицаТовары.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС2
	|		ПО СоответствиеНоменклатурыЕГАИС2.Номенклатура = ТаблицаТовары.Номенклатура
	|			И СоответствиеНоменклатурыЕГАИС2.Характеристика = ТаблицаТовары.Характеристика
	|			И (СоответствиеНоменклатурыЕГАИС2.Серия = &ПустаяСерия)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,СоответствиеНоменклатурыЕГАИС2.АлкогольнаяПродукция)) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура            КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика          КАК Характеристика,
	|	ТоварыКОформлению.Серия                   КАК Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТоварыКОформлению.Количество)       КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)       КАК КоличествоУпаковок,
	|	СУММА(ТоварыКОформлению.Сумма)            КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА СУММА(ТоварыКОформлению.Количество) = 0 ТОГДА 0
	|		ИНАЧЕ СУММА(ТоварыКОформлению.Сумма) / СУММА(ТоварыКОформлению.Количество)
	|	КОНЕЦ КАК ЧИСЛО(15, 2))                   КАК Цена
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленыеПозиции КАК СопоставленыеПозиции
	|		ПО СопоставленыеПозиции.Номенклатура = ТоварыКОформлению.Номенклатура
	|			И СопоставленыеПозиции.Характеристика = ТоварыКОформлению.Характеристика
	|			И СопоставленыеПозиции.Серия = ТоварыКОформлению.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Серия,
	|	СопоставленыеПозиции.АлкогольнаяПродукция
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|";
	
	Запрос.УстановитьПараметр("ВалютаРеглУчета",   Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ДатаОснования",     РеквизитыДокумента.Дата);
	Запрос.УстановитьПараметр("ВалютаОснования",   РеквизитыДокумента.Валюта);
	Запрос.УстановитьПараметр("ЭтаСсылка",         ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы",   Документы.ЧекЕГАИСВозврат.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ПустаяСерия",       ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ДокументОбъект.Товары.Очистить();
	ДокументОбъект.АкцизныеМарки.Очистить();
	
	ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса, ДанныеЗаполнения, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ПоступлениеТоваровУслуг

Процедура ЗаполнитьПоступлениеТоваровУслугНаОснованииТТНВходящаяЕГАИС(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТТНВходящаяЕГАИС.НомерТТН КАК НомерВходящегоДокумента,
	|	ТТНВходящаяЕГАИС.ДатаТТН КАК ДатаВходящегоДокумента,
	|	СтатусыДокументовЕГАИС.Статус КАК СтатусОбработки,
	|	ТТНВходящаяЕГАИС.Ссылка КАК ТоварноТранспортнаяНакладнаяЕГАИС,
	|	ТТНВходящаяЕГАИС.ТорговыйОбъект КАК Склад,
	|	ТТНВходящаяЕГАИС.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТТНВходящаяЕГАИС.Поставщик = ЗНАЧЕНИЕ(Справочник.КлассификаторОрганизацийЕГАИС.ПустаяСсылка)
	|			ТОГДА ТТНВходящаяЕГАИС.Грузоотправитель.ТорговыйОбъект
	|		ИНАЧЕ ТТНВходящаяЕГАИС.Поставщик.ТорговыйОбъект
	|	КОНЕЦ КАК Партнер,
	|	ВЫБОР
	|		КОГДА ТТНВходящаяЕГАИС.Поставщик = ЗНАЧЕНИЕ(Справочник.КлассификаторОрганизацийЕГАИС.ПустаяСсылка)
	|			ТОГДА ТТНВходящаяЕГАИС.Грузоотправитель.Контрагент
	|		ИНАЧЕ ТТНВходящаяЕГАИС.Поставщик.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ТТНВходящаяЕГАИС.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.КлассификаторОрганизацийЕГАИС.ПустаяСсылка)
	|			ТОГДА ТТНВходящаяЕГАИС.Поставщик.Контрагент
	|		ИНАЧЕ ТТНВходящаяЕГАИС.Грузоотправитель.Контрагент
	|	КОНЕЦ КАК Грузоотправитель
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО (СтатусыДокументовЕГАИС.Документ = ТТНВходящаяЕГАИС.Ссылка)
	|ГДЕ
	|	ТТНВходящаяЕГАИС.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И Товары.АлкогольнаяПродукция = ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТТНВходящаяЕГАИС.ТорговыйОбъект КАК Склад,
	|	ТТНВходящаяЕГАИСТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТТНВходящаяЕГАИСТовары.Номенклатура КАК Номенклатура,
	|	ТТНВходящаяЕГАИСТовары.Характеристика КАК Характеристика,
	|	ТТНВходящаяЕГАИСТовары.Серия КАК Серия,
	|	ТТНВходящаяЕГАИСТовары.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ТТНВходящаяЕГАИСТовары.Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ТТНВходящаяЕГАИСТовары.АлкогольнаяПродукция.ТипПродукции = ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ТТНВходящаяЕГАИСТовары.Номенклатура.ОбъемДАЛ, 1) <> 0
	|						ТОГДА ВЫБОР
	|								КОГДА ТТНВходящаяЕГАИС.ЕстьРасхождения
	|									ТОГДА ТТНВходящаяЕГАИСТовары.КоличествоФакт / ЕСТЬNULL(ТТНВходящаяЕГАИСТовары.Номенклатура.ОбъемДАЛ, 1)
	|								ИНАЧЕ ТТНВходящаяЕГАИСТовары.Количество / ЕСТЬNULL(ТТНВходящаяЕГАИСТовары.Номенклатура.ОбъемДАЛ, 1)
	|							КОНЕЦ
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТТНВходящаяЕГАИС.ЕстьРасхождения
	|					ТОГДА ТТНВходящаяЕГАИСТовары.КоличествоФакт
	|				ИНАЧЕ ТТНВходящаяЕГАИСТовары.Количество
	|			КОНЕЦ
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ТТНВходящаяЕГАИСТовары.АлкогольнаяПродукция.ТипПродукции = ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ТТНВходящаяЕГАИСТовары.Номенклатура.ОбъемДАЛ, 1) <> 0
	|						ТОГДА ВЫБОР
	|								КОГДА ТТНВходящаяЕГАИС.ЕстьРасхождения
	|									ТОГДА ТТНВходящаяЕГАИСТовары.КоличествоФакт / ЕСТЬNULL(ТТНВходящаяЕГАИСТовары.Номенклатура.ОбъемДАЛ, 1)
	|								ИНАЧЕ ТТНВходящаяЕГАИСТовары.Количество / ЕСТЬNULL(ТТНВходящаяЕГАИСТовары.Номенклатура.ОбъемДАЛ, 1)
	|							КОНЕЦ
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТТНВходящаяЕГАИС.ЕстьРасхождения
	|					ТОГДА ТТНВходящаяЕГАИСТовары.КоличествоФакт
	|				ИНАЧЕ ТТНВходящаяЕГАИСТовары.Количество
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоУпаковок,
	|	ТТНВходящаяЕГАИСТовары.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ТТНВходящаяЕГАИСТовары.АлкогольнаяПродукция.ТипПродукции = ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.Неупакованная)
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ТТНВходящаяЕГАИСТовары.Номенклатура.ОбъемДАЛ, 1) <> 0
	|						ТОГДА ТТНВходящаяЕГАИСТовары.Цена * ЕСТЬNULL(ТТНВходящаяЕГАИСТовары.Номенклатура.ОбъемДАЛ, 1)
	|					ИНАЧЕ ТТНВходящаяЕГАИСТовары.Цена
	|				КОНЕЦ
	|		ИНАЧЕ ТТНВходящаяЕГАИСТовары.Цена
	|	КОНЕЦ КАК Цена
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТТНВходящаяЕГАИСТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
	|		ПО ТТНВходящаяЕГАИСТовары.Ссылка = ТТНВходящаяЕГАИС.Ссылка
	|ГДЕ
	|	ТТНВходящаяЕГАИСТовары.Ссылка = &ДокументОснование";
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ВыборкаШапка = ПакетЗапросов[0].Выбрать();
	ВыборкаШапка.Следующий();
	
	МассивДопустимыхСтатусов = Новый Массив;
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.ПринятИзЕГАИС);
	
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.АктПодтвержденияКПередаче);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.АктПодтвержденияПереданВУТМ);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.АктПодтвержденияОбрабатываетсяЕГАИС);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.АктПодтвержденияОшибка);
	
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.АктРасхожденийКПередаче);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.АктРасхожденийПереданВУТМ);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.АктРасхожденийОбрабатываетсяЕГАИС);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.АктРасхожденийОшибка);
	
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.Подтвержден);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.ПодтвержденСРасхождениями);
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
		ВыборкаШапка.ТоварноТранспортнаяНакладнаяЕГАИС,
		ВыборкаШапка.СтатусОбработки,
		,
		,
		МассивДопустимыхСтатусов);
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, ВыборкаШапка);
	
	ЕстьНесопоставленнныеТовары = НЕ ПакетЗапросов[1].Пустой();
	Если ЕстьНесопоставленнныеТовары Тогда
		ТекстОшибки = НСтр("ru='В документе %Документ% есть несопоставленные товары. Ввод на основании такого документа запрещен.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ДокументОбъект.ЗаполнитьУсловияЗакупокПоУмолчанию();
	
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	
	ВыборкаТовары = ПакетЗапросов[ПакетЗапросов.Количество() - 1].Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		
		НоваяСтрокаТовары = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, ВыборкаТовары);
		НоваяСтрокаТовары.Сумма     = НоваяСтрокаТовары.КоличествоУпаковок * НоваяСтрокаТовары.Цена;
		НоваяСтрокаТовары.СуммаНДС  = Ценообразование.РассчитатьСуммуНДС(НоваяСтрокаТовары.Сумма, НоваяСтрокаТовары.СтавкаНДС, ДокументОбъект.ЦенаВключаетНДС);
		НоваяСтрокаТовары.СуммаСНДС = НоваяСтрокаТовары.Сумма + ?(ДокументОбъект.ЦенаВключаетНДС, 0, НоваяСтрокаТовары.СуммаНДС);
		
		Если ЗначениеЗаполнено(НоваяСтрокаТовары.Номенклатура) Тогда
			СтруктураДействий = Новый Структура;
			СтруктураДействий.Вставить("ЗаполнитьНоменклатуруПоставщикаПоНоменклатуре", ДокументОбъект.Партнер);
			СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", НоваяСтрокаТовары.Упаковка);
			СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
			СтруктураДействий.Вставить("ПроверитьСтатьюАналитикуРасходов", НоваяСтрокаТовары.Номенклатура);
			СтруктураДействий.Вставить("ОчиститьСуммуВзаиморасчетов");
			СтруктураДействий.Вставить("ПересчитатьСуммуСверхЗаказа", Новый Структура("РеализацияПоступлениеПоЗаказу, ТребуетсяЗалогЗаТару",
				ДокументОбъект.ПоступлениеПоЗаказам, ДокументОбъект.ТребуетсяЗалогЗаТару));
			
			КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрокаТовары, СтруктураДействий, КэшированныеЗначения);
			
			Если ИспользоватьСерииНоменклатуры Тогда
				НоваяСтрокаСерии = ДокументОбъект.Серии.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСерии, НоваяСтрокаТовары);
			Иначе
				НоваяСтрокаТовары.Серия = "";
				НоваяСтрокаТовары.СтатусУказанияСерий = 0;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ИспользоватьСерииНоменклатуры Тогда
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ДокументОбъект, Документы.ПриобретениеТоваровУслуг);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументОбъект, ПараметрыУказанияСерий);
		НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ДокументОбъект, ПараметрыУказанияСерий);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ЗарегистрироватьКлассификаторОрганизацийЕГАИСДляУстановкиСоответствий(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КлассификаторОрганизацийЕГАИС.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОбменаЕГАИС КАК НастройкиОбменаЕГАИС
	|		ПО КлассификаторОрганизацийЕГАИС.Код = НастройкиОбменаЕГАИС.ИдентификаторФСРАР
	|ГДЕ
	|	(КлассификаторОрганизацийЕГАИС.Контрагент <> НастройкиОбменаЕГАИС.УдалитьРесурсОрганизация
	|			ИЛИ КлассификаторОрганизацийЕГАИС.ТорговыйОбъект <> НастройкиОбменаЕГАИС.УдалитьРесурсСклад
	|			ИЛИ НЕ КлассификаторОрганизацийЕГАИС.СоответствуетОрганизации)";
	
	МассивСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, МассивСсылок);
	
КонецПроцедуры

Процедура ОбработатьКлассификаторОрганизацийЕГАИСДляУстановкиСоответствий(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Справочник.КлассификаторОрганизацийЕГАИС";
	МетаданныеОбъекта = Метаданные.Справочники.КлассификаторОрганизацийЕГАИС;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц);
	
	Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ВТСсылкиДляОбработки.Ссылка КАК Справочник.КлассификаторОрганизацийЕГАИС) КАК Ссылка
	|ПОМЕСТИТЬ СсылкиДляОбработки
	|ИЗ
	|	&ВТСсылкиДляОбработки КАК ВТСсылкиДляОбработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.Ссылка,
	|	НастройкиОбменаЕГАИС.УдалитьРесурсОрганизация      КАК ОрганизацияИзНастроек,
	|	НастройкиОбменаЕГАИС.УдалитьРесурсСклад            КАК ТорговыйОбъектИзНастроек,
	|	ЕСТЬNULL(НастройкиОбменаЕГАИС.ИдентификаторФСРАР, НЕОПРЕДЕЛЕНО) КАК ИдентификаторФСРАР
	|ИЗ
	|	СсылкиДляОбработки КАК СсылкиДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОбменаЕГАИС КАК НастройкиОбменаЕГАИС
	|		ПО СсылкиДляОбработки.Ссылка.Код = НастройкиОбменаЕГАИС.ИдентификаторФСРАР";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВТСсылкиДляОбработки", Результат.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ОбработанныеОбъекты = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Если Выборка.ИдентификаторФСРАР = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка,, Параметры.Очередь);
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиОбменаЕГАИС");
			ЭлементБлокировки.УстановитьЗначение("ИдентификаторФСРАР", Выборка.ИдентификаторФСРАР);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ОрганизацияЕГАИС = Выборка.Ссылка.ПолучитьОбъект();
			
			Если ОрганизацияЕГАИС = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка,, Параметры.Очередь);
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			Если ОрганизацияЕГАИС.СоответствуетОрганизации
				И Выборка.ОрганизацияИзНастроек = ОрганизацияЕГАИС.Контрагент
				И Выборка.ТорговыйОбъектИзНастроек = ОрганизацияЕГАИС.ТорговыйОбъект Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка,, Параметры.Очередь);
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ОрганизацияЕГАИС.СоответствуетОрганизации = Истина;
			ОрганизацияЕГАИС.Контрагент     = Выборка.ОрганизацияИзНастроек;
			ОрганизацияЕГАИС.ТорговыйОбъект = Выборка.ТорговыйОбъектИзНастроек;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОрганизацияЕГАИС);
			
			ЗафиксироватьТранзакцию();
		
		Исключение
			
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru = 'Не удалось обработать справочник: %Справочник% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Справочник%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			                         УровеньЖурналаРегистрации.Предупреждение,
			                         МетаданныеОбъекта,
			                         Выборка.Ссылка,
			                         ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ЗарегистрироватьАктыСписанияЕГАИСДляЗаполненияНоменклатуры(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК Товары
	|ГДЕ
	|	Товары.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			И Товары.АлкогольнаяПродукция.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьАктыСписанияЕГАИСДляЗаполненияНоменклатуры(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.АктСписанияЕГАИС";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СсылкиДляОбработки.Ссылка КАК Ссылка,
	|	СсылкиДляОбработки.Ссылка.ВерсияДанных КАК ВерсияДанных,
	|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Товары.АлкогольнаяПродукция.Номенклатура КАК Номенклатура,
	|	Товары.АлкогольнаяПродукция.Характеристика КАК Характеристика
	|ИЗ
	|	&ВТДляОбработки КАК СсылкиДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АктСписанияЕГАИС.Товары КАК Товары
	|		ПО (Товары.Ссылка = СсылкиДляОбработки.Ссылка)
	|ИТОГИ ПО
	|	Ссылка";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(
		Параметры.Очередь,
		ПолноеИмяОбъекта,
		МенеджерВременныхТаблиц);
	
	Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВТДляОбработки", Результат.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	
	ОбъектыДляОбработки = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ОбъектыДляОбработки.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			// Устанавливаем управляемую блокировку, чтобы провести ответственное чтение объекта
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ОбъектыДляОбработки.Ссылка);
			
			Блокировка.Заблокировать();
			
			Объект = ОбъектыДляОбработки.Ссылка.ПолучитьОбъект();
			Если Объект = Неопределено Тогда
				ОтменитьТранзакцию();
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ОбъектыДляОбработки.Ссылка);
				Продолжить;
			КонецЕсли;
			
			Если Объект.ВерсияДанных <> ОбъектыДляОбработки.ВерсияДанных Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ОбъектИзменен = Ложь;
			
			ВыборкаПоТоварам = ОбъектыДляОбработки.Выбрать();
			Пока ВыборкаПоТоварам.Следующий() Цикл
				
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("АлкогольнаяПродукция", ВыборкаПоТоварам.АлкогольнаяПродукция);
				ПараметрыОтбора.Вставить("Номенклатура", Справочники.Номенклатура.ПустаяСсылка());
				
				НайденныеСтроки = Объект.Товары.НайтиСтроки(ПараметрыОтбора);
				Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
					
					Если НЕ СтрокаТЧ.Номенклатура.Пустая() Тогда
						Продолжить;
					КонецЕсли;
					
					СтрокаТЧ.Номенклатура        = ВыборкаПоТоварам.Номенклатура;
					СтрокаТЧ.Характеристика      = ВыборкаПоТоварам.Характеристика;
					СтрокаТЧ.КоличествоУпаковок  = СтрокаТЧ.Количество;
					СтрокаТЧ.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
					
					ОбъектИзменен = Истина;
					
				КонецЦикла;
				
			КонецЦикла;
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Объект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ОбъектыДляОбработки.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъекта);
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать %ИмяОбъекта%: %Ссылка% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяОбъекта%", ПолноеИмяОбъекта);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", ОбъектыДляОбработки.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
										УровеньЖурналаРегистрации.Предупреждение,
										МетаданныеОбъекта,
										ОбъектыДляОбработки.Ссылка,
										ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсиюСтатусыОформленияДокументовЕГАИС(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.СтатусыОформленияДокументовЕГАИС";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СправочникНоменклатура.Ссылка                              КАК Ссылка,
	|	СправочникНоменклатура.ВидАлкогольнойПродукции.Маркируемый КАК МаркируемаяАлкогольнаяПродукция
	|ПОМЕСТИТЬ втАлкогольнаяПродукция
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|ГДЕ
	|	СправочникНоменклатура.АлкогольнаяПродукция
	|	И НЕ СправочникНоменклатура.АлкогольнаяПродукцияВоВскрытойТаре
	|ИНДЕКСИРОВАТЬ ПО
	|	СправочникНоменклатура.Ссылка
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.АктПостановкиНаБалансЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                                      КАК Основание
	|ИЗ
	|	Документ.ОприходованиеИзлишковТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.АктПостановкиНаБалансЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                                      КАК Основание
	|ИЗ
	|	Документ.ПересортицаТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.НоменклатураОприходование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.АктСписанияЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                            КАК Основание
	|ИЗ
	|	Документ.ВнутреннееПотреблениеТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.АктСписанияЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                            КАК Основание
	|ИЗ
	|	Документ.СписаниеНедостачТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.АктСписанияЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                            КАК Основание
	|ИЗ
	|	Документ.ПересортицаТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.АктСписанияЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                            КАК Основание
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.АктСписанияЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТаблицаДокумента.Ссылка                          КАК Основание
	|ИЗ
	|	Документ.СборкаТоваров КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТаблицаДокумента.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.ТТНВходящаяЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                          КАК Основание
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.ТТНВходящаяЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                          КАК Основание
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.ТТНВходящаяЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                          КАК Основание
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.ТТНВходящаяЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                          КАК Основание
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.ТТНИсходящаяЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                           КАК Основание
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.ТТНИсходящаяЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                           КАК Основание
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.ТТНИсходящаяЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                           КАК Основание
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.ТТНИсходящаяЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                           КАК Основание
	|ИЗ
	|	Документ.ПередачаТоваровМеждуОрганизациями.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.ЧекЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                           КАК Основание
	|ИЗ
	|	Документ.ВнутреннееПотреблениеТоваров.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|		И СправочникНоменклатураАлкогольнаяПродукция.МаркируемаяАлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.ЧекЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                           КАК Основание
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|		И СправочникНоменклатураАлкогольнаяПродукция.МаркируемаяАлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.ЧекЕГАИС.ПустаяСсылка) КАК Документ,
	|	ТаблицаДокумента.Ссылка                          КАК Основание
	|ИЗ
	|	Документ.СборкаТоваров КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТаблицаДокумента.Номенклатура
	|		И СправочникНоменклатураАлкогольнаяПродукция.МаркируемаяАлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Документ.ЧекЕГАИСВозврат.ПустаяСсылка) КАК Документ,
	|	ТабличнаяЧасть.Ссылка                          КАК Основание
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАлкогольнаяПродукция КАК СправочникНоменклатураАлкогольнаяПродукция
	|		ПО СправочникНоменклатураАлкогольнаяПродукция.Ссылка = ТабличнаяЧасть.Номенклатура
	|		И СправочникНоменклатураАлкогольнаяПродукция.МаркируемаяАлкогольнаяПродукция
	|");
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Таблица, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсиюСтатусыОформленияДокументовЕГАИС(Параметры) Экспорт
	
	Если ОбновлениеИнформационнойБазы.ЕстьЗаблокированныеПредыдущимиОчередямиДанные(Параметры.Очередь, "РегистрСведений.СтатусыДокументовЕГАИС") Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметры.ВыбиратьПорциями = Ложь;
	
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		"РегистрСведений.СтатусыОформленияДокументовЕГАИС",
		МенеджерВременныхТаблиц, ДополнительныеПараметры);
	
	Если Не Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если Не Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 500
	|	ДанныеДляОбработки.Документ   КАК Документ,
	|	ДанныеДляОбработки.Основание  КАК Основание
	|ПОМЕСТИТЬ втДокументыДляОбработки
	|ИЗ
	|	&ВТДанныеДляОбработки КАК ДанныеДляОбработки
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДляОбработки.Основание.Дата УБЫВ
	|;
	|
	|/////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ   КАК Документ,
	|	ДанныеДляОбработки.Основание  КАК Основание,
	|	ДанныеДокумента.ВерсияДанных  КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен      КАК Проведен,
	|	ДанныеДокумента.Дата          КАК Дата,
	|	ДанныеДокумента.Номер         КАК Номер,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Склад         КАК ТорговыйОбъект,
	|	ДанныеДокумента.Организация   КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПересортицаТоваров КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.ПересортицаТоваров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ   КАК Документ,
	|	ДанныеДляОбработки.Основание  КАК Основание,
	|	ДанныеДокумента.ВерсияДанных  КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен      КАК Проведен,
	|	ДанныеДокумента.Дата          КАК Дата,
	|	ДанныеДокумента.Номер         КАК Номер,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Склад         КАК ТорговыйОбъект,
	|	ДанныеДокумента.Организация   КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОприходованиеИзлишковТоваров КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.ОприходованиеИзлишковТоваров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ   КАК Документ,
	|	ДанныеДляОбработки.Основание  КАК Основание,
	|	ДанныеДокумента.ВерсияДанных  КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен      КАК Проведен,
	|	ДанныеДокумента.Дата          КАК Дата,
	|	ДанныеДокумента.Номер         КАК Номер,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Склад         КАК ТорговыйОбъект,
	|	ДанныеДокумента.Организация   КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотреблениеТоваров КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.ВнутреннееПотреблениеТоваров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ   КАК Документ,
	|	ДанныеДляОбработки.Основание  КАК Основание,
	|	ДанныеДокумента.ВерсияДанных  КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен      КАК Проведен,
	|	ДанныеДокумента.Дата          КАК Дата,
	|	ДанныеДокумента.Номер         КАК Номер,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Склад         КАК ТорговыйОбъект,
	|	ДанныеДокумента.Организация   КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ   КАК Документ,
	|	ДанныеДляОбработки.Основание  КАК Основание,
	|	ДанныеДокумента.ВерсияДанных  КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен      КАК Проведен,
	|	ДанныеДокумента.Дата          КАК Дата,
	|	ДанныеДокумента.Номер         КАК Номер,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Склад         КАК ТорговыйОбъект,
	|	ДанныеДокумента.Организация   КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеНедостачТоваров КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.СписаниеНедостачТоваров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ   КАК Документ,
	|	ДанныеДляОбработки.Основание  КАК Основание,
	|	ДанныеДокумента.ВерсияДанных  КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен      КАК Проведен,
	|	ДанныеДокумента.Дата          КАК Дата,
	|	ДанныеДокумента.Номер         КАК Номер,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Склад         КАК ТорговыйОбъект,
	|	ДанныеДокумента.Организация   КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СборкаТоваров КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.СборкаТоваров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ  КАК Документ,
	|	ДанныеДляОбработки.Основание КАК Основание,
	|	ДанныеДокумента.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен     КАК Проведен,
	|	ДанныеДокумента.Дата         КАК Дата,
	|	ДанныеДокумента.Номер        КАК Номер,
	|	ДанныеДокумента.Менеджер     КАК Ответственный,
	|	ДанныеДокумента.Склад        КАК ТорговыйОбъект,
	|	ДанныеДокумента.Организация  КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Документ ССЫЛКА Документ.ТТНИсходящаяЕГАИС
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ  КАК Документ,
	|	ДанныеДляОбработки.Основание КАК Основание,
	|	ДанныеДокумента.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен              КАК Проведен,
	|	ДанныеДокумента.Дата                  КАК Дата,
	|	ДанныеДокумента.Номер                 КАК Номер,
	|	ДанныеДокумента.Менеджер              КАК Ответственный,
	|	ДанныеДокумента.Склад                 КАК ТорговыйОбъект,
	|	ДанныеДокумента.ОрганизацияПолучатель КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Документ ССЫЛКА Документ.ТТНВходящаяЕГАИС
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ  КАК Документ,
	|	ДанныеДляОбработки.Основание КАК Основание,
	|	ДанныеДокумента.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен     КАК Проведен,
	|	ДанныеДокумента.Дата         КАК Дата,
	|	ДанныеДокумента.Номер        КАК Номер,
	|	ДанныеДокумента.Менеджер     КАК Ответственный,
	|	ДанныеДокумента.Склад        КАК ТорговыйОбъект,
	|	ДанныеДокумента.Организация  КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровМеждуОрганизациями КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Документ ССЫЛКА Документ.ТТНИсходящаяЕГАИС
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.ВозвратТоваровМеждуОрганизациями
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ  КАК Документ,
	|	ДанныеДляОбработки.Основание КАК Основание,
	|	ДанныеДокумента.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен              КАК Проведен,
	|	ДанныеДокумента.Дата                  КАК Дата,
	|	ДанныеДокумента.Номер                 КАК Номер,
	|	ДанныеДокумента.Менеджер              КАК Ответственный,
	|	ДанныеДокумента.Склад                 КАК ТорговыйОбъект,
	|	ДанныеДокумента.ОрганизацияПолучатель КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровМеждуОрганизациями КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Документ ССЫЛКА Документ.ТТНВходящаяЕГАИС
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.ВозвратТоваровМеждуОрганизациями
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ  КАК Документ,
	|	ДанныеДляОбработки.Основание КАК Основание,
	|	ДанныеДокумента.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен         КАК Проведен,
	|	ДанныеДокумента.Дата             КАК Дата,
	|	ДанныеДокумента.Номер            КАК Номер,
	|	ДанныеДокумента.Ответственный    КАК Ответственный,
	|	ДанныеДокумента.СкладОтправитель КАК ТорговыйОбъект,
	|	ДанныеДокумента.Организация      КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Документ ССЫЛКА Документ.ТТНИсходящаяЕГАИС
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.ПеремещениеТоваров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ  КАК Документ,
	|	ДанныеДляОбработки.Основание КАК Основание,
	|	ДанныеДокумента.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен              КАК Проведен,
	|	ДанныеДокумента.Дата                  КАК Дата,
	|	ДанныеДокумента.Номер                 КАК Номер,
	|	ДанныеДокумента.Ответственный         КАК Ответственный,
	|	ДанныеДокумента.СкладПолучатель       КАК ТорговыйОбъект,
	|	ДанныеДокумента.ОрганизацияПолучатель КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Документ ССЫЛКА Документ.ТТНВходящаяЕГАИС
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.ПеремещениеТоваров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ  КАК Документ,
	|	ДанныеДляОбработки.Основание КАК Основание,
	|	ДанныеДокумента.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен     КАК Проведен,
	|	ДанныеДокумента.Дата         КАК Дата,
	|	ДанныеДокумента.Номер        КАК Номер,
	|	ДанныеДокумента.Менеджер     КАК Ответственный,
	|	ДанныеДокумента.Склад        КАК ТорговыйОбъект,
	|	ДанныеДокумента.Организация  КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.ПриобретениеТоваровУслуг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ  КАК Документ,
	|	ДанныеДляОбработки.Основание КАК Основание,
	|	ДанныеДокумента.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен     КАК Проведен,
	|	ДанныеДокумента.Дата         КАК Дата,
	|	ДанныеДокумента.Номер        КАК Номер,
	|	ДанныеДокумента.Менеджер     КАК Ответственный,
	|	ДанныеДокумента.Склад        КАК ТорговыйОбъект,
	|	ДанныеДокумента.Организация  КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровПоставщику КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.ВозвратТоваровПоставщику
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ  КАК Документ,
	|	ДанныеДляОбработки.Основание КАК Основание,
	|	ДанныеДокумента.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен      КАК Проведен,
	|	ДанныеДокумента.Дата          КАК Дата,
	|	ДанныеДокумента.Номер         КАК Номер,
	|	ДанныеДокумента.Менеджер      КАК Ответственный,
	|	ДанныеДокумента.Склад         КАК ТорговыйОбъект,
	|	ДанныеДокумента.Организация   КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.РеализацияТоваровУслуг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Документ  КАК Документ,
	|	ДанныеДляОбработки.Основание КАК Основание,
	|	ДанныеДокумента.ВерсияДанных КАК ВерсияДанных,
	|	ВЫБОР КОГДА ДанныеДокумента.Проведен ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОтметитьВыполнениеОбработки,
	|	ДанныеДокумента.Проведен      КАК Проведен,
	|	ДанныеДокумента.Дата          КАК Дата,
	|	ДанныеДокумента.Номер         КАК Номер,
	|	ДанныеДокумента.Менеджер      КАК Ответственный,
	|	ДанныеДокумента.Склад         КАК ТорговыйОбъект,
	|	ДанныеДокумента.Организация   КАК Контрагент
	|ИЗ
	|	втДокументыДляОбработки КАК ДанныеДляОбработки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК ДанныеДокумента
	|	ПО ДанныеДокумента.Ссылка = ДанныеДляОбработки.Основание
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|	ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|	И ДанныеДляОбработки.Основание ССЫЛКА Документ.ВозвратТоваровОтКлиента
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ВТДанныеДляОбработки", Результат.ИмяВременнойТаблицы);
	
	ТаблицыДляЧтения = Новый Массив;
	ТаблицыДляЧтения.Добавить("Документ.ВозвратТоваровОтКлиента");
	ТаблицыДляЧтения.Добавить("Документ.РеализацияТоваровУслуг");
	ТаблицыДляЧтения.Добавить("Документ.ВозвратТоваровПоставщику");
	ТаблицыДляЧтения.Добавить("Документ.ПриобретениеТоваровУслуг");
	ТаблицыДляЧтения.Добавить("Документ.ПеремещениеТоваров");
	ТаблицыДляЧтения.Добавить("Документ.ВозвратТоваровМеждуОрганизациями");
	ТаблицыДляЧтения.Добавить("Документ.ПередачаТоваровМеждуОрганизациями");
	ТаблицыДляЧтения.Добавить("Документ.СборкаТоваров");
	ТаблицыДляЧтения.Добавить("Документ.СписаниеНедостачТоваров");
	ТаблицыДляЧтения.Добавить("Документ.ОтчетОРозничныхПродажах");
	ТаблицыДляЧтения.Добавить("Документ.ВнутреннееПотреблениеТоваров");
	ТаблицыДляЧтения.Добавить("Документ.ОприходованиеИзлишковТоваров");
	ТаблицыДляЧтения.Добавить("Документ.ПересортицаТоваров");
	
	РезультатЗаблокировано = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияСсылок(
		Параметры.Очередь,
		ТаблицыДляЧтения,
		МенеджерВременныхТаблиц);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Если Выборка.ОтметитьВыполнениеОбработки Тогда
				
				НаборЗаписей = РегистрыСведений.СтатусыОформленияДокументовЕГАИС.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Основание.Установить(Выборка.Основание);
				НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
				
				ЗафиксироватьТранзакцию();
				Продолжить;
				
			КонецЕсли;
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СтатусыОформленияДокументовЕГАИС");
			ЭлементБлокировки.УстановитьЗначение("Документ",  Выборка.Документ);
			ЭлементБлокировки.УстановитьЗначение("Основание", Выборка.Основание);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(Выборка.Основание.Метаданные().ПолноеИмя());
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Основание);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			ВерсияДанных = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Основание, "ВерсияДанных");
			Если ВерсияДанных <> Выборка.ВерсияДанных Тогда
				
				ОтменитьТранзакцию();
				Продолжить;
				
			КонецЕсли;
			
			ЗначенияРеквизитов = СтруктураРеквизитыРасчетаСтатусаОформления();
			ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Выборка);
			МетаданныеДокумента = Выборка.Документ.Метаданные();
			
			ЗаписьТребуется = ТребуетсяОформлениеДокументовЕГАИС(
				Выборка.Основание,
				МетаданныеДокумента);
			
			РассчитатьСтатусОформленияДляОбновления(
				МетаданныеДокумента,
				Выборка.Основание, ЗначенияРеквизитов, ЗаписьТребуется);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать запись регистра сведений ""СтатусыОформленияДокументовЕГАИС""
			                            |по причине %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			                         УровеньЖурналаРегистрации.Предупреждение,
			                         Метаданные.РегистрыСведений.СтатусыОформленияДокументовЕГАИС,
			                         Выборка.Основание,
			                         ТекстСообщения);
			
			Продолжить;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, "РегистрСведений.СтатусыОформленияДокументовЕГАИС");
	
КонецПроцедуры

// Рассчитать новый статус оформления документов ЕГАИС по документу-основанию.
//
// Параметры:
//  МетаданныеДокумента - Метаданные - Метаданные проверяемого документа.
//  ДокументОснование - ДокументСсылка - Ссылка на документ-основание.
//  РеквизитыДокументаОснования - Структура - Реквизиты документа-основания.
//  ЗаписьТребуется - Булево - Признак необходимости записи.
//
Процедура РассчитатьСтатусОформленияДляОбновления(МетаданныеДокумента, ДокументОснование, РеквизитыДокументаОснования, ЗаписьТребуется) Экспорт
	
	ПустаяСсылка = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеДокумента.ПолноеИмя()).ПустаяСсылка();
	
	ВыполнятьЗаписьВРегистр = Ложь;
	ДанныеЗаписи = РегистрыСведений.СтатусыОформленияДокументовЕГАИС.ЗначенияПоУмолчанию(ДокументОснование, ПустаяСсылка);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СтатусыОформления.СтатусОформления КАК СтатусОформления,
	|	СтатусыОформления.Контрагент       КАК Контрагент,
	|	СтатусыОформления.ТорговыйОбъект   КАК ТорговыйОбъект,
	|	СтатусыОформления.Ответственный    КАК Ответственный,
	|	СтатусыОформления.Дата             КАК Дата,
	|	СтатусыОформления.Номер            КАК Номер
	|ИЗ
	|	РегистрСведений.СтатусыОформленияДокументовЕГАИС КАК СтатусыОформления
	|ГДЕ
	|	СтатусыОформления.Основание = &Основание
	|	И СтатусыОформления.Документ = &ПустаяСсылка
	|");
	
	Запрос.УстановитьПараметр("Основание",    ДокументОснование);
	Запрос.УстановитьПараметр("ПустаяСсылка", ПустаяСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если (Не РеквизитыДокументаОснования.Проведен Или Не ЗаписьТребуется) Тогда
			
			НаборЗаписей = РегистрыСведений.СтатусыОформленияДокументовЕГАИС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Основание.Установить(ДокументОснование);
			НаборЗаписей.Отбор.Документ.Установить(ПустаяСсылка);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			Возврат;
			
		Иначе
			
			ВыполнятьЗаписьВРегистр = Истина;
			ДанныеЗаписи.СтатусОформления = ИнтеграцияЕГАИС.СтатусОформления(МетаданныеДокумента, ДокументОснование);
			ЗаполнитьЗначенияСвойств(ДанныеЗаписи, РеквизитыДокументаОснования);
			
		КонецЕсли;
		
	ИначеЕсли РеквизитыДокументаОснования.Проведен И ЗаписьТребуется Тогда
		
		ВыполнятьЗаписьВРегистр = Истина;
		ДанныеЗаписи.СтатусОформления = ИнтеграцияЕГАИС.СтатусОформления(МетаданныеДокумента, ДокументОснование);
		ЗаполнитьЗначенияСвойств(ДанныеЗаписи, РеквизитыДокументаОснования);
		
	КонецЕсли;
	
	Если ВыполнятьЗаписьВРегистр Тогда
		
		НаборЗаписей = РегистрыСведений.СтатусыОформленияДокументовЕГАИС.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Документ.Установить(ДанныеЗаписи.Документ);
		НаборЗаписей.Отбор.Основание.Установить(ДанныеЗаписи.Основание);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ДанныеЗаписи);
		
		НоваяЗапись.Документ         = ДанныеЗаписи.Документ;
		НоваяЗапись.Основание        = ДанныеЗаписи.Основание;
		НоваяЗапись.СтатусОформления = ДанныеЗаписи.СтатусОформления;
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
	Иначе
		
		НаборЗаписей = РегистрыСведений.СтатусыОформленияДокументовЕГАИС.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Документ.Установить(ПустаяСсылка);
		НаборЗаписей.Отбор.Основание.Установить(ДокументОснование);
		
		ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсиюСоответствиеНоменклатуры(Параметры) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
	|ГДЕ
	|	КлассификаторАлкогольнойПродукцииЕГАИС.УдалитьНоменклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|;
	|");
	
	ЭлементыКлассификатора = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ЭлементыКлассификатора);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсиюСоответствиеНоменклатуры(Параметры) Экспорт
	
	Если ОбновлениеИнформационнойБазы.ЕстьЗаблокированныеПредыдущимиОчередямиДанные(Параметры.Очередь, "Документ.ТТНИсходящаяЕГАИС") Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ПолноеИмяОбъекта = "Справочник.КлассификаторАлкогольнойПродукцииЕГАИС";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц);
	
	Если Не Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДляОбработки.Ссылка              КАК Ссылка,
	|	ДанныеДляОбработки.Ссылка.ВерсияДанных КАК ВерсияДанных
	|ИЗ
	|	ВтСсылкиДляОбработки КАК ДанныеДляОбработки
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВтСсылкиДляОбработки", Результат.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
			Если СправочникОбъект = Неопределено Тогда
				
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
				
				ЗафиксироватьТранзакцию();
				Продолжить;
				
			КонецЕсли;
			
			Если СправочникОбъект.ВерсияДанных <> Выборка.ВерсияДанных Тогда
				
				ОтменитьТранзакцию();
				Продолжить;
				
			КонецЕсли;
			
			НаборЗаписей = РегистрыСведений.СоответствиеНоменклатурыЕГАИС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Номенклатура.Установить(СправочникОбъект.УдалитьНоменклатура);
			НаборЗаписей.Отбор.Характеристика.Установить(СправочникОбъект.УдалитьХарактеристика);
			НаборЗаписей.Отбор.АлкогольнаяПродукция.Установить(Выборка.Ссылка);
			
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаписьНабора.АлкогольнаяПродукция = Выборка.Ссылка;
			ЗаписьНабора.Номенклатура         = СправочникОбъект.УдалитьНоменклатура;
			ЗаписьНабора.Характеристика       = СправочникОбъект.УдалитьХарактеристика;
			ЗаписьНабора.Порядок              = 1;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			СправочникОбъект.УдалитьНоменклатура   = Неопределено;
			СправочникОбъект.УдалитьХарактеристика = Неопределено;
			СправочникОбъект.УдалитьСопоставлено   = Ложь;
			СправочникОбъект.УдалитьУпаковки.Очистить();
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать элемент справочника: %Ссылка% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%",  Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Справочники.КлассификаторАлкогольнойПродукцииЕГАИС,
				Выборка.Ссылка,
				ТекстСообщения);
			
			Продолжить;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсиюТТНВходящаяЕГАИС(Параметры) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Документ.УдалитьТоварноТранспортнаяНакладнаяЕГАИС КАК Ссылка
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК Документ
	|ГДЕ
	|	Документ.УдалитьТоварноТранспортнаяНакладнаяЕГАИС <> ЗНАЧЕНИЕ(Документ.ТТНВходящаяЕГАИС.ПустаяСсылка)
	|	И Документ.УдалитьТоварноТранспортнаяНакладнаяЕГАИС ССЫЛКА Документ.ТТНВходящаяЕГАИС
	|");
	
	ДокументыКОбработке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДокументыКОбработке);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсиюТТНВходящаяЕГАИС(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.ТТНВходящаяЕГАИС";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц);
	
	Если Не Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДляОбработки.Ссылка              КАК Ссылка,
	|	ДанныеДляОбработки.Ссылка.ВерсияДанных КАК ВерсияДанных,
	|	ДанныеДокумента.Ссылка                 КАК ПриобретениеТоваровУслуг,
	|	ДанныеДокумента.ВерсияДанных           КАК ПриобретениеТоваровУслугВерсияДанных
	|ИЗ
	|	ВтСсылкиДляОбработки КАК ДанныеДляОбработки
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ДанныеДокумента
	|		ПО ДанныеДляОбработки.Ссылка = ДанныеДокумента.УдалитьТоварноТранспортнаяНакладнаяЕГАИС
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ДокументЗаблокировано
	|		ПО ДокументЗаблокировано.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	ДокументЗаблокировано.Ссылка ЕСТЬ NULL
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВтСсылкиДляОбработки", Результат.ИмяВременнойТаблицы);
	
	ТаблицыДляЧтения = Новый Массив;
	ТаблицыДляЧтения.Добавить("Документ.ПриобретениеТоваровУслуг");
	
	РезультатЗаблокировано = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияСсылок(
		Параметры.Очередь,
		ТаблицыДляЧтения,
		МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.ПриобретениеТоваровУслуг");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.ПриобретениеТоваровУслуг);
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			Если ДокументОбъект = Неопределено Тогда
				
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
				
				ЗафиксироватьТранзакцию();
				Продолжить;
				
			КонецЕсли;
			
			Если ДокументОбъект.ВерсияДанных <> Выборка.ВерсияДанных Тогда
				
				ОтменитьТранзакцию();
				Продолжить;
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Выборка.ПриобретениеТоваровУслуг) Тогда
				
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
				
				ЗафиксироватьТранзакцию();
				Продолжить;
				
			КонецЕсли;
			
			ПриобретениеТоваровУслугОбъект = Выборка.ПриобретениеТоваровУслуг.ПолучитьОбъект();
			Если ПриобретениеТоваровУслугОбъект = Неопределено Тогда
				
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
				
				ЗафиксироватьТранзакцию();
				Продолжить;
				
			КонецЕсли;
			
			Если ПриобретениеТоваровУслугОбъект.ВерсияДанных <> Выборка.ПриобретениеТоваровУслугВерсияДанных Тогда
				
				ОтменитьТранзакцию();
				Продолжить;
				
			КонецЕсли;
			
			ДокументОбъект.ДокументОснование = Выборка.ПриобретениеТоваровУслуг;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать документ: %Ссылка% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%",  Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Документы.ТТНВходящаяЕГАИС,
				Выборка.Ссылка,
				ТекстСообщения);
			
			Продолжить;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсиюТТНИсходящаяЕГАИС(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.Номенклатура.Имя));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТабличнаяЧасть.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ТТНИсходящаяЕГАИС.Товары КАК ТабличнаяЧасть
	|ГДЕ
	|	(ТабличнаяЧасть.Номенклатура = &Номенклатура
	|	ИЛИ ТабличнаяЧасть.КоличествоУпаковок = 0
	|	ИЛИ (ТабличнаяЧасть.КоличествоФакт <> 0 И НЕ ТабличнаяЧасть.Ссылка.ЕстьРасхождения))
	|";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсиюТТНИсходящаяЕГАИС(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.ТТНИсходящаяЕГАИС";
	МетаданныеОбъекта = Метаданные.Документы.ТТНИсходящаяЕГАИС;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц);
	
	Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДляОбработки.Ссылка                                    КАК Ссылка,
	|	ДанныеДляОбработки.Ссылка.ВерсияДанных                       КАК ВерсияДанных,
	|	ТабличнаяЧасть.НомерСтроки                                   КАК НомерСтроки,
	|	ТабличнаяЧасть.АлкогольнаяПродукция                          КАК АлкогольнаяПродукция,
	|	КлассификаторАлкогольнойПродукцииЕГАИС.УдалитьНоменклатура   КАК Номенклатура,
	|	КлассификаторАлкогольнойПродукцииЕГАИС.УдалитьХарактеристика КАК Характеристика
	|ИЗ
	|	ВтСсылкиДляОбработки КАК ДанныеДляОбработки
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТТНИсходящаяЕГАИС.Товары КАК ТабличнаяЧасть
	|		ПО ТабличнаяЧасть.Ссылка = ДанныеДляОбработки.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
	|		ПО ТабличнаяЧасть.АлкогольнаяПродукция = КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка
	|	
	|ИТОГИ ПО
	|	ДанныеДляОбработки.Ссылка
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВтСсылкиДляОбработки", Результат.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ВыборкаПоДокументам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ВыборкаПоДокументам.Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = ВыборкаПоДокументам.Ссылка.ПолучитьОбъект();
			Если ДокументОбъект = Неопределено Тогда
				
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ВыборкаПоДокументам.Ссылка);
				
				ЗафиксироватьТранзакцию();
				Продолжить;
				
			КонецЕсли;
			
			Если ДокументОбъект.ВерсияДанных <> ВыборкаПоДокументам.ВерсияДанных Тогда
				
				ОтменитьТранзакцию();
				Продолжить;
				
			КонецЕсли;
			
			ЕстьРасхождения = Ложь;
			Для Каждого СтрокаТЧ Из ДокументОбъект.Товары Цикл
				Если СтрокаТЧ.КоличествоФакт <> 0 Тогда
					ЕстьРасхождения = Истина;
				КонецЕсли;
			КонецЦикла;
			
			ДокументОбъект.ЕстьРасхождения = ЕстьРасхождения;
			
			ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
			Пока ВыборкаПоТоварам.Следующий() Цикл
				
				СтрокаТЧ = ДокументОбъект.Товары[ВыборкаПоТоварам.НомерСтроки - 1];
				СтрокаТЧ.Номенклатура       = ВыборкаПоТоварам.Номенклатура;
				СтрокаТЧ.Характеристика     = ВыборкаПоТоварам.Характеристика;
				СтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.Количество;
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать документ: %Ссылка% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%",  ВыборкаПоДокументам.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Документы.ТТНИсходящаяЕГАИС,
				ВыборкаПоДокументам.Ссылка,
				ТекстСообщения);
			
			Продолжить;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ОрганизацияЕГАИСПоОрганизацииИСкладу(Организация, Склад)
	
	МассивОрганизацийЕГАИС = ОрганизацииЕГАИСПоОрганизацииИСкладу(Организация, Склад);
	
	Если МассивОрганизацийЕГАИС = Неопределено 
		Или МассивОрганизацийЕГАИС.Количество() <> 1 Тогда
		
		Возврат Справочники.КлассификаторОрганизацийЕГАИС.ПустаяСсылка();
		
	Иначе
		
		Возврат МассивОрганизацийЕГАИС[0];
		
	КонецЕсли;
	
КонецФункции

Функция ОрганизацияЕГАИСПоПартнеруИКонтрагенту(Партнер, Контрагент)

	МассивОрганизацийЕГАИС = ОрганизацииЕГАИСПоПартнеруИКонтрагенту(Партнер, Контрагент);
	
	Если МассивОрганизацийЕГАИС = Неопределено 
		Или МассивОрганизацийЕГАИС.Количество() <> 1 Тогда
		
		Возврат Справочники.КлассификаторОрганизацийЕГАИС.ПустаяСсылка();
		
	Иначе
		
		Возврат МассивОрганизацийЕГАИС[0];
		
	КонецЕсли;

КонецФункции

Функция ОрганизацияПоОрганизацииЕГАИС(ОрганизацияЕГАИС)

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	КлассификаторОрганизацийЕГАИС.Контрагент
	|ИЗ
	|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|ГДЕ
	|	КлассификаторОрганизацийЕГАИС.СоответствуетОрганизации
	|	И КлассификаторОрганизацийЕГАИС.Ссылка = &ОрганизацияЕГАИС
	|	И КлассификаторОрганизацийЕГАИС.Контрагент <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("ОрганизацияЕГАИС", ОрганизацияЕГАИС);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Контрагент;
	Иначе
		Возврат Неопределено;
	КонецЕсли;	

КонецФункции

Функция ЕстьАлкогольнаяПродукция(ДокументСсылка, ИмяТабличнойЧасти, ИмяКолонки = "Номенклатура")
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	" + ДокументСсылка.Метаданные().ПолноеИмя() + "." + ИмяТабличнойЧасти + " КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументСсылка
	|	И    ТабличнаяЧасть." + ИмяКолонки + ".АлкогольнаяПродукция
	|	И Не ТабличнаяЧасть." + ИмяКолонки + ".АлкогольнаяПродукцияВоВскрытойТаре
	|");
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

Функция ЕстьМаркируемаяАлкогольнаяПродукция(ДокументСсылка, ИмяТабличнойЧасти, ИмяКолонки = "Номенклатура")
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	" + ДокументСсылка.Метаданные().ПолноеИмя() + "." + ИмяТабличнойЧасти + " КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументСсылка
	|	И    ТабличнаяЧасть." + ИмяКолонки + ".АлкогольнаяПродукция
	|	И    ТабличнаяЧасть." + ИмяКолонки + ".ВидАлкогольнойПродукции.Маркируемый
	|	И Не ТабличнаяЧасть." + ИмяКолонки + ".АлкогольнаяПродукцияВоВскрытойТаре
	|");
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

Функция ЕстьНеМаркируемаяАлкогольнаяПродукция(ДокументСсылка, ИмяТабличнойЧасти, ИмяКолонки = "Номенклатура")
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	" + ДокументСсылка.Метаданные().ПолноеИмя() + "." + ИмяТабличнойЧасти + " КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументСсылка
	|	И    ТабличнаяЧасть." + ИмяКолонки + ".АлкогольнаяПродукция
	|	И НЕ ТабличнаяЧасть." + ИмяКолонки + ".ВидАлкогольнойПродукции.Маркируемый
	|	И НЕ ТабличнаяЧасть." + ИмяКолонки + ".АлкогольнаяПродукцияВоВскрытойТаре
	|");
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

Функция ОрганизацииЕГАИСПоОрганизацииИСкладу(Организация, Склад)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	КлассификаторОрганизацийЕГАИС.Ссылка
	|ИЗ
	|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|ГДЕ
	|	НЕ КлассификаторОрганизацийЕГАИС.ПометкаУдаления
	|	И КлассификаторОрганизацийЕГАИС.СоответствуетОрганизации
	|	И КлассификаторОрганизацийЕГАИС.Контрагент = &Организация
	|	И КлассификаторОрганизацийЕГАИС.ТорговыйОбъект = &Склад
	|";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад",       Склад);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция ОрганизацииЕГАИСПоПартнеруИКонтрагенту(Партнер, Контрагент)
	
	Если Не ЗначениеЗаполнено(Контрагент) Или Не ЗначениеЗаполнено(Партнер) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	КлассификаторОрганизацийЕГАИС.Ссылка
	|ИЗ
	|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|ГДЕ
	|	НЕ КлассификаторОрганизацийЕГАИС.ПометкаУдаления
	|	И КлассификаторОрганизацийЕГАИС.Сопоставлено
	|	И КлассификаторОрганизацийЕГАИС.Контрагент = &Контрагент
	|	И КлассификаторОрганизацийЕГАИС.ТорговыйОбъект = &Партнер
	|	И НЕ КлассификаторОрганизацийЕГАИС.СоответствуетОрганизации";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Партнер", Партнер);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Процедура ДобавитьВРезультат(Результат, Приоритет, ДобавляемоеЗначение)
	
	Если ДобавляемоеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Значение = Результат.Получить(Приоритет);
	Если Значение = Неопределено Тогда
		Значение = Новый Массив();
	КонецЕсли;
	Значение.Добавить(ДобавляемоеЗначение);
	
	Результат.Вставить(Приоритет, Значение);
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, РезультатЗапроса, ДанныеЗаполнения, БезСопоставления = Ложь)
	
	ВыборкаТовары = РезультатЗапроса.Выбрать();
	
	Если ВыборкаТовары.Количество() = 0 Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru='В %1 отсутствует алкогольная продукция для заполнения.'"),
			ДанныеЗаполнения);
		
	КонецЕсли;
	
	Пока ВыборкаТовары.Следующий() Цикл
		
		Если ЗначениеЗаполнено(ВыборкаТовары.АлкогольнаяПродукция) ИЛИ БезСопоставления Тогда
			
			НоваяСтрока = ДокументОбъект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
			
		Иначе
			
			Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ВыборкаТовары, "Номенклатура") Тогда
				
				ДокументОбъект.Товары.Очистить();
				
				ТекстОшибки = НСтр("ru='Не выполнено сопоставление %Номенклатура% классификатору номенклатуры ЕГАИС.'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Номенклатура%", ВыборкаТовары.Номенклатура);
				
				ВызватьИсключение ТекстОшибки;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет служебные реквизиты в табличной части "Товары" в процедурах ПриСозданииНаСервере
//
// Параметры:
//  Форма				 - УправляемаяФорма - Форма
//  ИмяКолонкиКоличество - Строка - Имя колонки "Количество"
//
Процедура АкцизныеМаркиЗаполнитьСлужебныеРеквизиты(Форма, ИмяКолонкиКоличество = "Количество") Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект") Тогда
		Источник = Форма.Объект;
	Иначе
		Источник = Форма;
	КонецЕсли;
	
	Товары = Источник.Товары.Выгрузить();
	ДополнениеКИндексу = 0;
	Если Товары.Колонки.Найти("НомерСтроки") = Неопределено Тогда
		
		ДополнениеКИндексу = 1;
		Товары.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 0)));
		
		КоличествоСтрок = Товары.Количество() - 1;
		Для НомерСтроки = 0 По КоличествоСтрок Цикл
			Товары[НомерСтроки].НомерСтроки = НомерСтроки;
		КонецЦикла;
		
	КонецЕсли;
	
	ОрганизацияЕГАИС = Источник.ОрганизацияЕГАИС;
	
	ШтрихкодыУпаковок = АкцизныеМаркиЕГАИС.ШтрихкодыУпаковок(Источник, ОрганизацияЕГАИС, "НоменклатураЕГАИС");
	ШтрихкодыУпаковок.МаркированныеТовары.Индексы.Добавить("ИдентификаторСтроки");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Т.НомерСтроки       КАК НомерСтроки,
	|	Т.НоменклатураЕГАИС КАК АлкогольнаяПродукция,
	|	Т.Номенклатура      КАК Номенклатура
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР КОГДА Товары.Номенклатура.АлкогольнаяПродукцияВоВскрытойТаре ТОГДА
	|		ЛОЖЬ
	|	ИНАЧЕ
	|		ЕСТЬNULL(Товары.Номенклатура.ВидАлкогольнойПродукции.Маркируемый, ЛОЖЬ)
	|	КОНЕЦ КАК МаркируемаяАлкогольнаяПродукция
	|ИЗ
	|	Товары КАК Товары");
	
	Запрос.Параметры.Вставить("Товары", Товары);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокаТЧ = Источник.Товары[Выборка.НомерСтроки - 1 + ДополнениеКИндексу];
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ИдентификаторСтроки", СтрокаТЧ.ИдентификаторСтроки);
		КоличествоАкцизныхМарок = ШтрихкодыУпаковок.МаркированныеТовары.НайтиСтроки(ПараметрыОтбора).Количество();
		
		СтрокаТЧ.МаркируемаяАлкогольнаяПродукция = Выборка.МаркируемаяАлкогольнаяПродукция;
		СтрокаТЧ.КоличествоАкцизныхМарок = КоличествоАкцизныхМарок;
		
		АкцизныеМаркиКлиентСервер.ЗаполнитьИндексАкцизнойМарки(СтрокаТЧ, ИмяКолонкиКоличество);
		
	КонецЦикла;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "АдресСоответствияАкцизныхМарок") Тогда
		
		ВложенныеШтрихкоды = Новый Соответствие;
		АкцизныеМаркиЕГАИС.ПроверитьИЗаполнитьВложенныеШтрихкоды(ШтрихкодыУпаковок.ДеревоУпаковок.Строки, ВложенныеШтрихкоды, Новый Соответствие);
		
		Если ЭтоАдресВременногоХранилища(Форма.АдресСоответствияАкцизныхМарок) Тогда
			ПоместитьВоВременноеХранилище(ВложенныеШтрихкоды, Форма.АдресСоответствияАкцизныхМарок);
		Иначе
			Форма.АдресСоответствияАкцизныхМарок = ПоместитьВоВременноеХранилище(ВложенныеШтрихкоды, Форма.УникальныйИдентификатор);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрыЧека(ДокументСсылка) Экспорт
	
	ДанныеЖурнала = ПодключаемоеОборудованиеУТВызовСервера.ДанныеЖурналаФискальныхОпераций(ДокументСсылка);
	Если ДанныеЖурнала <> Неопределено Тогда
		ПараметрыРегистрации = МенеджерОборудованияВызовСервера.ПолучитьПараметрыРегистрацииУстройства(ДанныеЖурнала.Устройство);
		Если ПараметрыРегистрации = Неопределено Тогда
			НомерЧека     = ДанныеЖурнала.НомерЧекаККМ;
			НомерСмены    = ДанныеЖурнала.НомерСменыККМ;
			СерийныйНомер = ДанныеЖурнала.УстройствоСерийныйНомер;
		Иначе
			НомерЧека     = ДанныеЖурнала.НомерЧекаККМ;
			НомерСмены    = ДанныеЖурнала.НомерСменыККМ;
			СерийныйНомер = ПараметрыРегистрации.ЗаводскойНомерФН;
		КонецЕсли;
	Иначе
		НомерЧека     = "";
		НомерСмены    = "";
		СерийныйНомер = "";
	КонецЕсли;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("НомерЧека",     НомерЧека);
	ВозвращаемоеЗначение.Вставить("НомерСмены",    НомерСмены);
	ВозвращаемоеЗначение.Вставить("СерийныйНомер", СерийныйНомер);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область СерииНоменклатуры

Функция ПараметрыУказанияСерийТТНИсходящаяЕГАИС(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ТТНИсходящаяЕГАИС";
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено;
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Упаковка");
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийТТНВходящаяЕГАИС(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ТТНВходящаяЕГАИС";
	
	Если ТипЗнч(Объект.ТорговыйОбъект) = Тип("СправочникСсылка.Склады") Тогда
		
		ПараметрыУказанияСерий.ИмяПоляСклад = "ТорговыйОбъект";
		ПараметрыСерийСклада = СкладыСервер.ИспользованиеСерийНаСкладе(Объект.ТорговыйОбъект, Ложь);
	
		ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ПараметрыСерийСклада.ИспользоватьСерииНоменклатуры
															Или ПараметрыСерийСклада.УчитыватьСебестоимостьПоСериям;
		ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ПараметрыСерийСклада.УчитыватьСебестоимостьПоСериям;
	
		Если ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
			СкладскаяОперация = Перечисления.СкладскиеОперации.ПриемкаПоПеремещению;
		ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийТТНВходящейЕГАИС.ВозвратОтПокупателя Тогда
			СкладскаяОперация = Перечисления.СкладскиеОперации.ПриемкаПоВозвратуОтКлиента;
		Иначе
			СкладскаяОперация = Перечисления.СкладскиеОперации.ПриемкаОтПоставщика;
		КонецЕсли;
	
		ПараметрыУказанияСерий.СкладскиеОперации.Добавить(СкладскаяОперация);
		
	Иначе
		
		ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено;
	
		ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = Ложь;
		ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = Ложь;
		
	КонецЕсли;
	
	ПараметрыУказанияСерий.ИмяТЧСерии = "Товары";
	
	ПараметрыУказанияСерий.Дата = Объект.Дата;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийЧекЕГАИС(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ЧекЕГАИС";
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено;
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Упаковка");
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийЧекЕГАИСВозврат(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ЧекЕГАИСВозврат";
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено;
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Упаковка");
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийАктПостановкиНаБалансЕГАИС(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.АктПостановкиНаБалансЕГАИС";
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено;
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Упаковка");
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийАктСписанияЕГАИС(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.АктСписанияЕГАИС";
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено;
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Упаковка");
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийПередачаВРегистр2ЕГАИС(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ПередачаВРегистр2ЕГАИС";
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено;
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Упаковка");
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийВозвратИзРегистра2ЕГАИС(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ВозвратИзРегистра2ЕГАИС";
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии = "Товары";
	ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено;
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Упаковка");
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийСопоставлениеКлассификаторовЕГАИС(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Обработка.СопоставлениеКлассификаторовЕГАИС";
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии = "Товары";
	ПараметрыУказанияСерий.ИмяПоляКоличество = Неопределено;
	ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено;
	ПараметрыУказанияСерий.ИмяИсточникаЗначенийВФормеОбъекта = "ЭтаФорма";
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийШтрихкодыУпаковокТоваров(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Справочник.ШтрихкодыУпаковокТоваров";
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ТоварВШапке = Истина;
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии = "";
	ПараметрыУказанияСерий.ИмяТЧТовары = "";
	ПараметрыУказанияСерий.ИмяПоляКоличество = Неопределено;
	ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено;
	ПараметрыУказанияСерий.ИмяИсточникаЗначенийВФормеОбъекта = "ЭтаФорма";
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ПараметрыУказанияСерийСоответствиеНоменклатурыЕГАИС(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "РегистрСведений.СоответствиеНоменклатурыЕГАИС";
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	ПараметрыУказанияСерий.ТоварВШапке = Истина;
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.ИмяТЧСерии = "";
	ПараметрыУказанияСерий.ИмяТЧТовары = "";
	ПараметрыУказанияСерий.ИмяПоляКоличество = Неопределено;
	ПараметрыУказанияСерий.ИмяПоляСклад = Неопределено;
	ПараметрыУказанияСерий.ИмяИсточникаЗначенийВФормеОбъекта = "ЭтаФорма";
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Товары.Номенклатура,
		|	Товары.Серия,
		|	Товары.СтатусУказанияСерий,
		|	Товары.НомерСтроки
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.НомерСтроки КАК НомерСтроки,
		|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
		|	ВЫБОР
		|		КОГДА ВидыНоменклатуры.НастройкаИспользованияСерий = ЗНАЧЕНИЕ(Перечисление.НастройкиИспользованияСерийНоменклатуры.ПустаяСсылка)
		|			ИЛИ ВидыНоменклатуры.НастройкаИспользованияСерий ЕСТЬ NULL
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|					ТОГДА 2
		|				ИНАЧЕ 21
		|			КОНЕЦ
		|	КОНЕЦ КАК СтатусУказанияСерий
		|ПОМЕСТИТЬ ТаблицаСтатусов
		|ИЗ
		|	Товары КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО (ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры = ВидыНоменклатуры.Ссылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСтатусов.НомерСтроки КАК НомерСтроки,
		|	ТаблицаСтатусов.СтатусУказанияСерий КАК СтатусУказанияСерий
		|ИЗ
		|	ТаблицаСтатусов КАК ТаблицаСтатусов
		|ГДЕ
		|	ТаблицаСтатусов.СтарыйСтатусУказанияСерий <> ТаблицаСтатусов.СтатусУказанияСерий
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЗаполненияСтатусовУказанияСерийШтрихкодыУпаковокТоваров(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Товары.Номенклатура,
		|	Товары.Серия,
		|	Товары.СтатусУказанияСерий,
		|	Товары.НомерСтроки
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.НомерСтроки КАК НомерСтроки,
		|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
		|	ВЫБОР
		|		КОГДА ВидыНоменклатуры.НастройкаИспользованияСерий = ЗНАЧЕНИЕ(Перечисление.НастройкиИспользованияСерийНоменклатуры.ПустаяСсылка)
		|			ИЛИ ВидыНоменклатуры.НастройкаИспользованияСерий ЕСТЬ NULL
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|					ТОГДА 13
		|				ИНАЧЕ 13
		|			КОНЕЦ
		|	КОНЕЦ КАК СтатусУказанияСерий
		|ПОМЕСТИТЬ ТаблицаСтатусов
		|ИЗ
		|	Товары КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО (ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры = ВидыНоменклатуры.Ссылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСтатусов.НомерСтроки КАК НомерСтроки,
		|	ТаблицаСтатусов.СтатусУказанияСерий КАК СтатусУказанияСерий
		|ИЗ
		|	ТаблицаСтатусов КАК ТаблицаСтатусов
		|ГДЕ
		|	ТаблицаСтатусов.СтарыйСтатусУказанияСерий <> ТаблицаСтатусов.СтатусУказанияСерий
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЗаполненияСтатусовУказанияСерийСоответствиеНоменклатурыЕГАИС(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Товары.Номенклатура,
		|	Товары.Серия,
		|	Товары.СтатусУказанияСерий,
		|	Товары.НомерСтроки
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.НомерСтроки КАК НомерСтроки,
		|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
		|	ВЫБОР
		|		КОГДА ВидыНоменклатуры.НастройкаИспользованияСерий = ЗНАЧЕНИЕ(Перечисление.НастройкиИспользованияСерийНоменклатуры.ПустаяСсылка)
		|			ИЛИ ВидыНоменклатуры.НастройкаИспользованияСерий ЕСТЬ NULL
		|			ТОГДА 0
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|					ТОГДА 13
		|				ИНАЧЕ 13
		|			КОНЕЦ
		|	КОНЕЦ КАК СтатусУказанияСерий
		|ПОМЕСТИТЬ ТаблицаСтатусов
		|ИЗ
		|	Товары КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО (ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры = ВидыНоменклатуры.Ссылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСтатусов.НомерСтроки КАК НомерСтроки,
		|	ТаблицаСтатусов.СтатусУказанияСерий КАК СтатусУказанияСерий
		|ИЗ
		|	ТаблицаСтатусов КАК ТаблицаСтатусов
		|ГДЕ
		|	ТаблицаСтатусов.СтарыйСтатусУказанияСерий <> ТаблицаСтатусов.СтатусУказанияСерий
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЗаполненияСтатусовУказанияСерийТТНВходящаяЕГАИС(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.Номенклатура,
	|	Товары.Серия,
	|	Товары." + ПараметрыУказанияСерий.ИмяПоляКоличество + " КАК Количество,
	|	Товары.СтатусУказанияСерий
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL
	|			ИЛИ ПолитикиУчетаСерий.ПолитикаУчетаСерий = ЗНАЧЕНИЕ(Справочник.ПолитикиУчетаСерий.ПустаяСсылка)
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|			КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки
	|				ТОГДА ВЫБОР
	|					КОГДА Товары.Количество > 0 И Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА ВЫБОР
	|							КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|								ТОГДА 14
	|							ИНАЧЕ 10
	|						КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|						КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|							ТОГДА 13
	|						ИНАЧЕ 9
	|					КОНЕЦ
	|				КОНЕЦ
	|			КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтбора
	|				ТОГДА ВЫБОР
	|					КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчетСерийПоFEFO
	|						ТОГДА ВЫБОР
	|							КОГДА Товары.Количество > 0 И Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА 6
	|							ИНАЧЕ 5
	|						КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|						КОГДА Товары.Количество > 0 И Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|							ТОГДА 8
	|						ИНАЧЕ 7
	|					КОНЕЦ
	|				КОНЕЦ
	|			КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПриемке
	|				И (ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПриемкеОтПоставщика
	|					И &ПриемкаОтПоставщика
	|				  ИЛИ ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПриемкеПоВозвратуОтКлиента
	|					И &ПриемкаПоВозвратуОтКлиента
	|				  ИЛИ ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПриемкеПоПеремещению
	|					И &ПриемкаПоПеремещению
	|				  ИЛИ ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПриемкеПоПрочемуОприходованию
	|					И &ПриемкаПоПрочемуОприходованию)
	|				ТОГДА ВЫБОР
	|					КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|						ТОГДА ВЫБОР
	|							КОГДА Товары.Количество > 0 И Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА 4
	|							ИНАЧЕ 3
	|						КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|						КОГДА Товары.Количество > 0 И Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|							ТОГДА 2
	|						ИНАЧЕ 1
	|					КОНЕЦ
	|				КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ ТаблицаСтатусов
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|		ПО (ПолитикиУчетаСерий.Склад = &Склад)
	|			И (ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСтатусов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаСтатусов.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	ТаблицаСтатусов КАК ТаблицаСтатусов
	|ГДЕ
	|	ТаблицаСтатусов.СтарыйСтатусУказанияСерий <> ТаблицаСтатусов.СтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти