
#Область ПрограммныйИнтерфейс

// Записать операцию в журнал фискальных операций.
//
// Параметры:
//  РеквизитыОперацииКассовогоУзла - Структура - см. функцию ПодключаемоеОборудованиеУТКлиент.СтруктураРеквизитыФискальнойОперацииКассовогоУзла().
//
Процедура ЗаписатьВЖурналФискальныхОперацийУпрощенно(РеквизитыОперацииКассовогоУзла) Экспорт
	
	ИдентификаторЗаписи = Новый УникальныйИдентификатор;
	
	НаборЗаписей = РегистрыСведений.ЖурналФискальныхОпераций.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИдентификаторЗаписи.Установить(ИдентификаторЗаписи, Истина);
	НаборЗаписей.Отбор.ДокументОснование.Установить(РеквизитыОперацииКассовогоУзла.ДокументОснование, Истина);
	
	НоваяСтрока = НаборЗаписей.Добавить();
	НоваяСтрока.ИдентификаторЗаписи = ИдентификаторЗаписи;
	
	НоваяСтрока.Дата = РеквизитыОперацииКассовогоУзла.Дата;
	НоваяСтрока.ДокументОснование = РеквизитыОперацииКассовогоУзла.ДокументОснование;
	НоваяСтрока.Организация = РеквизитыОперацииКассовогоУзла.Организация;
	НоваяСтрока.ТорговыйОбъект = РеквизитыОперацииКассовогоУзла.ТорговыйОбъект;
	НоваяСтрока.Устройство = РеквизитыОперацииКассовогоУзла.Устройство;
	
	НоваяСтрока.ТипОперации = РеквизитыОперацииКассовогоУзла.ТипОперации;
	
	Если РеквизитыОперацииКассовогоУзла.ТипОперации = Перечисления.ТипыОперацииКассовогоУзла.ФискальнаяОперация Тогда
		
		НоваяСтрока.ФискальнаяОперацияТипРасчета = РеквизитыОперацииКассовогоУзла.ТипРасчета;
		
		НоваяСтрока.ФискальнаяОперацияВариантОтправкиЭлектронногоЧека = РеквизитыОперацииКассовогоУзла.ВариантОтправкиЭлектронногоЧека;
		НоваяСтрока.ФискальнаяОперацияКонтактныеДанныеЭлектронногоЧека = РеквизитыОперацииКассовогоУзла.КонтактныеДанныеЭлектронногоЧека;
		НоваяСтрока.ФискальнаяОперацияНомерЧекаККМ = РеквизитыОперацииКассовогоУзла.НомерЧекаККМ;
		НоваяСтрока.ФискальнаяОперацияНомерСменыККМ = РеквизитыОперацииКассовогоУзла.НомерСмены;
		
		НоваяСтрока.ФискальнаяОперацияСумма = РеквизитыОперацииКассовогоУзла.Сумма;
		НоваяСтрока.ФискальнаяОперацияСуммаОплатыНаличные = РеквизитыОперацииКассовогоУзла.СуммаОплатыНаличные;
		НоваяСтрока.ФискальнаяОперацияСуммаОплатыПлатежнаяКарта = РеквизитыОперацииКассовогоУзла.СуммаОплатыПлатежнаяКарта;
		НоваяСтрока.ФискальнаяОперацияСуммаКредит = РеквизитыОперацииКассовогоУзла.СуммаКредит;
		НоваяСтрока.ФискальнаяОперацияСуммаПредоплаты = РеквизитыОперацииКассовогоУзла.СуммаПредоплаты;
		
		Если РеквизитыОперацииКассовогоУзла.Данные <> Неопределено Тогда
			НоваяСтрока.Данные = Новый ХранилищеЗначения(РеквизитыОперацииКассовогоУзла.Данные);
		КонецЕсли;
		
	Иначе
		
		НоваяСтрока.ФискальнаяОперацияСумма = РеквизитыОперацииКассовогоУзла.Сумма;
		
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Сравнивает переданную версию ФФД на актуальность (На текущий момент актуальная версия 1.0.5)
//
// Параметры:
//  ВерсияФФД - Строка - Версия ФФД.
// 
// Возвращаемое значение:
//  Булево - Версия актуальна.
//
Функция ПоддерживаетсяФорматФФД1_0_5(ВерсияФФД) Экспорт
	
	Возврат ВерсияФФД = "1.0.5";
	
КонецФункции

#КонецОбласти
