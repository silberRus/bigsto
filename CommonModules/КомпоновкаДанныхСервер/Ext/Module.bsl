
#Область ПрограммныйИнтерфейс

// Процедура формирует отчет-расшифровку, по описанию, подготовленному соотвествующей фукнцией клиентского модуля 
//
// Параметры:
//		Форма - Форма - форма, в которой расположен отчет
//		ИмяРеквизитаФормы - Строка - имя реквизита формы типа ОтчетОбъект
//		ОписаниеОбработкиРасшифровки - ОписаниеОбработкиРасшифровкиКомпоновкиДанных - значение, сформированное соотвествующей клиентской функцией
//
Процедура ОбработатьРасшифровку(Форма, ИмяРеквизитаФормы, ОписаниеОбработкиРасшифровки) Экспорт
	ОтчетОбъект = Форма.РеквизитФормыВЗначение(ИмяРеквизитаФормы);
	
	ДанныеРасшифровки = ПолучитьИзВременногоХранилища(Форма["АдресДанныхРасшифровки" + ИмяРеквизитаФормы]);
	ИсточникДоступныхНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(Форма["АдресСхемы" + ИмяРеквизитаФормы]);
	
	ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ДанныеРасшифровки, ИсточникДоступныхНастроек);
	
	РезультирующиеНастройки = ОбработкаРасшифровки.ПрименитьНастройки(ОписаниеОбработкиРасшифровки.Идентификатор, 
																	ОписаниеОбработкиРасшифровки.ПрименяемыеНастройки);
	
	ОтчетОбъект.КомпоновщикНастроек.Инициализировать(ИсточникДоступныхНастроек);
	Если ТипЗнч(РезультирующиеНастройки) = Тип("НастройкиКомпоновкиДанных") Тогда
		ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(РезультирующиеНастройки);
	ИначеЕсли ТипЗнч(РезультирующиеНастройки) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
		ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(РезультирующиеНастройки);
	КонецЕсли;
	
	// Получить таблицу для вывода отчета.
	ТаблицаРезультатаОтчета = Форма["Таблица" + ИмяРеквизитаФормы];
	ТаблицаРезультатаОтчета.Очистить();
	
	// Вывести отчет.
	ОтчетОбъект.СкомпоноватьРезультат(ТаблицаРезультатаОтчета, ДанныеРасшифровки);
	
	Форма["АдресДанныхРасшифровки" + ИмяРеквизитаФормы] = ПоместитьВоВременноеХранилище(ДанныеРасшифровки, Новый УникальныйИдентификатор());
КонецПроцедуры

// Процедура формирует отчет, расположенный в форме настройками, которые сделал пользователь 
//
// Параметры:
//		Форма - Форма - форма, в которой расположен отчет
//		ИмяРеквизитаФормы - Строка - имя реквизита формы типа ОтчетОбъект
//		ИмяОтчета - Строка - имя формируемого отчета в метаданных
//		ИмяВариантаНастроек - Строка - имя варианта отчета в настройках отчета
//
Процедура СформироватьОтчет(Форма, ИмяРеквизитаФормы, ИмяОтчета, ИмяВариантаНастроек) Экспорт
	Перем ДанныеРасшифровки;
	
	Если Не ПравоДоступа("Использование", Метаданные.Отчеты[ИмяОтчета]) Тогда
		Возврат;
	КонецЕсли;
	
	// При обновлении отчета применяются пользовательские настройки.
	
	ОтчетОбъект = Форма.РеквизитФормыВЗначение(ИмяРеквизитаФормы);		
	
	Форма["АдресСхемы" + ИмяРеквизитаФормы] = ПоместитьВоВременноеХранилище(ОтчетОбъект.СхемаКомпоновкиДанных, Форма.УникальныйИдентификатор);
	ИсточникДоступныхНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(Форма["АдресСхемы" + ИмяРеквизитаФормы]);
	
	ОтчетОбъект.КомпоновщикНастроек.Инициализировать(ИсточникДоступныхНастроек);
	ОтчетОбъект.КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
	ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ОтчетОбъект.КомпоновщикНастроек.ПользовательскиеНастройки);
	
	Если ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Такси Тогда
		ПараметрМакетОформления = ОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыВывода.Элементы.Найти("МакетОформления");
		Если ПараметрМакетОформления.Значение = "Main" 
			Или ПараметрМакетОформления.Значение = "Основной" Тогда
			ПараметрМакетОформления.Значение = "ОформлениеОтчетовБежевый";
			ПараметрМакетОформления.Использование = Истина;
		КонецЕсли;
		
		Для Каждого ЭлементСтруктуры Из ОтчетОбъект.КомпоновщикНастроек.Настройки.Структура Цикл
			Если ТипЗнч(ЭлементСтруктуры) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
				ПараметрМакетОформления = ЭлементСтруктуры.Настройки.ПараметрыВывода.Элементы.Найти("МакетОформления");
				Если ПараметрМакетОформления.Значение = "Main" 
					Или ПараметрМакетОформления.Значение = "Основной" Тогда
					ПараметрМакетОформления.Значение = "ОформлениеОтчетовБежевый";
					ПараметрМакетОформления.Использование = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Форма.Элементы["Таблица" + ИмяРеквизитаФормы].РежимМасштабированияПросмотра = РежимМасштабированияПросмотра.Обычный;
	КонецЕсли;
	
	// Вывести отчет.
	ТаблицаРезультатаОтчета = Форма["Таблица" + ИмяРеквизитаФормы];
	ТаблицаРезультатаОтчета.Очистить();
	
	ОтчетОбъект.СкомпоноватьРезультат(ТаблицаРезультатаОтчета, ДанныеРасшифровки);

	Форма["АдресДанныхРасшифровки" + ИмяРеквизитаФормы] = ПоместитьВоВременноеХранилище(ДанныеРасшифровки, Новый УникальныйИдентификатор());
КонецПроцедуры

// Процедура инициализирует отчет, расположенный в форме настройками по умолчанию и устанавливает контекстные отборы 
//
// Параметры:
//		Форма - Форма - форма, в которой расположен отчет
//		ИмяРеквизитаФормы - Строка - имя реквизита формы типа ОтчетОбъект
//		ИмяОтчета - Строка - имя формируемого отчета в метаданных
//		ИмяВариантаНастроек - Строка - имя варианта отчета в настройках отчета
//      Отбор - Структура - ключ - имя отбора, значение - значение отбора
//      Параметры - Структура - ключ - имя параметра, значение - значение параметра
//
Процедура УстановитьСтандартныеНастройки(Форма, ИмяРеквизитаФормы, ИмяОтчета, ИмяВариантаНастроек, Отбор, Параметры = Неопределено) Экспорт
	Если Не ПравоДоступа("Использование", Метаданные.Отчеты[ИмяОтчета]) Тогда
		Возврат;
	КонецЕсли;
	
	ОтчетОбъект = Форма.РеквизитФормыВЗначение(ИмяРеквизитаФормы);		
	
	Форма["АдресСхемы" + ИмяРеквизитаФормы] = ПоместитьВоВременноеХранилище(ОтчетОбъект.СхемаКомпоновкиДанных, Форма.УникальныйИдентификатор);
	
	ИсточникДоступныхНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(Форма["АдресСхемы" + ИмяРеквизитаФормы]);
	
	ОтчетОбъект.КомпоновщикНастроек.Инициализировать(ИсточникДоступныхНастроек);
	
	НастройкиКомпоновкиДанных = ОтчетОбъект.СхемаКомпоновкиДанных.ВариантыНастроек[ИмяВариантаНастроек].Настройки;
	ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиКомпоновкиДанных);
	
	Для Каждого Элемент из Отбор Цикл
		ЭлементОтбора = ОтчетОбъект.КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Элемент.Ключ);
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ПравоеЗначение = Элемент.Значение;
		ЭлементОтбора.Использование = Истина;
	КонецЦикла;
	
	Если Параметры <> Неопределено Тогда
		Для каждого Элемент Из Параметры Цикл
			ОтчетОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра(Элемент.Ключ, Элемент.Значение);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

// Возвращает строковое представление уникального идентификатора
// Символы "-" заменяются на "_"
// Предназначена для использования в выражениях СКД
// 
// Параметры:
//	ИсточникИдентификатора - Ссылка, Строка, УникальныйИдентификатор - любая ссылка, строка, число
//
// Возвращаемое значение:
//	Строка - строковое представление уникального идентификатора
//
Функция СтрУникальныйИдентификатор(ИсточникИдентификатора) Экспорт
	СтрУникальныйИдентификатор = Неопределено;
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ИсточникИдентификатора)) Тогда
		УникальныйИдентификатор = ИсточникИдентификатора.УникальныйИдентификатор();
		СтрУникальныйИдентификатор = МониторингЦелевыхПоказателей.УникальныйИдентификаторВСтроку(УникальныйИдентификатор);
	ИначеЕсли ТипЗнч(ИсточникИдентификатора) = Тип("УникальныйИдентификатор") Тогда
		СтрУникальныйИдентификатор = МониторингЦелевыхПоказателей.УникальныйИдентификаторВСтроку(УникальныйИдентификатор);
	Иначе
		СтрУникальныйИдентификатор = ИсточникИдентификатора;
	КонецЕсли;
	
	Возврат СтрУникальныйИдентификатор;
КонецФункции

#Область ПостобработкаОтчетов

// Процедура изменяет настройки диаграмм и их элементов в части оформления 
// Параметры оформления (поддерживаемые) могут использовать независимо.
//
// Параметры:
//		КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - настройки отчета
//		ТабличныйДокумент - ТабличныйДокумент - результат вывода отчета
//		ПараметрыДиаграмм - Структура - Структура изменяемых параметров диаграмм
//			Поддерживаемые параметры:
//				МаксимумСерийКоличество - Число - больше 0, определяет количество выводимых серий диаграммы, "лишние" серии объединяются в сводную серию.
//					Значение 999 используется для снятия ограничения и вывода всех серий.
//				МаксимумСерийПроцент - Число - 0 - 100%, , определяет количество выводимых серий диаграммы, "лишние" серии объединяются в сводную серию. 
//					Значение 999 используется для снятия ограничения и вывода всех серий.
//				ТипДиаграммы - Строка - название типа диаграммы, как оно задано в конфигураторе
//				ВыделениеСерийДиаграмм - Число - "0" - не выделять, "1" - выделять первую серию, "2" - Выделять отслеживаемые аналитики
//				ГрадиентСерийДиаграмм - Булево - включает/отключает режима градиента цветом серий данных
//
Процедура ОформитьДиаграммыОтчета(КомпоновщикНастроек, ТабличныйДокумент, ПараметрыДиаграмм = Неопределено) Экспорт
	Если ПараметрыДиаграмм = Неопределено Тогда
		ПараметрыДиаграмм = ПараметрыДиаграмм(КомпоновщикНастроек);
	КонецЕсли;
	Для Каждого Рисунок Из ТабличныйДокумент.Рисунки Цикл
		ОбъектРисунка = Рисунок.Объект;
		Если ТипЗнч(ОбъектРисунка) = Тип("Диаграмма") Тогда
			Рисунок.Линия = Новый Линия(ТипЛинииРисункаТабличногоДокумента.Сплошная);
			Рисунок.ЦветЛинии = ЦветаСтиля.ЦветРамки;
			
			УстановитьОформлениеПоУмолчаниюДиаграммы(ОбъектРисунка, ПараметрыДиаграмм);
		КонецЕсли;
	КонецЦикла;
	
	УдалитьПараметрыОформленияДиаграмм(КомпоновщикНастроек, ТабличныйДокумент, ПараметрыДиаграмм);
КонецПроцедуры

// Возвращает структуру параметров диаграмм для переопределения
//
// Параметры:
//		КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - настройки отчета
// Возвращаемое значение:
//		Структура - в ключе задается имя параметра, в значении - значение параметры
Функция ПараметрыДиаграмм(КомпоновщикНастроек) Экспорт
	ПараметрыДиаграмм = Новый Структура;
	
	ПараметрМаксимумСерий = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "МаксимумСерийКоличество");
	Если ПараметрМаксимумСерий <> Неопределено И ПараметрМаксимумСерий.Использование Тогда
		ПараметрыДиаграмм.Вставить("МаксимумСерийКоличество", ПараметрМаксимумСерий.Значение);
	КонецЕсли;
	
	ПараметрТипДиаграммы = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ТипДиаграммы");
	Если ПараметрТипДиаграммы <> Неопределено И ПараметрТипДиаграммы.Использование Тогда
		ПараметрыДиаграмм.Вставить("ТипДиаграммы", ПараметрТипДиаграммы.Значение);
	КонецЕсли;
	
	ПараметрВыделениеСерийДиаграмм = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ВыделениеСерийДиаграмм");
	Если ПараметрВыделениеСерийДиаграмм <> Неопределено И ПараметрВыделениеСерийДиаграмм.Использование Тогда
		ПараметрыДиаграмм.Вставить("ВыделениеСерийДиаграмм", ПараметрВыделениеСерийДиаграмм.Значение);
	КонецЕсли;
	
	ПараметрГрадиентСерийДиаграмм = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ГрадиентСерийДиаграмм");
	Если ПараметрГрадиентСерийДиаграмм <> Неопределено И ПараметрГрадиентСерийДиаграмм.Использование Тогда
		ПараметрыДиаграмм.Вставить("ГрадиентСерийДиаграмм", ПараметрГрадиентСерийДиаграмм.Значение);
	КонецЕсли;
	
	ПараметрОтображениеМаркеровТочекДиаграмм = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ОтображениеМаркеровТочекДиаграмм");
	Если ПараметрОтображениеМаркеровТочекДиаграмм <> Неопределено И ПараметрОтображениеМаркеровТочекДиаграмм.Использование Тогда
		ПараметрыДиаграмм.Вставить("ОтображениеМаркеровТочекДиаграмм", ПараметрОтображениеМаркеровТочекДиаграмм.Значение);
	КонецЕсли;
	
	ПараметрОтслеживаемыеАналитики = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ОтслеживаемыеАналитики");
	Если ПараметрОтслеживаемыеАналитики <> Неопределено И ПараметрОтслеживаемыеАналитики.Использование Тогда
		ПараметрыДиаграмм.Вставить("ОтслеживаемыеАналитики", ПараметрОтслеживаемыеАналитики.Значение);
	КонецЕсли;
	
	Возврат ПараметрыДиаграмм;
КонецФункции

// Процедура удаляет строки с вспомогательными параметрами из секции параметров отчета 
//
// Параметры:
//		ТабличныйДокумент - ТабличныйДокумент - результат вывода отчета
//		НеиспользуемыеПараметрыОтчета - Массив - представления несипользуемых параметров отчета
//
Процедура СкрытьВспомогательныеПараметрыОтчета(СхемаКомпоновкиДанных, КомпоновщикНастроек, ТабличныйДокумент, ПараметрыОтчета) Экспорт
	СкрываемыеПараметрыОтчета = Новый Массив;
	Для Каждого ПараметрОтчета Из ПараметрыОтчета Цикл
		ДобавитьПредставленияПараметраВМассив(СхемаКомпоновкиДанных, КомпоновщикНастроек, СкрываемыеПараметрыОтчета, ПараметрОтчета);
	КонецЦикла;
	
	Для Каждого СкрываемыйПараметрОтчета Из СкрываемыеПараметрыОтчета Цикл 
		ВосстановитьЗаголовокПараметров = Ложь;
		
		НайденнаяОбласть = ТабличныйДокумент.НайтиТекст(СкрываемыйПараметрОтчета);
		Если НайденнаяОбласть <> Неопределено 
			И НайденнаяОбласть.РежимИзмененияРазмераКолонки = РежимИзмененияРазмера.Обычный Тогда
			УдаляемаяОбласть = ТабличныйДокумент.Область(НайденнаяОбласть.Верх,, НайденнаяОбласть.Низ);
			
			Если СтрНайти(ТабличныйДокумент.Область(НайденнаяОбласть.Верх, НайденнаяОбласть.Лево - 1).Текст, 
					НСтр("ru= 'Параметры'") + ":") > 0 Тогда
				ВосстановитьЗаголовокПараметров = Истина;
				Строка = НайденнаяОбласть.Верх;
				Колонка = НайденнаяОбласть.Лево - 1;
			КонецЕсли;
			
			ТабличныйДокумент.УдалитьОбласть(УдаляемаяОбласть, ТипСмещенияТабличногоДокумента.ПоВертикали);
			
			Если ВосстановитьЗаголовокПараметров
				И ПустаяСтрока(ТабличныйДокумент.Область(Строка, Колонка + 1, Строка, Колонка + 1).Текст) Тогда
				ВосстановитьЗаголовокПараметров = Ложь;
			КонецЕсли;
			
			Если ВосстановитьЗаголовокПараметров Тогда
				ТабличныйДокумент.Область(Строка, Колонка, Строка, Колонка).Текст = НСтр("ru= 'Параметры'") + ":";
			КонецЕсли;
			
			Если ТабличныйДокумент.ФиксацияСверху <> 0 Тогда
				ТабличныйДокумент.ФиксацияСверху = ТабличныйДокумент.ФиксацияСверху - 1;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Процедура добавляет в массив вспомогательные параметры отчета,
// которые не отключаются автоматически по значениям функциональных опций
// Следует использовать совместно с методом ОтчетыУТПереопределяемый.НастроитьПараметрыОтборыПоФункциональнымОпциям
//
// Параметры:
//		ПараметрыОтчета - Массив - представления несипользуемых параметров отчета
//
Процедура ДобавитьВспомогательныеПараметрыОтчетаПоФункциональнымОпциям(ПараметрыОтчета) Экспорт
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		ПараметрыОтчета.Добавить("ПоказыватьПродажи");
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют")
		Или ПолучитьФункциональнуюОпцию("БазоваяВерсия") Тогда
		ПараметрыОтчета.Добавить("Валюта");
		ПараметрыОтчета.Добавить("ВалютаОтчета");
		ПараметрыОтчета.Добавить("ДанныеОтчета");
		ПараметрыОтчета.Добавить("ДанныеПоДенежнымСредствам");
		ПараметрыОтчета.Добавить("ДанныеПоРасчетам");
		ПараметрыОтчета.Добавить("ВыводитьСуммы");
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВидовЦен") Тогда
		ПараметрыОтчета.Добавить("ВидЦены");
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЕдиницыИзмеренияДляОтчетов") Тогда
		ПараметрыОтчета.Добавить("ЕдиницыКоличества");
	КонецЕсли;
КонецПроцедуры

// Перебирает ячейки макета компоновки и подменяет их текст.
//
// Параметры:
//		ПараметрыЗамены - Структура - Ключ определяет текст, заданный в схеме компоновки (обрамленный знаками "%"), значение - текст замены
//		МакетКомпоновки - МакетКомпоновкиДанных - Макет компоновки, заголовки которого будут подменены
Процедура УстановитьЗаголовкиМакетаКомпоновки(ПараметрыЗамены, МакетКомпоновки) Экспорт
	
	Если Не ТипЗнч(ПараметрыЗамены) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекМакет Из МакетКомпоновки.Макеты Цикл
		Если ТипЗнч(ТекМакет.Макет) <> Тип("МакетОбластиКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого СтрокаТаблицыКомпоновки Из ТекМакет.Макет Цикл
			Для Каждого ЯчейкаТаблицыОбластиКомпоновки Из СтрокаТаблицыКомпоновки.Ячейки Цикл
				Для Каждого Элемент Из ЯчейкаТаблицыОбластиКомпоновки.Элементы Цикл
					Для каждого Поле Из ПараметрыЗамены Цикл
						СтрокаЗамены = "%" + Поле.Ключ + "%";
						Если СтрНайти(Элемент.Значение, СтрокаЗамены) > 0 Тогда
							Элемент.Значение = СтрЗаменить(Элемент.Значение, СтрокаЗамены, Поле.Значение);
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Процедура осуществляет поиск представлений параметров по их имени
// и помещает найденные представления в переданный массив
//
// Параметры:
//		СхемаКомпоновкиДанных - СхемаКомпоновкиДанных - схема отчета
//		КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - настройки отчета
//		Параметры - Массив - скрываемые из табличного документа отчета параметры
//		ИмяПараметра - Строка - имя скрываемого параметра отчета
//
Процедура ДобавитьПредставленияПараметраВМассив(СхемаКомпоновкиДанных, КомпоновщикНастроек, Параметры, ИмяПараметра) Экспорт
	Параметр = СхемаКомпоновкиДанных.Параметры.Найти(ИмяПараметра);
	Если Не Параметр = Неопределено Тогда
		ЗаголовокПараметра = Параметр.Заголовок;
		
		ПользовательскоеПредставлениеПараметра = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, ИмяПараметра).ПредставлениеПользовательскойНастройки;
		
		Параметры.Добавить(ЗаголовокПараметра);
		Параметры.Добавить(ПользовательскоеПредставлениеПараметра);
	КонецЕсли;
КонецПроцедуры

#Область СтандартныеСтроковыеПараметрыЗамены
	
// Возвращает структуру для подмены заголовков полей веса, объема, таблицы группировки количества по единицам
//
// Параметры:
//		КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - настройки отчета
// Возвращаемое значение:
//		Структура - в ключе задается шаблон замены заголовка, в значении - значение замены
Функция СтруктураЗаголовковПолейЕдиницИзмерений(КомпоновщикНастроек) Экспорт
	
	СтруктураЗаголовков = Новый Структура;
	
	ЕдиницаВеса = Строка(Константы.ЕдиницаИзмеренияВеса.Получить());
	ЕдиницаОбъема = Строка(Константы.ЕдиницаИзмеренияОбъема.Получить());
	ПараметрЕдиницыКоличества = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ЕдиницыКоличества");
	Если ПараметрЕдиницыКоличества <> Неопределено И ПараметрЕдиницыКоличества.Значение = 0 Тогда
		ЗаголовокЕдИзм = НСтр("ru='Итого товаров в единицах хранения'");
	Иначе
		ЗаголовокЕдИзм = НСтр("ru='Итого товаров в единицах для отчетов'");
	КонецЕсли;
	
	СтруктураЗаголовков.Вставить("ЕдиницаВеса", ЕдиницаВеса);
	СтруктураЗаголовков.Вставить("ЕдиницаОбъема", ЕдиницаОбъема);
	СтруктураЗаголовков.Вставить("ЗаголовокГруппировкиЕдИзм", ЗаголовокЕдИзм);

	Возврат СтруктураЗаголовков;
	
КонецФункции

// Возвращает структуру для подмены шаблона %ВалютаОтчета%
//
// Параметры:
//		КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - настройки отчета
// Возвращаемое значение:
//		Структура - в ключе задается шаблон замены заголовка, в значении - значение замены
Функция СтруктураЗаголовковВалют(КомпоновщикНастроек, Скобки = Истина) Экспорт
	СтруктураЗаголовковПолей = Новый Структура;
	
	ИспользоватьНесколькоВалют = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют");
	ПараметрДанныеОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ДанныеОтчета");
	ДанныеОтчета = ПараметрДанныеОтчета.Значение;
	
	ВалютаУправленческогоУчета		= Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегламентированногоУчета	= Константы.ВалютаРегламентированногоУчета.Получить();
	
	Если ИспользоватьНесколькоВалют Тогда
		Если ДанныеОтчета = 1 
			Или ДанныеОтчета = 2 Тогда
			СтруктураЗаголовковПолей.Вставить("ВалютаОтчета", ?(Скобки, "(", "") + ВалютаУправленческогоУчета + ?(Скобки, ")", ""));
		ИначеЕсли ДанныеОтчета = 3 Тогда
			СтруктураЗаголовковПолей.Вставить("ВалютаОтчета", ?(Скобки, "(", "") + ВалютаРегламентированногоУчета + ?(Скобки, ")", ""));
		КонецЕсли;
	Иначе
		СтруктураЗаголовковПолей.Вставить("ВалютаОтчета", "");
	КонецЕсли;
	
	Возврат СтруктураЗаголовковПолей;
КонецФункции

// Возвращает структуру для подмены шаблона %ВалютаОтчета%
//
// Параметры:
//		КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - настройки отчета
// Возвращаемое значение:
//		Структура - в ключе задается шаблон замены заголовка, в значении - значение замены
Функция СтруктураЗаголовковВалютСквознаяСебестоимость(КомпоновщикНастроек, Скобки = Истина) Экспорт
	СтруктураЗаголовковПолей = Новый Структура;
	
	ИспользоватьНесколькоВалют = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют");
	ПараметрДанныеОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ДанныеОтчета");
	ДанныеОтчета = ПараметрДанныеОтчета.Значение;
	
	Если ИспользоватьНесколькоВалют Тогда
		
		НаборКонстант = Константы.СоздатьНабор("ВалютаУправленческогоУчета, ВалютаРегламентированногоУчета");
		НаборКонстант.Прочитать();
		
		ВалютаУправленческогоУчета		= НаборКонстант.ВалютаУправленческогоУчета;
		ВалютаРегламентированногоУчета	= НаборКонстант.ВалютаРегламентированногоУчета;
		
		Если ДанныеОтчета = 1 
			Или ДанныеОтчета = 2
			Или ДанныеОтчета = 3 Тогда
			СтруктураЗаголовковПолей.Вставить("ВалютаОтчета", ?(Скобки, "(", "") + ВалютаУправленческогоУчета + ?(Скобки, ")", ""));
		ИначеЕсли ДанныеОтчета = 4 Тогда
			СтруктураЗаголовковПолей.Вставить("ВалютаОтчета", ?(Скобки, "(", "") + ВалютаРегламентированногоУчета + ?(Скобки, ")", ""));
		КонецЕсли;
		
	Иначе
		СтруктураЗаголовковПолей.Вставить("ВалютаОтчета", "");
	КонецЕсли;
	
	Возврат СтруктураЗаголовковПолей;
	
КонецФункции

#КонецОбласти 

#КонецОбласти

#Область НастройкиОтчетов

// Процедура включает родительские группировки в пользовательских настройках, если включена хоть одна дочерняя
//
// Параметры:
//		КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - настройки отчета
//		ПользовательскиеНастройкиМодифицированы - Булево - обязательный к установке признак модификации польз. настроек отчета
//
Процедура ИсправитьНастройкиГруппировок(КомпоновщикНастроек, ПользовательскиеНастройкиМодифицированы) Экспорт
	ПользовательскиеНастройки = КомпоновщикНастроек.ПользовательскиеНастройки;
	Настройки = КомпоновщикНастроек.Настройки;
	
	Для Каждого ПользовательскаяНастройка Из ПользовательскиеНастройки.Элементы Цикл
		Если (ТипЗнч(ПользовательскаяНастройка) = Тип("ГруппировкаКомпоновкиДанных") 
			Или ТипЗнч(ПользовательскаяНастройка) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
			Или ТипЗнч(ПользовательскаяНастройка) = Тип("ТаблицаКомпоновкиДанных"))
			И ПользовательскаяНастройка.Использование Тогда
			ИсправитьНастройкиРодительскойГруппировки(ПользовательскаяНастройка, ПользовательскиеНастройки, Настройки, ПользовательскиеНастройкиМодифицированы);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Процедура устанавливает все связанные параметры валюты отчета
//
// Параметры:
//		КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - настройки отчета
//		ПользовательскиеНастройкиМодифицированы - Булево - обязательный к установке признак модификации польз. настроек отчета
//
Процедура УстановитьПараметрыВалютыОтчета(КомпоновщикНастроек, ПользовательскиеНастройкиМодифицированы) Экспорт
	ПараметрДанныеОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ДанныеОтчета");
	Если ПараметрДанныеОтчета <> Неопределено Тогда
		ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
		ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
		Если Не ЗначениеЗаполнено(ПараметрДанныеОтчета.Значение) Тогда
			КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ДанныеОтчета", 1);
			
			ПользовательскиеНастройкиМодифицированы = Истина;
		КонецЕсли;
		
		ПараметрВалюта = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Валюта");
		Если ПараметрВалюта <> Неопределено Тогда
			Если ПараметрДанныеОтчета.Значение = 1
				ИЛИ ПараметрДанныеОтчета.Значение = 2 Тогда
				КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Валюта", ВалютаУправленческогоУчета);
			ИначеЕсли ПараметрДанныеОтчета.Значение = 3 Тогда
				КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Валюта", ВалютаРегламентированногоУчета);
			КонецЕсли;
				
			ПользовательскиеНастройкиМодифицированы = Истина;
		КонецЕсли;	
	КонецЕсли;
	
	ПараметрДанныеОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ДанныеРасчета");
	Если ПараметрДанныеОтчета <> Неопределено Тогда
		ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
		ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
		ПараметрВалюта = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ВалютаРасчета");
		Если ПараметрВалюта <> Неопределено Тогда
			Если ПараметрДанныеОтчета.Значение = 1
				ИЛИ ПараметрДанныеОтчета.Значение = 2 Тогда
				КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВалютаРасчета", ВалютаУправленческогоУчета);
			ИначеЕсли ПараметрДанныеОтчета.Значение = 3 Тогда
				КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВалютаРасчета", ВалютаРегламентированногоУчета);
			ИначеЕсли ПараметрДанныеОтчета.Значение = 0 Тогда
				КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВалютаРасчета", Справочники.Валюты.ПустаяСсылка());
			КонецЕсли;
				
			ПользовательскиеНастройкиМодифицированы = Истина;
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

// Процедура устанавливает формулу расчета и формат динамического периода
//
// Параметры:
//		СхемаКомпоновкиДанных - СхемаКомпоновкиДанных - СКД отчета
//		КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - настройки отчета
//		ДополнятьПериод - Булево - признак использования дополнения периода
//
Процедура НастроитьДинамическийПериод(СхемаКомпоновкиДанных, КомпоновщикНастроек, ДополнятьПериод = Ложь) Экспорт 
	
	ПараметрПериодичность = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Периодичность");
	
	Если ПараметрПериодичность <> Неопределено 
		И КомпоновкаДанныхКлиентСервер.ПолеИспользуется(КомпоновщикНастроек, Новый ПолеКомпоновкиДанных("ДинамическийПериод")) Тогда
		
		ИскомоеПоле = СхемаКомпоновкиДанных.ВычисляемыеПоля.Найти("ДинамическийПериод");
		
		Если ИскомоеПоле <> Неопределено Тогда
			
			Если Не ЗначениеЗаполнено(ПараметрПериодичность.Значение) Тогда
				ВызватьИсключение НСтр("ru= 'Некорректная настройка параметра ""Периодичность"".'") ;
			КонецЕсли;
			
			СтрокаДлительностьПериода = ОбщегоНазначения.ИмяЗначенияПеречисления(ПараметрПериодичность.Значение);
			ИскомоеПоле.Выражение = "Период" + СтрокаДлительностьПериода;
			
			ПараметрОформленияФормат = ИскомоеПоле.Оформление.Элементы.Найти("Формат");
			ПараметрОформленияФормат.Значение = ФорматнаяСтрокаПериодовКомпоновкиДанных(ПараметрПериодичность.Значение);
			ПараметрОформленияФормат.Использование = Истина;
			
			Если ДополнятьПериод Тогда
				
				ДополнениеПериода = ТипДополненияПериодаКомпоновкиДанных[СтрокаДлительностьПериода];
				ПараметрПериод = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Период");
				
				Если ПараметрПериод <> Неопределено Тогда
					
					ПолеДинамическийПериод = Новый ПолеКомпоновкиДанных("ДинамическийПериод");
					Группировки = КомпоновкаДанныхКлиентСервер.ПолучитьГруппировки(КомпоновщикНастроек);
					
					ЛитералыПоддерживаемыхПолейПериодов = "ПериодГод,ПериодПолугодие,ПериодКвартал,ПериодМесяц,ПериодДекада,ПериодНеделя,ПериодДень";
					ПоддерживаемыеПоляПериодов = СтрРазделить(ЛитералыПоддерживаемыхПолейПериодов, ",");
					
					Для Каждого Группировка Из Группировки Цикл
						
						Если Группировка.Значение.ПоляГруппировки.Элементы.Количество() = 1
							И Группировка.Значение.ПоляГруппировки.Элементы[0].Поле = ПолеДинамическийПериод Тогда
							
							ГруппировкаДинамическийПериод = Группировка.Значение.ПоляГруппировки.Элементы[0];
							ГруппировкаРодитель = Группировка.Значение.Родитель;
							
							НачалоПериодаДополнения = ТипДополненияПериодаКомпоновкиДанных.БезДополнения;
							КонецПериодаДополнения = ТипДополненияПериодаКомпоновкиДанных.БезДополнения;
								
							// Включим дополнение периода в зависимости от размещения группировки
							ГруппировкаДинамическийПериод.ТипДополнения = ДополнениеПериода;
							
							// Корневой элемент или группировка первого уровня
							Если ТипЗнч(ГруппировкаРодитель) = Тип("НастройкиКомпоновкиДанных")
									Или ТипЗнч(ГруппировкаРодитель) = Тип("ТаблицаКомпоновкиДанных") 
									Или ТипЗнч(ГруппировкаРодитель) = Тип("ДиаграммаКомпоновкиДанных") Тогда
							
								НачалоПериодаДополнения = ПараметрПериод.Значение.ДатаНачала;
								КонецПериодаДополнения = ПараметрПериод.Значение.ДатаОкончания;
								
							// Есть родительская группировка и в ней одно поле, что характерно для группировок по периодам
							ИначеЕсли ТипЗнч(ГруппировкаРодитель) = Тип("ГруппировкаКомпоновкиДанных")
									Или ТипЗнч(ГруппировкаРодитель) = Тип("ГруппировкаТаблицыКомпоновкиДанных") 
									Или ТипЗнч(ГруппировкаРодитель) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
							
								ЭлементыГруппировкиРодителя = ГруппировкаРодитель.ПоляГруппировки.Элементы;
								Если ЭлементыГруппировкиРодителя.Количество() = 1 Тогда
									
									ИндексПоддерживаемогоПериодаРодителя = ПоддерживаемыеПоляПериодов.Найти(Строка(ЭлементыГруппировкиРодителя[0].Поле));
									Если ИндексПоддерживаемогоПериодаРодителя <> Неопределено Тогда
										ПоддерживаемыйПериодРодитель = ПоддерживаемыеПоляПериодов[ИндексПоддерживаемогоПериодаРодителя];
										
										ТипДополненияПоРодительскойГруппировке = ТипДополненияПериодаКомпоновкиДанных[СтрЗаменить(ПоддерживаемыйПериодРодитель, "Период", "")];
										
										НачалоПериодаДополнения = ТипДополненияПоРодительскойГруппировке;
										КонецПериодаДополнения = ТипДополненияПоРодительскойГруппировке;
									
									КонецЕсли;
									
								КонецЕсли;
								
							КонецЕсли;
							
							ГруппировкаДинамическийПериод.НачалоПериода = НачалоПериодаДополнения;
							ГруппировкаДинамическийПериод.КонецПериода = КонецПериодаДополнения;
							
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает значение параметра схемы компоновки данных
//
// Параметры:
//	СхемаКомпоновкиДанных - СхемаКомпоновкиДанных
//	Параметр              - Строка, ПараметрКомпоновкиДанных - имя параметра КД для которого нужно вернуть значение параметра
//
// Возвращаемое значение:
//	ЗначениеПараметра - ПараметрСхемыКомпоновкиДанных - Неопределено, если параметр не найден
//
Функция ПолучитьПараметрСКД(СхемаКомпоновкиДанных, Параметр) Экспорт
	
	ЗначениеПараметра = Неопределено;
	ПолеПараметр = ?(ТипЗнч(Параметр) = Тип("Строка"), Новый ПараметрКомпоновкиДанных(Параметр), Параметр);
	
	Если ТипЗнч(СхемаКомпоновкиДанных) = Тип("СхемаКомпоновкиДанных") Тогда
		ЗначениеПараметра = СхемаКомпоновкиДанных.Параметры.Найти(ПолеПараметр);
	КонецЕсли;
	
	Возврат ЗначениеПараметра;
	
КонецФункции

// Устанавливает параметр схемы компоновки данных
//
// Параметры:
//	СхемаКомпоновкиДанных - СхемаКомпоновкиДанных
//	Параметр - Строка, ПараметрКомпоновкиДанных - параметр, который требуется установить
//	Значение - Произвольный - значение, которое требуется установить
//	Использование - Булево - признак испольнования параметра КД
//
// Возвращаемое значение:
//	ПараметрСхемыКомпоновкиДанных - установленный параметр настроек КД. Неопределено, если параметр не найден
//
Функция УстановитьПараметрСКД(СхемаКомпоновкиДанных, Параметр, Значение, Использование = Истина) Экспорт
	
	ЗначениеПараметра = ПолучитьПараметрСКД(СхемаКомпоновкиДанных, Параметр);
	
	Если ЗначениеПараметра <> Неопределено Тогда
		Если ТипЗнч(СхемаКомпоновкиДанных) <> Тип("СхемаКомпоновкиДанных") Тогда
			ЗначениеПараметра.Использование	= Использование;
		КонецЕсли;
		ЗначениеПараметра.Значение		= Значение;
	КонецЕсли;
	
	Возврат ЗначениеПараметра;
	
КонецФункции

// Находит среди элементов полей СКД поле по полному имени
//
// Параметры:
//	Элементы - КоллекцияПолейГруппировкиКомпоновкиДанных - поля СКД
//	ПолноеИмя - Строка - полное имя поля СКД
//
// Возвращаемое значение:
//	ПолеКомпоновкиДанных - поле СКД
//
Функция НайтиПолеСКДПоПолномуИмени(Элементы, ПолноеИмя) Экспорт

	масЧастейИмен = ИзПолногоИмениПоляПолучитьЧасти(ПолноеИмя);
	колЧастей = масЧастейИмен.Количество();
	
	текИмя = масЧастейИмен[0];
	Поле = НайтиПолеСКДПоИмени(Элементы, текИмя);
	Если Поле = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Сч = 2 По колЧастей Цикл
		текИмя = текИмя +"." + масЧастейИмен[Сч-1];
		Поле = НайтиПолеСКДПоИмени(Поле.Элементы, текИмя);
		Если Поле = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Поле;

КонецФункции

// Получает и возвращает запрос из переданного макета компоновки данных
//
// Параметры:
//  Макет           - МакетКомпоновкиДанных - макет, из которого требуется получить запрос.
//  ИмяНабораДанных - Строка - имя набора данных из макета, для которого получается запрос.
//
// Возвращаемое значение:
//	Строка - запрос, сформированный на основании макета компоновки
//
Функция ПолучитьЗапросИзМакетаКомпоновки(Макет, ИмяНабораДанных) Экспорт

	Запрос = Новый Запрос(Макет.НаборыДанных[ИмяНабораДанных].Запрос);

	Для Каждого Параметр Из Макет.ЗначенияПараметров Цикл
		Запрос.УстановитьПараметр(Параметр.Имя, Параметр.Значение);
	КонецЦикла;

	Возврат Запрос;

КонецФункции

// Получает и возвращает макет компоновки данных для схемы компоновки
//
// Параметры:
//  СхемаКомпоновки - СхемаКомпоновкиДанных - для которой получается макет компоновки
//  Настройки       - НастройкиКомпоновкиДанных - применяются к схеме
//
// Возвращаемое значение:
//	МакетКомпоновкиДанных - макет, скомпилированный из схемы и настроек
//
Функция ПолучитьМакетКомпоновки(СхемаКомпоновки, Настройки) Экспорт

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;

	Возврат КомпоновщикМакета.Выполнить(СхемаКомпоновки, Настройки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

КонецФункции

// Удаляет отбор из настроек и пользовательских настроек отчета
// Если переданы настройки компоновки данных, то только из них
//
// Параметры
//  НастройкиКомпоновкиДанных - КомпоновщикНастроек, НастройкиКомпоновкиДанных 
//  ИмяЭлемента  - Строка - имя элемента, который будет удален.
//
Процедура УдалитьЭлементОтбораИзВсехНастроекОтчета(НастройкиКомпоновкиДанных, ИмяЭлемента) Экспорт
	
	Если ТипЗнч(НастройкиКомпоновкиДанных) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Настройки = НастройкиКомпоновкиДанных.Настройки;
		ПользовательскиеНастройки = НастройкиКомпоновкиДанных.ПользовательскиеНастройки;
	ИначеЕсли ТипЗнч(НастройкиКомпоновкиДанных) = Тип("НастройкиКомпоновкиДанных") Тогда
		Настройки = НастройкиКомпоновкиДанных;
		ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	Иначе
		Возврат
	КонецЕсли;
	
	ЭлементыОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(Настройки.Отбор, ИмяЭлемента);
	Для Каждого ЭлементОтбора ИЗ ЭлементыОтбора Цикл
		ИдентификаторПользовательскойНастройки = ЭлементОтбора.ИдентификаторПользовательскойНастройки;
		Настройки.Отбор.Элементы.Удалить(ЭлементОтбора);
		
		ЭлементПользовательскихНастроек = ПользовательскиеНастройки.Элементы.Найти(ЭлементОтбора.ИдентификаторПользовательскойНастройки);
		Если ЭлементПользовательскихНастроек <> Неопределено Тогда
			ПользовательскиеНастройки.Элементы.Удалить(ИдентификаторПользовательскойНастройки);
		Иначе
			Для Каждого ЭлементПользНастроек Из ПользовательскиеНастройки.Элементы Цикл
				Если ТипЗнч(ЭлементПользНастроек) = Тип("ОтборКомпоновкиДанных") Тогда
					ЭлементыПользовательскихОтборов = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(ЭлементПользНастроек, ИмяЭлемента);
					Для Каждого ЭлементПользовательскогоОтбора ИЗ ЭлементыПользовательскихОтборов Цикл
						ЭлементПользНастроек.Элементы.Удалить(ЭлементПользовательскогоОтбора);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Удаляет параметр из пользовательских настроек отчета
//
// Параметры
//  НастройкиКомпоновкиДанных - КомпоновщикНастроек, НастройкиКомпоновкиДанных 
//  ИмяПараметра  - Строка - имя параметра, который будет удален.
//
Процедура УдалитьПараметрИзПользовательскихНастроекОтчета(НастройкиКомпоновкиДанных, ИмяПараметра) Экспорт
	
	Если ТипЗнч(НастройкиКомпоновкиДанных) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Настройки = НастройкиКомпоновкиДанных.Настройки;
	ИначеЕсли ТипЗнч(НастройкиКомпоновкиДанных) = Тип("НастройкиКомпоновкиДанных") Тогда
		Настройки = НастройкиКомпоновкиДанных;
	Иначе
		Возврат
	КонецЕсли;
	
	ЗначениеПоиска = Новый ПараметрКомпоновкиДанных(ИмяПараметра);
	ЭлементыПараметыДанных = Настройки.ПараметрыДанных;
	
	ЭлементПараметрыДанных = ЭлементыПараметыДанных.НайтиЗначениеПараметра(ЗначениеПоиска);
	Если ЭлементПараметрыДанных <> Неопределено Тогда
		ЭлементПараметрыДанных.ИдентификаторПользовательскойНастройки = "";
	КонецЕсли;
	
КонецПроцедуры

// Удаляет выбранное поле из настроек и пользовательских настроек отчета
//
// Параметры
//  НастройкиКомпоновкиДанных - КомпоновщикНастроек, НастройкиКомпоновкиДанных 
//  ИмяЭлемента  - Строка - имя элемента, который будет удален.
//
Процедура УдалитьВыбранноеПолеИзВсехНастроекОтчета(НастройкиКомпоновкиДанных, ИмяЭлемента) Экспорт
	
	Если ТипЗнч(НастройкиКомпоновкиДанных) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Настройки = НастройкиКомпоновкиДанных.Настройки;
		ПользовательскиеНастройки = НастройкиКомпоновкиДанных.ПользовательскиеНастройки;
	ИначеЕсли ТипЗнч(НастройкиКомпоновкиДанных) = Тип("НастройкиКомпоновкиДанных") Тогда
		Настройки = НастройкиКомпоновкиДанных;
		ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	Иначе
		Возврат
	КонецЕсли;
	
	ЗначениеПоиска  = Новый ПолеКомпоновкиДанных(ИмяЭлемента);
	МассивЭлементов = Новый Массив;
	
	ВыбранныеПоля   = Настройки.Выбор;
	ИдентификаторПользовательскойНастройкиВыбранныхПолей = ВыбранныеПоля.ИдентификаторПользовательскойНастройки;
	
	НайтиВыбранноеПолеРекурсивно(ВыбранныеПоля.Элементы, МассивЭлементов, ЗначениеПоиска);
	Для Каждого Элемент Из МассивЭлементов Цикл
		
		ИдентификаторПользовательскойНастройкиЭлемента = Настройки.Выбор.ПолучитьИдентификаторПоОбъекту(Элемент);;
		Если Элемент.Родитель <> Неопределено Тогда
			Элемент.Родитель.Элементы.Удалить(Элемент);
		Иначе
			ВыбранныеПоля.Элементы.Удалить(Элемент);
		КонецЕсли;
		
		ПользовательскиеНастройкиВыбранныхПолей = ПользовательскиеНастройки.Элементы.Найти(ИдентификаторПользовательскойНастройкиВыбранныхПолей);
		Если ПользовательскиеНастройкиВыбранныхПолей <> Неопределено Тогда
			ЭлементПользовательскихНастроек = ПользовательскиеНастройкиВыбранныхПолей.ПолучитьОбъектПоИдентификатору(ИдентификаторПользовательскойНастройкиЭлемента);
			Если ЭлементПользовательскихНастроек <> Неопределено Тогда
				Если ЭлементПользовательскихНастроек.Родитель <> Неопределено Тогда
					ЭлементПользовательскихНастроек.Родитель.Элементы.Удалить(ЭлементПользовательскихНастроек);
				Иначе
					ПользовательскиеНастройкиВыбранныхПолей.Элементы.Удалить(ЭлементПользовательскихНастроек);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Удаляет группировку из настроек и пользовательских настроек отчета
//
// Параметры
//  НастройкиКомпоновкиДанных - КомпоновщикНастроек, НастройкиКомпоновкиДанных 
//  ИмяЭлемента  - Строка - имя элемента, который будет удален.
//
Процедура УдалитьГруппировкуИзВсехНастроекОтчета(НастройкиКомпоновкиДанных, ИмяЭлемента) Экспорт
	
	Если ТипЗнч(НастройкиКомпоновкиДанных) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Настройки = НастройкиКомпоновкиДанных.Настройки;
		ПользовательскиеНастройки = НастройкиКомпоновкиДанных.ПользовательскиеНастройки;
	ИначеЕсли ТипЗнч(НастройкиКомпоновкиДанных) = Тип("НастройкиКомпоновкиДанных") Тогда
		Настройки = НастройкиКомпоновкиДанных;
		ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	Иначе
		Возврат
	КонецЕсли;
	
	ЗначениеПоиска  = Новый ПолеКомпоновкиДанных(ИмяЭлемента);
	СоответствиеЭлементов = Новый Соответствие;
	
	СтруктураГруппировок   = Настройки.Структура;
	ИдентификаторПользовательскойНастройкиСтруктуры = СтруктураГруппировок.ИдентификаторПользовательскойНастройки;
	
	НайтиГруппировкуРекурсивно(СтруктураГруппировок, СоответствиеЭлементов, ЗначениеПоиска);
	Для Каждого Группировка Из СоответствиеЭлементов Цикл
		Если Группировка.Ключ.ПоляГруппировки.Элементы.Количество() = 1 Тогда
			Группировка.Ключ.Родитель.Структура.Удалить(Группировка.Ключ);
		Иначе
			Группировка.Ключ.ПоляГруппировки.Элементы.Удалить(Группировка.Значение);
		КонецЕсли;
		
		ИдентификаторПользовательскойНастройкиЭлемента = Группировка.Ключ.ИдентификаторПользовательскойНастройки;
		
		ПользовательскиеНастройкиВыбранныхПолей = ПользовательскиеНастройки.Элементы.Найти(ИдентификаторПользовательскойНастройкиЭлемента);
		Если ПользовательскиеНастройкиВыбранныхПолей <> Неопределено Тогда
			ЭлементПользовательскихНастроек = ПользовательскиеНастройкиВыбранныхПолей.ПолучитьОбъектПоИдентификатору(ИдентификаторПользовательскойНастройкиЭлемента);
			Если ЭлементПользовательскихНастроек <> Неопределено Тогда
				Если ЭлементПользовательскихНастроек.Родитель <> Неопределено Тогда
					ЭлементПользовательскихНастроек.Родитель.Элементы.Удалить(ЭлементПользовательскихНастроек);
				Иначе
					ПользовательскиеНастройкиВыбранныхПолей.Элементы.Удалить(ЭлементПользовательскихНастроек);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Отключает использование выбранного поле в пользовательских настройках отчета
//
// Параметры
//  НастройкиКомпоновкиДанных - КомпоновщикНастроек, НастройкиКомпоновкиДанных 
//  ИмяЭлемента  - Строка - имя элемента, который будет отключен.
//
Процедура ОтключитьВыбранноеПолеВПользовательскихНастройках(НастройкиКомпоновкиДанных, ИмяЭлемента) Экспорт
	
	Если ТипЗнч(НастройкиКомпоновкиДанных) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Настройки = НастройкиКомпоновкиДанных.Настройки;
		ПользовательскиеНастройки = НастройкиКомпоновкиДанных.ПользовательскиеНастройки;
	ИначеЕсли ТипЗнч(НастройкиКомпоновкиДанных) = Тип("НастройкиКомпоновкиДанных") Тогда
		Настройки = НастройкиКомпоновкиДанных;
		ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	Иначе
		Возврат
	КонецЕсли;
	
	ВыбранныеПоля = Настройки.Выбор;
	ИдентификаторПользовательскойНастройкиВыбранныхПолей = ВыбранныеПоля.ИдентификаторПользовательскойНастройки;
	
	ПользовательскиеНастройкиВыбранныхПолей = ПользовательскиеНастройки.Элементы.Найти(
		ИдентификаторПользовательскойНастройкиВыбранныхПолей);
	
	Если ПользовательскиеНастройкиВыбранныхПолей <> Неопределено Тогда
		
		ЗначениеПоиска  = Новый ПолеКомпоновкиДанных(ИмяЭлемента);
		МассивЭлементов = Новый Массив;
		
		НайтиВыбранноеПолеРекурсивно(ПользовательскиеНастройкиВыбранныхПолей.Элементы, МассивЭлементов, ЗначениеПоиска);
		Для Каждого Элемент Из МассивЭлементов Цикл
			Элемент.Использование = Ложь;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Очищает структуру отчета от удаляемых полей.
//
// Параметры
//  СтруктураОтчета - КоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных 
//  УдаляемыеПоля  - Массив - содержит поля компоновки, которые необходимо удалить из структуры отчета.
//
Процедура ОчиститьСтруктуруПоФункциональнымОпциям(СтруктураОтчета, УдаляемыеПоля) Экспорт

	Для Каждого ЭлементСтруктуры Из СтруктураОтчета Цикл
		
		Если ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаКомпоновкиДанных")
			Или ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
			ОчиститьСтруктуруПоФункциональнымОпциям(ЭлементСтруктуры.Структура, УдаляемыеПоля);
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") Тогда
			
			ОчиститьСтруктуруПоФункциональнымОпциям(ЭлементСтруктуры.Строки, УдаляемыеПоля);
			Продолжить;
			
		КонецЕсли;
		
		ПоляГруппировки = ЭлементСтруктуры.ПоляГруппировки.Элементы;
		Индекс = ПоляГруппировки.Количество() - 1;
		Пока Индекс >= 0 Цикл
			
			Если Не УдаляемыеПоля.Найти(ПоляГруппировки[Индекс].Поле) = Неопределено Тогда
				ПоляГруппировки.Удалить(ПоляГруппировки[Индекс]);
			КонецЕсли;
			
			Индекс = Индекс - 1;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область МетодыИВыраженияСистемыКомпоновкиДанных

// Возвращает непечатный символ по имени
//
// Параметры
//	ИмяСимвола - Строка - имя непечатного символа как оно задано в системном перечислении Символы
//
// Возвращаемое значение:
//	Строка - значение символа
//
Функция ЗначениеСимвола(ИмяСимвола) Экспорт
	ВозвращаемыйСимвол = "";
	
	Если ИмяСимвола = "ВК" Тогда
		ВозвращаемыйСимвол = Символы.ВК;
	ИначеЕсли ИмяСимвола = "Втаб" Тогда
		ВозвращаемыйСимвол = Символы.ВТаб;
	ИначеЕсли ИмяСимвола = "НПП" Тогда
		ВозвращаемыйСимвол = Символы.НПП;
	ИначеЕсли ИмяСимвола = "ПС" Тогда
		ВозвращаемыйСимвол = Символы.ПС;
	ИначеЕсли ИмяСимвола = "ПФ" Тогда
		ВозвращаемыйСимвол = Символы.ПФ;
	ИначеЕсли ИмяСимвола = "Таб" Тогда
		ВозвращаемыйСимвол = Символы.Таб;
	КонецЕсли;
	
	Возврат ВозвращаемыйСимвол
КонецФункции

// Полный аналог СтрЗаменить для использования в выражениях СКД
//
// Параметры
//  Строка          - Строка - Исходная строка. 
//  ПодстрокаПоиска - Строка - Искомая подстрока. 
//  ПодстрокаЗамены - Строка - Подстрока, на которую будет заменена подстрока поиска. 
//
// Возвращаемое значение:
//  Строка - полученная в результате замены. См. функцию СтрЗаменить
//
Функция СтрЗаменитьКомпоновка(Строка, ПодстрокаПоиска, ПодстрокаЗамены) Экспорт
	Возврат СтрЗаменить(Строка, ПодстрокаПоиска, ПодстрокаЗамены);
КонецФункции

// Формирует новый макет компоновки после замены параметров в выражениях итоговых полей
// Вызывается из отчетов АнализСебестоимостиТоваровРасширенный и ВедомостьПоДенежнымСредствам
//
// Параметры
//  МакетКомпоновкиДанных - исходный макет, сформированный в отчете 
//  СхемаКомпоновкиДанных - схема компоновки отчета, в которой есть параметр ИспользуютсяОборотныеАналитики 
//							и этот параметр указан в выражениях итоговых полей
//	НастройкиОтчета - настройки, на основании которых будет формировать новый макет компоновки
//	ДанныеРасшифровки - данные расшифровки отчета
//
Процедура ОптимизацияИспользованияОборотнойАналитики(МакетКомпоновкиДанных, СхемаКомпоновкиДанных, НастройкиОтчета, Знач ДанныеРасшифровки) Экспорт
	ИспользуетсяОборотнаяАналитика = Ложь;
	
	Для Каждого СвязьНаборовДанных Из МакетКомпоновкиДанных.СвязиНаборовДанных Цикл 
		Если СвязьНаборовДанных.НаборДанныхИсточник = "ОборотнаяАналитика" 
			Или СвязьНаборовДанных.НаборДанныхПриемник = "ОборотнаяАналитика" Тогда
			
			ИспользуетсяОборотнаяАналитика = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОтчета, "ИспользуетсяОборотнаяАналитика", ИспользуетсяОборотнаяАналитика);
	
	Для Каждого ПолеИтога Из СхемаКомпоновкиДанных.ПоляИтога Цикл 
		Если СтрНайти(ПолеИтога.Выражение, "&ИспользуетсяОборотнаяАналитика") <> 0 Тогда
			ИмяПоляИтогаОсновнойАналитики = ПолеИтога.ПутьКДанным;
			ЧастиИмени = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(ИмяПоляИтогаОсновнойАналитики, ".");
			ИмяПоляИтогаПоДокументам = "Док_" + ЧастиИмени[ЧастиИмени.Количество()-1];
			
			ВыражениеИтога = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Сумма(%1)", 
					?(ИспользуетсяОборотнаяАналитика, ИмяПоляИтогаПоДокументам, ИмяПоляИтогаОсновнойАналитики));
			ПолеИтога.Выражение = СтрЗаменить(ПолеИтога.Выражение, "&ИспользуетсяОборотнаяАналитика", ВыражениеИтога);
		КонецЕсли;
	КонецЦикла;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
КонецПроцедуры

// Вычисляет порядковое место числа относительно других чисел массива после его сортировки
//
// Параметры:
//	ДанныеГрупповойОбработкиКомпоновкиДанных - ТаблицаЗначений - обрабатываемая таблица СКД, место определяется для значений первой колонки
//	ПорядокСортировки - НаправлениеСортировкиКомпоновкиДанных - определяет, сверху или снизу массива будет вычисляться порядковое место
//
// Возвращаемое значение:
//	Число - поле СКД
//
Функция МестоВПорядке(ДанныеГрупповойОбработкиКомпоновкиДанных, ПорядокСортировки) Экспорт
	МестоВПорядке = 0;
	
	Данные			= ДанныеГрупповойОбработкиКомпоновкиДанных.Данные;
	ИмяПоля			= ДанныеГрупповойОбработкиКомпоновкиДанных.Данные.Колонки[0].Имя;
	
	Если ДанныеГрупповойОбработкиКомпоновкиДанных.ТекущийЭлемент <> Неопределено Тогда
		ЗначениеПоля	= ДанныеГрупповойОбработкиКомпоновкиДанных.ТекущийЭлемент[0];
		
		Если ЗначениеПоля = 0 Тогда
			Возврат	"<>";
		КонецЕсли;
		
		КопияДанных = Данные.Скопировать();
		КопияДанных.Сортировать(ИмяПоля + " " + ПорядокСортировки);
		
		ИскомаяСтрока = КопияДанных.Найти(ЗначениеПоля, ИмяПоля);
		
		Если ИскомаяСтрока <> Неопределено Тогда
			МестоВПорядке = КопияДанных.Индекс(ИскомаяСтрока) + 1;
		КонецЕсли;
	КонецЕсли;
	
	Возврат МестоВПорядке;
КонецФункции

// Вычисляет сумму неклассифицированного оборота для отчетов, использующих оборотные аналитики
// Вызывается при расчете сумм ресурсов
//
// Параметры:
//	ДанныеГрупповойОбработкиКомпоновкиДанных - ДанныеГрупповойОбработкиКомпоновкиДанных
//			Состав колонок обрабатываемых данных: 
//				ИмяОборотнойАналитики - колонка со значениями оборотной аналитики
//				ИмяРесурса - колонка ресурсы основного набора данных
//				Об_ИмяРесурса - колонка ресурса набора данных по документам. Имя образуется путем добавления префикса "Об_" к имени колонки ИмяРесурса
//				Док_ИмяРесурса - колонка ресурса набора данных по оборотным аналитикам. Имя образуется путем добавления префикса "Об_" к имени колонки ИмяРесурса
//	ИмяРесурса - Строка - имя обрабатываемого ресурса
//	ИмяОборотнойАналитики - Строка - имя обрабатываемой аналитики
//
// Возвращаемое значение:
//	Число - поле СКД
//
Функция КлассификацияОборота(ДанныеГрупповойОбработкиКомпоновкиДанных, ИмяРесурса, ИтогРесурса, ИмяОборотнойАналитики, ЗначениеАналитики) Экспорт
	Данные = ДанныеГрупповойОбработкиКомпоновкиДанных.Данные;
	ТекущаяСтрока = ДанныеГрупповойОбработкиКомпоновкиДанных.ТекущийЭлемент;
	ВременныеДанные = ДанныеГрупповойОбработкиКомпоновкиДанных.ВременныеДанныеОбработки;
	ЗначениеОборота = 0;
	
	Если Не ВременныеДанные.Свойство("ОборотНакопительный") Тогда
		ВременныеДанные.Вставить("ОборотНакопительный", 0);
	КонецЕсли;
	Если Не ВременныеДанные.Свойство("НеклассифицированныйОборот") Тогда
		ВременныеДанные.Вставить("НеклассифицированныйОборот", ?(ИтогРесурса = Null, 0, ИтогРесурса));
	КонецЕсли;
	Если Не ВременныеДанные.Свойство("ОбработанноеЗначениеАналитики") Тогда
		ВременныеДанные.Вставить("ОбработанноеЗначениеАналитики", Новый Соответствие);
	КонецЕсли;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		ОбновлятьВременныеДанные = 
			// Расчет на 1-ом уровне группировки
			(ЗначениеАналитики <> Null И ВременныеДанные.ОбработанноеЗначениеАналитики[ЗначениеАналитики] = Неопределено)
			// Расчет на 2-ом и более уровнях группировки
			ИЛИ (ЗначениеАналитики = Null И ВременныеДанные.ОбработанноеЗначениеАналитики.Количество() = 0);

		Если ТекущаяСтрока["Док_"+ ИмяРесурса] = Null Тогда
			ЗначениеОборота = 0;
		ИначеЕсли ТекущаяСтрока["Об_"+ ИмяРесурса] > ТекущаяСтрока["Док_"+ ИмяРесурса] Тогда
			ЗначениеОборота = ТекущаяСтрока["Док_"+ ИмяРесурса];
		Иначе
			ЗначениеОборота = ТекущаяСтрока["Об_"+ ИмяРесурса];
		КонецЕсли;
		
		ЗначениеОборота = ?(ЗначениеОборота = Null, 0, ЗначениеОборота);
		
		Если ОбновлятьВременныеДанные Тогда
			ВременныеДанные.Вставить("ОборотНакопительный", 
				ВременныеДанные.ОборотНакопительный + ЗначениеОборота);
				
			ВременныеДанные.Вставить("НеклассифицированныйОборот", 
				ВременныеДанные.НеклассифицированныйОборот - ЗначениеОборота);
		КонецЕсли;
		
		Если ЗначениеАналитики = Null Тогда
			ЗначениеОборота = ВременныеДанные.НеклассифицированныйОборот;
		КонецЕсли;
		
		ВременныеДанные.ОбработанноеЗначениеАналитики.Вставить(ЗначениеАналитики, ЗначениеАналитики);
	Иначе 
		Если Данные.Итог("Док_"+ ИмяРесурса) = Null Тогда
			ЗначениеОборота = 0;
		ИначеЕсли Данные.Итог("Об_"+ ИмяРесурса) > Данные.Итог("Док_"+ ИмяРесурса) Тогда
			ЗначениеОборота = Данные.Итог("Док_"+ ИмяРесурса);
		Иначе
			ЗначениеОборота = Данные.Итог("Об_"+ ИмяРесурса);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗначениеОборота;
КонецФункции

Функция ГраницаПериода(Дата, ВидГраницыИмяЗначения) Экспорт
	Возврат Новый Граница(Дата, ВидГраницы[ВидГраницыИмяЗначения]);
КонецФункции
#КонецОбласти

#Область РаботаСоСхемойКомпоновкиДанных
	
// Возвращает пустую схему СКД
//
// Параметры:
//  ИмяИсточника - Строка - имя источника данных новой схемы компоновки (необязательное по умолчанию "ИсточникДанных1")
//  ТипИсточника - Строка - тип источника данных новой схему компоновки данных (необязательное по умолчанию "Local")
//
// Возвращаемое значение:
//  СхемаКомпоновкиДанных - пустая схема компоновки данных
//
Функция ПустаяСхема(ИмяИсточника = "ИсточникДанных1", ТипИсточника = "Local") Экспорт
	
	НоваяСхема = Новый СхемаКомпоновкиДанных;
	
	ИсточникДанных = НоваяСхема.ИсточникиДанных.Добавить();
	ИсточникДанных.Имя = ИмяИсточника;
	ИсточникДанных.ТипИсточникаДанных = ТипИсточника;
	
	Возврат НоваяСхема;
	
КонецФункции

// Возвращается независимую копию исходной схемы компоновки данных
//
// Параметры:
//  ИсходнаяСхема - СхемаКомпоновкиДанных - схема, которую требуется скопировать
//
// Возвращаемое значение:
//  СхемаКомпоновкиДанных - новыая схема компоновки данных с настройками, идентичными исходной схеме
//
Функция СкопироватьСхемуКомпоновкиДанных(ИсходнаяСхема) Экспорт
	
	Если ТипЗнч(ИсходнаяСхема) = Тип("СхемаКомпоновкиДанных") Тогда
		
		СхемаКомпоновкиДанных = СериализаторXDTO.ПрочитатьXDTO(СериализаторXDTO.ЗаписатьXDTO(ИсходнаяСхема));
		
	Иначе
		
		СхемаКомпоновкиДанных = Новый СхемаКомпоновкиДанных;
		
	КонецЕсли;
	
	Возврат СхемаКомпоновкиДанных;
	
КонецФункции

// Добавляет новый набор данных заданого типа и возвращает ссылку на него
//
// Параметры:
//  СхемаНабор   - СхемаКомпоновкиДанных, НаборыДанныхСхемыКомпоновкиДанных, НаборДанныхОбъединениеСхемыКомпоновкиДанных - схема или набор компоновки данных, в которой необходимо создать новый набор
//  ТипНабора    - Тип - Тип нового набора (необязательное по умолчанию "НаборДанныхЗапросСхемыКомпоновкиДанных")
//                     - Тип("НаборДанныхЗапросСхемыКомпоновкиДанных")
//                     - Тип("НаборДанныхОбъектСхемыКомпоновкиДанных")
//  ИмяНабора      - Строка - имя нового набора (необязательное по умолчанию "НаборДанных1")
//  ИмяИсточника - Строка - имя источника данных новой схемы компоновки (необязательное по умолчанию "ИсточникДанных1")
//  ИмяОбъекта   - Строка - имя внешнего источника данных (необязательное по умолчанию равно имени набора данных)
//
// Возвращаемое значение:
//  НаборДанныхЗапросСхемыКомпоновкиДанных, НаборДанныхОбъединениеСхемыКомпоновкиДанных, НаборДанныхОбъектСхемыКомпоновкиДанных - новый набор данных заданного типа
//
Функция ДобавитьПустойНаборДанных(СхемаНабор, ТипНабора = Неопределено, ИмяНабора = "НаборДанных1", ИмяИсточника = "ИсточникДанных1", ИмяОбъекта = "") Экспорт
	
	Если ТипНабора = Неопределено Тогда
		ТипНабора = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных");
	КонецЕсли;
	
	Если ТипЗнч(СхемаНабор) = Тип("СхемаКомпоновкиДанных") Тогда
		
		НаборыДанных = СхемаНабор.НаборыДанных;
		
	ИначеЕсли ТипЗнч(СхемаНабор) = Тип("НаборыДанныхСхемыКомпоновкиДанных") Тогда
		
		НаборыДанных = СхемаНабор;
		
	ИначеЕсли ТипЗнч(СхемаНабор) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
		
		НаборыДанных = СхемаНабор.Элементы;
		
	КонецЕсли;
	
	НовыйНабор = НаборыДанных.Добавить(ТипНабора);
	
	НовыйНабор.Имя = ИмяНабора;
	Если Не ТипНабора = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных")
		И ЗначениеЗаполнено(ИмяИсточника) Тогда
		
		НовыйНабор.ИсточникДанных = ИмяИсточника;
		
	КонецЕсли;
	
	Если ТипНабора = Тип("НаборДанныхОбъектСхемыКомпоновкиДанных") Тогда
		НовыйНабор.ИмяОбъекта = ?(ЗначениеЗаполнено(ИмяОбъекта), ИмяОбъекта, ИмяНабора);
	КонецЕсли;
		
	Возврат НовыйНабор;
	
КонецФункции

// Позволяет копировать наборы данных из одной схемы в другую или внутри схемы.
// Возвращает массив ссылок на скопированные наборы в приемнике.
//
// Параметры:
// 	ПриемникНаборовДанных - СхемаКомпоновкиДанных - 
//							НаборыДанныхСхемыКомпоновкиДанных -
//							НаборДанныхОбъединениеСхемыКомпоновкиДанных - 
// 	ИсточникНаборовДанных - СхемаКомпоновкиДанных - 
//							НаборыДанныхСхемыКомпоновкиДанных -
//							НаборДанныхОбъединениеСхемыКомпоновкиДанных - 
//							НаборДанныхЗапросСхемыКомпоновкиДанных - 
//							НаборДанныхОбъектСхемыКомпоновкиДанных - 
// 	ОсновноеИмяНабораДанных - Строка - имя набора данных в приемнике.
//									   Если в приемнике уже есть набор данных с таким именем, то к имени будет добавлен очередной порядковый номер.
//									   Если источник содержит несколько наборов данных, то основное имя применятся не будет.
//									   Если источник содержит одно объединение, то основное имя будет применено только к нему.
//									   Если имя не задано, то по возможности будут сохранятся исходные имена наборов данных.
//
// Возвращаемое значение:
// 	Массив - ссылки на скопированные наборы в приемнике
// 		* Элементы - НаборыДанныхСхемыКомпоновкиДанных, НаборДанныхОбъединениеСхемыКомпоновкиДанных,
// 					 НаборДанныхЗапросСхемыКомпоновкиДанных, НаборДанныхОбъектСхемыКомпоновкиДанных - отдельный набор данных
//
Функция СкопироватьНаборыДанных(ПриемникНаборовДанных, ИсточникНаборовДанных, ОсновноеИмяНабораДанных = "") Экспорт
	
	Перем СкопированныеНаборыДанных;
	
	СкопированныеНаборыДанных = Новый Массив;
	ЭтоОбъединение = Ложь;
	
	// Коллекция, в которую можно добавить набор данных
	Если ТипЗнч(ПриемникНаборовДанных) = Тип("СхемаКомпоновкиДанных") Тогда
		Приемник = ПриемникНаборовДанных.НаборыДанных;
	ИначеЕсли ТипЗнч(ПриемникНаборовДанных) = Тип("НаборыДанныхСхемыКомпоновкиДанных") Тогда
		Приемник = ПриемникНаборовДанных;
	ИначеЕсли ТипЗнч(ПриемникНаборовДанных) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
		Приемник = ПриемникНаборовДанных.Элементы;
		ЭтоОбъединение = Истина;
	Иначе 
		ВызватьИсключение НСтр("ru= 'Некорректный тип параметра 1'") ;
	КонецЕсли;
		
	// Коллекция, в которой содержатся наборы данных
	Если ТипЗнч(ИсточникНаборовДанных) = Тип("СхемаКомпоновкиДанных") Тогда
		Источник = ИсточникНаборовДанных.НаборыДанных;
	ИначеЕсли ТипЗнч(ИсточникНаборовДанных) = Тип("НаборыДанныхСхемыКомпоновкиДанных") Тогда
		Источник = ИсточникНаборовДанных;
	ИначеЕсли ТипЗнч(ИсточникНаборовДанных) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
		Источник = ИсточникНаборовДанных.Элементы;
	ИначеЕсли ТипЗнч(ИсточникНаборовДанных) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных")
		Или ТипЗнч(ИсточникНаборовДанных) = Тип("НаборДанныхОбъектСхемыКомпоновкиДанных") Тогда
		Источник = Новый Массив;
		Источник.Добавить(ИсточникНаборовДанных);
	Иначе 
		ВызватьИсключение НСтр("ru= 'Некорректный тип параметра 2'") ;
	КонецЕсли;
		
	Для Каждого НаборИсточник Из Источник Цикл 
		ТипНабора = ТипЗнч(НаборИсточник);
		ИмяНабора = ?(ЗначениеЗаполнено(ОсновноеИмяНабораДанных), ОсновноеИмяНабораДанных, НаборИсточник.Имя);
		ПоляИсточника = НаборИсточник.Поля;
		
		НовыйНабор = КомпоновкаДанныхСервер.ДобавитьПустойНаборДанных(Приемник, ТипНабора, ИмяНабора);
		ЗаполнитьЗначенияСвойств(НовыйНабор, НаборИсточник, , "Имя");
		
		СкопированныеНаборыДанных.Добавить(НовыйНабор);
		
		Если ТипНабора = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
			// Рекурсивно перенести наборы данных
			СкопированныеВРекурсии = СкопироватьНаборыДанных(НовыйНабор, НаборИсточник);
			
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СкопированныеНаборыДанных, СкопированныеВРекурсии, Истина);
		КонецЕсли;
		
		СкопироватьПоляНабораДанных(НовыйНабор, НаборИсточник);
		
		Если ТипЗнч(ПриемникНаборовДанных) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
			СкопироватьПоляНабораДанных(ПриемникНаборовДанных, НовыйНабор, ЭтоОбъединение);
		КонецЕсли;
			
	КонецЦикла;
	
	Возврат СкопированныеНаборыДанных;
	
КонецФункции

// Позволяет копировать поля из одного набора данных в другой.
// Если поле уже есть в приемнике, то оно пропускается.
// Возвращает массив ссылок на скопированные поля набора данных в приемнике.
//
// Параметры:
// 	ПриемникПолейНабораДанных - НаборДанныхОбъединениеСхемыКомпоновкиДанных - 
//								НаборДанныхЗапросСхемыКомпоновкиДанных - 
//								НаборДанныхОбъектСхемыКомпоновкиДанных - 
// 	ИсточникПолейНабораДанных - НаборДанныхОбъединениеСхемыКомпоновкиДанных - 
//								НаборДанныхЗапросСхемыКомпоновкиДанных - 
//								НаборДанныхОбъектСхемыКомпоновкиДанных - 
// 	ДублироватьИмяВПутиПоля   - Булево - признак необходимости приравнять путь к данным и выражение поля (по умолчанию Ложь)
//
// Возвращаемое значение:
// 	Массив - ссылки на скопированные поля набораданных в приемнике
// 		* Элементы - ПолеНабораДанныхСхемыКомпоновкиДанных, ПапкаПолейНабораДанныхСхемыКомпоновкиДанных - отдельное поле набора данных
//
Функция СкопироватьПоляНабораДанных(ПриемникПолейНабораДанных, ИсточникПолейНабораДанных, ДублироватьИмяВПутиПоля = Ложь) Экспорт
	
	Перем СкопированныеПоляНабораДанных;
	
	СкопированныеПоляНабораДанных = Новый Массив;
	
	ПоляПриемника = ПриемникПолейНабораДанных.Поля;
	ПоляИсточника = ИсточникПолейНабораДанных.Поля;
	
	// Перенести поля, кроме свойств ПараметрыРедактирования, 
	Для Каждого ПолеИсточника Из ПоляИсточника Цикл 
		Если Не ПоляПриемника.Найти(ПолеИсточника.ПутьКДанным) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ПолеИсточника) = Тип("ПолеНабораДанныхСхемыКомпоновкиДанных") Тогда
			ПолеПриемника = ФинансоваяОтчетностьСервер.НовоеПолеНабора(ПриемникПолейНабораДанных, "НовоеПоле");
			ЗаполнитьЗначенияСвойств(ПолеПриемника, ПолеИсточника);
			
			Если ДублироватьИмяВПутиПоля Тогда
				ПолеПриемника.ПутьКДанным = ПолеПриемника.Поле;
			КонецЕсли;
			
			ДоступныеЗначенияПоляИсточника = ПолеИсточника.ПолучитьДоступныеЗначения();
			Если Не ДоступныеЗначенияПоляИсточника = Неопределено Тогда
				ПолеПриемника.УстановитьДоступныеЗначения(ДоступныеЗначенияПоляИсточника);
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ПолеПриемника.Роль, ПолеИсточника.Роль);
		Иначе
			ПолеПриемника = ФинансоваяОтчетностьСервер.НоваяГруппаНабора(ПриемникПолейНабораДанных, "НоваяГруппа");
			ЗаполнитьЗначенияСвойств(ПолеПриемника, ПолеИсточника);
		КонецЕсли;
		
		СкопированныеПоляНабораДанных.Добавить(ПолеПриемника);
	КонецЦикла;
	
	Возврат СкопированныеПоляНабораДанных;
	
КонецФункции

// Создает новую связь наборов данных в переданной схеме.
// Возвращает объект созданной связи.
//
// Параметры:
// 	СхемаКомпоновкиДанных    - СхемаКомпоновкиДанных - схема, в которую нужно добаить связь.
// 	НаборДанныхИсточник      - Строка,
//							   НаборДанныхОбъединениеСхемыКомпоновкиДанных,
//							   НаборДанныхЗапросСхемыКомпоновкиДанных,
//							   НаборДанныхОбъектСхемыКомпоновкиДанных - имя набора данных, от которого будет осуществляться связь.
// 	НаборДанныхПриемник      - Строка,
//							   НаборДанныхОбъединениеСхемыКомпоновкиДанных,
//							   НаборДанныхЗапросСхемыКомпоновкиДанных,
//							   НаборДанныхОбъектСхемыКомпоновкиДанных - имя набора данных, к которому будет осуществляться связь.
// 	ВыражениеИсточник        - Строка - выражение, по которому будет определяться значение связи источника.
// 	ВыражениеПриемник        - Строка - выражение, по которому будет определяться значение связи приемника.
// 	Параметр                 - Строка - имя параметра, в который будет помещено значение связи источника.
// 	РазрешенСписокПараметров - Булево - возможность использования в качестве значения параметра список значений.
//
// Возвращаемое значение:
// 	СвязьНаборовДанныхСхемыКомпоновкиДанных - добавленная связь
//
Функция ДобавитьСвязьНаборовДанных(СхемаКомпоновкиДанных, НаборДанныхИсточник, НаборДанныхПриемник, ВыражениеИсточник, ВыражениеПриемник, Параметр = Неопределено, РазрешенСписокПараметров = Ложь) Экспорт
	
	Перем НоваяСвязь;
	
	Если ТипЗнч(НаборДанныхИсточник) = Тип("Строка") Тогда
		ИмяНабораДанныхИсточника = НаборДанныхИсточник;
	ИначеЕсли ТипЗнч(НаборДанныхИсточник) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных")
			Или ТипЗнч(НаборДанныхИсточник) = Тип("НаборДанныхОбъектСхемыКомпоновкиДанных")
			Или ТипЗнч(НаборДанныхИсточник) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
		ИмяНабораДанныхИсточника = НаборДанныхИсточник.Имя;
	Иначе 
		ВызватьИсключение НСтр("ru= 'Некорректный тип параметра 2'") ;
	КонецЕсли;
	
	Если ТипЗнч(НаборДанныхПриемник) = Тип("Строка") Тогда
		ИмяНабораДанныхПриемника = НаборДанныхПриемник;
	ИначеЕсли ТипЗнч(НаборДанныхПриемник) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных")
			Или ТипЗнч(НаборДанныхПриемник) = Тип("НаборДанныхОбъектСхемыКомпоновкиДанных")
			Или ТипЗнч(НаборДанныхПриемник) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
		ИмяНабораДанныхПриемника = НаборДанныхПриемник.Имя;
	Иначе 
		ВызватьИсключение НСтр("ru= 'Некорректный тип параметра 3'") ;
	КонецЕсли;
	
	НоваяСвязь = СхемаКомпоновкиДанных.СвязиНаборовДанных.Добавить();
	НоваяСвязь.НаборДанныхИсточник      = ИмяНабораДанныхИсточника;
	НоваяСвязь.НаборДанныхПриемник      = ИмяНабораДанныхПриемника;
	НоваяСвязь.ВыражениеИсточник        = ВыражениеИсточник;
	НоваяСвязь.ВыражениеПриемник        = ВыражениеПриемник;
	Если ЗначениеЗаполнено(Параметр) Тогда
		НоваяСвязь.Параметр                 = Параметр;
		НоваяСвязь.РазрешенСписокПараметров = РазрешенСписокПараметров;
	КонецЕсли;
	
	Возврат НоваяСвязь;
	
КонецФункции 

// Добавляет или перезаполняет свойства параметра в схеме компоновки данных
// 
// Параметры:
//	СхемаКомпоновкиДанных     - СхемаКомпоновкиДанных - схема, в которую нужно добавить параметры или найти и перезаполнить свойства
//	ИмяПараметра              - Строка - имя параметра
//	ТипЗначения               - Тип, Неопределено - тип значений параметра
//	Значение                  - Произвольный, Неопределено - значение по умолчанию
//	ИспользованиеВсегда       - Булево - Истина, тогда параметры будет использоваться всегда. Ложь - автоматическое управление платформой
//	ПерезаполнитьСуществующий - Булево - Истина, перед добавлением будет осуществляться поиск параметра с заданным именем.
//		Внимание, операция ресурсоемкая и следует включать только если нельзя проверить уникальность параметров иначе
//
// Возвращаемое значение:
//	ПараметрКомпоновкиДанных - созданный или перезаполненный параметр
// 
Функция ДобавитьПараметр(СхемаКомпоновкиДанных, ИмяПараметра, ТипЗначения = Неопределено, Значение = Неопределено, ИспользованиеВсегда = Истина, ПерезаполнитьСуществующий = Ложь) Экспорт
	
	Перем Параметр;
	
	Если Не ПерезаполнитьСуществующий Тогда
		Параметр = СхемаКомпоновкиДанных.Параметры.Добавить();
	Иначе
		Параметр = СхемаКомпоновкиДанных.Параметры.Найти(ИмяПараметра);
		ПараметрСуществует = Параметр <> Неопределено;
		
		Если Не ПараметрСуществует Тогда
			Параметр = СхемаКомпоновкиДанных.Параметры.Добавить();
		КонецЕсли;
	КонецЕсли;
	
	Параметр.Имя = ИмяПараметра;
	Если ТипЗначения <> Неопределено Тогда
		Параметр.ТипЗначения = ТипЗначения;
	КонецЕсли;
	
	Если Значение <> Неопределено Тогда
		Параметр.Значение = Значение;
	КонецЕсли;
	
	Параметр.Использование = ?(ИспользованиеВсегда, ИспользованиеПараметраКомпоновкиДанных.Всегда, ИспользованиеПараметраКомпоновкиДанных.Авто);
	
	Возврат Параметр;
	
КонецФункции

// Добавляет отбор для сравнения со списком значений, выгруженных из указанной колонки таблицы
// 
// Параметры:
//	ЭлементНастроекКомпоновки - КомпоновщикНастроекКомпоновкиДанных, НастройкиКомпоновкиДанных - элемент структуры компоновки данных
//	ТаблицаДанных - ТаблицаЗначений - таблица с данными
//	ИмяКолонки - Строка - имя колонки, со значениями которой будут сравнение в отборе
//
Процедура ДобавитьОтборПоКолонкеТаблицыЗначений(ЭлементНастроекКомпоновки, ТаблицаДанных, ИмяКолонки) Экспорт
	
	Перем МассивЗначенийОтбора, СписокЗначений;
	
	МассивЗначенийОтбора = ТаблицаДанных.ВыгрузитьКолонку(ИмяКолонки);
	МассивЗначенийОтбора = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивЗначенийОтбора);
	
	Если Не (МассивЗначенийОтбора.Количество() = 1 
		И ТипЗнч(МассивЗначенийОтбора[0]) = Тип("Null")) Тогда
		СписокЗначений = Новый СписокЗначений;
		СписокЗначений.ЗагрузитьЗначения(МассивЗначенийОтбора);
		
		КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(ЭлементНастроекКомпоновки, ИмяКолонки, СписокЗначений, ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;

КонецПроцедуры

// Возвращает признак того, что хотя бы одно из переданных имен таблиц встречается в хотя бы одном тексте запроса одного из наборов данных
// 
// Параметры:
//	НаборыДанных - НаборыДанныхСхемыКомпоновкиДанных - коллекция наборов данных
//	ИменаТаблиц - Массив - имена искомых таблиц
//
// Возвращаемое значение:
//	Булево - Истина, имена таблиц встречаются в запросах наборов данных
// 
Функция НаборыДанныхСсылаютсяВЗапросахНаТаблицы(НаборыДанных, ИменаТаблиц) Экспорт
	// Проверим запросы источников данных, на использование имени таблицы
	Для Каждого НаборДанных из НаборыДанных Цикл
		Если ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") Тогда
			Для Каждого ИмяТаблицы из ИменаТаблиц Цикл
				Запрос = НРег(НаборДанных.Запрос);
				СтрокаПоиска = НРег(ИмяТаблицы);
				Если СтрНайти(Запрос, СтрокаПоиска) > 0 Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") Тогда
			Если НаборыДанныхСсылаютсяВЗапросахНаТаблицы(НаборДанных.Элементы, ИменаТаблиц) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Позволяет копировать параметры из одной схемы в другую или внутри схемы.
// Возвращает массив ссылок на скопированные параметры в приемнике.
// ВНИМАНИЕ. Параметры редактирования не переносятся.
//
// Параметры:
// 	ПриемникПараметров - СхемаКомпоновкиДанных - схема-приемник
//					   - ПараметрыСхемыКомпоновкиДанных - коллекция-приемник параметров
// 	ИсточникПараметров - СхемаКомпоновкиДанных - схема-источник
//					   - ПараметрыСхемыКомпоновкиДанных - коллекция-источник параметров
//	ПерезаполнитьСуществующий - Булево - Истина, перед добавлением будет осуществляться поиск параметра с заданным именем.
//		Внимание, операция ресурсоемкая и следует включать только если нельзя проверить уникальность параметров иначе
//
//
// Возвращаемое значение:
// 	Массив - ссылки на скопированные параметры в приемнике
// 		* Элементы - ПараметрКомпоновкиДанных - отдельный параметр
//
Функция СкопироватьПараметры(ПриемникПараметров, ИсточникПараметров, ПерезаполнитьСуществующий = Ложь) Экспорт
	
	Перем СкопированныеПараметры;
	
	СкопированныеПараметры = Новый Массив;
	
	// Коллекция, в которую можно добавить набор данных
	Если ТипЗнч(ПриемникПараметров) = Тип("СхемаКомпоновкиДанных") Тогда
		Приемник = ПриемникПараметров.Параметры;
	ИначеЕсли ТипЗнч(ПриемникПараметров) = Тип("ПараметрыСхемыКомпоновкиДанных") Тогда
		Приемник = ПриемникПараметров;
	Иначе 
		ВызватьИсключение НСтр("ru= 'Некорректный тип параметра 1'") ;
	КонецЕсли;
		
	// Коллекция, в которой содержатся наборы данных
	Если ТипЗнч(ИсточникПараметров) = Тип("СхемаКомпоновкиДанных") Тогда
		Источник = ИсточникПараметров.Параметры;
	ИначеЕсли ТипЗнч(ИсточникПараметров) = Тип("ПараметрыСхемыКомпоновкиДанных") Тогда
		Источник = ИсточникПараметров;
	Иначе 
		ВызватьИсключение НСтр("ru= 'Некорректный тип параметра 2'") ;
	КонецЕсли;
		
	Для Каждого ПараметрИсточника Из Источник Цикл
		Если Не ПерезаполнитьСуществующий Тогда
			НовыйПараметр = Приемник.Добавить();
		Иначе
			НовыйПараметр = Приемник.Найти(ПараметрИсточника.Имя);
			
			Если НовыйПараметр = Неопределено Тогда
				НовыйПараметр = Приемник.Добавить();
			КонецЕсли;
		КонецЕсли;
	
		ЗаполнитьЗначенияСвойств(НовыйПараметр, ПараметрИсточника);
	КонецЦикла;
	
	Возврат СкопированныеПараметры;
	
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Получает полное имя поля по частям
//
// Параметры:
//	ПолноеИмя - полное имя поля СКД
//
// Возвращаемое значение:
//	масЧастей - массив, содержащий части полного имени
//
Функция ИзПолногоИмениПоляПолучитьЧасти(ПолноеИмя)

	масЧастей = Новый Массив;
	СтрИмя = ПолноеИмя;
	
	Пока Не ПустаяСтрока(СтрИмя) Цикл
		Если Лев(СтрИмя, 1) = "[" Тогда
			
			Поз = СтрНайти(СтрИмя, "]");
			Если Поз = 0 Тогда
				масЧастей.Добавить(Сред(СтрИмя, 2));
				СтрИмя = "";
			Иначе
				масЧастей.Добавить(Сред(СтрИмя, 1, Поз));
				СтрИмя = Сред(СтрИмя, Поз + 2);
			КонецЕсли;
			
		Иначе
			
			Поз = СтрНайти(СтрИмя, ".");
			Если Поз = 0 Тогда
				масЧастей.Добавить(СтрИмя);
				СтрИмя = "";
			Иначе
				масЧастей.Добавить(Лев(СтрИмя, Поз - 1));
				СтрИмя = Сред(СтрИмя, Поз + 1);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат масЧастей;

КонецФункции

// Находит среди элементов полей СКД поле по имени.
//
// Параметры:
//	Элементы - поля СКД
//	Имя - Строка - имя поля СКД
//
// Возвращаемое значение:
//	ПолеКомпоновкиДанных - поле СКД
//
Функция НайтиПолеСКДПоИмени(Элементы, Имя)
	
	Для Каждого Элемент Из Элементы Цикл
		Если ВРЕГ(Строка(Элемент.Поле)) = ВРЕГ(Имя) Тогда
			Возврат Элемент;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ИсправитьНастройкиРодительскойГруппировки(ПользовательскаяНастройка, ПользовательскиеНастройки, Настройки, ПользовательскиеНастройкиМодифицированы)
	ИдентификаторПользовательскойНастройки = ПользовательскаяНастройка.ИдентификаторПользовательскойНастройки;
	
	Если Не ПустаяСтрока(ИдентификаторПользовательскойНастройки) Тогда
		ОбъектНастройки = ОтчетыКлиентСервер.ПолучитьОбъектПоПользовательскомуИдентификатору(Настройки, ИдентификаторПользовательскойНастройки);
	Иначе
		ОбъектНастройки = ПользовательскаяНастройка;
	КонецЕсли;
	РодительОбъектаНастройки = ОбъектНастройки.Родитель;
	
	Если ТипЗнч(РодительОбъектаНастройки) = Тип("ГруппировкаКомпоновкиДанных") 
		Или ТипЗнч(РодительОбъектаНастройки) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
		Или ТипЗнч(РодительОбъектаНастройки) = Тип("ТаблицаКомпоновкиДанных") Тогда
		
		ИдентификаторПользовательскойНастройкиРодителя = РодительОбъектаНастройки.ИдентификаторПользовательскойНастройки;
		
		Если Не ПустаяСтрока(ИдентификаторПользовательскойНастройкиРодителя) Тогда
			ПользовательскаяНастройкаРодитель = ОтчетыКлиентСервер.НайтиПользовательскуюНастройку(ПользовательскиеНастройки, ИдентификаторПользовательскойНастройкиРодителя);
			ПользовательскаяНастройкаРодитель.Использование = Истина;
			ПользовательскиеНастройкиМодифицированы = Истина;
			
			ИсправитьНастройкиРодительскойГруппировки(ПользовательскаяНастройкаРодитель, ПользовательскиеНастройки, Настройки, ПользовательскиеНастройкиМодифицированы);
		Иначе
			ИсправитьНастройкиРодительскойГруппировки(РодительОбъектаНастройки, ПользовательскиеНастройки, Настройки, ПользовательскиеНастройкиМодифицированы);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция СтруктураСерийЦветов(СерииДиаграммы)
	СтруктураСерийЦветов = Новый Структура;
	СтруктураЦвета = Новый Структура("Красный, Зеленый, Синий", 0, 0, 0);

	Для Каждого СерияДиаграммы Из СерииДиаграммы Цикл
		СтруктураЦветаСерии = Новый Структура; 
		ОбщегоНазначенияУТКлиентСервер.ДополнитьСтруктуру(СтруктураЦветаСерии, СтруктураЦвета, Истина);
		
		ВидЦветаСерии = СерияДиаграммы.Цвет.Вид;
		Если ВидЦветаСерии = ВидЦвета.WebЦвет Тогда
			МониторингЦелевыхПоказателей.ПолучитьRGBWebЦвета(СерияДиаграммы.Цвет,
															СтруктураЦветаСерии.Красный,
															СтруктураЦветаСерии.Зеленый,
															СтруктураЦветаСерии.Синий);
		ИначеЕсли ВидЦветаСерии = ВидЦвета.Абсолютный Тогда 
			СтруктураЦветаСерии.Красный = СерияДиаграммы.Цвет.Красный;
			СтруктураЦветаСерии.Зеленый = СерияДиаграммы.Цвет.Зеленый;
			СтруктураЦветаСерии.Синий = СерияДиаграммы.Цвет.Синий;
		КонецЕсли;
		
		ОписаниеЦвета = "Цвет_"
						+ Строка(СтруктураЦветаСерии.Красный)
						+ "_"
						+ Строка(СтруктураЦветаСерии.Зеленый)
						+ "_"
						+ Строка(СтруктураЦветаСерии.Синий);
		Если Не СтруктураСерийЦветов.Свойство(ОписаниеЦвета) Тогда
			СтруктураСерийЦветов.Вставить(ОписаниеЦвета, Новый Структура);
			
			СтруктураСерийЦветов[ОписаниеЦвета].Вставить("Серии", Новый Массив);
			СтруктураСерийЦветов[ОписаниеЦвета].Серии.Добавить(СерияДиаграммы);
			
			СтруктураСерийЦветов[ОписаниеЦвета].Вставить("Цвет", СтруктураЦветаСерии);
		Иначе
			СтруктураСерийЦветов[ОписаниеЦвета].Серии.Добавить(СерияДиаграммы);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураСерийЦветов;
КонецФункции

Процедура СформироватьГрадиентПоСериямЦветов(СтруктураСерийЦветов)
	Для Каждого СерияРазныхЦветов Из СтруктураСерийЦветов Цикл
		ОбрабатываемыеСерии = СерияРазныхЦветов.Значение.Серии;
		КоличествоСерий = ОбрабатываемыеСерии.Количество();
		
		Если КоличествоСерий = 1 Тогда
			Продолжить;
		КонецЕсли;
		
		ЦветСерии = СерияРазныхЦветов.Значение.Цвет;
		
		Тон = 0; 
		Насыщенность = 0; 
		Светлота = 0;
		
		НомерСерии = 0;
		Для Каждого ОбрабатываемаяСерия Из ОбрабатываемыеСерии Цикл
			Если Тон + Насыщенность + Светлота = 0 Тогда
				МониторингЦелевыхПоказателей.ПолучитьHSVпоRGB(ЦветСерии.Красный, 
					ЦветСерии.Зеленый, 
					ЦветСерии.Синий, 
					Тон, Насыщенность, Светлота);
			КонецЕсли;
			
			// Установим коэффициент 0.8, чтобы градиент не уходил в белый цвет
			ШагГрадиентаСветлоты = 0.8 * (100 - Светлота) / (КоличествоСерий - 1);
			
			// Насыщенность ограничивать не требуется
			ШагГрадиентаНасыщенности = (100 - ?(Насыщенность = 0, 100, Насыщенность)) / (КоличествоСерий - 1);
			
			НовыйКрасный = 0; 
			НовыйЗеленый = 0;
			НовыйСиний = 0;
			
			МониторингЦелевыхПоказателей.ПолучитьRGBпоHSV(Тон, 
				Насыщенность + ШагГрадиентаНасыщенности * НомерСерии, 
				Светлота + ШагГрадиентаСветлоты * НомерСерии, 
				НовыйКрасный, НовыйЗеленый, НовыйСиний);
			
			НовыйЦветСерии = Новый Цвет(НовыйКрасный, НовыйЗеленый, НовыйСиний);
			
			ОбрабатываемаяСерия.Цвет = НовыйЦветСерии;
			
			НомерСерии = НомерСерии + 1;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

// Устанавливает для переданной диаграммы стандартное оформление
// 
// Параметры:
//	Диаграмма - Диаграмма - изменяемая диаграмма
//	ПараметрыДиаграммы - Структура - структура параметров, соответствующих свойствам диаграммы
//
Процедура УстановитьОформлениеПоУмолчаниюДиаграммы(Диаграмма, ПараметрыДиаграммы = Неопределено)
	// Оформление диаграммы
	ПараметрыДиаграммыПереданы = (ПараметрыДиаграммы <> Неопределено);
	
	Если ПараметрыДиаграммыПереданы Тогда
		ПараметрОтчет = Неопределено;
		ОтчетОбъектПередан = ПараметрыДиаграммы.Свойство("Отчет", ПараметрОтчет) 
			И ПараметрОтчет <> Неопределено;
		
		ПараметрРасширенноеОформлениеСерий = Ложь;
		ПараметрРасширенноеОформлениеСерий = ПараметрыДиаграммы.Свойство("РасширенноеОформлениеСерий", ПараметрРасширенноеОформлениеСерий)
			И ПараметрРасширенноеОформлениеСерий;
		
		ПараметрМаксимумСерий = 0;
		Если ПараметрыДиаграммы.Свойство("МаксимумСерийКоличество", ПараметрМаксимумСерий)
			И ПараметрМаксимумСерий > 0 И ПараметрМаксимумСерий <> 999 Тогда
			Диаграмма.МаксимумСерий = МаксимумСерий.Ограничено;
			Диаграмма.МаксимумСерийКоличество = ПараметрМаксимумСерий;
		ИначеЕсли ПараметрыДиаграммы.Свойство("МаксимумСерийПроцент", ПараметрМаксимумСерий)
			И ПараметрМаксимумСерий > 0 Тогда
			Диаграмма.МаксимумСерий = МаксимумСерий.Процент;
			Диаграмма.МаксимумСерийПроцент = ПараметрМаксимумСерий;
		ИначеЕсли ПараметрМаксимумСерий = 999 И ПараметрМаксимумСерий <> 999 Тогда
			Диаграмма.МаксимумСерий = МаксимумСерий.НеЗадано;
		КонецЕсли;
		
		ПараметрТипДиаграммы = Неопределено;
		Если ПараметрыДиаграммы.Свойство("ТипДиаграммы", ПараметрТипДиаграммы) Тогда
			Если ПараметрТипДиаграммы <> "Произвольный" Тогда
				Диаграмма.ТипДиаграммы = ТипДиаграммы[ПараметрТипДиаграммы];
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Оформление области построения
	ОбластьПостроения = Диаграмма.ОбластьПостроения;
	ОбластьПостроения.ЦветШкалы = Метаданные.ЭлементыСтиля.ЦветШкалыДиаграммы.Значение;
	ОбластьПостроения.ФорматШкалыЗначений = "ЧГ=3,0";
	
	// Оформление области легенды
	ОбластьЛегенды = Диаграмма.ОбластьЛегенды;
	ОбластьЛегенды.Прокрутка = Истина;
	
	// Оформление серий и точек
	СерииДиаграммы = Диаграмма.Серии;
	ОтображениеМаркеровТочекДиаграмм = Ложь;
	Для Каждого СерияДиаграммы Из СерииДиаграммы Цикл
		Если ПараметрыДиаграммы.Свойство("ОтображениеМаркеровТочекДиаграмм", ОтображениеМаркеровТочекДиаграмм) Тогда
			УстановитьОформлениеПоУмолчаниюСерии(СерияДиаграммы, ОтображениеМаркеровТочекДиаграмм);
		Иначе
			УстановитьОформлениеПоУмолчаниюСерии(СерияДиаграммы);
		КонецЕсли;
		
		// Оформим серии диаграммы по расширенному алгоритму отчета
		Если ПараметрыДиаграммыПереданы И ОтчетОбъектПередан И ПараметрРасширенноеОформлениеСерий Тогда
			ПараметрОтчет.УстановитьРасширенноеОформлениеСерии(СерияДиаграммы);
		КонецЕсли;
	КонецЦикла;
	
	Если ПараметрыДиаграммы <> Неопределено Тогда
		ПараметрВыделениеСерийДиаграмм = Неопределено;
		ПараметрГрадиентСерийДиаграмм = Ложь;
		Если ПараметрыДиаграммы.Свойство("ВыделениеСерийДиаграмм", ПараметрВыделениеСерийДиаграмм)
			И ПараметрВыделениеСерийДиаграмм <> 0 
			И ПараметрыДиаграммы.Свойство("ГрадиентСерийДиаграмм", ПараметрГрадиентСерийДиаграмм) 
			И ПараметрГрадиентСерийДиаграмм Тогда
			СтруктураСерийЦветов = СтруктураСерийЦветов(СерииДиаграммы);
			СформироватьГрадиентПоСериямЦветов(СтруктураСерийЦветов);
		КонецЕсли;
	КонецЕсли;	

	УстановитьОформлениеПоУмолчаниюСводнойСерии(Диаграмма.СводнаяСерия);
КонецПроцедуры

Процедура УстановитьОформлениеПоУмолчаниюСерии(Серия, ОтображатьМаркерыТочек = Истина)
	// Устанавливаемые свойства серии
	Если Не ОтображатьМаркерыТочек Тогда
		Серия.Маркер = ТипМаркераДиаграммы.Нет;
	КонецЕсли;
КонецПроцедуры
 
Процедура УстановитьОформлениеПоУмолчаниюСводнойСерии(Серия)
	// Устанавливаемые свойства серии
	Серия.Маркер = ТипМаркераДиаграммы.Нет;
	Серия.Линия = Новый Линия(ТипЛинииДиаграммы.Сплошная, 3);
	Серия.Цвет = Метаданные.ЭлементыСтиля.ЦветСводнойСерииДиаграммы.Значение;
КонецПроцедуры

Процедура УдалитьПараметрыОформленияДиаграмм(КомпоновщикНастроек, ТабличныйДокумент, ПараметрыДиаграмм)
	
	// Параметры, обрабатываемые в коде
	Если ПараметрыДиаграмм <> Неопределено Тогда
		ДоступныеПараметры = КомпоновщикНастроек.Настройки.ПараметрыДанных.ДоступныеПараметры;
		
		Для Каждого ПараметрДиаграмм Из ПараметрыДиаграмм Цикл
			ПараметрДиаграмм = ДоступныеПараметры.НайтиПараметр(Новый ПараметрКомпоновкиДанных(ПараметрДиаграмм.Ключ));
			
			Если ПараметрДиаграмм <> Неопределено Тогда
				НайденнаяОбласть = ТабличныйДокумент.НайтиТекст(ПараметрДиаграмм.Заголовок);
				
				Если НайденнаяОбласть <> Неопределено Тогда
					УдаляемаяОбласть = ТабличныйДокумент.Область(НайденнаяОбласть.Верх,, НайденнаяОбласть.Низ);
					ТабличныйДокумент.УдалитьОбласть(УдаляемаяОбласть, ТипСмещенияТабличногоДокумента.ПоВертикали);
					
					Если ТабличныйДокумент.ФиксацияСверху <> 0 Тогда
						ТабличныйДокумент.ФиксацияСверху = ТабличныйДокумент.ФиксацияСверху - 1;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
КонецПроцедуры

Функция ФорматнаяСтрокаПериодовКомпоновкиДанных(Периодичность)
	ФорматнаяСтрока = "";
	
	Если Периодичность = Перечисления.Периодичность.День Тогда
		ФорматнаяСтрока = "ДЛФ=D";
	ИначеЕсли Периодичность = Перечисления.Периодичность.Неделя Тогда
		ФорматнаяСтрока = "ДЛФ=D";
	ИначеЕсли Периодичность = Перечисления.Периодичность.Декада Тогда
		ФорматнаяСтрока = "ДЛФ=D";
	ИначеЕсли Периодичность = Перечисления.Периодичность.Месяц Тогда
		ФорматнаяСтрока = "ДФ='MМММ гггг'";
	ИначеЕсли Периодичность = Перечисления.Периодичность.Квартал Тогда
		ФорматнаяСтрока = "ДФ='к ""кв."" гггг'";
	ИначеЕсли Периодичность = Перечисления.Периодичность.Полугодие Тогда
		ФорматнаяСтрока = "ДФ=ММ.гггг";
	ИначеЕсли Периодичность = Перечисления.Периодичность.Год Тогда
		ФорматнаяСтрока = "ДФ=yyyy";
	КонецЕсли;
	
	Возврат ФорматнаяСтрока;
КонецФункции

Процедура НайтиВыбранноеПолеРекурсивно(КоллекцияЭлементов, МассивЭлементов, ЗначениеПоиска)

	Для каждого Элемент Из КоллекцияЭлементов Цикл
		
		Если ТипЗнч(Элемент) = Тип("ВыбранноеПолеКомпоновкиДанных") Тогда
			Если Элемент.Поле = ЗначениеПоиска Тогда
				МассивЭлементов.Добавить(Элемент);
			КонецЕсли;
		Иначе
			НайтиВыбранноеПолеРекурсивно(Элемент.Элементы, МассивЭлементов, ЗначениеПоиска);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура НайтиГруппировкуРекурсивно(КоллекцияЭлементов, СоответствиеЭлементов, ЗначениеПоиска)
	Для каждого Элемент Из КоллекцияЭлементов Цикл
		Если ТипЗнч(Элемент) = Тип("ГруппировкаКомпоновкиДанных") Тогда
			Для Каждого ПолеГруппировки Из Элемент.ПоляГруппировки.Элементы Цикл 
				Если ПолеГруппировки.Поле = ЗначениеПоиска Тогда
					СоответствиеЭлементов.Вставить(Элемент, ПолеГруппировки)
				КонецЕсли;
			КонецЦикла;
			НайтиГруппировкуРекурсивно(Элемент.Структура, СоответствиеЭлементов, ЗначениеПоиска);
		ИначеЕсли ТипЗнч(Элемент) = Тип("ТаблицаКомпоновкиДанных") Тогда
			НайтиГруппировкуРекурсивно(Элемент.Строки, СоответствиеЭлементов, ЗначениеПоиска);
			НайтиГруппировкуРекурсивно(Элемент.Колонки, СоответствиеЭлементов, ЗначениеПоиска);
		ИначеЕсли ТипЗнч(Элемент) = Тип("ДиаграммаКомпоновкиДанных") Тогда
			НайтиГруппировкуРекурсивно(Элемент.Серии, СоответствиеЭлементов, ЗначениеПоиска);
			НайтиГруппировкуРекурсивно(Элемент.Точки, СоответствиеЭлементов, ЗначениеПоиска);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

