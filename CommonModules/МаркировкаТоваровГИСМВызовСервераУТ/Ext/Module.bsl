#Область ПрограммныйИнтерфейс

// Получает массив GTIN для переданного товара
//
// Параметры:
//  Номенклатура	 - СправочникСсылка.Номенклатура - номенклатура (маркируемый товар).
//  Характеристика	 - СправочникСсылка.Номенклатура - характеристика номенклатуры (маркируемого товара).
// 
// Возвращаемое значение:
//  Массив - массив GTIN
//
Функция МассивGTINМаркированногоТовара(Номенклатура, Характеристика) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Штрихкод
	|ИЗ
	|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	ШтрихкодыНоменклатуры.Номенклатура = &Номенклатура
	|	И ШтрихкодыНоменклатуры.Характеристика = &Характеристика");
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	
	МассивШтрихкодов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Штрихкод");
	СписокGTIN =  Новый Массив;
	
	Для Каждого Штрихкод Из МассивШтрихкодов Цикл
		
		Если МенеджерОборудованияКлиентСервер.ПроверитьКорректностьGTIN(Штрихкод) Тогда
			СписокGTIN.Добавить(Штрихкод);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокGTIN;
	
КонецФункции

// Получает массив номенклатуры КиЗ по переданному GTIN маркированного товара и списка номеклатуры КиЗ,
// подходящей под выбранные категории КиЗ в документе.
//
// Параметры:
//  СписокНоменклатураКиЗ	 - Массив - список номенклатуры КиЗ, отобранной по категориям КиЗ в документе.
//  GTIN					 - Массив - массив GTIN маркируемой номенклатуры.
// 
// Возвращаемое значение:
//  Массив - массив номенклатуры КиЗ
//
Функция ПолучитьМассивКиз(СписокНоменклатураКиЗ, GTIN) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура.Ссылка КАК НоменклатураКИЗ
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО (Номенклатура.Ссылка = ХарактеристикиНоменклатуры.Владелец
	|				ИЛИ Номенклатура.ВидНоменклатуры = ХарактеристикиНоменклатуры.Владелец)
	|ГДЕ
	|	Номенклатура.Ссылка В (&СписокНоменклатураКиЗ)
	|	И ЕСТЬNULL(ХарактеристикиНоменклатуры.КиЗГИСМGTIN, Номенклатура.КиЗГИСМGTIN) В(&GTIN)";
	
	Запрос.УстановитьПараметр("GTIN", GTIN);
	Запрос.УстановитьПараметр("СписокНоменклатураКиЗ", СписокНоменклатураКиЗ);
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("НоменклатураКИЗ");
	Возврат Результат
	
КонецФункции

// Аналитика расходов получение данных выбора
//
// Параметры:
//  ДанныеВыбора		 - СписокЗначений - список значений для заполнений данных выбора.
//  Текст				 - Строка - строка для обработки.
//  СтандартнаяОбработка - Булево - стандартная обработка.
//
Процедура АналитикаРасходовПолучениеДанныхВыбора(ДанныеВыбора, Текст, СтандартнаяОбработка) Экспорт
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = Новый СписокЗначений;
		ПродажиСервер.ЗаполнитьДанныеВыбораАналитикиРасходов(ДанныеВыбора, Текст);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик - Статья расходов при изменении сервер
//
// Параметры:
//  Форма	 - УправляемаяФорма - обрабатываемая форма.
//
Процедура СтатьяРасходовПриИзмененииСервер(Форма) Экспорт
	
	ДоходыИРасходыСервер.СтатьяРасходовПриИзменении(
		Форма.Объект,
		Форма.Объект.СтатьяРасходов,
		Форма.Объект.АналитикаРасходов);
	
	МаркировкаТоваровГИСМУТ.ОбновитьКонтрольЗаполненияАналитикиРасходов(Форма.Объект.СтатьяРасходов, Форма.Объект.АналитикаРасходов, Форма.АналитикаРасходовОбязательна);
	МаркировкаТоваровГИСМУТ.ПроверитьЧтоВсеСтатьиРаспределяются(Форма.Объект.СтатьяРасходов, Форма.ВсеСтатьиРаспределяются, Форма.УчетНДСПоФактИспользованию, Форма.ОперацияИдентификации);
	
	МаркировкаТоваровГИСМУТ.УправлениеДоступностью(Форма);
КонецПроцедуры

#КонецОбласти
