
#Область ПрограммныйИнтерфейс

// Формирует структуру для создания оплаты по нескольким заявкам на расходование средств
// Если в переданных заявках отличаются ключевые реквизиты шапки, выдается сообщение об ошибке
//
// Параметры:
//    МассивСсылок   - Массив - завки на расходование средств, по которым необходимо ввести оплату
//    ФормаОплаты - Строка - форма оплаты заявки
//    РаспределениеОплаты - Массив - строки остатков распределения оплаты
//
// Возвращаемое значение:
//    Булево - Ложь, если в переданных заказах отличаются реквизиты шапки
//
Функция СформироватьДанныеЗаполненияОплаты(МассивСсылок, ФормаОплаты, РаспределениеОплаты) Экспорт
	
	ТекстЗапроса =
		ТекстЗапросаПроверкаСтатуса() +
		ТекстЗапросаРаспределениеОплаты();
	
	Если МассивСсылок.Количество() > 1 Тогда
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаВозможностьГруппировкиЗаявок();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Если ФормаОплаты = "Безналичная" Тогда
		Запрос.УстановитьПараметр("ТипМестаОплаты", Тип("СправочникСсылка.БанковскиеСчетаОрганизаций"));
	ИначеЕсли ФормаОплаты = "Наличная" Тогда
		Запрос.УстановитьПараметр("ТипМестаОплаты", Тип("СправочникСсылка.Кассы"));
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	Отказ = Ложь;
	Если Не РезультатЗапроса[0].Пустой() > 0 Тогда
		
		ДопустимыеСтатусы = Новый Массив;
		ДопустимыеСтатусы.Добавить(Перечисления.СтатусыЗаявокНаРасходованиеДенежныхСредств.КОплате);
		
		ВыборкаЗаявки = РезультатЗапроса[0].Выбрать();
		Пока ВыборкаЗаявки.Следующий() Цикл
			
			ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
				ВыборкаЗаявки.Заявка,
				ВыборкаЗаявки.Статус,
				ВыборкаЗаявки.ЕстьОшибкиПроведен,
				ВыборкаЗаявки.ЕстьОшибкиСтатус,
				ДопустимыеСтатусы);
		КонецЦикла;
		
	ИначеЕсли МассивСсылок.Количество() > 1 Тогда
		
		Выборка = РезультатЗапроса[2].Выбрать();
		Выборка.Следующий();
		Если СообщитьОбОшибкахФормированияДанныхЗаполненияОплаты(Выборка) Тогда
			
			ТекстОшибки = НСтр("ru='Ввод одного документа оплаты на основании выделенных заявок невозможен'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	РаспределениеОплаты = ОбщегоНазначения.ТаблицаЗначенийВМассив(РезультатЗапроса[1].Выгрузить());
	
	Возврат Не Отказ;
	
КонецФункции

// Формирует структуру параметров для открытия помощника формирования платежных документов
// Если в переданных заявках отличаются ключевые реквизиты шапки, выдается предупреждение
//
// Параметры:
// Ссылки - Массив, СписокЗначений - завки на расходование средств, по которым необходимо создать платежные поручения
//
// Возвращаемое значение:
// Структура - результат предварительных проверок и адрес подготовленных данных
//     АдресДокументовДляФормированияПлатежей - Строка - адрес таблицы значений во временном хранилище
//         Колонки таблицы значений:
//             БанковскийСчет - СправочникСсылка.БанковскиеСчетаОрганизаций - счет, с которого будет осуществляться платеж
//             Валюта - СправочникСсылка.Валюты - валюта платежа
//             ДатаПлатежа - Дата - дата, на которую будет создан документ списания ДС
//             Документ - ДокументСсылка, СправочникСсылка - документ, на основании которого нужно сформировать списание ДС
//             СуммаКОплате - Число - сумма создаваемого списания ДС
//     ЕстьОшибкиПроведен - Булево
//     ЕстьОшибкиФормаОплаты - Булево
//     ЕстьОшибкиСтатус - Булево
//
Функция СформироватьДанныеДляПомощникаФормированияПлатежныхДокументов(Ссылки) Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РаспределениеПоСчетам.БанковскийСчетКасса) КАК КоличествоБанковскихСчетов,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РаспределениеПоСчетам.ДатаПлатежа) КАК КоличествоДат,
	|	МАКСИМУМ(РаспределениеПоСчетам.БанковскийСчетКасса) КАК БанковскийСчет,
	|	МАКСИМУМ(РаспределениеПоСчетам.ДатаПлатежа) КАК ДатаПлатежа,
	|	РаспределениеПоСчетам.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Распределение
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств.РаспределениеПоСчетам КАК РаспределениеПоСчетам
	|ГДЕ
	|	РаспределениеПоСчетам.Ссылка В(&МассивСсылок)
	|
	|СГРУППИРОВАТЬ ПО
	|	РаспределениеПоСчетам.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Заявка.Валюта КАК Валюта,
	|	Заявка.Ссылка КАК Ссылка,
	|	Заявка.СуммаДокумента КАК СуммаКОплате,
	|	ВЫБОР
	|		КОГДА Распределение.КоличествоБанковскихСчетов = 1
	|			ТОГДА Распределение.БанковскийСчет
	|		ИНАЧЕ Заявка.БанковскийСчет
	|	КОНЕЦ КАК БанковскийСчет,
	|	ВЫБОР
	|		КОГДА Распределение.КоличествоДат = 1
	|			ТОГДА Распределение.ДатаПлатежа
	|		ИНАЧЕ Заявка.ДатаПлатежа
	|	КОНЕЦ КАК ДатаПлатежа
	|ПОМЕСТИТЬ ПодготовкаЗаявок
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК Заявка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Распределение КАК Распределение
	|		ПО Заявка.Ссылка = Распределение.Ссылка
	|ГДЕ
	|	Заявка.Ссылка В(&МассивСсылок)
	|
	|СГРУППИРОВАТЬ ПО
	|	Заявка.Ссылка,
	|	Заявка.СуммаДокумента,
	|	ВЫБОР
	|		КОГДА Распределение.КоличествоБанковскихСчетов = 1
	|			ТОГДА Распределение.БанковскийСчет
	|		ИНАЧЕ Заявка.БанковскийСчет
	|	КОНЕЦ,
	|	Заявка.Валюта,
	|	ВЫБОР
	|		КОГДА Распределение.КоличествоДат = 1
	|			ТОГДА Распределение.ДатаПлатежа
	|		ИНАЧЕ Заявка.ДатаПлатежа
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодготовкаЗаявок.Валюта,
	|	ПодготовкаЗаявок.Ссылка,
	|	ПодготовкаЗаявок.СуммаКОплате,
	|	ПодготовкаЗаявок.БанковскийСчет,
	|	ПодготовкаЗаявок.ДатаПлатежа
	|ИЗ
	|	ПодготовкаЗаявок КАК ПодготовкаЗаявок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Заявка.Ссылка КАК Заявка,
	|	Заявка.Статус КАК Статус,
	|	Заявка.ФормаОплатыЗаявки КАК ФормаОплаты,
	|	НЕ Заявка.Проведен КАК ЕстьОшибкиПроведен,
	|	Заявка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаРасходованиеДенежныхСредств.КОплате) КАК ЕстьОшибкиСтатус,
	|	Заявка.ФормаОплатыЗаявки <> ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная) КАК ЕстьОшибкиФормаОплаты
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК Заявка
	|ГДЕ
	|	Заявка.Ссылка В(&МассивСсылок)
	|	И (НЕ Заявка.Проведен
	|			ИЛИ Заявка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаРасходованиеДенежныхСредств.КОплате))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ПодготовкаЗаявок.ДатаПлатежа) КАК ПланироватьСДаты,
	|	РАЗНОСТЬДАТ(МИНИМУМ(ПодготовкаЗаявок.ДатаПлатежа), МАКСИМУМ(ПодготовкаЗаявок.ДатаПлатежа), ДЕНЬ) КАК ДнейПланирования
	|ИЗ
	|	ПодготовкаЗаявок КАК ПодготовкаЗаявок";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Если ТипЗнч(Ссылки) = Тип("СписокЗначений") Тогда
		Запрос.УстановитьПараметр("МассивСсылок", Ссылки.ВыгрузитьЗначения());
	Иначе
		Запрос.УстановитьПараметр("МассивСсылок", Ссылки);
	КонецЕсли;

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ТаблицаДокументовДляОплаты = РезультатЗапроса[2].Выгрузить();
	
	// Для передачи в возвращаемое значение
	АдресДокументовДляФормированияПлатежей = ПоместитьВоВременноеХранилище(ТаблицаДокументовДляОплаты, Новый УникальныйИдентификатор);
	
	Отказ = Ложь;
	
	ЕстьОшибкиПроведен = Ложь;
	ЕстьОшибкиСтатус = Ложь;
	ЕстьОшибкиФормаОплаты = Ложь;
	Если Не РезультатЗапроса[3].Пустой() > 0 Тогда
		
		ВыборкаЗаявки = РезультатЗапроса[3].Выбрать();
		
		Пока ВыборкаЗаявки.Следующий() Цикл
			
			Если ВыборкаЗаявки.ЕстьОшибкиПроведен Тогда
				ЕстьОшибкиПроведен = Истина;
				
				ТекстОшибки = НСтр("ru='Документ %Документ% не проведен. Ввод на основании непроведенного документа запрещен'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ВыборкаЗаявки.Заявка);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
				
			ИначеЕсли ВыборкаЗаявки.ЕстьОшибкиСтатус Тогда
				ЕстьОшибкиСтатус = Истина;
				
				ТекстОшибки = НСтр("ru='Документ %Документ% находится в статусе ""%Статус%"". Ввод на основании разрешен только в статусе ""К оплате""'");
				
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ВыборкаЗаявки.Заявка);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Статус%",   ВыборкаЗаявки.Статус);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
				
			ИначеЕсли ВыборкаЗаявки.ЕстьОшибкиФормаОплаты Тогда
				ЕстьОшибкиФормаОплаты = Истина;
				
				ТекстОшибки = НСтр("ru='Документ %Документ% имеет форму оплаты ""%ФормаОплаты%"". В помощнике формирования платежных документов могут быть использованы заявки с безналичной формой оплаты.'");
				
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ВыборкаЗаявки.Заявка);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ФормаОплаты%",   ВыборкаЗаявки.ФормаОплаты);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ЕстьОшибкиПериод = Ложь;
	Если РезультатЗапроса[4].Пустой() Тогда
		ЕстьОшибкиПериод = Истина;
		
		ТекстОшибки = НСтр("ru='По выбранным документам невозможно определить период, за который нужно сформировать платежные документы.'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	Иначе
		ВыборкаПоДатам = РезультатЗапроса[4].Выбрать();
		ВыборкаПоДатам.Следующий();
		
		ПланироватьСДаты = ВыборкаПоДатам.ПланироватьСДаты;
		ДнейПланирования = ВыборкаПоДатам.ДнейПланирования;
	КонецЕсли;
	
	ДанныеДляПомощника = Новый Структура; 
	ДанныеДляПомощника.Вставить("АдресДокументовДляФормированияПлатежей",	АдресДокументовДляФормированияПлатежей);
	ДанныеДляПомощника.Вставить("ПланироватьСДаты",							ПланироватьСДаты);
	ДанныеДляПомощника.Вставить("ДнейПланирования",							ДнейПланирования);
	ДанныеДляПомощника.Вставить("ЕстьОшибкиПериод",							ЕстьОшибкиПериод);
	ДанныеДляПомощника.Вставить("ЕстьОшибкиПроведен",						ЕстьОшибкиПроведен);
	ДанныеДляПомощника.Вставить("ЕстьОшибкиФормаОплаты",					ЕстьОшибкиФормаОплаты);
	ДанныеДляПомощника.Вставить("ЕстьОшибкиСтатус",							ЕстьОшибкиСтатус);
	ДанныеДляПомощника.Вставить("ЕстьОшибки", 
		ЕстьОшибкиПериод Или ЕстьОшибкиПроведен Или ЕстьОшибкиФормаОплаты Или ЕстьОшибкиСтатус);
	
	Возврат ДанныеДляПомощника;
	
КонецФункции

// Преобразует двоичные данные файла в текст
//
// Параметры:
//    АдресФайла - Строка - Адрес временного хранилища с данными файла
//    Кодировка - Строка - "DOS" или "Windows"
//
// Возвращаемое значение:
//    ТекстовыйДокумент - Текст файла
//
Функция ТекстовыйДокументИзВременногоХранилищаФайла(АдресФайла, Кодировка) Экспорт
	
	ИмяВременногоФайла  = ПолучитьИмяВременногоФайла("txt");
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(АдресФайла);
	ДвоичныеДанныеФайла.Записать(ИмяВременногоФайла);
	Текст = Новый ТекстовыйДокумент();
	Если Кодировка = "DOS" Тогда
		Кодир = "cp866";
	Иначе
		Кодир = "windows-1251";
	КонецЕсли;
	
	Текст.Прочитать(ИмяВременногоФайла, Кодир);
	
	Попытка
		УдалитьФайлы(ИмяВременногоФайла);
	Исключение
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Загрузка банковской выписки.Удаление временного файла'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат Текст;
	
КонецФункции

// Получает наименование договора для графика исполнения этого договора
//
// Параметры:
//    Ссылка - ДокументСсылка.ГрафикИсполненияДоговора - Ссылка на график исполнения
//
// Возвращаемое значение:
//    Строка - Наименование
//
Функция НаименованиеДоговора(Ссылка) Экспорт
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Договор.Наименование");
	
КонецФункции

// Реквизиты эквайрингового терминала для получения представления
//
// Параметры:
//    Ссылка - СправочникСсылка.ЭквайринговыеТерминалы - Ссылка на терминал
//
// Возвращаемое значение:
//    Структура - Значения реквизитов
//
Функция ЗначенияРеквизитовЭквайринговогоТерминала(Ссылка) Экспорт
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Код, ЭтоГруппа");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаПроверкаСтатуса()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Заявка.Ссылка КАК Заявка,
	|	Заявка.Статус КАК Статус,
	|	НЕ Заявка.Проведен КАК ЕстьОшибкиПроведен,
	|	ВЫБОР КОГДА НЕ (Заявка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаРасходованиеДенежныхСредств.КОплате))
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОшибкиСтатус
	|	
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК Заявка
	|ГДЕ
	|	Заявка.Ссылка В (&МассивСсылок)
	|	И (НЕ Заявка.Проведен ИЛИ
	|		Заявка.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаРасходованиеДенежныхСредств.КОплате))
	|;
	|
	|///////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВозможностьГруппировкиЗаявок()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|			ВЫБОР КОГДА Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
	|					ИЛИ Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
	|					ИЛИ Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.ПлатежнаяКарта)
	|				ТОГДА Заявка.ФормаОплатыЗаявки
	|			КОНЕЦ) > 1
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОтличияФормаОплаты,
	|	МИНИМУМ(ВЫБОР КОГДА Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
	|			ИЛИ Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
	|			ИЛИ Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.ПлатежнаяКарта)
	|		ТОГДА Заявка.ФормаОплатыЗаявки
	|		КОНЕЦ) КАК ФормаОплатыЗаявки,
	|	
	|	МИНИМУМ(ВЫБОР КОГДА Заявка.ПланированиеСуммы = ЗНАЧЕНИЕ(Перечисление.СпособыПланированияОплатыЗаявок.ВВалютеПлатежа)
	|			ТОГДА Заявка.Валюта
	|		КОГДА Заявка.ПланированиеСуммы = ЗНАЧЕНИЕ(Перечисление.СпособыПланированияОплатыЗаявок.ВВалютеВзаиморасчетов)
	|			ТОГДА
	|				ВЫБОР КОГДА Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
	|						И Заявка.Касса <> ЗНАЧЕНИЕ(Справочник.Кассы.ПустаяСсылка)
	|					ТОГДА Заявка.Касса.ВалютаДенежныхСредств
	|					КОГДА Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
	|						И Заявка.БанковскийСчет <> ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка)
	|					ТОГДА Заявка.БанковскийСчет.ВалютаДенежныхСредств
	|				КОНЕЦ
	|		КОНЕЦ) КАК Валюта,
	|	МИНИМУМ(Заявка.Валюта) КАК ВалютаКандидат,
	|	ВЫБОР КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|		ВЫБОР КОГДА Заявка.ПланированиеСуммы = ЗНАЧЕНИЕ(Перечисление.СпособыПланированияОплатыЗаявок.ВВалютеПлатежа)
	|			ТОГДА Заявка.Валюта
	|		КОГДА Заявка.ПланированиеСуммы = ЗНАЧЕНИЕ(Перечисление.СпособыПланированияОплатыЗаявок.ВВалютеВзаиморасчетов)
	|			ТОГДА 
	|				ВЫБОР КОГДА Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
	|						И Заявка.Касса <> ЗНАЧЕНИЕ(Справочник.Кассы.ПустаяСсылка)
	|					ТОГДА Заявка.Касса.ВалютаДенежныхСредств
	|					КОГДА Заявка.ФормаОплатыЗаявки = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
	|						И Заявка.БанковскийСчет <> ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка)
	|					ТОГДА Заявка.БанковскийСчет.ВалютаДенежныхСредств
	|				КОНЕЦ
	|		КОНЕЦ) > 1
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОтличияВалютаОплаты,
	|	
	|	МИНИМУМ(Заявка.Организация) КАК Организация,
	|	ВЫБОР КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заявка.Организация) > 1
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОтличияОрганизация,
	|	
	|	МИНИМУМ(Заявка.ХозяйственнаяОперация) КАК ХозяйственнаяОперация,
	|	ВЫБОР КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заявка.ХозяйственнаяОперация) > 1
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОтличияХозяйственнаяОперация,
	|	
	|	МИНИМУМ(Заявка.ХозяйственнаяОперацияПоЗарплате) КАК ХозяйственнаяОперацияПоЗарплате,
	|	ВЫБОР КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заявка.ХозяйственнаяОперацияПоЗарплате) > 1
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОтличияХозяйственнаяОперацияПоЗарплате,
	|	
	|	МИНИМУМ(Заявка.Контрагент) КАК Контрагент,
	|	ВЫБОР КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заявка.Контрагент) > 1
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОтличияКонтрагент,
	|	
	|	МИНИМУМ(Заявка.ПодотчетноеЛицо) КАК ПодотчетноеЛицо,
	|	ВЫБОР КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заявка.ПодотчетноеЛицо) > 1
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОтличияПодотчетноеЛицо,
	|	
	|	ВЫБОР КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|		ВЫБОР КОГДА Заявка.Касса <> ЗНАЧЕНИЕ(Справочник.Кассы.ПустаяСсылка)
	|			ТОГДА Заявка.Касса
	|		КОНЕЦ) > 1
	|			ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОтличияКасса,
	|	МИНИМУМ(ВЫБОР КОГДА Заявка.Касса <> ЗНАЧЕНИЕ(Справочник.Кассы.ПустаяСсылка)
	|		ТОГДА Заявка.Касса
	|	КОНЕЦ) КАК Касса,
	|	
	|	ВЫБОР КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ
	|		ВЫБОР КОГДА Заявка.БанковскийСчет <> ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка)
	|			ТОГДА Заявка.БанковскийСчет
	|		КОНЕЦ) > 1
	|			ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОтличияБанковскийСчет,
	|	МИНИМУМ(ВЫБОР КОГДА Заявка.БанковскийСчет <> ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка)
	|		ТОГДА Заявка.БанковскийСчет
	|	КОНЕЦ) КАК БанковскийСчет,
	|	
	|	ВЫБОР КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заявка.Подразделение) > 1 ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|		ИНАЧЕ МИНИМУМ(Заявка.Подразделение)
	|	КОНЕЦ КАК Подразделение,
	|	
	|	ВЫБОР КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заявка.БанковскийСчетКонтрагента) > 1 ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка)
	|		ИНАЧЕ МИНИМУМ(Заявка.БанковскийСчетКонтрагента)
	|	КОНЕЦ КАК БанковскийСчетКонтрагента,
	|	
	|	ВЫБОР КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заявка.СтатьяДвиженияДенежныхСредств) > 1 ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|		ИНАЧЕ МИНИМУМ(Заявка.СтатьяДвиженияДенежныхСредств)
	|	КОНЕЦ КАК СтатьяДвиженияДенежныхСредств,
	|	
	|	ВЫБОР КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Заявка.СтатьяАктивовПассивов) > 1 ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|		ИНАЧЕ МИНИМУМ(Заявка.СтатьяАктивовПассивов)
	|	КОНЕЦ КАК СтатьяАктивовПассивов,
	|	
	|	МИНИМУМ(ВЫБОР КОГДА Заявка.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств))
	|		ТОГДА Заявка.КассаПолучатель
	|		ИНАЧЕ Неопределено
	|	КОНЕЦ) КАК КассаПолучатель,
	|	МИНИМУМ(ВЫБОР КОГДА Заявка.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств))
	|		ТОГДА Заявка.БанковскийСчетПолучатель
	|		ИНАЧЕ Неопределено
	|	КОНЕЦ) КАК БанковскийСчетПолучатель,
	|	
	|	МИНИМУМ(Заявка.ВалютаКонвертации) КАК ВалютаКонвертации,
	|	МИНИМУМ(Заявка.КурсКонвертации) КАК КурсКонвертации,
	|	
	|	МИНИМУМ(ВЫБОР КОГДА Заявка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеВБюджет)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ) КАК ПеречислениеВБюджет,
	|	
	|	МИНИМУМ(Заявка.АналитикаАктивовПассивов) КАК АналитикаАктивовПассивов,
	|	
	|	МИНИМУМ(Заявка.Контрагент.Наименование) КАК КонтрагентНаименование,
	|	МИНИМУМ(Заявка.Контрагент.НаименованиеПолное) КАК КонтрагентНаименованиеПолное,
	|	МИНИМУМ(Заявка.ПодотчетноеЛицо.Наименование) КАК ПодотчетноеЛицоНаименование
	|	
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК Заявка
	|
	|ГДЕ
	|	Заявка.Ссылка В (&МассивСсылок)
	|;
	|
	|//////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРаспределениеОплаты()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ГрафикПлатежей.БанковскийСчетКасса,
	|	ГрафикПлатежей.ДатаПлатежа,
	|	СУММА(ГрафикПлатежей.Сумма) КАК Сумма
	|ИЗ
	|	РегистрСведений.ГрафикПлатежей КАК ГрафикПлатежей
	|ГДЕ
	|	ГрафикПлатежей.ОбъектОплаты В(&МассивСсылок)
	|	И ГрафикПлатежей.ПоступлениеСписание = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание)
	|СГРУППИРОВАТЬ ПО
	|	ГрафикПлатежей.БанковскийСчетКасса,
	|	ГрафикПлатежей.ДатаПлатежа
	|;
	|
	|///////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция СообщитьОбОшибкахФормированияДанныхЗаполненияОплаты(ВыборкаРеквизитыШапки)
	
	Отказ = Ложь;
	
	ТекстСообщения = НСтр("ru='У выделенных заявок на расходование денежных средств отличается поле ""%ПредставлениеПоля%""'");
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияФормаОплаты Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Форма оплаты'")),,,, Отказ);
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияХозяйственнаяОперация Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Хозяйственная операция'")),,,, Отказ);
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияХозяйственнаяОперацияПоЗарплате Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Хозяйственная операция по зарплате'")),,,, Отказ);
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияВалютаОплаты Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Валюта'")),,,, Отказ);
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияОрганизация Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Организация'")),,,, Отказ);
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияКонтрагент Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Контрагент'")),,,, Отказ);
	КонецЕсли;
	
	Если ВыборкаРеквизитыШапки.ЕстьОтличияПодотчетноеЛицо Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ТекстСообщения, "%ПредставлениеПоля%", НСтр("ru='Подотчетное лицо'")),,,, Отказ);
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

Функция ОплатитьСтрокиГрафика(СтрокиГрафика, ТипДокумента) Экспорт
	
	Возврат ДенежныеСредстваСервер.ОплатитьСтрокиГрафика(СтрокиГрафика, ТипДокумента);
	
КонецФункции


#КонецОбласти
