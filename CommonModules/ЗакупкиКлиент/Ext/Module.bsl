////////////////////////////////////////////////////////////////////////////////
// Модуль "ЗакупкиКлиент", содержит процедуры и функции для 
// обработки действий пользователя в процессе работы с документами закупки
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ПроцедурыИФункцииРаботыСВыборомЗначений

// Процедура - обработчик события "НачалоВыбора" элемента формы "Соглашение"
//
// Параметры:
//	Партнер                  - СправочникСсылка.Партнеры - ссылка на партнера, для которого выберите соглашение
//	Документ                 - ДокументСсылка - ссылка на ранее выбранное соглашение для  начального позиционирования в списке
//	ДатаДокумента            - Дата - дата документа, в котором выбирается соглашение
//	ДоступноДляЗакупки       - Булево - Если Истина, отбираются только соглашения с установленным флагом ДоступноДляЗакупки
//	БезКомиссионныхСоглашений - Булево - Если Истина, отбираются только соглашения с хоз. операций "Закупка у поставщика"
//
Процедура НачалоВыбораСоглашенияСПоставщиком(Элемент,
	                                         СтандартнаяОбработка,
	                                         Партнер,
	                                         Документ,
	                                         ДатаДокумента='00010101',
	                                         СтруктураДополнительногоОтбора = Неопределено) Экспорт

	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Документ", Документ);
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	ДополнительныеПараметры.Вставить("ДатаДокумента", ДатаДокумента);
	
	Если Не ЗначениеЗаполнено(СтруктураДополнительногоОтбора) Тогда
		СтруктураДополнительногоОтбора = Новый Структура;
	КонецЕсли;
	Для Каждого ПараметрВыбора Из Элемент.ПараметрыВыбора Цикл
		СтруктураДополнительногоОтбора.Вставить(СтрЗаменить(ПараметрВыбора.Имя,"Отбор.",""), ПараметрВыбора.Значение);
	КонецЦикла;
	ДополнительныеПараметры.Вставить("СтруктураДополнительногоОтбора", СтруктураДополнительногоОтбора);
	
	Если Не ЗначениеЗаполнено(Партнер) Тогда
		
		ОткрытьФорму(
			"Справочник.Партнеры.ФормаВыбора",
			Новый Структура("Отбор", Новый Структура("Поставщик", Истина)),,,,,
			Новый ОписаниеОповещения("НачалоВыбораСоглашенияСПоставщикомВыборПартнераЗавершение", ЗакупкиКлиент, ДополнительныеПараметры));
		
	Иначе
		НачалоВыбораСоглашенияСПоставщикомВыборПартнераЗавершение(Партнер, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

// Проверяет заполненность реквизитов, необходимых для заполнения цен в документе по соглашению с поставщиком
//
// Параметры:
//  Партнер                 - СправочникСсылка.Партнеры -  партнер, для которого отбираются соглашения.
//  ДополнительныеПараметры - Структура - дополнительные параметры для отбора соглашений.
//
Процедура НачалоВыбораСоглашенияСПоставщикомВыборПартнераЗавершение(Партнер, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Партнер) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры.СтруктураДополнительногоОтбора) Тогда
		СтруктураОтбора = ДополнительныеПараметры.СтруктураДополнительногоОтбора;
		СтруктураОтбора.Вставить("Партнер", Партнер);
	Иначе
		СтруктураОтбора = Новый Структура("Партнер", Партнер);
	КонецЕсли;
	
	ОткрытьФорму (
		"Справочник.СоглашенияСПоставщиками.ФормаВыбора",
		Новый Структура("ДатаДокумента,ТекущаяСтрока,Отбор",
			ДополнительныеПараметры.ДатаДокумента,
			ДополнительныеПараметры.Документ,
			СтруктураОтбора),
		ДополнительныеПараметры.Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииПроверкиВозможностиВыполненияДействий

// Проверяет заполненность реквизитов, необходимых для заполнения цен в документе по соглашению с поставщиком
//
// Параметры:
//  Документ                    - ДокументОбъект, для которого выполняются проверки
//  ИмяТабличнойЧасти           - Строка - имя табличной части, в которой необходимо осуществить проверку
//  ПредставлениеТабличнойЧасти - Строка - представление табличной части для информирования пользователя
//
// Возвращаемое значение:
//  Булево - Ложь, если необходимые данные не заполнены.
//
Функция НеобходимоЗаполнениеЦенПоСоглашению(Документ, ИмяТабличнойЧасти, ПредставлениеТабличнойЧасти) Экспорт

	Если Не ЗначениеЗаполнено(Документ.Соглашение) Тогда
		
		ПоказатьПредупреждение(,НСтр("ru='В документе не указано соглашение с поставщиком, или соглашения с поставщиком отсутствуют. Цены не могут быть заполнены'"));
		Возврат Ложь;
		
	ИначеЕсли Не ЗначениеЗаполнено(Документ.Валюта) Тогда
		
		ПоказатьПредупреждение(,НСтр("ru='В документе не указана валюта. Цены не могут быть заполнены'"));
		Возврат Ложь;
		
	ИначеЕсли Документ[ИмяТабличнойЧасти].Количество() = 0 Тогда
		
		ТекстПредупреждения = НСтр("ru='В документе не заполнена таблица %ПредставлениеТабличнойЧасти%. Цены не могут быть заполнены'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%ПредставлениеТабличнойЧасти%", ПредставлениеТабличнойЧасти);
		ПоказатьПредупреждение(,ТекстПредупреждения);
		Возврат Ложь;
		
	Иначе
		
		Возврат Истина;
		
	КонецЕсли;

КонецФункции

// Проверяет заполненность реквизитов, необходимых для заполнения цен в документе по условию цен поставщика
//
// Параметры:
//  Документ                    - ДокументОбъект, для которого выполняются проверки
//  ИмяТабличнойЧасти           - Строка - имя табличной части, в которой необходимо осуществить проверку
//  ПредставлениеТабличнойЧасти - Строка - представление табличной части для информирования пользователя
//
// Возвращаемое значение:
//  Булево - Ложь, если необходимые данные не заполнены.
//
Функция НеобходимоЗаполнениеЦенПоВидуЦенПоставщика(Документ, ИмяТабличнойЧасти, ПредставлениеТабличнойЧасти) Экспорт

	Если Не ЗначениеЗаполнено(Документ.Валюта) Тогда
		
		ПоказатьПредупреждение(,НСтр("ru='В документе не указана валюта. Цены не могут быть заполнены'"));
		Возврат Ложь;
		
	ИначеЕсли Документ[ИмяТабличнойЧасти].Количество() = 0 Тогда
		
		ТекстПредупреждения = НСтр("ru='В документе не заполнена таблица %ПредставлениеТабличнойЧасти%. Цены не могут быть заполнены'");
		ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%ПредставлениеТабличнойЧасти%", ПредставлениеТабличнойЧасти);
		ПоказатьПредупреждение(,ТекстПредупреждения);
		Возврат Ложь;
		
	Иначе
		
		Возврат Истина;
		
	КонецЕсли;

КонецФункции

// Вызывает проверку на наличие в информационной базе ранее сопоставленной номенклатуры поставщика
// Если ранее сопоставленная номенклатура найдена - предупреждает об этом пользователя
//
// Параметры:
//   Объект - ДанныеФормыКоллекция - документ, для которого необходимо осуществлять проверку.
//   ОписаниеОповещения - ОписаниеОповещения - определяет процедуру, которая будет вызвана после завершения проверки.
//   НеВыполнятьПроверкуДляПользователя - Булево - флаг, определяющий нужно ли осуществлять проверку.
//
Процедура ПроверитьСопоставленнуюНоменклатуруПоставщика(Объект, ОписаниеОповещения, НеВыполнятьПроверкуДляПользователя) Экспорт
	
	Если Объект.Товары.Количество() > 0 И Не НеВыполнятьПроверкуДляПользователя Тогда
		
		ВыводитьПредупреждение = ЗакупкиВызовСервера.ПроверитьСопоставленнуюНоменклатуруПоставщика(Объект);
		
		Если ВыводитьПредупреждение Тогда
			
			ТекстВопроса = НСтр("ru='Для некоторой номенклатуры найдена ранее сопоставленная номенклатура поставщика.'");
			ТекстВопроса = ТекстВопроса + Символы.ПС + НСтр("ru='Несопоставленная номенклатура поставщика будет сопоставлена с номенклатурой. Продолжить?'");
			
			ПоказатьВопрос(
				ОписаниеОповещения,
				ТекстВопроса,
				РежимДиалогаВопрос.ДаНет);
			
		Иначе
			
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, Неопределено);
			
		КонецЕсли;
		
	Иначе
		
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

// При попытке выбора номенклатуры поставщика в строке таблицы пользователь получает сообщение о незаполненности партнера
//
// Параметры:
//	Объект              - ДанныеФормыСтруктура - документ, для выдачи сообщения об ошибках
//	Поле                - ПолеФормы - поле, в котором пользователь осуществляет выбор
//	ИмяПроверяемогоПоля - Строка - имя поля, для которого необходимо выдать ошибку
//
Процедура ПриВыбореНоменклатурыПоставщика(Объект, Поле, ИмяПроверяемогоПоля, ПредставлениеПартнера) Экспорт
	
	Если Поле.Имя = ИмяПроверяемогоПоля Тогда
		
		Если Не ЗначениеЗаполнено(Объект.Партнер) Тогда
			
			ОчиститьСообщения();
			ТекстСообщения = НСтр("ru = 'Поле ""%ПредставлениеПартнера%"" не заполнено'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПредставлениеПартнера%", ПредставлениеПартнера);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, "Объект.Партнер");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Проверяет является ли оповещение в форме событием изменения документов оплаты
//
// Параметры:
//  ИмяСобытия - Строка - имя события из процедуры формы "ОбработкаОповещения".
//
// Возвращаемое значение:
//  Булево - Истина, если оповещение является оплатой.
//
Функция ИзменилисьДокументыОплатыПоставщиком(ИмяСобытия) Экспорт
	
	Возврат
		ИмяСобытия = "Запись_АвансовыйОтчет"
		Или ИмяСобытия = "Запись_СписаниеБезналичныхДенежныхСредств"
		Или ИмяСобытия = "Запись_ПоступлениеБезналичныхДенежныхСредств"
		Или ИмяСобытия = "Запись_РасходныйКассовыйОрдер"
		Или ИмяСобытия = "Запись_ВзаимозачетЗадолженности"
		Или ИмяСобытия = "Запись_СписаниеЗадолженности";
	
КонецФункции

#КонецОбласти

#Область ПроцедурыОповещенияПользователяОВыполненныхДействиях

//Показывает оповещение пользователя об окончании заполнения условий закупок по умолчанию
//
Процедура ОповеститьОбОкончанииЗаполненияУсловийЗакупокПоУмолчанию() Экспорт

	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Условия закупок заполнены'"),
		,
		НСтр("ru='Условия закупок по умолчанию заполнены'"),
		БиблиотекаКартинок.Информация32);

КонецПроцедуры

//Показывает оповещение пользователя об окончании заполнения условий закупок
//
Процедура ОповеститьОбОкончанииЗаполненияУсловийЗакупок() Экспорт

	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Условия закупок заполнены'"),
		,НСтр("ru='Условия закупок по соглашению с поставщиком заполнены'"),
		БиблиотекаКартинок.Информация32);

КонецПроцедуры

//Показывает оповещение пользователя об окончании заполнения цен по соглашению с клиентом
//
// Параметры:
//	ЦеныРассчитаны - Булево - Признак успешного расчета цен хотя бы в одной строке
//
Процедура ОповеститьОбОкончанииЗаполненияЦенПоСоглашению(ЦеныРассчитаны = Истина) Экспорт

	Если ЦеныРассчитаны Тогда
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Цены заполнены'"),
			,
			НСтр("ru='Цены по соглашению с поставщиком заполнены'"),
			БиблиотекаКартинок.Информация32);
	Иначе
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Цены не заполнены'"),
			,
			НСтр("ru='Ни в одной строке цены по соглашению с поставщиком не заполнены'"),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
КонецПроцедуры

// Показывает оповещение пользователя об окончании заполнения цен по условию цен поставщика.
//
// Параметры:
//  ЦеныРассчитаны       - Булево - Признак успешного расчета цен хотя бы в одной строке.
//  ВидЦеныПоставщика - СправочникСсылка.ВидыЦенПоставщиков - Вид цены поставщика, по которому осуществлялось заполнение цен.
//
Процедура ОповеститьОбОкончанииЗаполненияЦенПоВидуЦеныПоставщика(ЦеныРассчитаны, ВидЦеныПоставщика) Экспорт

	Если ЦеныРассчитаны Тогда
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Цены заполнены'"),
			,
			СтрЗаменить(НСтр("ru='Цены по виду цены поставщика ""%ВидЦеныПоставщика%"" заполнены'"), "%ВидЦеныПоставщика%", ВидЦеныПоставщика),
			БиблиотекаКартинок.Информация32);
	Иначе
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Цены не заполнены'"),
			,
			СтрЗаменить(НСтр("ru='Ни в одной строке цены по виду цены поставщика ""%ВидЦеныПоставщика%"" не заполнены'"), "%ВидЦеныПоставщика%", ВидЦеныПоставщика),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
КонецПроцедуры

//Показывает оповещение пользователя об окончании заполнения дат поступления всех строк тч
//
// Параметры:
//	ДатаПоступления - Дата - дата отгрузки, по которой заполнялась дата в табличной части
//	ВыделенныеСтроки - Массив - влияет на текст сообщения
//
Процедура ОповеститьОбОкончанииЗаполненияДатПоступления(ДатаПоступления, ЗаполнениеВыделенныхСтрок) Экспорт

	Если ЗаполнениеВыделенныхСтрок Тогда
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Дата поступления заполнена'"),
			,
			СтрЗаменить(НСтр("ru='Для выделенных строк установлена дата поступления %ДатаПоступления%'"), "%ДатаПоступления%", Формат(ДатаПоступления, "ДЛФ=D")),
			БиблиотекаКартинок.Информация32);
		
	Иначе
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Дата поступления заполнена'"),
			,
			СтрЗаменить(НСтр("ru='Для всех строк установлена дата поступления %ДатаПоступления%'"), "%ДатаПоступления%", Формат(ДатаПоступления, "ДЛФ=D")),
			БиблиотекаКартинок.Информация32);
		
	КонецЕсли;
	
КонецПроцедуры

//Показывает оповещение пользователя об окончании перезаполнения товаров по фактической приемке
//
// Параметры:
//	ЕстьИзменения           - Булево - Если Истина - есть изменения в табличной части
//	КоличествоПерезаполнено - Булево - Если Истина - количество было перезаполнено по фактической приемке
//
Процедура ОповеститьОбОкончанииПерезаполненияТоваровПоПриемке(ЕстьИзменения, КоличествоПерезаполнено) Экспорт
	
	Если ЕстьИзменения Тогда
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Количество перезаполнено'"),
			,
			НСтр("ru='В строках перезаполнено количество.'"),
			БиблиотекаКартинок.Информация32);
		
	Иначе
		
		Если КоличествоПерезаполнено Тогда
			
			ПоказатьОповещениеПользователя(
				НСтр("ru = 'Перезаполнение не требуется'"),
				,
				НСтр("ru='Перезаполнение не требуется, т.к. во всех строках количество совпадает с указанным.'"),
				БиблиотекаКартинок.Информация32);
			
		Иначе
		
			ПоказатьОповещениеПользователя(
				НСтр("ru = 'Перезаполнение не требуется'"),
				,
				НСтр("ru='Ни по одной строке не начата приемка.'"),
				БиблиотекаКартинок.Информация32);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбработкиКомандПользователя

// Процедура для выбора условия цен поставщика из формы выбора.
//
// Параметры:
//	Партнер - СправочникСсылка.Партнер - партнер, для которого выберите условия цен поставщиков
//
Процедура ВыбратьВидЦеныПоставщика(ОписаниеОповещения, Партнер, ЦенаВключаетНДС = Неопределено) Экспорт

	Отбор = Новый Структура("Владелец", Партнер);
	Если ЦенаВключаетНДС <> Неопределено Тогда
		Отбор.Вставить("ЦенаВключаетНДС", ЦенаВключаетНДС);
	КонецЕсли;

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОписаниеОповещения);
	
	ОткрытьФорму(
		"Справочник.ВидыЦенПоставщиков.ФормаВыбора",
		Новый Структура("Отбор", Отбор),,,,,
		Новый ОписаниеОповещения("ВыбратьВидЦеныПоставщикаЗавершение", ЗакупкиКлиент, ДополнительныеПараметры));

КонецПроцедуры

// Обработчик завершения выбора вида цен поставщика.
//
// Параметры:
//  ВидЦеныПоставщика       - СправочникСсылка.ВидыЦенПоставщиков - выбранный вид цен поставщика.
//  ДополнительныеПараметры - Структура - дополнительные параметры.
//
Процедура ВыбратьВидЦеныПоставщикаЗавершение(ВидЦеныПоставщика, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещения, ВидЦеныПоставщика);
	
КонецПроцедуры

// Формирует список выбора номенклатуры поставщика с отбором по номенклатуре, характеристике, упаковке
//
// Параметры:
//	Партнер       - СправочникСсылка.Партнеры - владелец номенклатуры поставщика
//	ТекущаяСтрока - ДанныеФормыЭлементКоллекции - текущая строка таблицы Товары
//	СписокВыбора  - СписокЗначений - список выбора номенклатуры поставщика
//
Процедура ЗаполнитьСписокВыбораНоменклатурыПоставщика(Партнер, ТекущаяСтрока, СписокВыбора) Экспорт
	
	Если ТекущаяСтрока = Неопределено Или Не ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		СписокВыбора.Очистить();
	Иначе
		
		МассивЗначений = ЗакупкиВызовСервера.СформироватьСписокВыбораНоменклатурыПоставщика(
			Партнер,
			ТекущаяСтрока.Номенклатура,
			ТекущаяСтрока.Характеристика);
		
		СписокВыбора.ЗагрузитьЗначения(МассивЗначений);
		
	КонецЕсли;
	
КонецПроцедуры

// Формирует список выбора номеров ГТД с отбором по номенклатуре, характеристике
//
// Параметры:
//	ТекущаяСтрока - ДанныеФормыЭлементКоллекции - текущая строка таблицы Товары
//	СписокВыбора  - СписокЗначений - список выбора номеров ГТД
//
Процедура ЗаполнитьСписокВыбораНомеровГТД(ТекущаяСтрока, СписокВыбора) Экспорт
	
	Если ТекущаяСтрока = Неопределено Или Не ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		СписокВыбора.Очистить();
	Иначе
		СписокЗначений = ЗакупкиВызовСервера.ЗаполнитьСписокВыбораНомеровГТД(
			ТекущаяСтрока.Номенклатура,
			ТекущаяСтрока.Характеристика,
			ТекущаяСтрока.СтранаПроисхождения);
		
		СписокВыбора.Очистить();
		Для каждого ТекСтр Из СписокЗначений Цикл
			НовСтрока = СписокВыбора.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока,ТекСтр);
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму списка номенклатуры поставщика с отбором по владельцу, номенклатуре, характеристике, упаковке
//
// Параметры:
//	Партнер       - СправочникСсылка.Партнеры - владелец номенклатуры поставщика
//	Ссылка        - ДокументСсылка - ссылка на документ из формы которого вызывается процедура
//	ТекущаяСтрока - ДанныеФормыЭлементКоллекции - текущая строка таблицы Товары
//
Процедура ПоказатьНоменклатуруПоставщика(Партнер, Ссылка, ТекущаяСтрока) Экспорт
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(Партнер) Тогда
		Отказ = Истина;
	КонецЕсли;
		
	Если ТекущаяСтрока = Неопределено Тогда
		Отказ = Истина;
	ИначеЕсли Не ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		
		ОчиститьСообщения();
		
		Если Не ЗначениеЗаполнено(Партнер) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Поле ""Партнер"" не заполнено'"), Ссылка, "Объект.Партнер");
		КонецЕсли;
		
		Если ТекущаяСтрока = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Выберите строку таблицы ""Товары""'"), Ссылка, "Объект.Товары");
		ИначеЕсли Не ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не заполнена колонка ""Номенклатура""'"),
				Ссылка,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.Товары", ТекущаяСтрока.НомерСтроки, "Номенклатура"));
		КонецЕсли;
		
	Иначе
		
		СтруктураПараметров = Новый Структура();
		СтруктураПараметров.Вставить("Владелец",       Партнер);
		СтруктураПараметров.Вставить("Номенклатура",   ТекущаяСтрока.Номенклатура);
		СтруктураПараметров.Вставить("Характеристика", ТекущаяСтрока.Характеристика);
		СтруктураПараметров.Вставить("Упаковка",       ТекущаяСтрока.Упаковка);
		
		ОткрытьФорму("Справочник.НоменклатураПоставщиков.Форма.ФормаСписка", Новый Структура("Отбор", СтруктураПараметров));
		
	КонецЕсли;
	
КонецПроцедуры

// Проверяет заполненность реквизитов, необходимых для заполнения товаров по приемке
//
// Параметры:
//  ЕстьПринятыеТовары              - Булево - признак наличия принятых товаров.
//  ЗаполнятьПринимающимисяТоварами - Булево - признак, указывающий на необходимость заполнения принимающимися товарами.
//
// Возвращаемое значение:
//  Булево - Ложь, если пользователь отказался от заполнения.
//
Функция ВопросОПерезаполненииПринятымиТоварами(ОписаниеОповещения, ЕстьПринятыеТовары) Экспорт
	
	СписокКнопок = Новый СписокЗначений();
	
	Если ЕстьПринятыеТовары Тогда
		
		ТекстВопроса = НСтр("ru='Не все товары приняты. Учитывать при заполнении товары, которые принимаются?'");
		СписокКнопок.Добавить("Учитывать", НСтр("ru = 'Учитывать'"));
		СписокКнопок.Добавить("НеУчитывать", НСтр("ru = 'Не учитывать'"));
		
	Иначе
		
		ТекстВопроса = НСтр("ru='Отсутствуют принятые товары. Учитывать при заполнении товары, которые принимаются?'");
		СписокКнопок.Добавить("Учитывать", НСтр("ru = 'Учитывать'"));
		
	КонецЕсли;
	
	СписокКнопок.Добавить("НеПерезаполнять", НСтр("ru = 'Не перезаполнять'"));
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОписаниеОповещения", ОписаниеОповещения);
	ПоказатьВопрос(
		Новый ОписаниеОповещения("ВопросОПерезаполненииПринятымиТоварамиЗавершение", ЗакупкиКлиент, ДополнительныеПараметры),
		ТекстВопроса,
		СписокКнопок);
	
КонецФункции

// Обработчик завершения ответа на вопрос перезаполнения принятыми товарами.
//
// Параметры:
//  ОтветНаВопрос           - Строка - вариант ответа на вопрос, выбранный пользователем.
//  ДополнительныеПараметры - Структура - дополнительные параметры.
//
Процедура ВопросОПерезаполненииПринятымиТоварамиЗавершение(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ЗаполнятьПринимающимисяТоварами", (ОтветНаВопрос = "Учитывать"));
	ВозвращаемоеЗначение.Вставить("Результат", (ОтветНаВопрос <> "НеПерезаполнять"));
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещения, ВозвращаемоеЗначение);
	
КонецПроцедуры

// Открывает отчет ДинамикаИзмененияЦенНоменклатурыПоставщика с отбором из формы
//
// Параметры:
//	Форма - УправляемаяФорма - форма документа, из которого открывается отчет
//
Процедура ОткрытьОтчетПоДинамикеЦенПоставщика(Форма) Экспорт
	
	СписокНоменклатуры  = Новый СписокЗначений;
	СписокХарактеристик = Новый СписокЗначений;
	Для Каждого ИдентификаторСтрокиДереваЦен Из Форма.Элементы.ДеревоЦен.ВыделенныеСтроки Цикл
		СтрокаТЧ = Форма.ДеревоЦен.НайтиПоИдентификатору(ИдентификаторСтрокиДереваЦен);
		СписокНоменклатуры.Добавить(СтрокаТЧ.Номенклатура);
		Если СтрокаТЧ.ХарактеристикиИспользуются Тогда
			Если СтрокаТЧ.ПолучитьРодителя() = Неопределено Тогда
				Для Каждого СтрокаХарактеристика Из СтрокаТЧ.ПолучитьЭлементы() Цикл
					Если СписокХарактеристик.НайтиПоЗначению(СтрокаХарактеристика.Характеристика) = Неопределено Тогда
						СписокХарактеристик.Добавить(СтрокаХарактеристика.Характеристика);
					КонецЕсли;
				КонецЦикла;
			Иначе
				Если СписокХарактеристик.НайтиПоЗначению(СтрокаТЧ.Характеристика) = Неопределено Тогда
					СписокХарактеристик.Добавить(СтрокаТЧ.Характеристика);
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если СписокХарактеристик.НайтиПоЗначению(
					ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка")) = Неопределено Тогда
				СписокХарактеристик.Добавить(ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СписокВидовЦен = Новый СписокЗначений;
	Для Каждого СтрокаВидЦены Из УстановкаЦенКлиентСервер.ВыбранныеСтрокиТаблицыВидовЦен(Форма) Цикл
		СписокВидовЦен.Добавить(СтрокаВидЦены.Ссылка, СтрокаВидЦены.Наименование);
	КонецЦикла;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Номенклатура", СписокНоменклатуры);
	Отбор.Вставить("Характеристика", СписокХарактеристик);
	Отбор.Вставить("ВидЦеныПоставщика", СписокВидовЦен);
	Отбор.Вставить("Партнер", Форма.Объект.Партнер);
	
	ПараметрыФормы = Новый Структура("Отбор, КлючНазначенияИспользования, КлючВарианта, СформироватьПриОткрытии",
	        Отбор,
	        НСтр("ru = 'Динамика изменения цен по номенклатуре (Диаграмма)'"),
	        НСтр("ru = 'Динамика изменения цен по номенклатуре (Диаграмма)'"),
	        Истина);
	
	ОткрытьФорму("Отчет.ДинамикаИзмененияЦенНоменклатурыПоставщика.Форма",
	        ПараметрыФормы);
	
КонецПроцедуры

#Область РаботаСоСчетФактурами

// Общая процедура обработки навигационной ссылки текста счета-фактуры в документах продажи.
// В завимости от выбранной ссылки вызывает создание/открытие счетов-фактуры документа.
//
// Параметры:
//  Форма - УправляемаяФорма - Форма, в которой располагается элемент.
//  НавигационнаяСсылка - Строка - Значение гиперссылки форматированной строки.
//  СтандартнаяОбработка - Булево - Признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаНавигационнойСсылкиТекстСчетФактура(Форма, НавигационнаяСсылка, СтандартнаяОбработка, Организация) Экспорт
	
	ОбратноеОбложениеНДС = Ложь;
	
	Если ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
	 ИЛИ ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
	 ИЛИ ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
		ОбратноеОбложениеНДС = 
		(Форма.Объект.НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя"));
	КонецЕсли;
	
	Если НавигационнаяСсылка = "ОткрытьСписокСчетовФактурПолученных" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		СтруктураОтбор = Новый Структура(
			"ДокументОснование, Организация, ПометкаУдаления",
			Форма.Объект.Ссылка, Организация, Ложь);
		
		ПараметрыФормы = Новый Структура("Отбор", СтруктураОтбор);
		
		Если ОбратноеОбложениеНДС Тогда
			ОткрытьФорму("Документ.СчетФактураПолученныйНалоговыйАгент.ФормаСписка", ПараметрыФормы, Форма);
		Иначе
			ОткрытьФорму("Документ.СчетФактураПолученный.ФормаСписка", ПараметрыФормы, Форма);
		КонецЕсли;
		
	ИначеЕсли НавигационнаяСсылка = "ВвестиНовыйСчетФактуру" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ВвестиСчетФактуру(Форма, Организация , Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Создает документ "Счет-фактура полученный" с признаком "Исправление" или открывает существующий.
//
// Параметры:
//  Форма - Форма - Форма, из которой вызвана команда. Устанавливается владелецем открываемой формы счета-фактуры;
//  Организация - СправочникСсылка.Организации - Организация, на имя которой оформляется счет-фактура;
//  ОткрыватьСуществующую - Булево - Признак необходимости поиска и открытия формы найденного счета-фактуры;
//  Исправление - Булево - Истина - Создается исправление счета-фактуры;
//  Корректировочный - Булево - Создается корректировочный счет-фактура.
//
Процедура ВвестиСчетФактуру(Форма, Организация, ОткрыватьСуществующую = Ложь, Исправление = Ложь, Корректировочный = Ложь) Экспорт
	
	ВводитьСчетФактуру = Истина;
	ОбратноеОбложениеНДС = Ложь;
	
	Если ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
	 ИЛИ ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
	 ИЛИ ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
		ОбратноеОбложениеНДС = 
		(Форма.Объект.НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя"));
	КонецЕсли;
	
	Если ОткрыватьСуществующую Тогда
		СчетФактураДокумента = ЗакупкиВызовСервера.СчетФактураДокумента(
			Форма.Объект.Ссылка,
			Организация,
			Ложь);
		Если ЗначениеЗаполнено(СчетФактураДокумента) Тогда
			ВводитьСчетФактуру = Ложь;
			
			ПараметрыФормы = Новый Структура("Ключ, ДокументОснование",
				СчетФактураДокумента, Форма.Объект.Ссылка);
			Если ОбратноеОбложениеНДС Тогда
				ОткрытьФорму("Документ.СчетФактураПолученныйНалоговыйАгент.ФормаОбъекта", ПараметрыФормы, Форма);
			Иначе
				ОткрытьФорму("Документ.СчетФактураПолученный.ФормаОбъекта", ПараметрыФормы, Форма);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ВводитьСчетФактуру Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Исправление",           Исправление);
		ДополнительныеПараметры.Вставить("Корректировочный",      Корректировочный);
		ДополнительныеПараметры.Вставить("Организация",           Организация);
		ДополнительныеПараметры.Вставить("ОткрыватьСуществующую", ОткрыватьСуществующую);
		ДополнительныеПараметры.Вставить("ОбратноеОбложениеНДС",  ОбратноеОбложениеНДС);
		ДополнительныеПараметры.Вставить("Форма", Форма);
		ДополнительныеПараметры.Вставить("Контрагент", Форма.Объект.Контрагент);
		
		Оповещение = Новый ОписаниеОповещения(
			"ВвестиСчетФактуруЗавершение",
			ЭтотОбъект,
			ДополнительныеПараметры);
		
		ПроверитьВозможностьСозданияСчетФактуры(Форма, Оповещение);
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик завершение ввода счета-фактуры.
//
// Параметры:
//  Результат               - Булево - Истина, если счет-фактуру можно вводить.
//  ДополнительныеПараметры - Структура - дополнительные параметры.
//
Процедура ВвестиСчетФактуруЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Или Результат = КодВозвратаДиалога.Да Тогда
		
		Если ДополнительныеПараметры.Исправление Тогда
			СчетФактураДокумента = ЗакупкиВызовСервера.СчетФактураДокумента(
				ДополнительныеПараметры.Форма.Объект.Ссылка,
				ДополнительныеПараметры.Организация,
				Ложь);
			Если ЗначениеЗаполнено(СчетФактураДокумента)
				И НЕ ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(СчетФактураДокумента, "Проведен") Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Ввод исправительного счета-фактуры возможен только на основании проведенного счета-фактуры'"));
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ДокументОснование", ДополнительныеПараметры.Форма.Объект.Ссылка);
		ПараметрыОткрытия.Вставить("Исправление",       ДополнительныеПараметры.Исправление);
		ПараметрыОткрытия.Вставить("Контрагент",        ДополнительныеПараметры.Контрагент);
		ПараметрыОткрытия.Вставить("Корректировочный",  ДополнительныеПараметры.Корректировочный);
		ПараметрыОткрытия.Вставить("Организация",       ДополнительныеПараметры.Организация);
		
		ПараметрыФормы = Новый Структура(
			"Основание, ДокументОснование",
		ПараметрыОткрытия, ДополнительныеПараметры.Форма.Объект.Ссылка);
		
		Если ДополнительныеПараметры.ОбратноеОбложениеНДС Тогда
			ОткрытьФорму("Документ.СчетФактураПолученныйНалоговыйАгент.ФормаОбъекта", ПараметрыФормы, ДополнительныеПараметры.Форма);
		Иначе
			ОткрытьФорму("Документ.СчетФактураПолученный.ФормаОбъекта", ПараметрыФормы, ДополнительныеПараметры.Форма);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Служебная процедура
Процедура ПроверитьВозможностьСозданияСчетФактурыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	ВводитьСчетФактуру = Ложь;
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Попытка
			Форма = ДополнительныеПараметры.Форма;
			ВводитьСчетФактуру = Форма.Записать(
				Новый Структура(
					"РежимЗаписи, ДействиеПослеЗаписи",
					РежимЗаписиДокумента.Проведение, ДополнительныеПараметры.Оповещение));
		Исключение
			ПоказатьПредупреждение(,НСтр("ru = 'Не удалось выполнить проведение документа'"));
		КонецПопытки;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, ВводитьСчетФактуру);
	
КонецПроцедуры

// Проверяет проведен ли документ, из которого создается счет-фактура.
// Предлагает провести его, если нужно, и вызывает создание счета-фактуры.
//
// Параметры:
//  Форма - УправляемаяФорма - Форма, из которой вызвана команда создания.
//  ДополнительныеПараметры - Структура - Содержит .
//  Оповещение - ОписаниеОповещения - Процедура, которая вызовет создание счета-фактуры при успешной проверке.
//
Процедура ПроверитьВозможностьСозданияСчетФактуры(Форма, Оповещение) Экспорт
	
	Если Не ЗначениеЗаполнено(Форма.Объект.Ссылка)
		Или Не Форма.Объект.Проведен
		Или Форма.Модифицированность Тогда
		
		ДополнительныеПараметры = Новый Структура("Оповещение, Форма", Оповещение, Форма);
		ОповещениеВопроса = Новый ОписаниеОповещения(
			"ПроверитьВозможностьСозданияСчетФактурыЗавершение",
			ЭтотОбъект, ДополнительныеПараметры);
		
		ТекстВопроса = НСтр("ru = 'Ввод счета-фактуры возможен только после проведения документа, провести документ?'");
		ПоказатьВопрос(ОповещениеВопроса, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Возврат;
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Оповещение, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСНоменклатуройПоставщикаВДокументахЗакупки

// Открывает форму заполнения номенклатуры по номенклатуре поставщика.
//
// Параметры:
//  ОписаниеОповещения - ОписаниеОповещения - описание оповещения.
//  АдресТоваровВХранилище - Строка - адрес товаров в хранилище.
//  Форма - УправляемаяФорма - владелец открываемой формы.
//
Процедура ОткрытьФормуЗаполненияНоменклатурыПоставщика(ОписаниеОповещения, АдресТоваровВХранилище, Форма, ОтборТипНоменклатуры = Неопределено) Экспорт
	
	Если АдресТоваровВХранилище = Неопределено Тогда
		
		ПоказатьПредупреждение(,НСтр("ru = 'Отсутствуют строки, в которых указана номенклатура поставщика, но не указана номенклатура'"));
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Неопределено);
		Возврат;
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("АдресТоваровВХранилище", АдресТоваровВХранилище);
	Если ЗначениеЗаполнено(ОтборТипНоменклатуры) Тогда
		ПараметрыФормы.Вставить("ОтборТипНоменклатуры",ОтборТипНоменклатуры);
	КонецЕсли;
	
	ОткрытьФорму(
		"ОбщаяФорма.ЗаполнениеНоменклатурыПоНоменклатуреПоставщика",
		ПараметрыФормы,
		Форма,,,,
		ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

// Оповещает систему о записи счета-фактуры полученного.
//
// Параметры:
//	СчетФактура - ДокументСсылка.СчетФактураПолученный - Измененный/созданный счет-фактура.
//	ПараметрыЗаписи - Структура - Параметр оповещения. Могут быть переданы любые необходимые данные.
//
Процедура ОповеститьОЗаписиСчетаФактуры(СчетФактура, ПараметрыЗаписи = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(СчетФактура) Тогда
		
		Если ПараметрыЗаписи = Неопределено Тогда
			ПараметрыЗаписи = Новый Структура;
		КонецЕсли;
		
		Оповестить("Запись_СчетФактураПолученный", ПараметрыЗаписи, СчетФактура);
		НовыйСчетФактура = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

// Создает документ "Заявление о ввозе товаров" или открывает существующий.
//
// Параметры:
//  Форма - Форма - Форма, из которой вызвана команда. Устанавливается владелецем открываемой формы заявления;
//  Организация - СправочникСсылка.Организации - Организация, на имя которой оформляется заявление о ввозе;
//  ОткрыватьСуществующее - Булево - Признак необходимости поиска и открытия формы найденного заявления;
//
Процедура ВвестиЗаявлениеОВвозеТоваров(Форма, Организация, ОткрыватьСуществующее = Ложь) Экспорт
	
	ВводитьЗаявлениеОВвозеТоваров = Истина;
	
	Если ОткрыватьСуществующее Тогда
		ЗаявлениеОВвозеТоваров = ЗакупкиВызовСервера.ЗаявлениеОВвозеТоваровДокумента(Форма.Объект.Ссылка, Организация);
		Если ЗначениеЗаполнено(ЗаявлениеОВвозеТоваров) Тогда
			ВводитьЗаявлениеОВвозеТоваров = Ложь;
			
			ПараметрыФормы = Новый Структура("Ключ, ДокументОснование",
				ЗаявлениеОВвозеТоваров, Форма.Объект.Ссылка);
			ОткрытьФорму("Документ.ЗаявлениеОВвозеТоваров.ФормаОбъекта", ПараметрыФормы, Форма);
		КонецЕсли;
	КонецЕсли; 
	
	Если ВводитьЗаявлениеОВвозеТоваров
		И (НЕ ЗначениеЗаполнено(Форма.Объект.Ссылка)
		ИЛИ НЕ Форма.Объект.Проведен
		ИЛИ Форма.Модифицированность) Тогда
	 	
		ТекстВопроса = НСтр("ru = 'Ввод заявления о ввозе товаров возможен только после проведения документа, провести документ?'");
		КодОтвета = Неопределено;

		Параметры = Новый Структура("Организация, ОткрыватьСуществующее, Форма", Организация, ОткрыватьСуществующее, Форма);
		ОповещениеПослеВопроса = Новый ОписаниеОповещения("ВвестиЗаявлениеОВвозеПослеВопроса", ЭтотОбъект, Параметры);
		ПоказатьВопрос(ОповещениеПослеВопроса, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
		Возврат;
		
	КонецЕсли;
	
	ВвестиЗаявлениеОВвозеТоваровФрагмент(Форма, Организация, ВводитьЗаявлениеОВвозеТоваров, ОткрыватьСуществующее);
	
КонецПроцедуры

// Открывает форму заявления о ввозе в случае утвердительного ответа пользователя на вопрос.
//
// Параметры:
//  РезультатВопроса - КодВозвратаДиалога - вариант ответа пользователя на вопрос.
//  ДополнительныеПараметры - Структура - дополнительные параметры.
//
Процедура ВвестиЗаявлениеОВвозеПослеВопроса(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Организация = ДополнительныеПараметры.Организация;
	ОткрыватьСуществующее = ДополнительныеПараметры.ОткрыватьСуществующее;
	Форма = ДополнительныеПараметры.Форма;

	КодОтвета = РезультатВопроса;
	Если КодОтвета = КодВозвратаДиалога.Да Тогда
		Попытка
			ВводитьЗаявлениеОВвозеТоваров = Форма.Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
		Исключение
			ПоказатьПредупреждение(, НСтр("ru = 'Не удалось выполнить проведение документа'"));
			Возврат;
		КонецПопытки;
	Иначе
		ВводитьЗаявлениеОВвозеТоваров = Ложь;
	КонецЕсли;

	ВвестиЗаявлениеОВвозеТоваровФрагмент(Форма, Организация, ВводитьЗаявлениеОВвозеТоваров, ОткрыватьСуществующее);

КонецПроцедуры

// Создает документ "Заявление о ввозе товаров" или открывает существующий.
//
// Параметры:
//  Форма - Форма - Форма, из которой вызвана команда.
//  Организация - СправочникСсылка.Организации - организация, для котороый создается заявление.
//  ВводитьЗаявлениеОВвозеТоваров - Булево - признак, указывающий на необходимость ввода заявления.
//  ОткрыватьСуществующее - Булево - признак, указывающий на необходимость открытия существующего заявления.
//
Процедура ВвестиЗаявлениеОВвозеТоваровФрагмент(Форма, Организация, ВводитьЗаявлениеОВвозеТоваров, ОткрыватьСуществующее)

	Перем ДанныеЗаявленияОВвозеТоваров, ПараметрыФормы;

	Если ВводитьЗаявлениеОВвозеТоваров Тогда

		ДанныеЗаявленияОВвозеТоваров = Новый Структура;
		ДанныеЗаявленияОВвозеТоваров.Вставить("ДокументОснование", Форма.Объект.Ссылка);
		ДанныеЗаявленияОВвозеТоваров.Вставить("Организация",       Организация);
		ДанныеЗаявленияОВвозеТоваров.Вставить("Контрагент",        Форма.Объект.Контрагент);
		
		ПараметрыФормы = Новый Структура("Основание, ДокументОснование", ДанныеЗаявленияОВвозеТоваров, Форма.Объект.Ссылка);
		ОткрытьФорму("Документ.ЗаявлениеОВвозеТоваров.ФормаОбъекта", ПараметрыФормы, Форма);
 
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти
