////////////////////////////////////////////////////////////////////////////////
// ИнтеграцияСЯндексКассойКлиентПереопределяемый: механизм интеграции с сервисом Яндекс.Касса.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ЗаполнениеОбработчиковФормы

// Переопределяемая процедура, вызываемая из одноименного обработчика события формы.
//
// Параметры:
// 	Форма - форма, из обработчика события которой происходит вызов процедуры.
//	см. справочную информацию по событиям управляемой формы.
//
Процедура ПослеЗаписи(Форма, ПараметрыЗаписи) Экспорт
	
	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(Форма, ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область СобытияЭлементовФормыНастройкиЯндексКассы

// Процедура, в которой следует описывать обработку событий смены страниц.
// Процедура вызывается после отработки стандартных обработчиков открытия страницы.
// Для выполнения серверных вызовов рекомендуется пользоваться серверной процедурой
// ИнтеграцияСЯндексКассойСлужебныйВызовСервера.ПриСмененСтраницыФормыНастройкиЯндексКассы(Форма, НоваяСтраница)
// которая будет вызывать ИнтеграцияСЯндексКассойПереопределяемый.ПриСмененСтраницыФормыНастройкиЯндексКассы(Форма, НоваяСтраница)
// именно там и следует описывать серверный вызов.
//
// Параметры:
//  Форма - УправляемаяФорма - форма настройки Яндекс.Кассы.
//  НоваяСтраница -  Строка - Имя страницы формы
//
Процедура ПриСмененСтраницыФормыНастройкиЯндексКассы(Форма, НоваяСтраница) Экспорт 
	
	ДополнительныеПараметры = Новый Структура();
	
	// Инициализация параметров для обработки на сервере
	Если НоваяСтраница = "СтраницаНастройкиУчета" Тогда
		
		// Поле ввода: Банковский счет
		Если Не ЗначениеЗаполнено(Форма.БанковскийСчет) 
			И Не Форма.ДополнительныеПараметрыФормы.ИспользоватьНесколькоРасчетныхСчетов Тогда 
			
			// Нужно скрыть поле Банковский счет и установить значение по умолчанию
			ДополнительныеПараметры.Вставить("БанковскийСчет", Форма.БанковскийСчет);
			ДополнительныеПараметры.Вставить("ИспользоватьНесколькоРасчетныхСчетов", Форма.ДополнительныеПараметрыФормы.ИспользоватьНесколькоРасчетныхСчетов); 
				
		КонецЕсли;
		
		// Поле ввода: Эквайер
		Если Не ЗначениеЗаполнено(Форма.Эквайер) Тогда 
			
			// Для страницы аналитик учета необходимо автоматически заполнять эквайера
			ДополнительныеПараметры.Вставить("Эквайер", Форма.Эквайер);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Обработка параметров на сервере
	Если ДополнительныеПараметры.Количество() Тогда 
		
		ИнтеграцияСЯндексКассойСлужебныйВызовСервера.ПриСмененСтраницыФормыНастройкиЯндексКассы(НоваяСтраница,
			ДополнительныеПараметры);
			
	КонецЕсли;
		
	// Обработка результата
	Если НоваяСтраница = "СтраницаНастройкиУчета" Тогда
		
		// Поле ввода: Банковский счет
		Если ДополнительныеПараметры.Свойство("БанковскийСчет")
			И ДополнительныеПараметры.Свойство("ИспользоватьНесколькоРасчетныхСчетов") Тогда 
			
			Форма.БанковскийСчет = ДополнительныеПараметры.БанковскийСчет;
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
				"БанковскийСчет",
				"Видимость",
				Не ЗначениеЗаполнено(Форма.БанковскийСчет));
			
		КонецЕсли;
			
		// Поле ввода: Эквайер
		Если ДополнительныеПараметры.Свойство("Эквайер") Тогда
			
			Форма.Эквайер = ДополнительныеПараметры.Эквайер;
			
		КонецЕсли;
		
		// Поле ввода: Подразделение
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
			"Подразделение",
			"Видимость",
			Форма.ДополнительныеПараметрыФормы.ИспользоватьПодразделения);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура, в которой следует описывать обработку событий элементов формы.
//
// Параметры:
//  ИмяСобытия - Строка - название события.
//  Форма - УправляемаяФорма - форма на которой произошло событие элемента формы.
//  Элемент - ЭлементФормы - элемент Формы, из обработчика события которой происходит вызов процедуры.
//  ПараметрыСобытия - Структура - параметры событий, см. справочную информацию по событиям элементов формы.
//  СтандартнаяОбработка - Булево - признак выполнения стандартной (системной) обработки события. 
//									Если в теле процедуры-обработчика установить данному параметру значение Ложь, 
//									стандартная обработка события производиться не будет
//
Процедура ОбработкаСобытияЭлементаФормыНастройкиЯндексКассы(ИмяСобытия,
															Форма,
															Элемент,
															ПараметрыСобытия = Неопределено, 
															СтандартнаяОбработка = Ложь) Экспорт 

	Если ИмяСобытия = "НачалоВыбора" Тогда
		
		Если Элемент.Имя = "СтатьяДвиженияДенежныхСредствОплаты"
			Или Элемент.Имя = "СтатьяДвиженияДенежныхСредствВозвраты" Тогда 
			
			Если Элемент.Имя = "СтатьяДвиженияДенежныхСредствОплаты" Тогда 
				ХозяйственнаяОперация = 
					ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента");
			ИначеЕсли Элемент.Имя = "СтатьяДвиженияДенежныхСредствВозвраты" Тогда 
				ХозяйственнаяОперация = 
					ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту");
			Иначе
				Возврат;
			КонецЕсли;
				
			СтандартнаяОбработка = Ложь;
			
			Отбор = Новый Структура();
			Отбор.Вставить("ХозяйственнаяОперация", ХозяйственнаяОперация);
			
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("Отбор",         Отбор);
			ПараметрыФормы.Вставить("ТекущаяСтрока", Форма[Элемент.Имя]);
			
			ОткрытьФорму("Справочник.СтатьиДвиженияДенежныхСредств.ФормаВыбора", 
				ПараметрыФормы, Элемент, Форма.УникальныйИдентификатор);
			
		ИначеЕсли Элемент.Имя = "СтатьяРасходов" Тогда 
			
			СтандартнаяОбработка = Ложь;
			
			Отбор = Новый Структура();
			Отбор.Вставить("ВариантРаспределенияРасходов",
				ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности"));
			Отбор.Вставить("ХозяйственнаяОперация",
				ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ОтчетБанкаПоОперациямЭквайринга"));
			
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("Отбор", Отбор);
			
			ОткрытьФорму("ПланВидовХарактеристик.СтатьиРасходов.ФормаВыбора",
				ПараметрыФормы, Элемент, Форма.УникальныйИдентификатор);
			
		ИначеЕсли Элемент.Имя = "АналитикаРасходов" Тогда 
			
			Если Не Форма.ДополнительныеПараметрыФормы.Свойство("АналитикаРасходовЗаказРеализация") Тогда 
				Форма.ДополнительныеПараметрыФормы.Вставить("АналитикаРасходовЗаказРеализация", Ложь);
			КонецЕсли;
			АналитикаРасходовЗаказРеализация = Форма.ДополнительныеПараметрыФормы.АналитикаРасходовЗаказРеализация;

			Если АналитикаРасходовЗаказРеализация Тогда
				ПродажиКлиент.НачалоВыбораАналитикиРасходов(Элемент, СтандартнаяОбработка);
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ПриИзменении" Тогда 
			
		Если Элемент.Имя = "СтатьяРасходов" Тогда
			
			Если Не Форма.ДополнительныеПараметрыФормы.Свойство("АналитикаРасходовЗаказРеализация") Тогда
				Форма.ДополнительныеПараметрыФормы.Вставить("АналитикаРасходовЗаказРеализация", Ложь);
			КонецЕсли;
			Если Не Форма.ДополнительныеПараметрыФормы.Свойство("КэшированныеЗначения") Тогда 
				Форма.ДополнительныеПараметрыФормы.Вставить("КэшированныеЗначения", Неопределено);
			КонецЕсли;
			
			АналитикаРасходовЗаказРеализация = Форма.ДополнительныеПараметрыФормы.АналитикаРасходовЗаказРеализация;
			КэшированныеЗначения             = Форма.ДополнительныеПараметрыФормы.КэшированныеЗначения;
			
			СтруктураДействий = Новый Структура(
				"ЗаполнитьПризнакАналитикаРасходовОбязательна, ЗаполнитьПризнакАналитикаРасходовЗаказРеализация");
			
			ДанныеОбъекта = Новый Структура;
			ДанныеОбъекта.Вставить("СтатьяРасходов", Форма.СтатьяРасходов);
			ДанныеОбъекта.Вставить("АналитикаРасходовОбязательна", Форма.АналитикаРасходовОбязательна);
			ДанныеОбъекта.Вставить("АналитикаРасходовЗаказРеализация", АналитикаРасходовЗаказРеализация);
			
			ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ДанныеОбъекта,
				СтруктураДействий, КэшированныеЗначения);
			
			Форма.АналитикаРасходовОбязательна = ДанныеОбъекта.АналитикаРасходовОбязательна;
			Форма.ДополнительныеПараметрыФормы.Вставить("АналитикаРасходовЗаказРеализация",
				ДанныеОбъекта.АналитикаРасходовЗаказРеализация);
			Форма.ДополнительныеПараметрыФормы.Вставить("КэшированныеЗначения",
				КэшированныеЗначения);
			
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ОбработкаВыбора" Тогда
		
		Если Элемент.Имя = "АналитикаРасходов" Тогда
			
			Если ТипЗнч(ПараметрыСобытия.ВыбранноеЗначение) = Тип("Структура") Тогда
				СтандартнаяОбработка = Ложь;
				
				Форма.АналитикаРасходов = ПараметрыСобытия.ВыбранноеЗначение.АналитикаРасходов;
				Форма.Модифицированность = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

