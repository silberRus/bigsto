
#Область ПрограммныйИнтерфейс

// Подбирает справки 2 по остаткам для списания по FIFO.
//
// Параметры:
//  Товары - ТабличнаяЧасть, ДанныеФормыКоллекция, ТаблицаЗначений - табличная часть, содержащая реквизиты:
//                                                                     АлкогольнаяПродукция, Справка2, Количество,
//  ОрганизацияЕГАИС - СправочникСсылка.КлассификаторОрганизацийЕГАИС - организация учета остатков,
//  Период - Дата, Граница - дата получения остатков. Если не указана, то берутся последние.
//  СтруктураПересчетаСуммы - Структура со свойствами:
//   * Поля - Структура - содержит поля для пересчета суммы в табличной части документа,
//   * Строки - Массив - содержит элементы типа ДанныеФормыЭлементКоллекции, ссылки нас строки для пересчета сумм,
//   * ИтогКоличество - Число - сумма значений в поле "Количество" в строках переданных в параметре "Строки".
//
Процедура ПодобратьСправки2ДляСписанияИзРегистра1(Товары, ОрганизацияЕГАИС, Период, СтруктураПересчетаСуммы) Экспорт
	
	Данные = ПодготовитьДанныеДляПодбораСправок2(Товары);
	Если Данные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаОстатки.АлкогольнаяПродукция                                                КАК АлкогольнаяПродукция,
	|	ЕСТЬNULL(ТаблицаОстатки.Справка2.ДокументОснование.ДатаТТН, ДАТАВРЕМЯ(1, 1, 1))    КАК ДатаТТН,
	|	ТаблицаОстатки.Справка2                                                            КАК Справка2,
	|	ТаблицаОстатки.ОрганизацияЕГАИС                                                    КАК ОрганизацияЕГАИС,
	|	ТаблицаОстатки.СвободныйОстатокОстаток - ЕСТЬNULL(СправкиВДокументе.Количество, 0) КАК Количество
	|ПОМЕСТИТЬ
	|	ВтОстатки
	|ИЗ
	|	РегистрНакопления.ОстаткиАлкогольнойПродукцииЕГАИС.Остатки(&Период,
	|			ОрганизацияЕГАИС = &ОрганизацияЕГАИС
	|				И АлкогольнаяПродукция В (
	|					ВЫБРАТЬ
	|						Т.АлкогольнаяПродукция
	|					ИЗ
	|						втТаблицаДляЗаполнения КАК Т
	|						
	|					ОБЪЕДИНИТЬ ВСЕ
	|					
	|					ВЫБРАТЬ
	|						Т.АлкогольнаяПродукция
	|					ИЗ
	|						втТаблицаСоответствияБезАлкоПродукции КАК Т
	|					)
	|					) КАК ТаблицаОстатки
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТаблицаДляСписания КАК СправкиВДокументе
	|		ПО ТаблицаОстатки.АлкогольнаяПродукция = СправкиВДокументе.АлкогольнаяПродукция
	|			И ТаблицаОстатки.Справка2 = СправкиВДокументе.Справка2
	|;
	|	
	|ВЫБРАТЬ
	|	АкцизныеМарки.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	АкцизныеМарки.Справка2 КАК Справка2,
	|	КОЛИЧЕСТВО(АкцизныеМарки.АкцизнаяМарка) КАК Количество
	|ПОМЕСТИТЬ
	|	АкцизныеМарки
	|ИЗ
	|	РегистрСведений.АкцизныеМаркиЕГАИС КАК АкцизныеМарки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОстатки КАК втОстатки
	|		ПО ВтОстатки.Справка2 = АкцизныеМарки.Справка2
	|			И ВтОстатки.ОрганизацияЕГАИС = АкцизныеМарки.ОрганизацияЕГАИС
	|ГДЕ
	|	АкцизныеМарки.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.ВНаличии),ЗНАЧЕНИЕ(Перечисление.СтатусыАкцизныхМарок.КПостановкеНаБаланс))
	|СГРУППИРОВАТЬ ПО
	|	АкцизныеМарки.ОрганизацияЕГАИС,
	|	АкцизныеМарки.Справка2;
	|
	|ВЫБРАТЬ
	|	ТаблицаОстатки.АлкогольнаяПродукция                                                КАК АлкогольнаяПродукция,
	|	ЕСТЬNULL(ТаблицаОстатки.Справка2.ДокументОснование.ДатаТТН, ДАТАВРЕМЯ(1, 1, 1))    КАК ДатаТТН,
	|	ТаблицаОстатки.Справка2                                                            КАК Справка2,
	|	ТаблицаОстатки.Количество - ЕСТЬNULL(АкцизныеМарки.Количество, 0)                  КАК Количество
	|ИЗ
	|	ВТОстатки КАК ТаблицаОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ АкцизныеМарки КАК АкцизныеМарки
	|		ПО ТаблицаОстатки.Справка2 = АкцизныеМарки.Справка2
	|			И ТаблицаОстатки.ОрганизацияЕГАИС = АкцизныеМарки.ОрганизацияЕГАИС
	|ГДЕ
	|	ТаблицаОстатки.Количество - ЕСТЬNULL(АкцизныеМарки.Количество, 0) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	АлкогольнаяПродукция,
	|	ДатаТТН
	|ИТОГИ ПО
	|	АлкогольнаяПродукция");
	
	Запрос.МенеджерВременныхТаблиц = Данные.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ОрганизацияЕГАИС", ОрганизацияЕГАИС);
	Запрос.УстановитьПараметр("Период", Период);
	
	КоэффициентыПересчетаВЕдиницыЕГАИС = Данные.КоэффициентыПересчетаВЕдиницыЕГАИС;
	ТаблицаСоответствия = Данные.ТаблицаСоответствия;
	ТаблицаСоответствияБезАлкоПродукции = Данные.ТаблицаСоответствияБезАлкоПродукции;
	
	ВыборкаАлкогольнаяПродукция = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаАлкогольнаяПродукция.Следующий() Цикл
		
		Выборка = ВыборкаАлкогольнаяПродукция.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			АлкогольнаяПродукция = ВыборкаАлкогольнаяПродукция.АлкогольнаяПродукция;
			ОстатокПоСправке     = Выборка.Количество;
			Справка2             = Выборка.Справка2;
			
			ЕстьСтрокиДляЗаполнения = ЗаполнитьСправку2ВТабличнойЧасти(
				Товары,
				АлкогольнаяПродукция,
				Справка2,
				ОстатокПоСправке,
				СтруктураПересчетаСуммы,
				КоэффициентыПересчетаВЕдиницыЕГАИС,
				ТаблицаСоответствия,
				ТаблицаСоответствияБезАлкоПродукции);
			
			Если Не ЕстьСтрокиДляЗаполнения Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Подбирает справки 2 для списания по LIFO.
//
// Параметры:
//  Товары - ТабличнаяЧасть, ДанныеФормыКоллекция, ТаблицаЗначений - табличная часть, содержащая реквизиты:
//                                                                     АлкогольнаяПродукция, Справка2, Количество,
//  ОрганизацияЕГАИС - СправочникСсылка.КлассификаторОрганизацийЕГАИС - организация учета остатков,
//  Период - Дата - дата получения остатков.
//  СтруктураПересчетаСуммы - Структура со свойствами:
//   * Поля - Структура - содержит поля для пересчета суммы в табличной части документа,
//   * Строки - Массив - содержит элементы типа ДанныеФормыЭлементКоллекции, ссылки нас строки для пересчета сумм,
//   * ИтогКоличество - Число - сумма значений в поле "Количество" в строках переданных в параметре "Строки".
//
// Возвращаемое значение:
//  Булево - Истина, если все справки заполнены.
//
Функция ПодобратьСправки2ДляВозвратаИзРегистра2(Товары, ОрганизацияЕГАИС, Период, СтруктураПересчетаСуммы) Экспорт
	
	ОстатокВРегистре2 = Новый ТаблицаЗначений;
	ОстатокВРегистре2.Колонки.Добавить("Последняя",            Новый ОписаниеТипов("Булево"));
	ОстатокВРегистре2.Колонки.Добавить("АлкогольнаяПродукция", Новый ОписаниеТипов("СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС"));
	ОстатокВРегистре2.Колонки.Добавить("Справка2",             Новый ОписаниеТипов("СправочникСсылка.Справки2ЕГАИС"));
	ОстатокВРегистре2.Колонки.Добавить("Количество",           Новый ОписаниеТипов("Число"));
	ОстатокВРегистре2.Индексы.Добавить("АлкогольнаяПродукция, Последняя");
	
	Данные = ПодготовитьДанныеДляПодбораСправок2(Товары);
	Если Данные = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Таблицы = Новый Структура;
	Таблицы.Вставить("ОстатокВРегистре2",  ОстатокВРегистре2);
	Таблицы.Вставить("ТаблицаДляСписания", Данные.ТаблицаДляСписания);
	
	Справки2Подобраны = ОбработатьПорциюДанныхДляПодбораСправок2ДляВозвратаИзРегистра2(
		Данные.МенеджерВременныхТаблиц,
		ОрганизацияЕГАИС,
		Период,
		Таблицы);
	
	КоэффициентыПересчетаВЕдиницыЕГАИС = Данные.КоэффициентыПересчетаВЕдиницыЕГАИС;
	ТаблицаСоответствия = Данные.ТаблицаСоответствия;
	
	Для Каждого СтрокаТЧ Из ОстатокВРегистре2 Цикл
		
		АлкогольнаяПродукция = СтрокаТЧ.АлкогольнаяПродукция;
		ОстатокПоСправке     = СтрокаТЧ.Количество;
		Справка2             = СтрокаТЧ.Справка2;
		
		ЗаполнитьСправку2ВТабличнойЧасти(
			Товары,
			АлкогольнаяПродукция,
			Справка2,
			ОстатокПоСправке,
			СтруктураПересчетаСуммы,
			КоэффициентыПересчетаВЕдиницыЕГАИС,
			ТаблицаСоответствия);
		
		Справки2Заполнены = Справки2ЗаполненыВТабличнойЧасти(Товары);
		Если Справки2Заполнены Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Справки2ЗаполненыВТабличнойЧасти(Товары);
	
КонецФункции

// Формирует пустую структуру сообщения XML
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * ТекстОшибки                 - Строка - Текст ошибки.
//   * Ошибки                      - Соответствие - Описание ошибок.
//   * Описание                    - Строка - Описание для отображения в форме подписания.
//   * ТекстСообщенияXML           - Строка - Текст сообщения XML.
//   * КонвертSOAP                 - Строка - Текст сообщения SOAP.
//   * ТипСообщения                - ПеречислениеСсылка.ТипыСообщенийГИСМ - Тип сообщения.
//   * Организация                 - ОпределяемыйТип.Организации - Организация.
//   * Документ                    - ДокументСсылка - Документ.
//   * Версия                      - Строка - Версия сообщения.
//   * GLN                         - Строка - Регистрационный номер GLN.
//   * СообщениеОснование          - СправочникСсылка.ГИСМПрисоединенныеФайлы - сообщение основание.
//   * ДанныеДляПолученияДокумента - Структура - Данные для получения документа.
//   * Основание                   - ДокументСсылка - Документ основание.
//
Функция СтруктураСообщенияXML(ДляКлиента = Ложь) Экспорт
	
	СообщениеXML = Новый Структура;
	
	СообщениеXML.Вставить("ТекстСообщенияXML");
	СообщениеXML.Вставить("ОрганизацияЕГАИС");
	
	Если ДляКлиента Тогда
		
		СообщениеXML.Вставить("Ссылка");
		СообщениеXML.Вставить("АдресЗапроса");
		
	Иначе
		
		СообщениеXML.Вставить("ТекстОшибки", "");
		СообщениеXML.Вставить("Ошибки", Новый Соответствие);
		
		// Для отображения в сообщениях пользователям
		СообщениеXML.Вставить("Описание", "");
		
		СообщениеXML.Вставить("Операция");
		СообщениеXML.Вставить("ФорматОбмена");
		
		// Для сохранения в Протокол обмена
		СообщениеXML.Вставить("ТипСообщения");
		СообщениеXML.Вставить("Документ");
		СообщениеXML.Вставить("Версия", 0);
		
		// Для обновления в формах
		СообщениеXML.Вставить("ДокументОснование");
		
	КонецЕсли;
	
	Возврат СообщениеXML;
	
КонецФункции

// Сформировать описание операции для документа.
//
// Параметры:
//  Описание - Строка - Описание операции.
//  ДокументСсылка - ДокументСсылка - Документ.
//  НомерВерсии - Число - Номер версии.
// 
// Возвращаемое значение:
//  Строка - Описание операции.
//
Функция ОписаниеОперации(Описание, ДокументСсылка, НомерВерсии = Неопределено) Экспорт
	
	Если НомерВерсии = Неопределено И ДокументСсылка = Неопределено Тогда
		Возврат Описание;
	ИначеЕсли НомерВерсии = Неопределено И ДокументСсылка <> Неопределено Тогда
		Возврат СтрШаблон(НСтр("ru = '%1 по документу ""%2""'"), Описание, ДокументСсылка);
	ИначеЕсли НомерВерсии <> Неопределено И ДокументСсылка = Неопределено Тогда
		Возврат СтрШаблон(НСтр("ru = '%1. Версия %2'"), Описание, НомерВерсии);
	Иначе
		Возврат СтрШаблон(НСтр("ru = '%1 по документу ""%2"". Версия %3'"), Описание, ДокументСсылка, НомерВерсии);
	КонецЕсли;
	
КонецФункции

// Сформировать описание операции для документа
//
// Параметры:
//  ОперацияПередачиДанных - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция обмена с ГИСМ
//  ДокументСсылка - ДокументСсылка - Документ ссылка
//  НомерВерсии - Число - Номер версии
// 
// Возвращаемое значение:
//  Строка - Описание операции
//
Функция ОписаниеОперацииПередачиДанных(ОперацияПередачиДанных, ДокументСсылка = Неопределено, НомерВерсии = Неопределено) Экспорт
	
	КатегорииОпераций = КатегорииОпераций();
	ОписаниеОперации = КатегорииОпераций.ПередачаДанных.Получить(ОперацияПередачиДанных);
	
	Возврат ОписаниеОперации(ОписаниеОперации, ДокументСсылка, НомерВерсии);
	
КонецФункции

// Сформировать описание операции для документа
//
// Параметры:
//  ОперацияПередачиДанных - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция обмена с ГИСМ
//  ДокументСсылка - ДокументСсылка - Документ ссылка
//  НомерВерсии - Число - Номер версии
// 
// Возвращаемое значение:
//  Строка - Описание операции
//
Функция ОписаниеОперацииПолученияДанных(ОперацияПередачиДанных) Экспорт
	
	КатегорииОпераций = КатегорииОпераций();
	ОписаниеОперации = КатегорииОпераций.ПолучениеДанных.Получить(ОперацияПередачиДанных);
	
	Возврат ОписаниеОперации(ОписаниеОперации, Неопределено, Неопределено);
	
КонецФункции

// Возвращает операций обмена с ГИСМ, разбитые на категории
// 
// Возвращаемое значение:
//  Структура - со свойствами:
//    * ПередачаДанных - Соответствие - Операции передачи данных.
//    * ПолучениеДанных - Соответствие - Операции получения данных.
//
Функция КатегорииОпераций() Экспорт
	
	ПередачаДанных = Новый Соответствие;
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЧекККМ,
		НСтр("ru = 'Передача сведений о розничной продаже маркируемой алкогольной продукции'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ТТН,
		НСтр("ru = 'Передача сведений об оптовой продаже алкогольной продукции'"));
	
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТН,
		НСтр("ru = 'Подтверждение или отказ от отмены проведения ТТН по запросу клиента'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНПодтверждение,
		НСтр("ru = 'Подтверждение отмены проведения ТТН по запросу клиента'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНОтказ,
		НСтр("ru = 'Отказ отмены проведения ТТН по запросу клиента'"));
	
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросНаОтменуПроведенияТТН,
		НСтр("ru = 'Передача запроса на отмену проведения ТТН'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросНаОтменуПроведенияАктаСписания,
		НСтр("ru = 'Передача запроса на отмену проведения акта списания'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросНаОтменуПроведенияАктаПостановкиНаБаланс,
		НСтр("ru = 'Передача запроса на отмену проведения акта постановки на баланс'"));
	
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхождений,
		НСтр("ru = 'Подтверждение акта расхождений (или отказ)'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийОтказ,
		НСтр("ru = 'Отказ от акта расхождений ТТН'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийПодтверждение,
		НСтр("ru = 'Подтверждение акта расхождений ТТН'"));
	
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ВозвратИзРегистра2,
		НСтр("ru = 'Передача сведений о возврате алкогольной продукции из регистра №2'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ПередачаВРегистр2,
		НСтр("ru = 'Передача сведений о перемещении алкогольной продукции в регистр №2'"));
		
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра1,
		НСтр("ru = 'Передача сведений о списании алкогольной продукции из регистра №1'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра2,
		НСтр("ru = 'Передача сведений о списании алкогольной продукции из регистра №2'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра3,
		НСтр("ru = 'Передача сведений об отвязывании акцизной марки от партии алкогольной продукции'"));
	
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр1,
		НСтр("ru = 'Передача сведений о постановке на баланс в регистр №1'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр2,
		НСтр("ru = 'Передача сведений о постановке на баланс в регистр №2'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр3,
		НСтр("ru = 'Передача сведений о привязке акцизной марки к партии алкогольной продукции'"));
	
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.АктТТН,
		НСтр("ru = 'Передача акта подтверждения ТТН или акта отказ от ТТН'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.АктТТНРасхождения,
		НСтр("ru = 'Передача акта расхождений по ТТН'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.АктТТНПодтверждение,
		НСтр("ru = 'Передача акта подтверждения ТТН'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.АктТТНОтказ,
		НСтр("ru = 'Передача акта отказа от ТТН'"));
	
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросТТН,
		НСтр("ru = 'Запрос данных ТТН'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросСправки1,
		НСтр("ru = 'Запрос данных справки 1'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросСправки2,
		НСтр("ru = 'Запрос данных справки 2'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросОстатковВРегистре1,
		НСтр("ru = 'Запрос остатков в регистре №1'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросОстатковВРегистре2,
		НСтр("ru = 'Запрос остатков в регистре №2'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросДанныхОрганизации,
		НСтр("ru = 'Запрос сведений об организации'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросАлкогольнойПродукции,
		НСтр("ru = 'Запрос сведений об алкогольной продукции'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ИнформацияОФорматеОбмена,
		НСтр("ru = 'Передача сведений об используемом формате обмена с ЕГАИС'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ИнформацияОФорматеОбмена,
		НСтр("ru = 'Передача сведений об используемом формате обмена с ЕГАИС'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросАкцизныхМарок,
		НСтр("ru = 'Запрос новых штрихкодов акцизных марок (PDF-417)'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросОтчетаДвиженияМеждуРегистрами,
		НСтр("ru = 'Запрос отчета ""Движения между регистрами""'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросОтчетаДвиженияПоСправке2,
		НСтр("ru = 'Запрос отчета ""Движения по справке 2""'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросОтчетаИнформацияОбОрганизации,
		НСтр("ru = 'Запрос отчета ""Информация об организации""'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросОтчетаНеобработанныеТТН,
		НСтр("ru = 'Запрос отчета ""Необработанные ТТН""'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросОтчетаОбработанныеЧеки,
		НСтр("ru = 'Запрос отчета ""Обработанные чеки""'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросОтчетаОстаткиВРегистре1,
		НСтр("ru = 'Запрос отчета ""Остатки в регистре №1""'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросОтчетаОстаткиВРегистре2,
		НСтр("ru = 'Запрос отчета ""Остатки в регистре №2""'"));
	
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросОтчетаОстаткиВРегистре3,
		НСтр("ru = 'Запрос отчета ""Остатки в регистре №3""'"));
	ПередачаДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросОтчетаИсторияСправок2,
		НСтр("ru = 'Запрос отчета ""История справок 2""'"));
	
	
	ПолучениеДанных = Новый Соответствие;
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ТТН,
		НСтр("ru = 'Получение документа ТТН ЕГАИС (входящая)'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ЗапросНаОтменуПроведенияТТН,
		НСтр("ru = 'Получение запроса на отмену проведения ТТН'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.УведомлениеОРегистрацииДвиженияАктаПостановкиНаБаланс,
		НСтр("ru = 'Получение уведомления о регистрации движения акта постановки на баланс'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.УведомлениеОРегистрацииДвиженияТТН,
		НСтр("ru = 'Получение уведомления о регистрации движения ТТН'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ИсторияСправок2ПоТТН,
		НСтр("ru = 'Получение истории справок 2 по ТТН'"));
	
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.АктТТН,
		НСтр("ru = 'Получение акта подтверждения ТТН или акта отказа от ТТН'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.АктТТНРасхождения,
		НСтр("ru = 'Получение акта расхождений по ТТН'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.АктТТНПодтверждение,
		НСтр("ru = 'Получение акта подтверждения ТТН'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.АктТТНОтказ,
		НСтр("ru = 'Получение акта отказа от ТТН'"));
		
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхождений,
		НСтр("ru = 'Получение квитанции подтверждения акта расхождений или отказа от акта расхождений'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийОтказ,
		НСтр("ru = 'Получение квитанции отказа от акта расхождений ТТН'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийПодтверждение,
		НСтр("ru = 'Получение квитанции подтверждения акта расхождений ТТН'"));
	
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.КвитанцияПолученЕГАИС,
		НСтр("ru = 'Квитанция о получении документа в ЕГАИС'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.КвитанцияПроведенЕГАИС,
		НСтр("ru = 'Квитанция о проведении документа в ЕГАИС'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросСправки2,
		НСтр("ru = 'Получение справки 2'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросСправки1,
		НСтр("ru = 'Получение справки 1'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросОстатковВРегистре2,
		НСтр("ru = 'Получение остатков в регистре №2'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросОстатковВРегистре1,
		НСтр("ru = 'Получение остатков в регистре №1'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросДанныхОрганизации,
		НСтр("ru = 'Получение сведений об организации'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросАлкогольнойПродукции,
		НСтр("ru = 'Получение алкогольной продукции'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросАкцизныхМарок,
		НСтр("ru = 'Получение новых акцизных марок (PDF-417)'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросОтчетаДвиженияМеждуРегистрами,
		НСтр("ru = 'Получение отчета ""Движения между регистрами""'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросОтчетаДвиженияПоСправке2,
		НСтр("ru = 'Получение отчета ""Движения по справке 2""'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросОтчетаИнформацияОбОрганизации,
		НСтр("ru = 'Получение отчета ""Информация об организации""'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросОтчетаНеобработанныеТТН,
		НСтр("ru = 'Получение отчета ""Необработанные ТТН""'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросОтчетаОбработанныеЧеки,
		НСтр("ru = 'Получение отчета ""Обработанные чеки""'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросОтчетаОстаткиВРегистре1,
		НСтр("ru = 'Получение отчета ""Остатки в регистре №1""'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросОтчетаОстаткиВРегистре2,
		НСтр("ru = 'Получение отчета ""Остатки в регистре №2""'"));
	
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросОтчетаОстаткиВРегистре3,
		НСтр("ru = 'Получение отчета ""Остатки в регистре №3""'"));
	ПолучениеДанных.Вставить(
		Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросОтчетаИсторияСправок2,
		НСтр("ru = 'Получение отчета ""История справок 2""'"));
	
	Категории = Новый Структура;
	Категории.Вставить("ПередачаДанных",  ПередачаДанных);
	Категории.Вставить("ПолучениеДанных", ПолучениеДанных);
	
	Возврат Категории;
	
КонецФункции

// Создает таблицу последовательности операций.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица с колонками:
//   * Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция.
//   * Индекс - Число - Индекс операции в последовательности.
//   * ТипСообщения - ПеречислениеСсылка.ТипыЗапросовЕГАИС - Тип сообщения.
//   * КвитанцияУТМ - Булево - Признак наличия у операции квитанции УТМ.
//   * КвитанцияПолученЕГАИС - Булево - Признак наличия у операции квитанции Получен ЕГАИС.
//   * КвитанцияПроведенЕГАИС - Булево - Признак наличия у операции квитанции Проведен ЕГАИС.
//   * ДальнейшиеДействия - Массив - Дальнейшие действия при операции.
//
Функция ПустаяТаблицаПоследовательностьОпераций() Экспорт
	
	ПоследовательностьОпераций = Новый ТаблицаЗначений;
	ПоследовательностьОпераций.Колонки.Добавить("Операция");
	ПоследовательностьОпераций.Колонки.Добавить("Индекс");
	ПоследовательностьОпераций.Колонки.Добавить("ТипСообщения");
	
	ПоследовательностьОпераций.Колонки.Добавить("КвитанцияУТМ");
	ПоследовательностьОпераций.Колонки.Добавить("КвитанцияПолученЕГАИС");
	ПоследовательностьОпераций.Колонки.Добавить("КвитанцияПроведенЕГАИС");
	
	ПоследовательностьОпераций.Колонки.Добавить("ДальнейшиеДействия");
	
	Возврат ПоследовательностьОпераций;
	
КонецФункции

// Возвращает предыдущую операцию последовательности операций.
//
// Параметры:
//  ПоследовательностьОпераций - ТаблицаЗначений - см. функцию ПустаяТаблицаПоследовательностьОпераций().
//  СтрокаПоследовательности - СтрокаТаблицыЗначений - см. функцию ПустаяТаблицаПоследовательностьОпераций().
//  ИсходныйИндекс - Число - Индекс текущей операции.
// 
// Возвращаемое значение:
//  СтрокаТаблицыЗначений - см. функцию ПустаяТаблицаПоследовательностьОпераций().
//
Функция ПредыдущаяОперация(ПоследовательностьОпераций, СтрокаПоследовательности, Знач ИсходныйИндекс = Неопределено) Экспорт
	
	ИндексСтроки = ПоследовательностьОпераций.Индекс(СтрокаПоследовательности);
	
	Если ИндексСтроки = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПредыдущаяОперация = ПоследовательностьОпераций.Получить(ИндексСтроки - 1);
	
	Если ИсходныйИндекс = Неопределено Тогда
		ИсходныйИндекс = СтрокаПоследовательности.Индекс;
	КонецЕсли;
	
	// Пропускаем абстрактные операции
	Если ПредыдущаяОперация.Операция = Перечисления.ВидыДокументовЕГАИС.АктТТН
		Или ПредыдущаяОперация.Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхождений
		Или ПредыдущаяОперация.Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТН
		Или ПредыдущаяОперация.Операция = Перечисления.ВидыДокументовЕГАИС.ИсторияСправок2ПоТТН Тогда
		Возврат ПредыдущаяОперация(ПоследовательностьОпераций, ПредыдущаяОперация, ИсходныйИндекс);
	КонецЕсли;
	
	АнализироватьПредыдущуюОперацию = ПроверитьИндекс(Формат(ПредыдущаяОперация.Индекс, "ЧГ=0;ЧН=0"), Формат(ИсходныйИндекс, "ЧГ=0;ЧН=0"));
	
	Если АнализироватьПредыдущуюОперацию
		И ПредыдущаяОперация.Индекс <> 0 Тогда
		Возврат ПредыдущаяОперация(ПоследовательностьОпераций, ПредыдущаяОперация, ИсходныйИндекс);
	КонецЕсли;
	
	Возврат ПредыдущаяОперация;
	
КонецФункции

// Добавляет операцию в последовательность операций.
//
// Параметры:
//  ПоследовательностьОпераций - ТаблицаЗначений - см. функцию ПустаяТаблицаПоследовательностьОпераций().
//  Индекс - Число - Индекс добавляемой операции.
//  ТипСообщения - ПеречислениеСсылка.ТипыЗапросовЕГАИС - Тип сообщения.
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция.
//  РассчитатьДействияДляДокумента - ДокументСсылка - Документ, для которого нужно вычислить дальнейшие действия.
//  КвитанцияПолученЕГАИС - Булево - Признак наличия у операции квитанции Получен ЕГАИС.
//  КвитанцияПроведенЕГАИС - Булево - Признак наличия у операции квитанции Проведен ЕГАИС.
// 
// Возвращаемое значение:
//  СтрокаТаблицыЗначений - см. функцию ПустаяТаблицаПоследовательностьОпераций().
//
Функция ДобавитьОперациюВПоследовательность(ПоследовательностьОпераций,
                                            Индекс,
                                            ТипСообщения,
                                            Операция,
                                            РассчитатьДействияДляДокумента = Неопределено,
                                            КвитанцияПолученЕГАИС = Истина,
                                            КвитанцияПроведенЕГАИС = Истина) Экспорт

	НоваяСтрока = ПоследовательностьОпераций.Добавить();
	НоваяСтрока.Операция     = Операция;
	НоваяСтрока.Индекс       = Индекс;
	НоваяСтрока.ТипСообщения = ТипСообщения;
	
	НоваяСтрока.КвитанцияУТМ           = Истина;
	НоваяСтрока.КвитанцияПолученЕГАИС  = КвитанцияПолученЕГАИС;
	НоваяСтрока.КвитанцияПроведенЕГАИС = КвитанцияПроведенЕГАИС;
	
	НоваяСтрока.ДальнейшиеДействия = Новый Массив;
	
	Если ЗначениеЗаполнено(РассчитатьДействияДляДокумента) Тогда
		
		ПолноеИмя = РассчитатьДействияДляДокумента.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		
		Если ПоследовательностьОпераций.Количество() = 1 Тогда
			
			НоваяСтрока.ДальнейшиеДействия.Добавить(МенеджерОбъекта.ДальнейшееДействиеПоУмолчанию());
			
		Иначе
			
			ПредыдущаяОперация = ПредыдущаяОперация(ПоследовательностьОпераций, НоваяСтрока);
			Если ПредыдущаяОперация = Неопределено Тогда
				
				НоваяСтрока.ДальнейшиеДействия.Добавить(МенеджерОбъекта.ДальнейшееДействиеПоУмолчанию());
				
			Иначе
				
				Если ПредыдущаяОперация.ТипСообщения = Перечисления.ТипыЗапросовЕГАИС.Исходящий Тогда
					Статусы = МенеджерОбъекта.СтатусПослеПередачиДанных(
						РассчитатьДействияДляДокумента,
						ПредыдущаяОперация.Операция, Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПереданоВУТМ);
				Иначе
					Статусы = МенеджерОбъекта.СтатусПослеПолученияДанных(
						РассчитатьДействияДляДокумента,
						ПредыдущаяОперация.Операция);
				КонецЕсли;
				
				НоваяСтрока.ДальнейшиеДействия = РегистрыСведений.СтатусыДокументовЕГАИС.ДальнейшиеДействия(Статусы);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НоваяСтрока;
	
КонецФункции

// Проверяет, что проверяемый индекс операции является более общим для исходного индекса операции последовательности.
//
// Параметры:
//  ИндексОперации - Число - Индекс операции.
//  ИсходныйИндекс - Число - Исходный индекс операции.
// 
// Возвращаемое значение:
//  Булево - Исходный индекс операции принадлежит к индексу текущий операции.
//
Функция ПроверитьИндекс(ИндексОперации, ИсходныйИндекс)
	
	Если ИндексОперации <> ИсходныйИндекс Тогда
		Если СтрДлина(ИсходныйИндекс) = СтрДлина(ИндексОперации) Тогда
			Возврат Истина;
		Иначе
			Если СтрДлина(ИсходныйИндекс) > 1 Тогда
				Возврат ПроверитьИндекс(ИндексОперации, Лев(ИсходныйИндекс, СтрДлина(ИсходныйИндекс) - 1));
			Иначе
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Преобразует объект XDTO в XML
//
// Параметры:
//  ОбъектXDTO - ОбъектXDTO - Объект XDTO
//  ИмяТипа - Строка - Имя типа
// 
// Возвращаемое значение:
//  Строка - Текст сообщения XML
//
Функция ОбъектXDTOВXML(ОбъектXDTO, ИдентификаторФСРАР, ПространствоИмен, ИмяТипа) Экспорт
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("UTF-8");
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("Documents", КорневоеПространствоИмен());
	
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен(
		Перечисления.ВидыДокументовЕГАИС.ПрефиксПространстваИмен(КорневоеПространствоИмен()),
		КорневоеПространствоИмен());
	
	Зависимости = Новый Массив;
	Зависимости.Добавить("http://www.w3.org/2001/XMLSchema-instance");
	Зависимости.Добавить("http://www.w3.org/2001/XMLSchema");
	
	ЗависимыеПространстваИмен(ФабрикаXDTO.Пакеты.Получить(ПространствоИмен).Зависимости, Зависимости);
	
	Если Зависимости.Найти(ПространствоИмен) = Неопределено Тогда
		Зависимости.Добавить(ПространствоИмен);
	КонецЕсли;
	
	Для Каждого ЗависимоеПространство Из Зависимости Цикл
		Префикс = Перечисления.ВидыДокументовЕГАИС.ПрефиксПространстваИмен(ЗависимоеПространство);
		Если НЕ ПустаяСтрока(Префикс) Тогда
			ЗаписьXML.ЗаписатьСоответствиеПространстваИмен(Префикс, ЗависимоеПространство);
		КонецЕсли;
	КонецЦикла;
	
	СодержимоеXDTO = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(КорневоеПространствоИмен(), "DocBody"));
	СодержимоеXDTO[ИмяТипа] = ОбъектXDTO;
	
	ОтправительXDTO = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(КорневоеПространствоИмен(), "SenderInfo"));
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОтправительXDTO, "FSRAR_ID", ИдентификаторФСРАР);
	
	ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, ОтправительXDTO, "Owner",    КорневоеПространствоИмен());
	ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, СодержимоеXDTO,  "Document", КорневоеПространствоИмен());
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ТекстXML = ЗаписьXML.Закрыть();
	ТекстXML = СтрЗаменить(ТекстXML, "unqualified_element:", "");
	
	Возврат ТекстXML;
	
КонецФункции

// Преобразует объект XDTO в XML.
//
// Параметры:
//  ОбъектXDTO - ОбъектXDTO - Объект XDTO
//  ПространствоИмен - Строка - Имя пространства имен.
//  ИмяТипа - Строка - Имя типа
// 
// Возвращаемое значение:
//  Строка - Текст сообщения XML.
//
Функция ЧекXDTOВXML(ОбъектXDTO, ПространствоИмен, ИмяТипа) Экспорт
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("UTF-8");
	
	ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, ОбъектXDTO, ИмяТипа);
	
	ТекстXML = ЗаписьXML.Закрыть();
	ТекстXML = СтрЗаменить(ТекстXML, "xmlns=""" + ПространствоИмен + """ ", "");
	
	Возврат ТекстXML;
	
КонецФункции

// Проверяет, входит ли проверяемых тип в состав определяемого типа "Владелец присоединенных файлов ГИСМ"
//
// Параметры:
//  ПроверяемыйТип - Тип - тип, который проверяется на вхождение
// 
// Возвращаемое значение:
//  Булево - Истина, если тип входит в состав определяемого типа "Владелец присоединенных файлов ГИСМ".
//
Функция ТипПодсистемыЕГАИС(ПроверяемыйТип) Экспорт
	
	Возврат ПроверяемыйТип = Тип("СправочникСсылка.КлассификаторОрганизацийЕГАИС");
	
КонецФункции

// Получает текст сообщения XML из присоединенного файла, в котором хранится сообщение протокола обмена.
//
// Параметры:
//  Сообщение - Справочник.ПрисоединенныеФайлыЕГАИС, Строка - хранимый файл сообщения, из которого извлекается текст сообщения XML.
// 
// Возвращаемое значение:
//  Строка - полученный текст сообщения XML.
//
Функция ТекстСообщенияXMLИзПротокола(Сообщение) Экспорт
	
	Если ТипЗнч(Сообщение) = Тип("Строка") Тогда
		Возврат Сообщение;
	КонецЕсли;
	
	ДвоичныеДанные = ПрисоединенныеФайлы.ПолучитьДвоичныеДанныеФайла(Сообщение);
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ИмяВременногоФайла, КодировкаТекста.UTF8, "");
	ТекстСообщенияXML = ТекстовыйДокумент.ПолучитьТекст();
	
	Попытка
		УдалитьФайлы(ИмяВременногоФайла);
	Исключение
		ИнтеграцияЕГАИСВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат ТекстСообщенияXML;
	
КонецФункции

// Инициализирует объект ПараметрыЗаписиXML.
//
// Параметры:
//  ИспользоватьОтступы - Булево - признак использования отступов, по умолчанию Истина.
//  СимволОтступа       - Строка - символ, которым отображается отступ, по умолчанию два пробела.
// 
// Возвращаемое значение:
//   -  ПараметрыЗаписиXML - набор параметров, который используется при записи XML.
//
Функция ПараметрыФорматированияXML(ИспользоватьОтступы = Истина, СимволОтступа = "  ") Экспорт
	
	ПараметрыЗаписи = Новый ПараметрыЗаписиXML(
		"UTF-8",
		"1.0",
		ИспользоватьОтступы,
		ИспользоватьОтступы,
		СимволОтступа);
	
	Возврат ПараметрыЗаписи;
	
КонецФункции

// Форматирует текст сообщения в формате XML
//
// Параметры:
//  ТекстСообщенияXML       - Строка - текст сообщения, который.
//  ПараметрыФорматирования - ПараметрыЗаписиXML - параметры записи XML.
// 
// Возвращаемое значение:
//  Строка - отформатированная строка XML.
//
Функция ФорматироватьXMLСПараметрами(ТекстСообщенияXML, ПараметрыФорматирования) Экспорт
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ТекстСообщенияXML);
	
	ПостроительDOM = Новый ПостроительDOM;
	ДокументDOM = ПостроительDOM.Прочитать(ЧтениеXML);
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку(ПараметрыФорматирования);
	
	ЗаписьDOM = Новый ЗаписьDOM;
	ЗаписьDOM.Записать(ДокументDOM, ЗаписьXML);
	
	Возврат ЗаписьXML.Закрыть();
	
КонецФункции

// Подготавливает сообщение к передаче в сервис ГИСМ.
//
// Параметры:
//  ТекстСообщенияXML - Строка - Текст исходящего сообщения.
//  Реквизиты - Структура - Реквизиты передаваемого сообщения.
//   * ТипСообщения - ПеречислениеСсылка.ТипыЗапросовЕГАИС - Тип сообщения.
//   * Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция.
//   * Документ - ДокументСсылка - Документ.
//   * СообщениеОснование - СправочникСсылка.ЕГАИСПрисоединенныеФайлы - Сообщение-основание
//   * Описание - Строка - Описание сообщения.
//   * ИдентификаторЗапроса - Строка - Идентификатор запроса.
//   * ФорматОбмена - ПеречислениеСсылка.ФорматыОбменаЕГАИС - Формат обмена.
//   * ОрганизацияЕГАИС - СправочникСсылка.КлассификаторОрганизацийЕГАИС - Организация ЕГАИС.
//   * СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийЕГАИС - Статус обработки сообщения.
//   * Версия - Число - Номер версии.
//  Немедленно - Булево - Признак немедленной передачи сообщения в УТМ, без очереди сообщений.
// 
// Возвращаемое значение:
//  Массив - содержит структуры со следующими полями:
//    * НовыйСтатус
//    * ИсходящееСообщение
//
Функция ПодготовитьСообщениеКПередаче(ТекстСообщенияXML, Реквизиты, Немедленно = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("НовыйСтатус");
	ВозвращаемоеЗначение.Вставить("ИсходящееСообщение");
	ВозвращаемоеЗначение.Вставить("ТекстОшибки");
	
	// Используется для проверка соединения с УТМ
	Если Реквизиты.ОрганизацияЕГАИС = "ПроверкаПодключенияКУТМ" И Немедленно Тогда
		ВозвращаемоеЗначение.ИсходящееСообщение = ТекстСообщенияXML;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		ИсходящееСообщение = ДобавитьЗаписьВПротоколОбмена(
			ТекстСообщенияXML,
			Реквизиты).Ссылка;
		
		Если Не Немедленно Тогда
			ДобавитьСообщениеВОчередьНаПередачуДанных(ИсходящееСообщение, Реквизиты.ОрганизацияЕГАИС);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Реквизиты.Документ) Тогда
			
			ПараметрыОбновленияСтатуса = ИнтеграцияЕГАИС.ПараметрыОбновленияСтатуса();
			ПараметрыОбновленияСтатуса.ОбновлятьДвижения = Истина;
			ПараметрыОбновленияСтатуса.СтатусОбработки   = Реквизиты.СтатусОбработки;
			ПараметрыОбновленияСтатуса.ФорматОбмена      = Реквизиты.ФорматОбмена;
			
			ПолноеИмя = Реквизиты.Документ.Метаданные().ПолноеИмя();
			МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
			НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПодготовкиКПередачеДанных(
				Реквизиты.Документ,
				Реквизиты.Операция,
				ПараметрыОбновленияСтатуса);
			
		Иначе
			НовыйСтатус = Неопределено;
		КонецЕсли;
		
		ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
		ВозвращаемоеЗначение.НовыйСтатус        = НовыйСтатус;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'При подготовке к передаче сообщения по документу %1 возникла ошибка:
			           |Текст ошибки: %2'"),
			Реквизиты.Документ,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ИнтеграцияЕГАИСВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(ТекстОшибки);
		
		ВызватьИсключение КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Добавить сообщение в регистра сведений ОчередьПередачиДанныхЕГАИС.
//
// Параметры:
//  Сообщение - Справочник.ЕГАИСПрисоединенныеФайлы - сообщение, которое добавляется в очередь.
//  ОрганизацияЕГАИС - Справочник.КлассификаторОрганизацийЕГАИС - Организация ЕГАИС.
//
Процедура ДобавитьСообщениеВОчередьНаПередачуДанных(Сообщение, ОрганизацияЕГАИС)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НоваяЗапись = РегистрыСведений.ОчередьПередачиДанныхЕГАИС.СоздатьМенеджерЗаписи();
	НоваяЗапись.Сообщение = Сообщение;
	НоваяЗапись.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	НоваяЗапись.Записать();
	
КонецПроцедуры

// Удалить сообщение из регистра сведений ОчередьПередачиДанныхЕГАИС.
//
// Параметры:
//  Сообщение - Справочник.ЕГАИСПрисоединенныеФайлы - сообщение, которое удаляется из очереди.
//
Процедура УдалитьСообщениеИзОчередиПередачиДанных(Сообщение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ОчередьПередачиДанныхЕГАИС.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сообщение.Установить(Сообщение, Истина);
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Добавить запись в протокол обмена.
//
// Параметры:
//  ТекстСообщенияXML - Строка - Текст сообщения XML.
//  Реквизиты - Структура - Значения реквизитов сообщения.
//  ПроверятьХешБезСсылки - Булево - Признак проверки хеша без ссылки.
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * НовоеСообщение - Булево - Это новое сообщение.
//   * Ссылка - СправочникСсылка.ГИСМПрисоединенныеФайлы - Ссылка на присоединенный файл.
//
Функция ДобавитьЗаписьВПротоколОбмена(ТекстСообщенияXML, Реквизиты, ПроверятьХешБезСсылки = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ХешированиеДанныхОбъект = Новый ХешированиеДанных(ХешФункция.SHA256);
	ХешированиеДанныхОбъект.Добавить(ТекстСообщенияXML);
	ХешСуммаBase64 = Base64Строка(ХешированиеДанныхОбъект.ХешСумма);
	
	СообщениеОснование = Справочники.ЕГАИСПрисоединенныеФайлы.ПустаяСсылка();
	Если Реквизиты.Свойство("СообщениеОснование") Тогда
		СообщениеОснование = Реквизиты.СообщениеОснование;
	КонецЕсли;
	
	Если ПроверятьХешБезСсылки Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЕГАИСПрисоединенныеФайлы.Ссылка КАК Ссылка,
		|	ЕГАИСПрисоединенныеФайлы.Документ КАК Документ
		|ИЗ
		|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
		|ГДЕ
		|	ЕГАИСПрисоединенныеФайлы.ХешСумма = &ХешСумма
		|	И ЕГАИСПрисоединенныеФайлы.Документ ССЫЛКА Документ.ТТНВходящаяЕГАИС
		|");
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.ТТНВходящаяЕГАИС", Реквизиты.Документ.Метаданные().ПолноеИмя());
		Запрос.УстановитьПараметр("ХешСумма", ХешСуммаBase64);
		
	Иначе
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЕГАИСПрисоединенныеФайлы.Ссылка КАК Ссылка,
		|	ЕГАИСПрисоединенныеФайлы.Документ КАК Документ
		|ИЗ
		|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
		|ГДЕ
		|	ЕГАИСПрисоединенныеФайлы.Документ = &Документ
		|	И ЕГАИСПрисоединенныеФайлы.СообщениеОснование = &СообщениеОснование
		|	И ЕГАИСПрисоединенныеФайлы.ХешСумма = &ХешСумма");
		
		Запрос.УстановитьПараметр("Документ",           Реквизиты.Документ);
		Запрос.УстановитьПараметр("ХешСумма",           ХешСуммаBase64);
		Запрос.УстановитьПараметр("СообщениеОснование", СообщениеОснование);
		
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ДокументОснование = Неопределено;
	
	Если Выборка.Следующий() Тогда
		
		НовоеСообщение = Ложь;
		
		Ссылка   = Выборка.Ссылка;
		Документ = Выборка.Документ;
		
		Если ЗначениеЗаполнено(Выборка.Документ)
			И Выборка.Документ.Метаданные().Реквизиты.Найти("ДокументОснование") <> Неопределено Тогда
			ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Документ, "ДокументОснование");
		КонецЕсли;
		
	Иначе
		
		НовоеСообщение = Истина;
		
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.УстановитьТекст(ТекстСообщенияXML);
		ТекстовыйДокумент.Записать(ИмяВременногоФайла, КодировкаТекста.UTF8, "");
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
		АдресФайлаВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
		
		Попытка
			УдалитьФайлы(ИмяВременногоФайла);
		Исключение
			ИнтеграцияЕГАИСВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
		ПараметрыФайла = Новый Структура();
		ПараметрыФайла.Вставить("Автор",              Пользователи.АвторизованныйПользователь());
		ПараметрыФайла.Вставить("ВладелецФайлов",     Реквизиты.ОрганизацияЕГАИС);
		ПараметрыФайла.Вставить("ИмяБезРасширения",   Строка(Новый УникальныйИдентификатор));
		ПараметрыФайла.Вставить("РасширениеБезТочки", "xml");
		ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное");
		
		ПрисоединенныйФайл = ПрисоединенныеФайлы.ДобавитьПрисоединенныйФайл(
			ПараметрыФайла,
			АдресФайлаВоВременномХранилище,,,
			Справочники.ЕГАИСПрисоединенныеФайлы.ПолучитьСсылку());
		
		ПрисоединенныйФайлОбъект = ПрисоединенныйФайл.ПолучитьОбъект();
		ПрисоединенныйФайлОбъект.ХешСумма = ХешСуммаBase64;
		
		ЗаполнитьЗначенияСвойств(ПрисоединенныйФайлОбъект, Реквизиты);
		
		ПрисоединенныйФайлОбъект.Записать();
		
		Ссылка = ПрисоединенныйФайлОбъект.Ссылка;
		Документ = ПрисоединенныйФайлОбъект.Документ;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("НовоеСообщение",    НовоеСообщение);
	ВозвращаемоеЗначение.Вставить("Ссылка",            Ссылка);
	ВозвращаемоеЗначение.Вставить("Документ",          Документ);
	ВозвращаемоеЗначение.Вставить("ДокументОснование", ДокументОснование);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Обработчик подписки на событие ПередЗаписью владельца присоединенного файла.
// Помечает на удаление связанные файлы.
//
// Параметры:
//  Источник        - ДокументОбъект - владелец присоединенного файла.
//  Отказ           - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//  РежимЗаписи     - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//  РежимПроведения - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
// 
Процедура УстановитьПометкуУдаленияПрисоединенныхФайловДокументаЕГАИС(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПометитьНаУдалениеПрисоединенныеФайлыЕГАИС(Источник, Ложь);
	
КонецПроцедуры

// Обработчик подписки на событие ПередЗаписью владельца присоединенного файла.
// Помечает на удаление связанные файлы.
//
// Параметры:
//  Источник        - ДокументОбъект - владелец присоединенного файла.
//  Отказ           - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//  РежимЗаписи     - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
//  РежимПроведения - Булево - параметр, передаваемый в подписку на событие ПередЗаписью.
// 
Процедура УстановитьПометкуУдаленияПрисоединенныхФайловОрганизацииЕГАИС(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПометитьНаУдалениеПрисоединенныеФайлыЕГАИС(Источник, Истина);
	
КонецПроцедуры

// Получить статусы ЕГАИС, цвет текста которых "требует внимания ЕГАИС"
// 
// Возвращаемое значение:
//  Массив - Статусы ЕГАИС, для которых требуется устанавливать цвет "требует внимания ЕГАИС"
//
Функция СтатусыЦветТекстаТребуетВниманияЕГАИС() Экспорт
	
	СтатусыЦветТекстаТребуетВниманияЕГАИС = Новый Массив;
	
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОшибкаПередачи);
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктОтказаОшибка);
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийПодтверждениеОшибка);
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.АктРасхожденийОтказОшибка);
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияПодтверждениеОшибка);
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ЗапросНаОтменуПроведенияОтказОшибка);
	
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.АктПодтвержденияОшибка);
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.АктОтказаОшибка);
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.АктРасхожденийОшибка);
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.ЗапросНаОтменуПроведенияОшибка);
	
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиПередачиВРегистр2ЕГАИС.ОшибкаПередачи);
	
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиОстатковЕГАИС.ОшибкаПередачи);
	
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиВозвратаИзРегистра2ЕГАИС.ОшибкаПередачи);
	
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиАктаСписанияЕГАИС.ОшибкаПередачи);
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиАктаСписанияЕГАИС.ЗапросНаОтменуПроведенияОшибка);
	
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ОшибкаПередачи);
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ЗапросНаОтменуПроведенияОшибка);
	
	СтатусыЦветТекстаТребуетВниманияЕГАИС.Добавить(Перечисления.СтатусыИнформированияЕГАИС.ОшибкаПередачи);
	
	Возврат СтатусыЦветТекстаТребуетВниманияЕГАИС;
	
КонецФункции

// Получить представление статуса ЕГАИС.
//
// Параметры:
//  СтатусЕГАИС - ПеречислениеСсылка - Статус документа ЕГАИС.
//  ВходящиеДальнейшееДействие - Массив,ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС
//                             - Список разрешенных дальнейших действий.
// 
// Возвращаемое значение:
//  ФорматированнаяСтрока - Представление статуса ЕГАИС.
//
Функция ПредставлениеСтатусаЕГАИС(СтатусЕГАИС, ВходящиеДальнейшееДействие, ДопустимыеДействия) Экспорт

	МассивДопустимыеДействия = Новый Массив;
	
	Если ТипЗнч(ДопустимыеДействия) = Тип("ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС") Тогда
		МассивДопустимыеДействия.Добавить(ДопустимыеДействия);
	Иначе
		МассивДопустимыеДействия = ДопустимыеДействия;
	КонецЕсли;
	
	Если ТипЗнч(СтатусЕГАИС) <> Тип("ФорматированнаяСтрока") Тогда
		
		СтатусыЦветТекстаТребуетВниманияЕГАИС = СтатусыЦветТекстаТребуетВниманияЕГАИС();
		
		Если СтатусыЦветТекстаТребуетВниманияЕГАИС.Найти(СтатусЕГАИС) <> Неопределено Тогда
			СтатусЕГАИСПредставление = Новый ФорматированнаяСтрока(Строка(СтатусЕГАИС),,ЦветаСтиля.ЦветТекстаТребуетВниманияГИСМ);
		Иначе
			СтатусЕГАИСПредставление = Новый ФорматированнаяСтрока(Строка(СтатусЕГАИС));
		КонецЕсли;
		
	Иначе
		СтатусЕГАИСПредставление = СтатусЕГАИС;
	КонецЕсли;
	
	ДальнейшиеДействия = Новый Массив;
	Если ТипЗнч(ВходящиеДальнейшееДействие) = Тип("ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС") И ЗначениеЗаполнено(ВходящиеДальнейшееДействие) Тогда
		ДальнейшиеДействия.Добавить(ВходящиеДальнейшееДействие);
	ИначеЕсли ТипЗнч(ВходящиеДальнейшееДействие) = Тип("Массив") Тогда
		ДальнейшиеДействия = ВходящиеДальнейшееДействие;
	КонецЕсли;
	
	СтрокиДальнейшееДействие = Новый Массив;
	СтрокиДальнейшееДействие.Добавить(СтатусЕГАИСПредставление);
	СтрокиДальнейшееДействие.Добавить(", ");
	
	Для Каждого ДальнейшееДействие Из ДальнейшиеДействия Цикл
		
		Если Не ЗначениеЗаполнено(ДальнейшееДействие) Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстГиперссылки = "";
		Если ДопустимоеДальнейшееДействие(ДальнейшееДействие, МассивДопустимыеДействия) Тогда
			Если ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ЗапроситеОстатки Тогда
				ТекстГиперссылки = "ЗапроситьОстатки";
			ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ЗапроситеАкцизныеМарки Тогда
				ТекстГиперссылки = "ЗапроситьАкцизныеМарки";
			ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ЗапроситеОтчет Тогда
				ТекстГиперссылки = "ЗапроситьОтчет";
			ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные Тогда
				ТекстГиперссылки = "ПередатьДанные";
			ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПодтвердитеАктОРасхождениях Тогда
				ТекстГиперссылки = "ПодтвердитьАктОРасхождениях";
			ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтАктаОРасхождениях Тогда
				ТекстГиперссылки = "ОтказатьсяОтАктаОРасхождениях";
			ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтНакладной Тогда
				ТекстГиперссылки = "ОтказатьсяОтНакладной";
			ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ВыполнитеПроверку Тогда
				ТекстГиперссылки = "ВыполнитьПроверку";
			ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПодтвердитеПолучение Тогда
				ТекстГиперссылки = "ПодтвердитьПолучение";
			ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ЗапроситеОтменуПроведения Тогда
				ТекстГиперссылки = "ЗапроситьОтменуПроведения";
			ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПодтвердитеЗапросНаОтменуПроведения Тогда
				ТекстГиперссылки = "ПодтвердитьЗапросНаОтменуПроведения";
			ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОткажитесьОтЗапросаНаОтменуПроведения Тогда
				ТекстГиперссылки = "ОтказатьсяОтЗапросаНаОтменуПроведения";
			ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию Тогда
				ТекстГиперссылки = "ОтменитьОперацию";
			ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеПередачуДанных Тогда
				ТекстГиперссылки = "ОтменитьПередачу";
			ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтработайтеРасхождения Тогда
				ТекстГиперссылки = "ОтработайтеРасхождения";
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекстГиперссылки) Тогда
			
			Если СтрокиДальнейшееДействие.Количество() > 2 Тогда
				СтрокиДальнейшееДействие.Добавить(" " + НСтр("ru = 'или'") + " ");
			КонецЕсли;
			
			СтрокаДальнейшееДействие = Новый ФорматированнаяСтрока(
				НРег(Строка(ДальнейшееДействие)),
				Новый Шрифт(,,,,Истина),
				ЦветаСтиля.ЦветГиперссылкиГИСМ,
				,
				ТекстГиперссылки);
				
			СтрокиДальнейшееДействие.Добавить(СтрокаДальнейшееДействие);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтрокиДальнейшееДействие.Количество() > 2 Тогда
		СтатусЕГАИСПредставление = Новый ФорматированнаяСтрока(СтрокиДальнейшееДействие);
	КонецЕсли;
	
	Возврат СтатусЕГАИСПредставление;

КонецФункции

// Проверяет, имеется ли выбранное действие в массиве допустимых действий
//
// Параметры:
//  ДальнейшееДействие - Перечисления.ДальнейшееДействиеЕГАИС - выбранное действие.
//  ДопустимыеДействия - Массив - массив допустимых действий.
// 
// Возвращаемое значение:
//  Булево - Истина, если действие допустимо, Ложь в обратном случае.
//
Функция ДопустимоеДальнейшееДействие(ДальнейшееДействие, ДопустимыеДействия)
	
	Возврат ДопустимыеДействия.Найти(ДальнейшееДействие) <> Неопределено;
	
КонецФункции

// Рассчитать новый статус оформления документов ЕГАИС по документу-основанию.
//
// Параметры:
//  МетаданныеДокумента - Метаданные - Метаданные проверяемого документа.
//  ДокументОснование - ДокументСсылка - Ссылка на документ-основание.
//  РеквизитыДокументаОснования - Структура - Реквизиты документа-основания.
//  ЗаписьТребуется - Булево - Признак необходимости записи.
//
Процедура РассчитатьСтатусОформления(МетаданныеДокумента, ДокументОснование, РеквизитыДокументаОснования, ЗаписьТребуется) Экспорт
	
	ПустаяСсылка = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеДокумента.ПолноеИмя()).ПустаяСсылка();
	
	ВыполнятьЗаписьВРегистр = Ложь;
	ДанныеЗаписи = РегистрыСведений.СтатусыОформленияДокументовЕГАИС.ЗначенияПоУмолчанию(ДокументОснование, ПустаяСсылка);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СтатусыОформления.СтатусОформления КАК СтатусОформления,
	|	СтатусыОформления.Контрагент       КАК Контрагент,
	|	СтатусыОформления.ТорговыйОбъект   КАК ТорговыйОбъект,
	|	СтатусыОформления.Ответственный    КАК Ответственный,
	|	СтатусыОформления.Дата             КАК Дата,
	|	СтатусыОформления.Номер            КАК Номер
	|ИЗ
	|	РегистрСведений.СтатусыОформленияДокументовЕГАИС КАК СтатусыОформления
	|ГДЕ
	|	СтатусыОформления.Основание = &Основание
	|	И СтатусыОформления.Документ = &ПустаяСсылка
	|");
	
	Запрос.УстановитьПараметр("Основание",    ДокументОснование);
	Запрос.УстановитьПараметр("ПустаяСсылка", ПустаяСсылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если (Не РеквизитыДокументаОснования.Проведен Или Не ЗаписьТребуется) Тогда
			
			РегистрыСведений.СтатусыОформленияДокументовЕГАИС.УдалитьЗаписьРаспоряженияНаОформление(ДокументОснование, ПустаяСсылка);
			Возврат;
			
		Иначе
			
			ВыполнятьЗаписьВРегистр = Истина;
			ДанныеЗаписи.СтатусОформления = СтатусОформления(МетаданныеДокумента, ДокументОснование);
			ЗаполнитьЗначенияСвойств(ДанныеЗаписи, РеквизитыДокументаОснования);
			
		КонецЕсли;
		
	ИначеЕсли РеквизитыДокументаОснования.Проведен И ЗаписьТребуется Тогда
		
		ВыполнятьЗаписьВРегистр = Истина;
		ДанныеЗаписи.СтатусОформления = СтатусОформления(МетаданныеДокумента, ДокументОснование);
		ЗаполнитьЗначенияСвойств(ДанныеЗаписи, РеквизитыДокументаОснования);
		
	КонецЕсли;
	
	Если ВыполнятьЗаписьВРегистр Тогда
		РегистрыСведений.СтатусыОформленияДокументовЕГАИС.ВыполнитьЗаписьВРегистр(ДанныеЗаписи);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает статус оформления по документу-основанию.
//
// Параметры:
//  МетаданныеДокумента - Метаданные - Метаданные проверяемого документа.
//  ДокументОснование - ДокументСсылка - Ссылка на документ-основание.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОформленияДокументовЕГАИС - Статус оформления документа.
//
Функция СтатусОформления(МетаданныеДокумента, ДокументОснование) Экспорт
	
	Запрос = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеДокумента.ПолноеИмя()).ЗапросСтатусаОформления(ДокументОснование);
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("ПустаяСсылка",      Справочники.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если Выборка.ТребуетсяСопоставлениеНоменклатуры Тогда
		
		Статус = Перечисления.СтатусыОформленияДокументовЕГАИС.ТребуетсяСопоставлениеНоменклатуры;
		
	ИначеЕсли Выборка.ЕстьОшибкиОформления Тогда
		
		Статус = Перечисления.СтатусыОформленияДокументовЕГАИС.ЕстьОшибкиОформления;
		
	ИначеЕсли Выборка.ЕстьПолностьюОформленныеТовары И (Не Выборка.ЕстьНеОформленныеТовары) Тогда
		
		Статус = Перечисления.СтатусыОформленияДокументовЕГАИС.Оформлено;
		
	ИначеЕсли Выборка.ЕстьПолностьюОформленныеТовары И Выборка.ЕстьНеОформленныеТовары Тогда
		
		Статус = Перечисления.СтатусыОформленияДокументовЕГАИС.ОформленоЧастично;
		
	ИначеЕсли Выборка.ЕстьОформленныеТовары Тогда
		
		Статус = Перечисления.СтатусыОформленияДокументовЕГАИС.ОформленоЧастично;
		
	Иначе
		
		Статус = Перечисления.СтатусыОформленияДокументовЕГАИС.НеОформлено;
		
	КонецЕсли;
	
	Возврат Статус;
	
КонецФункции

// Создает (или обновляет, если найдена по рег. номеру) справку на основании переданных данных.
//
// Параметры:
//  ДанныеСправки - Структура - заполненная структура, полученная функцией ИнтеграцияЕГАИСКлиентСервер.СтруктураДанныхСправки(1)(2),
//  ВидСправки - ПеречислениеСсылка.ВидыДокументовЕГАИС - вид справки,
//  ДополнительныеПараметры - Произвольный - параметры прикладной конфигурации,
//  ТекстОшибки - Строка - возвращаемый текст ошибки создания справки,
//  ВызыватьОбработчик - Булево - признак вызова обработчика переопределяемого модуля.
//
// Возвращаемое значение:
//   СправочникСсылка.Справки1ЕГАИС, СправочникСсылка.Справки2ЕГАИС - созданная (найденная) справка.
//
Функция СоздатьСправку(ДанныеСправки, ВидСправки) Экспорт
	
	Если ВидСправки = Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросСправки1 Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СправкиЕГАИС.Ссылка КАК Ссылка,
		|	СправкиЕГАИС.РегистрационныйНомер КАК РегистрационныйНомер
		|ПОМЕСТИТЬ ВтСправки1
		|ИЗ
		|	Справочник.Справки1ЕГАИС КАК СправкиЕГАИС
		|ГДЕ
		|	СправкиЕГАИС.РегистрационныйНомер = &РегистрационныйНомер
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Справки2ЕГАИС.Ссылка КАК Справка2,
		|	ВтСправки1.Ссылка    КАК Справка1
		|ИЗ
		|	Справочник.Справки2ЕГАИС КАК Справки2ЕГАИС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСправки1 КАК ВтСправки1
		|		ПО (ВтСправки1.РегистрационныйНомер = Справки2ЕГАИС.НомерСправки1)
		|			И (Справки2ЕГАИС.Справка1 = ЗНАЧЕНИЕ(Справочник.Справки1ЕГАИС.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтСправки1.Ссылка КАК Справка1
		|ИЗ
		|	ВтСправки1 КАК ВтСправки1");
		
		Запрос.УстановитьПараметр("РегистрационныйНомер", ДанныеСправки.РегистрационныйНомер);
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		ВыборкаПоСправкам2КОбновлению = РезультатЗапроса[1].Выбрать();
		Пока ВыборкаПоСправкам2КОбновлению.Следующий() Цикл
			
			Справка2Объект = ВыборкаПоСправкам2КОбновлению.Справка2.ПолучитьОбъект();
			Справка2Объект.Справка1 = ВыборкаПоСправкам2КОбновлению.Справка1;
			Справка2Объект.Записать();
			
		КонецЦикла;
		
		Выборка = РезультатЗапроса[2].Выбрать();
		Если Выборка.Следующий() Тогда
			СправкаОбъект = Выборка.Справка1.ПолучитьОбъект();
		Иначе
			СправкаОбъект = Справочники.Справки1ЕГАИС.СоздатьЭлемент();
		КонецЕсли;
		
		Для Каждого КлючЗначение Из ДанныеСправки Цикл
			
			Если ЗначениеЗаполнено(КлючЗначение.Значение)
				И СправкаОбъект[КлючЗначение.Ключ] <> КлючЗначение.Значение Тогда
				СправкаОбъект[КлючЗначение.Ключ] = КлючЗначение.Значение;
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Справки2ЕГАИС.Ссылка КАК Справка2,
		|	Справки1ЕГАИС.Ссылка КАК Справка1
		|ИЗ
		|	Справочник.Справки2ЕГАИС КАК Справки2ЕГАИС
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Справки1ЕГАИС КАК Справки1ЕГАИС
		|		ПО (Справки1ЕГАИС.РегистрационныйНомер = Справки2ЕГАИС.НомерСправки1)
		|ГДЕ
		|	Справки2ЕГАИС.РегистрационныйНомер = &РегистрационныйНомер");
		
		Запрос.УстановитьПараметр("РегистрационныйНомер", ДанныеСправки.РегистрационныйНомер);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			СправкаОбъект = Выборка.Справка2.ПолучитьОбъект();
		Иначе
			СправкаОбъект = Справочники.Справки2ЕГАИС.СоздатьЭлемент();
		КонецЕсли;
		
		СправкаОбъект.Справка1 = Выборка.Справка1;
		
		Для Каждого ДанныеДиапазона Из ДанныеСправки.ДиапазоныМарок Цикл
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ТипМарки",       ДанныеДиапазона.ТипМарки);
			ПараметрыОтбора.Вставить("СерияМарки",     ДанныеДиапазона.СерияМарки);
			ПараметрыОтбора.Вставить("НачальныйНомер", ДанныеДиапазона.НачальныйНомер);
			ПараметрыОтбора.Вставить("КонечныйНомер",  ДанныеДиапазона.КонечныйНомер);
			
			НайденныеСтроки = СправкаОбъект.ДиапазоныНомеровАкцизныхМарок.НайтиСтроки(ПараметрыОтбора);
			Если НайденныеСтроки.Количество() = 0 Тогда
				ЗаполнитьЗначенияСвойств(СправкаОбъект.ДиапазоныНомеровАкцизныхМарок.Добавить(), ДанныеДиапазона);
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого КлючЗначение Из ДанныеСправки Цикл
			
			Если КлючЗначение.Ключ = "ДиапазоныМарок" Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(КлючЗначение.Значение)
				И СправкаОбъект[КлючЗначение.Ключ] <> КлючЗначение.Значение Тогда
				СправкаОбъект[КлючЗначение.Ключ] = КлючЗначение.Значение;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если СправкаОбъект.Модифицированность() Тогда
		СправкаОбъект.Записать();
	КонецЕсли;
	
	Возврат СправкаОбъект.Ссылка;
	
КонецФункции

// Процедура запуска регламентного задания ОбработкаОтветовЕГАИС.
//
Процедура ЗапуститьОбработкуОтветовЕГАИС() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.Найти("ОбработкаОтветовЕГАИС"));
	
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЕГАИСВызовСервера.ВыполнитьОбмен();
	
КонецПроцедуры

// Выполняет установку параметров сеанса. Вызывается из модуля сеанса.
//
// Параметры:
//   ИмяПараметра           - строка с именем параметра сеанса.
//   УстановленныеПараметры - массив всех установленных параметров сеанса.
//
Процедура УстановитьПараметрыСеанса(ИмяПараметра, УстановленныеПараметры) Экспорт
	
	Если ИмяПараметра = "ИдентификаторСеансаЕГАИС" Тогда
		ПараметрыСеанса.ИдентификаторСеансаЕГАИС = Новый УникальныйИдентификатор;
		УстановленныеПараметры.Добавить(ИмяПараметра);
	КонецЕсли;
	
КонецПроцедуры

// Определяет следующие свойства регламентных заданий:
//  - зависимость от функциональных опций.
//  - возможность выполнения в различных режимах работы программы.
//  - прочие параметры.
//
Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки) Экспорт
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ОбработкаОтветовЕГАИС;
	Настройка.ФункциональнаяОпция = Метаданные.ФункциональныеОпции.ВестиСведенияДляДекларацийПоАлкогольнойПродукции;
	Настройка.ДоступноВМоделиСервиса = Ложь;
	
КонецПроцедуры

// Возвращает имя справочника, содержащего классификатор видов алкогольной продукции.
//
// Возвращаемое значение:
//   Строка - имя справочника.
//
Функция СправочникВидовАлкогольнойПродукции() Экспорт

	МассивТипов = Метаданные.ОпределяемыеТипы.ВидАлкогольнойПродукции.Тип.Типы();
	
	Если МассивТипов.Количество() = 1 Тогда
		ОбъектМетаданных = Метаданные.НайтиПоТипу(МассивТипов[0]);
		Возврат ОбъектМетаданных.Имя;
	Иначе
		ВызватьИсключение НСтр("ru = 'Определено более одного справочника видов алкогольной продукции.'");
	КонецЕсли;

КонецФункции

// Функция возвращает таблицу значений классификатора видов алкогольной продукции.
//
Функция КлассификаторВидовАлкогольнойПродукции() Экспорт
	
	ТаблицаВидовПродукции = Новый ТаблицаЗначений;
	
	Макет = ПолучитьОбщийМакет("КлассификаторВидовАлкогольнойПродукции");
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(Макет.ПолучитьТекст());
	
	Если Не ЧтениеXML.Прочитать() Тогда
		ВызватьИсключение НСтр("ru = 'Пустой XML'");
	ИначеЕсли ЧтениеXML.Имя <> "Items" Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка в структуре XML'");
	КонецЕсли;
	
	ИменаКолонок = СтрЗаменить(ЧтениеXML.ПолучитьАтрибут("Columns"), ",", Символы.ПС);
	КоличествоКолонок = СтрЧислоСтрок(ИменаКолонок);
	
	Для Сч = 1 По КоличествоКолонок Цикл
		ИмяКолонки = СтрПолучитьСтроку(ИменаКолонок, Сч);
		
		Если ИмяКолонки = "Маркируемый" Тогда
			ТаблицаВидовПродукции.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Булево"));
		Иначе
			ТаблицаВидовПродукции.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Строка"));
		КонецЕсли;
	КонецЦикла;
	
	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.Имя = "Items" Тогда
			Прервать;
		ИначеЕсли ЧтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента Тогда
			Продолжить;
		ИначеЕсли ЧтениеXML.Имя <> "Item" Тогда
			ВызватьИсключение НСтр("ru = 'Ошибка в структуре XML'");
		КонецЕсли;
		
		новСтр = ТаблицаВидовПродукции.Добавить();
		Для Сч = 1 По КоличествоКолонок Цикл
			ИмяКолонки = СтрПолучитьСтроку(ИменаКолонок, Сч);
			
			Если ИмяКолонки = "Маркируемый" Тогда
				новСтр[Сч-1] = Булево(СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ЧтениеXML.ПолучитьАтрибут(ИмяКолонки)));
			Иначе
				новСтр[Сч-1] = ЧтениеXML.ПолучитьАтрибут(ИмяКолонки);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаВидовПродукции.Сортировать(ТаблицаВидовПродукции.Колонки[0].Имя + " Возр");
	
	Возврат ТаблицаВидовПродукции;
	
КонецФункции

// Проводит документ для внутреннего учета остатков по регистру №1
//
Процедура ПровестиДокумент(ДокументОбъект, Отказ, РежимПроведения) Экспорт
	
	// Инициализация дополнительных свойств для проведения документа
	ИнициализироватьДополнительныеСвойстваДляПроведения(ДокументОбъект.Ссылка, ДокументОбъект.ДополнительныеСвойства, РежимПроведения);
	
	// Инициализация данных документа
	Документы[ДокументОбъект.Метаданные().Имя].ИнициализироватьДанныеДокумента(ДокументОбъект.Ссылка, ДокументОбъект.ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПодготовитьНаборыЗаписейКРегистрацииДвижений(ДокументОбъект);
	
	РегистрыНакопления.ОстаткиАлкогольнойПродукцииЕГАИС.ОтразитьДвижения(ДокументОбъект.ДополнительныеСвойства, ДокументОбъект.Движения, Отказ);
	
	ЗаписатьНаборыЗаписей(ДокументОбъект);
	
	ИнтеграцияЕГАИСПереопределяемый.ОбработкаПроведения(ДокументОбъект, Отказ, РежимПроведения);
	
	ОчиститьДополнительныеСвойстваДляПроведения(ДокументОбъект.ДополнительныеСвойства);
	
КонецПроцедуры

// Возвращает значение по умолчанию переданного определяемого типа.
//
Функция ПустоеЗначениеОпределяемогоТипа(ИмяТипа) Экспорт
	
	Возврат Метаданные.ОпределяемыеТипы[ИмяТипа].Тип.ПривестиЗначение();
	
КонецФункции

// Формирует массив пустых значений определяемого типа.
// 
// Возвращаемое значение:
//  Массив - Пустые значения определяемого типа.
//
Функция МассивПустыхЗначенийОпределяемогоТипа(ОпределяемыйТип) Экспорт
	
	МассивПустыхЗначений = Новый Массив;
	Для Каждого ТипЗначения Из ОпределяемыйТип.Тип.Типы() Цикл
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(ТипЗначения);
		ОписаниеТипа = Новый ОписаниеТипов(МассивТипов);
		МассивПустыхЗначений.Добавить(ОписаниеТипа.ПривестиЗначение());
	КонецЦикла;
	
	Если МассивПустыхЗначений.Количество() > 1 Тогда
		МассивПустыхЗначений.Добавить(Неопределено);
	КонецЕсли;
	
	Возврат МассивПустыхЗначений;
	
КонецФункции

// Заполняет в табличной части сопоставленную номенклатуру и алкогольную продукцию.
//
// Параметры:
//  ТаблицаСопоставления - ТаблицаЗначений, ДанныеФормыКоллекция - таблица для заполнения.
//
Процедура ЗаполнитьСопоставленнуюПродукцию(ТаблицаСопоставления) Экспорт
	
	Если ТипЗнч(ТаблицаСопоставления) = Тип("ТаблицаЗначений") Тогда
		Если ТаблицаСопоставления.Колонки.Найти("НомерСтроки") = Неопределено Тогда
			ТаблицаСопоставления.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
			
			НомерСтроки = 1;
			Для Каждого СтрокаТаблицы Из ТаблицаСопоставления Цикл
				СтрокаТаблицы.НомерСтроки = НомерСтроки;
				НомерСтроки = НомерСтроки + 1;
			КонецЦикла;
		КонецЕсли;
		
		Если ТаблицаСопоставления.Колонки.Найти("Номенклатура") = Неопределено Тогда
			ТаблицаСопоставления.Колонки.Добавить("Номенклатура", Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
		КонецЕсли;
		
		Если ТаблицаСопоставления.Колонки.Найти("Характеристика") = Неопределено Тогда
			ТаблицаСопоставления.Колонки.Добавить("Характеристика", Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
		КонецЕсли;
		
		Если ТаблицаСопоставления.Колонки.Найти("АлкогольнаяПродукция") = Неопределено Тогда
			ТаблицаСопоставления.Колонки.Добавить("АлкогольнаяПродукция", Новый ОписаниеТипов("СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС"));
		КонецЕсли;
		
		Если ТаблицаСопоставления.Колонки.Найти("Сопоставлено") = Неопределено Тогда
			ТаблицаСопоставления.Колонки.Добавить("Сопоставлено", Новый ОписаниеТипов("Булево"));
		КонецЕсли;
		
		Если ТаблицаСопоставления.Колонки.Найти("СопоставленоКоличество") = Неопределено Тогда
			ТаблицаСопоставления.Колонки.Добавить("СопоставленоКоличество", Новый ОписаниеТипов("Число"));
		КонецЕсли;
		
		Если ТаблицаСопоставления.Колонки.Найти("СопоставлениеНоменклатура") = Неопределено Тогда
			ТаблицаСопоставления.Колонки.Добавить("СопоставлениеНоменклатура", Новый ОписаниеТипов("Строка"));
		КонецЕсли;
		
		Если ТаблицаСопоставления.Колонки.Найти("СопоставлениеХарактеристика") = Неопределено Тогда
			ТаблицаСопоставления.Колонки.Добавить("СопоставлениеХарактеристика", Новый ОписаниеТипов("Строка"));
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаСопоставления", ?(ТипЗнч(ТаблицаСопоставления) = Тип("ТаблицаЗначений"), ТаблицаСопоставления, ТаблицаСопоставления.Выгрузить()));
	Запрос.УстановитьПараметр("ПустаяНоменклатура"  , ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.Номенклатура.Имя));
	Запрос.УстановитьПараметр("ПустаяХарактеристика", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Имя));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаСопоставления.НомерСтроки КАК НомерСтроки,
	|	ТаблицаСопоставления.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТаблицаСопоставления.Номенклатура КАК Номенклатура,
	|	ТаблицаСопоставления.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ТаблицаСопоставления
	|ИЗ
	|	&ТаблицаСопоставления КАК ТаблицаСопоставления
	|ГДЕ
	|	(ТаблицаСопоставления.Номенклатура = &ПустаяНоменклатура
	|			ИЛИ ТаблицаСопоставления.АлкогольнаяПродукция = ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	КОЛИЧЕСТВО(СоответствиеНоменклатурыЕГАИС.Номенклатура) КАК СопоставленоКоличество
	|ПОМЕСТИТЬ АлкогольнаяПродукцияСопоставленоКоличество
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПРАВОЕ СОЕДИНЕНИЕ ТаблицаСопоставления КАК ТаблицаСопоставления
	|		ПО СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция = ТаблицаСопоставления.АлкогольнаяПродукция
	|ГДЕ
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция <> ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)
	|	И НЕ СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура,
	|	СоответствиеНоменклатурыЕГАИС.Характеристика КАК Характеристика,
	|	АлкогольнаяПродукцияСопоставленоКоличество.СопоставленоКоличество КАК СопоставленоКоличество
	|ПОМЕСТИТЬ СопоставленнаяАлкогольнаяПродукция
	|ИЗ
	|	АлкогольнаяПродукцияСопоставленоКоличество КАК АлкогольнаяПродукцияСопоставленоКоличество
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО АлкогольнаяПродукцияСопоставленоКоличество.АлкогольнаяПродукция = СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция
	|			И (СоответствиеНоменклатурыЕГАИС.Порядок = 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаСопоставления.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ТаблицаСопоставления.Номенклатура <> &ПустаяНоменклатура
	|			ТОГДА ТаблицаСопоставления.Номенклатура
	|		КОГДА ЕСТЬNULL(СопоставленнаяАлкогольнаяПродукция.СопоставленоКоличество, 0) = 1
	|			ТОГДА СопоставленнаяАлкогольнаяПродукция.Номенклатура
	|		ИНАЧЕ &ПустаяНоменклатура
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаСопоставления.Номенклатура <> &ПустаяНоменклатура
	|			ТОГДА ТаблицаСопоставления.Характеристика
	|		КОГДА ЕСТЬNULL(СопоставленнаяАлкогольнаяПродукция.СопоставленоКоличество, 0) = 1
	|			ТОГДА СопоставленнаяАлкогольнаяПродукция.Характеристика
	|		ИНАЧЕ &ПустаяХарактеристика
	|	КОНЕЦ КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаСопоставления.Номенклатура <> &ПустаяНоменклатура
	|			ТОГДА ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка))
	|		ИНАЧЕ ТаблицаСопоставления.АлкогольнаяПродукция
	|	КОНЕЦ КАК АлкогольнаяПродукция,
	|	ЕСТЬNULL(СопоставленнаяАлкогольнаяПродукция.СопоставленоКоличество, 0) КАК СопоставленоКоличество,
	|	ЕСТЬNULL(ПРЕДСТАВЛЕНИЕ(СопоставленнаяАлкогольнаяПродукция.Номенклатура), """") КАК НоменклатураНаименование
	|ИЗ
	|	ТаблицаСопоставления КАК ТаблицаСопоставления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО ТаблицаСопоставления.Номенклатура = СоответствиеНоменклатурыЕГАИС.Номенклатура
	|			И ТаблицаСопоставления.Характеристика = СоответствиеНоменклатурыЕГАИС.Характеристика
	|			И (ТаблицаСопоставления.Номенклатура <> &ПустаяНоменклатура)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленнаяАлкогольнаяПродукция КАК СопоставленнаяАлкогольнаяПродукция
	|		ПО ТаблицаСопоставления.АлкогольнаяПродукция = СопоставленнаяАлкогольнаяПродукция.АлкогольнаяПродукция";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("НомерСтроки", Выборка.НомерСтроки);
		СтрокаТаблицы = ТаблицаСопоставления.НайтиСтроки(ПараметрыОтбора)[0];
		
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
		
		Если Выборка.СопоставленоКоличество = 1 Тогда
			СтрокаТаблицы.Сопоставлено = Истина;
			СтрокаТаблицы.СопоставлениеНоменклатура   = Строка(Выборка.Номенклатура);
			СтрокаТаблицы.СопоставлениеХарактеристика = Строка(Выборка.Характеристика);
		ИначеЕсли Выборка.СопоставленоКоличество > 1 Тогда
			СтрокаТаблицы.Сопоставлено = Истина;
			СтрокаТаблицы.СопоставлениеНоменклатура   = СтрШаблон(НСтр("ru = '%1 ( + еще %2...)'"), Выборка.НоменклатураНаименование, Выборка.СопоставленоКоличество - 1);
			СтрокаТаблицы.СопоставлениеХарактеристика = НСтр("ru = '<Не сопоставлено>'");
		Иначе
			СтрокаТаблицы.Сопоставлено = Ложь;
			СтрокаТаблицы.СопоставлениеНоменклатура   = НСтр("ru = '<Не сопоставлено>'");
			СтрокаТаблицы.СопоставлениеХарактеристика = НСтр("ru = '<Не сопоставлено>'");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Позволяет переопределить справочники хранения файлов по типам владельцев.
// 
// Параметры:
//  ТипВладелецФайла  - Тип - тип ссылки объекта, к которому добавляется файл.
//
//  ИменаСправочников - Соответствие - содержит в ключах имена справочников.
//                      При вызове содержит стандартное имя одного справочника,
//                      помеченного, как основной (если существует).
//                      Основной справочник используется для интерактивного
//                      взаимодействия с пользователем. Чтобы указать основной
//                      справочник, нужно установить Истина в значение соответствия.
//                      Если установить Истина более одного раза, тогда будет ошибка.
//
Процедура ПриОпределенииСправочниковХраненияФайлов(ТипВладелецФайла, ИменаСправочников) Экспорт
	
	Если ТипВладелецФайла = Тип("СправочникСсылка.КлассификаторОрганизацийЕГАИС") Тогда
		ИменаСправочников.Вставить("ЕГАИСПрисоединенныеФайлы", Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает текст запроса для формирования временной таблицы коэффициентов пересчета базовых единиц измерения
// номенклатуры в единицы ЕГАИС:
//   - для упакованного товара: коэффициент пересчета в штуки (бутылки)
//   - для неупакованного товара: коэффициент пересчета в декалитры.
// Временная таблица используется при проведении документов по регистру ОстаткиЕГАИС и при передаче данных в УТМ.
//
// Параметры:
//  ИмяТаблицыТовары - Строка - Имя таблицы с колонками: АлкогольнаяПродукция, Номенклатура, Характеристика.
//  ИмяВременнойТаблицы - Строка - Имя результирующей временной таблицы.
//  ДобавлятьРазделитель - Булево - Признак добавления разделителя запросов.
//
// Возвращаемое значение:
//  Строка - Текст запроса.
//
Функция ТекстЗапросаВТКоэффициентыПересчетаВЕдиницыЕГАИС(ИмяТаблицыТовары = "ВТТовары", ИмяВременнойТаблицы = "ВТКоэффициентыПересчетаВЕдиницыЕГАИС", ДобавлятьРазделитель = Ложь) Экспорт
	
	ТекстЗапроса = ИнтеграцияЕГАИСПереопределяемый.ТекстЗапросаВТКоэффициентыПересчетаВЕдиницыЕГАИС(ИмяТаблицыТовары, ИмяВременнойТаблицы);
	
	Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.АлкогольнаяПродукция  КАК АлкогольнаяПродукция,
		|	Таблица.Номенклатура          КАК Номенклатура,
		|	Таблица.Характеристика        КАК Характеристика,
		|	Таблица.Серия                 КАК Серия,
		|	ЛОЖЬ                          КАК ПроверятьОбъемДАЛ,
		|	0                             КАК ОбъемДАЛ,
		|	1                             КАК Коэффициент
		|ПОМЕСТИТЬ ШаблонИмяВременнойТаблицы
		|ИЗ
		|	ШаблонТаблицаТовары КАК Таблица
		|";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ШаблонТаблицаТовары",       ИмяТаблицыТовары);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ШаблонИмяВременнойТаблицы", ИмяВременнойТаблицы);
		
	КонецЕсли;
	
	Если ДобавлятьРазделитель Тогда
		ТекстЗапроса = ТекстЗапроса +
		";
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#Область ГиперссылкиВводаНаОсновании

// Возвращает структуру данных описания гиперссылки документа Акт списания ЕГАИС.
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * КомандаСоздать - Строка - Текст гиперссылки создания нового документа.
//   * ИмяКомандыСоздать - Строка - Имя команды Создать.
//   * ИмяКомандыОткрыть - Строка - Имя команды Открыть.
//   * ДокументОтсутствуетНетПравНаСоздание - Строка - Текст гиперссылки на создание документа, в случае отсутствия прав.
//   * Представление - Строка - Текст гиперссылки на список документов.
//   * НесколькоДокументовПредставление - Строка - Текст гиперссылки если найдено несколько документов.
//
Функция ПредставлениеДокументаАктСписанияЕГАИС()
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("КомандаСоздать",                       НСтр("ru = 'Создать акт списания ЕГАИС'"));
	ВозвращаемоеЗначение.Вставить("ИмяКомандыСоздать",                    "СоздатьАктСписанияЕГАИС");
	ВозвращаемоеЗначение.Вставить("ИмяКомандыОткрыть",                    "ОткрытьАктСписанияЕГАИС");
	ВозвращаемоеЗначение.Вставить("ДокументНеОформлен",                   НСтр("ru = 'Акт списания ЕГАИС не оформлен'"));
	ВозвращаемоеЗначение.Вставить("ДокументОтсутствуетНетПравНаСоздание", НСтр("ru = 'Акт списания ЕГАИС не создан'"));
	ВозвращаемоеЗначение.Вставить("Представление",                        НСтр("ru = 'Акт списания ЕГАИС: %1'"));
	ВозвращаемоеЗначение.Вставить("НесколькоДокументовПредставление",     НСтр("ru = 'Акты списания ЕГАИС (%1)'"));
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Возвращает структуру данных описания гиперссылки документа Акт списания ЕГАИС.
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * КомандаСоздать - Строка - Текст гиперссылки создания нового документа.
//   * ИмяКомандыСоздать - Строка - Имя команды Создать.
//   * ИмяКомандыОткрыть - Строка - Имя команды Открыть.
//   * ДокументОтсутствуетНетПравНаСоздание - Строка - Текст гиперссылки на создание документа, в случае отсутствия прав.
//   * Представление - Строка - Текст гиперссылки на список документов.
//   * НесколькоДокументовПредставление - Строка - Текст гиперссылки если найдено несколько документов.
//
Функция ПредставлениеДокументаАктПостановкиНаБалансЕГАИС()
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("КомандаСоздать",                       НСтр("ru = 'Создать акт постановки на баланс ЕГАИС'"));
	ВозвращаемоеЗначение.Вставить("ИмяКомандыСоздать",                    "СоздатьАктПостановкиНаБалансЕГАИС");
	ВозвращаемоеЗначение.Вставить("ИмяКомандыОткрыть",                    "ОткрытьАктПостановкиНаБалансЕГАИС");
	ВозвращаемоеЗначение.Вставить("ДокументНеОформлен",                   НСтр("ru = 'Акт постановки на баланс ЕГАИС не оформлен'"));
	ВозвращаемоеЗначение.Вставить("ДокументОтсутствуетНетПравНаСоздание", НСтр("ru = 'Акт постановки на баланс ЕГАИС не создан'"));
	ВозвращаемоеЗначение.Вставить("Представление",                        НСтр("ru = 'Акт постановки на баланс ЕГАИС: %1'"));
	ВозвращаемоеЗначение.Вставить("НесколькоДокументовПредставление",     НСтр("ru = 'Акты постановки на баланс ЕГАИС (%1)'"));
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Возвращает структуру данных описания гиперссылки документа ТТН ЕГАИС (исходящая).
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * КомандаСоздать - Строка - Текст гиперссылки создания нового документа.
//   * ИмяКомандыСоздать - Строка - Имя команды Создать.
//   * ИмяКомандыОткрыть - Строка - Имя команды Открыть.
//   * ДокументОтсутствуетНетПравНаСоздание - Строка - Текст гиперссылки на создание документа, в случае отсутствия прав.
//   * Представление - Строка - Текст гиперссылки на список документов.
//   * НесколькоДокументовПредставление - Строка - Текст гиперссылки если найдено несколько документов.
//
Функция ПредставлениеДокументаТТНИсходящаяЕГАИС()
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("КомандаСоздать",                       НСтр("ru = 'Создать ТТН ЕГАИС (исходящую)'"));
	ВозвращаемоеЗначение.Вставить("ИмяКомандыСоздать",                    "СоздатьТТНИсходящаяЕГАИС");
	ВозвращаемоеЗначение.Вставить("ИмяКомандыОткрыть",                    "ОткрытьТТНИсходящаяЕГАИС");
	ВозвращаемоеЗначение.Вставить("ДокументНеОформлен",                   НСтр("ru = 'ТТН ЕГАИС (исходящая) не оформлена'"));
	ВозвращаемоеЗначение.Вставить("ДокументОтсутствуетНетПравНаСоздание", НСтр("ru = 'ТТН ЕГАИС (исходящая) не создана'"));
	ВозвращаемоеЗначение.Вставить("Представление",                        НСтр("ru = 'ТТН ЕГАИС (исходящая): %1'"));
	ВозвращаемоеЗначение.Вставить("НесколькоДокументовПредставление",     НСтр("ru = 'ТТН ЕГАИС (исходящие) (%1)'"));
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Возвращает структуру данных описания гиперссылки документа ТТН ЕГАИС (входящая).
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * КомандаСоздать - Строка - Текст гиперссылки создания нового документа.
//   * ИмяКомандыСоздать - Строка - Имя команды Создать.
//   * ИмяКомандыОткрыть - Строка - Имя команды Открыть.
//   * ДокументОтсутствуетНетПравНаСоздание - Строка - Текст гиперссылки на создание документа, в случае отсутствия прав.
//   * Представление - Строка - Текст гиперссылки на список документов.
//   * НесколькоДокументовПредставление - Строка - Текст гиперссылки если найдено несколько документов.
//
Функция ПредставлениеДокументаТТНВходящаяЕГАИС()
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("КомандаСоздать",                       НСтр("ru = 'ТТН ЕГАИС (входящая) не получена'"));
	ВозвращаемоеЗначение.Вставить("ИмяКомандыСоздать",                    Неопределено);
	ВозвращаемоеЗначение.Вставить("ИмяКомандыОткрыть",                    "ОткрытьТТНВходящаяЕГАИС");
	ВозвращаемоеЗначение.Вставить("ДокументНеОформлен",                   НСтр("ru = 'ТТН ЕГАИС (входящая) не оформлена'"));
	ВозвращаемоеЗначение.Вставить("ДокументОтсутствуетНетПравНаСоздание", НСтр("ru = 'ТТН ЕГАИС (входящая) не получена'"));
	ВозвращаемоеЗначение.Вставить("Представление",                        НСтр("ru = 'ТТН ЕГАИС (входящая): %1'"));
	ВозвращаемоеЗначение.Вставить("НесколькоДокументовПредставление",     НСтр("ru = 'ТТН ЕГАИС (входящие) (%1)'"));
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Возвращает структуру данных описания гиперссылки документа Чек ЕГАИС.
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * КомандаСоздать - Строка - Текст гиперссылки создания нового документа.
//   * ИмяКомандыСоздать - Строка - Имя команды Создать.
//   * ИмяКомандыОткрыть - Строка - Имя команды Открыть.
//   * ДокументОтсутствуетНетПравНаСоздание - Строка - Текст гиперссылки на создание документа, в случае отсутствия прав.
//   * Представление - Строка - Текст гиперссылки на список документов.
//   * НесколькоДокументовПредставление - Строка - Текст гиперссылки если найдено несколько документов.
//
Функция ПредставлениеДокументаЧекЕГАИС()
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("КомандаСоздать",                       НСтр("ru = 'Создать чек ЕГАИС'"));
	ВозвращаемоеЗначение.Вставить("ИмяКомандыСоздать",                    "СоздатьЧекЕГАИС");
	ВозвращаемоеЗначение.Вставить("ИмяКомандыОткрыть",                    "ОткрытьЧекЕГАИС");
	ВозвращаемоеЗначение.Вставить("ДокументНеОформлен",                   НСтр("ru = 'Чек ЕГАИС не оформлен'"));
	ВозвращаемоеЗначение.Вставить("ДокументОтсутствуетНетПравНаСоздание", НСтр("ru = 'Чек ЕГАИС не создан'"));
	ВозвращаемоеЗначение.Вставить("Представление",                        НСтр("ru = 'Чек ЕГАИС: %1'"));
	ВозвращаемоеЗначение.Вставить("НесколькоДокументовПредставление",     НСтр("ru = 'Чек ЕГАИС (%1)'"));
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Возвращает структуру данных описания гиперссылки документа Чек ЕГАИС на возврат.
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * КомандаСоздать - Строка - Текст гиперссылки создания нового документа.
//   * ИмяКомандыСоздать - Строка - Имя команды Создать.
//   * ИмяКомандыОткрыть - Строка - Имя команды Открыть.
//   * ДокументОтсутствуетНетПравНаСоздание - Строка - Текст гиперссылки на создание документа, в случае отсутствия прав.
//   * Представление - Строка - Текст гиперссылки на список документов.
//   * НесколькоДокументовПредставление - Строка - Текст гиперссылки если найдено несколько документов.
//
Функция ПредставлениеДокументаЧекЕГАИСВозврат()
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("КомандаСоздать",                       НСтр("ru = 'Создать чек ЕГАИС на возврат'"));
	ВозвращаемоеЗначение.Вставить("ИмяКомандыСоздать",                    "СоздатьЧекЕГАИСВозврат");
	ВозвращаемоеЗначение.Вставить("ИмяКомандыОткрыть",                    "ОткрытьЧекЕГАИСВозврат");
	ВозвращаемоеЗначение.Вставить("ДокументНеОформлен",                   НСтр("ru = 'Чек ЕГАИС на возврат не оформлен'"));
	ВозвращаемоеЗначение.Вставить("ДокументОтсутствуетНетПравНаСоздание", НСтр("ru = 'Чек ЕГАИС на возврат не создан'"));
	ВозвращаемоеЗначение.Вставить("Представление",                        НСтр("ru = 'Чек ЕГАИС на возврат: %1'"));
	ВозвращаемоеЗначение.Вставить("НесколькоДокументовПредставление",     НСтр("ru = 'Чек ЕГАИС на возврат (%1)'"));
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Возвращает структуру данных описания гиперссылки документа Передача в регистр 2.
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * КомандаСоздать - Строка - Текст гиперссылки создания нового документа.
//   * ИмяКомандыСоздать - Строка - Имя команды Создать.
//   * ИмяКомандыОткрыть - Строка - Имя команды Открыть.
//   * ДокументОтсутствуетНетПравНаСоздание - Строка - Текст гиперссылки на создание документа, в случае отсутствия прав.
//   * Представление - Строка - Текст гиперссылки на список документов.
//   * НесколькоДокументовПредставление - Строка - Текст гиперссылки если найдено несколько документов.
//
Функция ПредставлениеДокументаПередачаВРегистр2ЕГАИС()
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("КомандаСоздать",                       НСтр("ru = 'Создать передачу в регистр №2 ЕГАИС'"));
	ВозвращаемоеЗначение.Вставить("ИмяКомандыСоздать",                    "СоздатьПередачаВРегистр2ЕГАИС");
	ВозвращаемоеЗначение.Вставить("ИмяКомандыОткрыть",                    "ОткрытьПередачаВРегистр2ЕГАИС");
	ВозвращаемоеЗначение.Вставить("ДокументНеОформлен",                   НСтр("ru = 'Передача в регистр №2 ЕГАИС не оформлена'"));
	ВозвращаемоеЗначение.Вставить("ДокументОтсутствуетНетПравНаСоздание", НСтр("ru = 'Передача в регистр №2 ЕГАИС не создана'"));
	ВозвращаемоеЗначение.Вставить("Представление",                        НСтр("ru = 'Передача в регистр №2 ЕГАИС: %1'"));
	ВозвращаемоеЗначение.Вставить("НесколькоДокументовПредставление",     НСтр("ru = 'Передача в регистр №2 ЕГАИС (%1)'"));
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Возвращает структуру данных описания гиперссылки документа Возврат из регистра 2.
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * КомандаСоздать - Строка - Текст гиперссылки создания нового документа.
//   * ИмяКомандыСоздать - Строка - Имя команды Создать.
//   * ИмяКомандыОткрыть - Строка - Имя команды Открыть.
//   * ДокументОтсутствуетНетПравНаСоздание - Строка - Текст гиперссылки на создание документа, в случае отсутствия прав.
//   * Представление - Строка - Текст гиперссылки на список документов.
//   * НесколькоДокументовПредставление - Строка - Текст гиперссылки если найдено несколько документов.
//
Функция ПредставлениеДокументаВозвратИзРегистра2ЕГАИС()
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("КомандаСоздать",                       НСтр("ru = 'Создать возврат из регистра №2 ЕГАИС'"));
	ВозвращаемоеЗначение.Вставить("ИмяКомандыСоздать",                    "СоздатьВозвратИзРегистра2ЕГАИС");
	ВозвращаемоеЗначение.Вставить("ИмяКомандыОткрыть",                    "ОткрытьВозвратИзРегистра2ЕГАИС");
	ВозвращаемоеЗначение.Вставить("ДокументНеОформлен",                   НСтр("ru = 'Возврат из регистра №2 ЕГАИС не оформлен'"));
	ВозвращаемоеЗначение.Вставить("ДокументОтсутствуетНетПравНаСоздание", НСтр("ru = 'Возврат из регистра №2 ЕГАИС не создан'"));
	ВозвращаемоеЗначение.Вставить("Представление",                        НСтр("ru = 'Возврат из регистра №2 ЕГАИС: %1'"));
	ВозвращаемоеЗначение.Вставить("НесколькоДокументовПредставление",     НСтр("ru = 'Возврат из регистра №2 ЕГАИС (%1)'"));
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Получить данные для заполнения представления документа.
//
// Параметры:
//  МетаданныеДокумента - ОбъектМетаданных - Метаданные документа.
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//  * КомандаСоздать - Строка - Представление документа, если документ требуется создать.
//  * ИмяКомандыСоздать - Строка - Имя команды "Создать".
//  * ИмяКомандыОткрыть - Строка - Имя команды "Открыть".
//  * ДокументОтсутствуетНетПравНаСоздание - Строка - Представление документа, если документ не создан.
//  * Представление - Строка - Представление документа.
//  * НесколькоДокументовПредставление - Строка - Представление документа, если их несколько.
//
Функция ПредставлениеДокумента(МетаданныеДокумента) Экспорт
	
	Если МетаданныеДокумента = Метаданные.Документы.АктСписанияЕГАИС Тогда
		
		Возврат ПредставлениеДокументаАктСписанияЕГАИС();
		
	ИначеЕсли МетаданныеДокумента = Метаданные.Документы.АктПостановкиНаБалансЕГАИС Тогда
		
		Возврат ПредставлениеДокументаАктПостановкиНаБалансЕГАИС();
		
	ИначеЕсли МетаданныеДокумента = Метаданные.Документы.ТТНИсходящаяЕГАИС Тогда
		
		Возврат ПредставлениеДокументаТТНИсходящаяЕГАИС();
		
	ИначеЕсли МетаданныеДокумента = Метаданные.Документы.ТТНВходящаяЕГАИС Тогда
		
		Возврат ПредставлениеДокументаТТНВходящаяЕГАИС();
		
	ИначеЕсли МетаданныеДокумента = Метаданные.Документы.ЧекЕГАИС Тогда
		
		Возврат ПредставлениеДокументаЧекЕГАИС();
		
	ИначеЕсли МетаданныеДокумента = Метаданные.Документы.ЧекЕГАИСВозврат Тогда
		
		Возврат ПредставлениеДокументаЧекЕГАИСВозврат();
		
	ИначеЕсли МетаданныеДокумента = Метаданные.Документы.ПередачаВРегистр2ЕГАИС Тогда
		
		Возврат ПредставлениеДокументаПередачаВРегистр2ЕГАИС();
		
	ИначеЕсли МетаданныеДокумента = Метаданные.Документы.ВозвратИзРегистра2ЕГАИС Тогда
		
		Возврат ПредставлениеДокументаВозвратИзРегистра2ЕГАИС();
		
	КонецЕсли;
	
КонецФункции

// Получить данные документа ЕГАИС
//
// Параметры:
//  МетаданныеДокумента - ОбъектМетаданных - Метаданные документа.
//  ДокументыПоОснованию - Структура - структура со свойствами:
//   * ВозвратИзРегистра2ЕГАИС - Структура со свойствами:
//     ** Ссылка - ДокументСсылка - Документ.
//     ** Статус - ПеречислениеСсылка.СтатусыОбработкиВозвратаИзРегистра2ЕГАИС - Статус документа.
//   * ПередачаВРегистр2ЕГАИС - Структура со свойствами:
//     ** Ссылка - ДокументСсылка - Документ.
//     ** Статус - ПеречислениеСсылка.СтатусыОбработкиПередачиВРегистр2ЕГАИС - Статус документа.
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * Представление - Строка - Представление документа.
//   * МассивДокументов - Массив - Массив документов.
//   * МетаданныеДокумента - ОбъектМетаданных - Метаданные документа.
//
Функция ДанныеДокументаЕГАИС(МетаданныеДокумента, ДокументыПоОснованию, СтатусыОформления) Экспорт
	
	ПравоДобавления = ПравоДоступа("Добавление", МетаданныеДокумента);
	ПравоЧтения     = ПравоДоступа("Чтение",     МетаданныеДокумента);
	
	Если Не ПравоЧтения Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ФорматированныеСтроки = Новый Массив;
	
	Представления = ПредставлениеДокумента(МетаданныеДокумента);
	
	МассивДокументов = ДокументыПоОснованию[МетаданныеДокумента.Имя];
	
	ЕстьСтатусОформления = СтатусыОформления.Свойство(МетаданныеДокумента.Имя);
	Если ЕстьСтатусОформления Тогда
		СтатусОформления = СтатусыОформления[МетаданныеДокумента.Имя];
	Иначе
		СтатусОформления = Перечисления.СтатусыОформленияДокументовЕГАИС.ПустаяСсылка();
	КонецЕсли;
	
	Если МассивДокументов.Количество() > 0 Тогда
		
		МассивДокументов = ДокументыПоОснованию[МетаданныеДокумента.Имя];
		Если МассивДокументов.Количество() = 1 Тогда
			
			ДанныеДокумента = МассивДокументов[0];
			
			ТекстНадписи = СтрШаблон(Представления.Представление, ДанныеДокумента.Статус);
			
			Если СтатусОформления = Перечисления.СтатусыОформленияДокументовЕГАИС.ОформленоЧастично Тогда
				ИмяКоманды = "ОткрытьПротоколОбмена";
			ИначеЕсли СтатусОформления = Перечисления.СтатусыОформленияДокументовЕГАИС.НеОформлено Тогда
				ИмяКоманды = "ОткрытьПротоколОбмена";
			ИначеЕсли СтатусОформления = Перечисления.СтатусыОформленияДокументовЕГАИС.ЕстьОшибкиОформления Тогда
				ИмяКоманды = "ОткрытьПротоколОбмена";
			Иначе
				ИмяКоманды = Представления.ИмяКомандыОткрыть;
			КонецЕсли;
			
		ИначеЕсли МассивДокументов.Количество() > 1 Тогда
			
			ТекстНадписи = СтрШаблон(Представления.НесколькоДокументовПредставление, МассивДокументов.Количество());
			ИмяКоманды   = "ОткрытьПротоколОбмена";
			
		КонецЕсли;
		
		Если ТекстНадписи = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Если СтатусОформления <> Перечисления.СтатусыОформленияДокументовЕГАИС.Оформлено Тогда
			
			ТекстНадписи = ТекстНадписи + ", " + СтатусОформления;
			
		КонецЕсли;
		
		Если ИмяКоманды = Неопределено Тогда
			Цвет = Неопределено;
		Иначе
			Цвет = ЦветаСтиля.ЦветГиперссылкиГИСМ;
		КонецЕсли;
		
		ФорматированныеСтроки.Добавить(
			Новый ФорматированнаяСтрока(
				ТекстНадписи,,
				Цвет,,
				ИмяКоманды));
		
	Иначе
		
		Если СтатусОформления <> Перечисления.СтатусыОформленияДокументовЕГАИС.Оформлено Тогда
			
			Если ПравоДобавления Тогда
				ТекстНадписи = Представления.КомандаСоздать;
				ИмяКоманды   = Представления.ИмяКомандыСоздать;
			Иначе
				ТекстНадписи = Представления.ДокументОтсутствуетНетПравНаСоздание;
				ИмяКоманды   = Неопределено;
			КонецЕсли;
			
			Если ТекстНадписи = Неопределено Тогда
				Возврат Неопределено;
			КонецЕсли;
			
			Если ИмяКоманды = Неопределено Тогда
				Цвет = Неопределено;
			Иначе
				Цвет = ЦветаСтиля.ЦветГиперссылкиГИСМ;
			КонецЕсли;
			
			ФорматированныеСтроки.Добавить(
				Новый ФорматированнаяСтрока(
					ТекстНадписи,,
					Цвет,,
					ИмяКоманды));
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("Представление",       Новый ФорматированнаяСтрока(ФорматированныеСтроки));
	ВозвращаемоеЗначение.Вставить("МассивДокументов",    МассивДокументов);
	ВозвращаемоеЗначение.Вставить("МетаданныеДокумента", МетаданныеДокумента);
	ВозвращаемоеЗначение.Вставить("СтатусОформления",    СтатусОформления);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область Проведение

// Определяет необходимость подготовить таблицу для формирования движений
//
// Параметры:
//  ИмяРегистра	- Строка - имя регистра. Например "ТоварыНаСкладах"
//  Регистры	- Строка, Структура, Неопределено - список регистров, разделенных запятой, или структура, в ключах которой - имена регистров
//													Если неопределено - то всегда возвращается ИСТИНА
// 
// Возвращаемое значение:
//   - Булево - Истина, если требуется инициализировать указанную таблицу
//
Функция ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Экспорт

	Если ЗначениеЗаполнено(Регистры) Тогда
		
		Если ТипЗнч(Регистры) = Тип("Строка") Тогда
			МассивРегистров = Новый Структура(Регистры);
		Иначе
			МассивРегистров = Регистры;
		КонецЕсли;
		
		Если НЕ МассивРегистров.Свойство(ИмяРегистра) Тогда
			Возврат Ложь;
		КонецЕсли; 
		
	КонецЕсли; 
	
	Возврат Истина;

КонецФункции

// Проверяет наличие текста запроса для формирования указанной таблицы
//
// Параметры:
//  ИмяТаблицы		- Строка - имя таблицы
//	ТекстыЗапроса 	- Список значений - список значений, значениями которого являются блоки запроса,
//	                                  синонимами - имена таблиц в которые необходимо поместить
//	                                  результат выполнения каждого отдельного блока запроса.
// 
// Возвращаемое значение:
//   - Булево - Истина, если текст запроса есть.
//
Функция ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Экспорт

	Для каждого ТекстЗапроса Из ТекстыЗапроса Цикл
		Если НРег(ТекстЗапроса.Представление) = НРег(ИмяТаблицы) Тогда
			Возврат Истина;
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат Ложь;

КонецФункции

// Процедура инициализирует общие структуры, используемые при проведении документов.
// Вызывается из модуля документов при проведении.
//
Процедура ИнициализироватьДополнительныеСвойстваДляПроведения(ДокументСсылка, ДополнительныеСвойства, РежимПроведения = Неопределено) Экспорт

	// В структуре "ДополнительныеСвойства" создаются свойства с ключами "ТаблицыДляДвижений", "ДляПроведения".

	// "ТаблицыДляДвижений" - структура, которая будет содержать таблицы значений с данными для выполнения движений.
	ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", Новый Структура);

	// "ДляПроведения" - структура, содержащая свойства и реквизиты документа, необходимые для проведения.
	ДополнительныеСвойства.Вставить("ДляПроведения", Новый Структура);
	
	// Структура, содержащая ключ с именем "МенеджерВременныхТаблиц", в значении которого хранится менеджер временных таблиц.
	// Содержит для каждой временной таблицы ключ (имя временной таблицы) и значение (признак наличия записей во временной таблице).
	ДополнительныеСвойства.ДляПроведения.Вставить("СтруктураВременныеТаблицы", Новый Структура("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц));
	ДополнительныеСвойства.ДляПроведения.Вставить("РежимПроведения",           РежимПроведения);
	ДополнительныеСвойства.ДляПроведения.Вставить("МетаданныеДокумента",       ДокументСсылка.Метаданные());
	ДополнительныеСвойства.ДляПроведения.Вставить("Ссылка",                    ДокументСсылка);
	
КонецПроцедуры

// Процедура компонует текст запроса, выполняет запрос и выгружает результаты запроса в таблицы
//
// Параметры:
//	Запрос				- Запрос - запрос, параметры которого предварительно установлены.
//	ТекстыЗапроса		- Список значений - в списке перечислены тексты запросов и их имена.
//	Таблицы				- Структура - структура в которую будут помещены полученные таблицы для движений.
//	ДобавитьРазделитель	- Булево - Истина, если нужно добавить разделитель ";" между запросами.
//	ДобавлятьСловоТаблица	- Булево - Истина, если к имени таблицы движений нужно в начало добавить слово "Таблица".
//
Процедура ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, Таблицы, ДобавитьРазделитель = Ложь, ДобавлятьСловоТаблица = Истина, ТолькоОтмеченные=Ложь) Экспорт
	
	ТаблицыЗапроса = ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса,, ДобавитьРазделитель);
	
	// Помещение результатов запроса в таблицы
	Для Каждого ТекстЗапроса из ТекстыЗапроса Цикл

		ИмяТаблицы = ТекстЗапроса.Представление;

		Если Не ПустаяСтрока(ИмяТаблицы) И (Не ТолькоОтмеченные Или ТекстЗапроса.Пометка) Тогда

			Если ДобавлятьСловоТаблица Тогда
				// Таблицы для проведения должны начинаться с "Таблица"
				Если НЕ СтрНачинаетсяС(ИмяТаблицы, "Таблица") Тогда
					ИмяТаблицы = "Таблица" + ИмяТаблицы;
				КонецЕсли;
			КонецЕсли;
			
			Таблицы.Вставить(ИмяТаблицы, ТаблицыЗапроса[ТекстЗапроса.Представление]);

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

// Формирует пакет запросов и возвращает результат каждого запроса
//
// Параметры:
//	Запрос			- Запрос - запрос, параметры которого предварительно установлены.
//	ТекстыЗапроса	- Список значений - в списке перечислены тексты запросов и их имена.
//	ОбходРезультата - ОбходРезультатаЗапроса - вариант обхода результата запроса.
//
// Возвращаемое значение:
//   Структура   - структура в которую помещены полученные таблицы
//
Функция ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса, ОбходРезультата = Неопределено, ДобавитьРазделитель = Ложь)

	Таблицы = Новый Структура;
	
	// Инициализация варианта обхода результата запроса.
	Если ОбходРезультата = Неопределено Тогда
		ОбходРезультата = ОбходРезультатаЗапроса.Прямой;
	КонецЕсли;
	
	МассивТекстаЗапроса = Новый Массив;
	
	// Формирование текст запроса.
	Для Каждого ТекстЗапроса из ТекстыЗапроса Цикл
		Если ЗначениеЗаполнено(ТекстЗапроса.Представление) Тогда
			МассивТекстаЗапроса.Добавить("// " + ТекстЗапроса.Представление);
		КонецЕсли; 
		МассивТекстаЗапроса.Добавить(ТекстЗапроса.Значение);
		Если ДобавитьРазделитель Тогда
			МассивТекстаЗапроса.Добавить("
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|");
		КонецЕсли;
	КонецЦикла;
	
	Запрос.Текст = СтрСоединить(МассивТекстаЗапроса, Символы.ПС);
	
	// Выполнение запроса.
	Результат = Запрос.ВыполнитьПакет();

	// Помещение результатов запроса в таблицы
	Для Каждого ТекстЗапроса из ТекстыЗапроса Цикл

		ИмяТаблицы = ТекстЗапроса.Представление;

		Если Не ПустаяСтрока(ИмяТаблицы) Тогда

			Индекс = ТекстыЗапроса.Индекс(ТекстЗапроса);
			Таблицы.Вставить(ИмяТаблицы, Результат[Индекс].Выгрузить(ОбходРезультата));

		КонецЕсли;

	КонецЦикла;

	Возврат Таблицы;
	
КонецФункции

// Процедура выполняет подготовку наборов записей документа к записи движений.
// 1. Очищает наборы записей от "старых записей" (ситуация возможна только в толстом клиенте)
// 2. Взводит флаг записи у наборов, по которым документ имеет движения
// Вызывается из модуля документов при проведении.
//
Процедура ПодготовитьНаборыЗаписейКРегистрацииДвижений(Объект, ЭтоНовый = Ложь) Экспорт
	Перем ЭтоНовыйДокумент, МетаданныеДвижения;
	
	Для Каждого НаборЗаписей Из Объект.Движения Цикл

		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей.Очистить();
		КонецЕсли;

	КонецЦикла;
	
	Если НЕ Объект.ДополнительныеСвойства.Свойство("ЭтоНовый", ЭтоНовыйДокумент) Тогда
		ЭтоНовыйДокумент = ЭтоНовый;
	КонецЕсли;
	
	Если НЕ ЭтоНовыйДокумент Тогда

		Если Объект.ДополнительныеСвойства.Свойство("ДляПроведения")
		 И Объект.ДополнительныеСвойства.ДляПроведения.Свойство("МетаданныеДокумента") Тогда
			МетаданныеДвижения = Объект.ДополнительныеСвойства.ДляПроведения.МетаданныеДокумента.Движения;
		Иначе
			МетаданныеДвижения = Объект.Метаданные().Движения;
		КонецЕсли;
		
		МассивИменРегистров = ИспользуемыеРегистры(Объект.Ссылка, МетаданныеДвижения);

		Для Каждого ИмяРегистра Из МассивИменРегистров Цикл
			Объект.Движения[ИмяРегистра].Записывать = Истина;
		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

// Функция формирует массив имен регистров, по которым документ имеет движения.
// Вызывается при подготовке записей к регистрации движений.
//
Функция ИспользуемыеРегистры(Регистратор, Движения, МассивИсключаемыхРегистров = Неопределено)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);

	Результат = Новый Массив;
	МаксимумТаблицВЗапросе = 256;

	СчетчикТаблиц   = 0;
	СчетчикДвижений = 0;

	ВсегоДвижений = Движения.Количество();
	МассивТекстаЗапроса = Новый Массив;
	Для Каждого Движение Из Движения Цикл

		СчетчикДвижений = СчетчикДвижений + 1;

		ПропуститьРегистр = МассивИсключаемыхРегистров <> Неопределено
							И МассивИсключаемыхРегистров.Найти(Движение.Имя) <> Неопределено;

		Если Не ПропуститьРегистр Тогда

			Если СчетчикТаблиц > 0 Тогда

				МассивТекстаЗапроса.Добавить("
				|ОБЪЕДИНИТЬ ВСЕ
				|");

			КонецЕсли;

			СчетчикТаблиц = СчетчикТаблиц + 1;

			МассивТекстаЗапроса.Добавить(
			"
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|""" + Движение.Имя + """ КАК ИмяРегистра
			|
			|ИЗ " + Движение.ПолноеИмя() + "
			|
			|ГДЕ Регистратор = &Регистратор
			|");

		КонецЕсли;

		Если СчетчикТаблиц = МаксимумТаблицВЗапросе Или СчетчикДвижений = ВсегоДвижений Тогда

			Запрос.Текст = СтрСоединить(МассивТекстаЗапроса, Символы.ПС);
			МассивТекстаЗапроса.Очистить();
			
			СчетчикТаблиц = 0;

			Если Результат.Количество() = 0 Тогда

				Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИмяРегистра");

			Иначе

				Выборка = Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					Результат.Добавить(Выборка.ИмяРегистра);
				КонецЦикла;

			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Процедура записывает движения документа. Дополнительно происходит копирование параметров
// в модули наборов записей для выполнения регистрации изменений в движениях.
// Процедура вызывается из модуля документов при проведении.
//
Процедура ЗаписатьНаборыЗаписей(Объект) Экспорт
	Перем РегистрыДляКонтроля, РассчитыватьИзменения, ПараметрыКонтроля;
	
	Для Каждого Движение Из Объект.Движения Цикл
		
		Движение.ДополнительныеСвойства.Вставить("ЭтоНовый", Объект.ДополнительныеСвойства.ЭтоНовый);
		Движение.ДополнительныеСвойства.Вставить("РежимЗаписи", Объект.ДополнительныеСвойства.РежимЗаписи);
		Движение.ДополнительныеСвойства.Вставить("ДатаРегистратора", Объект.Дата);
		Движение.ДополнительныеСвойства.Вставить("СтруктураОтраженияРеглПроводок", Новый Структура);
		
		Движение.ДополнительныеСвойства.Вставить("ДляПроведения", 
			Новый Структура("СтруктураВременныеТаблицы",
				// Структура для передачи данных в модули наборов записей.
				Объект.ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы));
		
	КонецЦикла;
	
	// Регистры, для которых будут рассчитаны таблицы изменений движений.
	Если Объект.ДополнительныеСвойства.ДляПроведения.Свойство("РегистрыДляКонтроля", РегистрыДляКонтроля) Тогда
		
		// Установка флага регистрации изменений в наборе записей.
		Если НЕ Объект.ДополнительныеСвойства.Свойство("РассчитыватьИзменения", РассчитыватьИзменения) Тогда
			РассчитыватьИзменения = Истина;
		КонецЕсли;
		
		Для Каждого НаборЗаписей Из РегистрыДляКонтроля Цикл
			Если НаборЗаписей.Записывать Тогда
				
				НаборЗаписей.ДополнительныеСвойства.Вставить("РассчитыватьИзменения", РассчитыватьИзменения);
				
			КонецЕсли;
		КонецЦикла;

	КонецЕсли;
	
	Объект.Движения.Записать();
	
КонецПроцедуры

// Выполняет закрытие менеджера временных таблиц в структуре дополнительных свойств документа, используемых 
// при проведении.
//
// Параметры:
//	ДополнительныеСвойства - Структура - структура с дополнительными свойствами документа, используемыми
//		при проведении.
//
Процедура ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства) Экспорт

	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();

КонецПроцедуры

#КонецОбласти

#Область ВыгрузкаДанных

// Функция возвращает объект XDTO, соответствующий переданной организации.
//
Процедура ЗаполнитьВXDTOОрганизацию_v1(ОбъектXDTOРодитель, ИмяПоля, Источник, Префикс = "", СообщениеXML) Экспорт
	
	ЕстьДанныеИсточникаДляЗаполнения = ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(Источник, Префикс + "Код");
	Если Не ЕстьДанныеИсточникаДляЗаполнения Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектXDTO = ИнтеграцияЕГАИС.ОбъектXDTOПоИмениТипа(ОбъектXDTOРодитель, ИмяПоля);
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Ссылка");
	Реквизиты.Вставить("Код");
	Реквизиты.Вставить("Наименование");
	Реквизиты.Вставить("НаименованиеПолное");
	Реквизиты.Вставить("ИНН");
	Реквизиты.Вставить("КПП");
	Реквизиты.Вставить("КодСтраны");
	Реквизиты.Вставить("КодРегиона");
	Реквизиты.Вставить("ПочтовыйИндекс");
	Реквизиты.Вставить("ПредставлениеАдреса");
	
	Для Каждого КлючИЗначение Из Реквизиты Цикл
		ИмяРеквизитаИсточника = Префикс + КлючИЗначение.Ключ;
		Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(Источник, ИмяРеквизитаИсточника) Тогда
			Реквизиты[КлючИЗначение.Ключ] = Источник[ИмяРеквизитаИсточника];
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Реквизиты.Ссылка) Тогда
		Identity = Строка(Реквизиты.Ссылка.УникальныйИдентификатор());
	Иначе
		Identity = Неопределено;
	КонецЕсли;
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "Identity",    Identity,                           СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "ClientRegId", Реквизиты.Код,                      СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "FullName",    Реквизиты.НаименованиеПолное,       СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "ShortName",   Реквизиты.Наименование,             СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "INN",         Реквизиты.ИНН,                      СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "KPP",         Реквизиты.КПП,                      СообщениеXML);
	
	ОбъектXDTO.address = ИнтеграцияЕГАИС.ОбъектXDTOПоИмениТипа(ОбъектXDTO, "address");
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO.address, "Country",     Формат(Реквизиты.КодСтраны, "ЧЦ=3; ЧН=; ЧВН="),      СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO.address, "Index",       Формат(Реквизиты.ПочтовыйИндекс, "ЧЦ=6; ЧВН=; ЧГ="), СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO.address, "RegionCode",  Формат(Реквизиты.КодРегиона, "ЧЦ=2; ЧВН="),          СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO.address, "description", Реквизиты.ПредставлениеАдреса,                       СообщениеXML);
	
	ОбъектXDTOРодитель[ИмяПоля] = ОбъектXDTO;
	
КонецПроцедуры

// Функция возвращает объект XDTO, соответствующий переданной организации.
//
Процедура ЗаполнитьВXDTOОрганизацию_v2(ОбъектXDTOРодитель, ИмяПоля, Источник, Префикс = "", СообщениеXML) Экспорт
	
	ЕстьДанныеИсточникаДляЗаполнения = ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(Источник, Префикс + "Код");
	Если Не ЕстьДанныеИсточникаДляЗаполнения Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектXDTO = ИнтеграцияЕГАИС.ОбъектXDTOПоИмениТипа(ОбъектXDTOРодитель, ИмяПоля);
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Код");
	Реквизиты.Вставить("Наименование");
	Реквизиты.Вставить("НаименованиеПолное");
	Реквизиты.Вставить("ИНН");
	Реквизиты.Вставить("КПП");
	Реквизиты.Вставить("КодСтраны");
	Реквизиты.Вставить("КодРегиона");
	Реквизиты.Вставить("ПредставлениеАдреса");
	Реквизиты.Вставить("ТипОрганизации");
	Реквизиты.Вставить("ИдентификаторОрганизацииТС");
	
	Для Каждого КлючИЗначение Из Реквизиты Цикл
		ИмяРеквизитаИсточника = Префикс + КлючИЗначение.Ключ;
		Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(Источник, ИмяРеквизитаИсточника) Тогда
			Реквизиты[КлючИЗначение.Ключ] = Источник[ИмяРеквизитаИсточника];
		КонецЕсли;
	КонецЦикла;
	
	Если Реквизиты.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ЮридическоеЛицоРФ Тогда
		ОрганизацияXDTO = ИнтеграцияЕГАИС.ОбъектXDTOПоИмениТипа(ОбъектXDTO, "UL");
	ИначеЕсли Реквизиты.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ИндивидуальныйПредпринимательРФ Тогда
		ОрганизацияXDTO = ИнтеграцияЕГАИС.ОбъектXDTOПоИмениТипа(ОбъектXDTO, "FL");
	ИначеЕсли Реквизиты.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ИностранныйКонтрагент Тогда
		ОрганизацияXDTO = ИнтеграцияЕГАИС.ОбъектXDTOПоИмениТипа(ОбъектXDTO, "FO");
	ИначеЕсли Реквизиты.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.КонтрагентТаможенногоСоюза Тогда
		ОрганизацияXDTO = ИнтеграцияЕГАИС.ОбъектXDTOПоИмениТипа(ОбъектXDTO, "TS");
	КонецЕсли;
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОрганизацияXDTO, "ClientRegId", Реквизиты.Код,                        СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОрганизацияXDTO, "FullName",    Реквизиты.НаименованиеПолное,         СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОрганизацияXDTO, "ShortName",   Реквизиты.Наименование,               СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОрганизацияXDTO, "INN",         Реквизиты.ИНН,                        СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОрганизацияXDTO, "KPP",         Реквизиты.КПП,                        СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОрганизацияXDTO, "TSNUM",       Реквизиты.ИдентификаторОрганизацииТС, СообщениеXML);
	
	ОрганизацияXDTO.address = ИнтеграцияЕГАИС.ОбъектXDTOПоИмениТипа(ОрганизацияXDTO, "address");
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОрганизацияXDTO.address, "Country",     Формат(Реквизиты.КодСтраны, "ЧЦ=3; ЧН=; ЧВН="), СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОрганизацияXDTO.address, "RegionCode",  Формат(Реквизиты.КодРегиона, "ЧЦ=2; ЧВН="),     СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОрганизацияXDTO.address, "description", Реквизиты.ПредставлениеАдреса,                  СообщениеXML);
	
	Если Реквизиты.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ЮридическоеЛицоРФ Тогда
		ОбъектXDTO.UL = ОрганизацияXDTO;
	ИначеЕсли Реквизиты.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ИндивидуальныйПредпринимательРФ Тогда
		ОбъектXDTO.FL = ОрганизацияXDTO;
	ИначеЕсли Реквизиты.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ИностранныйКонтрагент Тогда
		ОбъектXDTO.FO = ОрганизацияXDTO;
	ИначеЕсли Реквизиты.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.КонтрагентТаможенногоСоюза Тогда
		ОбъектXDTO.TS = ОрганизацияXDTO;
	КонецЕсли;
	
	ОбъектXDTOРодитель[ИмяПоля] = ОбъектXDTO;
	
КонецПроцедуры

// Функция возвращает объект XDTO, соответствующий переданной алкогольной продукции.
//
Процедура ЗаполнитьВXDTOАлкогольнуюПродукцию_v1(ОбъектXDTO, Источник, Префикс = "", СообщениеXML) Экспорт
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Ссылка");
	Реквизиты.Вставить("Код");
	Реквизиты.Вставить("Наименование");
	Реквизиты.Вставить("НаименованиеПолное");
	Реквизиты.Вставить("Объем");
	Реквизиты.Вставить("Крепость");
	Реквизиты.Вставить("КодВидаПродукции");
	Реквизиты.Вставить("ТипПродукции");
	Реквизиты.Вставить("ВидЛицензии");
	
	Для Каждого КлючИЗначение Из Реквизиты Цикл
		ИмяРеквизитаИсточника = Префикс + КлючИЗначение.Ключ;
		Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(Источник, ИмяРеквизитаИсточника) Тогда
			Реквизиты[КлючИЗначение.Ключ] = Источник[ИмяРеквизитаИсточника];
		КонецЕсли;
	КонецЦикла;
	
	ТипыАП = Новый Соответствие;
	ТипыАП.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.АлкогольнаяПродукция, "АП");
	ТипыАП.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.Пиво, "АП");
	ТипыАП.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.Спирт, "Спирт");
	ТипыАП.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.СпиртосодержащаяНеПищеваяПродукция, "ССНП");
	ТипыАП.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.СпиртосодержащаяПищеваяПродукция, "ССП");
	
	Если ЗначениеЗаполнено(Реквизиты.Ссылка) Тогда
		Identity = Строка(Реквизиты.Ссылка.УникальныйИдентификатор());
	Иначе
		Identity = Неопределено;
	КонецЕсли;
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "Identity",  Identity,                      СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "Type",      ТипыАП[Реквизиты.ВидЛицензии], СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "FullName",  Реквизиты.НаименованиеПолное,  СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "ShortName", Реквизиты.Наименование,        СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "AlcCode",   Реквизиты.Код,                 СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "Capacity",  Реквизиты.Объем,               СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "AlcVolume", Реквизиты.Крепость,            СообщениеXML);
	
	ИнтеграцияЕГАИС.ЗаполнитьВXDTOОрганизацию_v1(ОбъектXDTO, "Producer", Источник, "Производитель", СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьВXDTOОрганизацию_v1(ОбъектXDTO, "Importer", Источник, "Импортер",      СообщениеXML);
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "ProductVCode", Реквизиты.КодВидаПродукции, СообщениеXML);
	
КонецПроцедуры

// Функция возвращает объект XDTO, соответствующий переданной алкогольной продукции.
//
Процедура ЗаполнитьВXDTOАлкогольнуюПродукцию_v2(ОбъектXDTO, Источник, Префикс = "", СообщениеXML) Экспорт
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Код");
	Реквизиты.Вставить("Наименование");
	Реквизиты.Вставить("НаименованиеПолное");
	Реквизиты.Вставить("Объем");
	Реквизиты.Вставить("Крепость");
	Реквизиты.Вставить("КодВидаПродукции");
	Реквизиты.Вставить("ТипПродукции");
	Реквизиты.Вставить("ВидЛицензии");
	
	Для Каждого КлючИЗначение Из Реквизиты Цикл
		ИмяРеквизитаИсточника = Префикс + КлючИЗначение.Ключ;
		Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(Источник, ИмяРеквизитаИсточника) Тогда
			Реквизиты[КлючИЗначение.Ключ] = Источник[ИмяРеквизитаИсточника];
		КонецЕсли;
	КонецЦикла;
	
	ТипыАП = Новый Соответствие;
	ТипыАП.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.АлкогольнаяПродукция, "АП");
	ТипыАП.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.Пиво, "АП");
	ТипыАП.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.Спирт, "Спирт");
	ТипыАП.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.СпиртосодержащаяНеПищеваяПродукция, "ССНП");
	ТипыАП.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.СпиртосодержащаяПищеваяПродукция, "ССП");
	
	ТипыПродукции = Новый Соответствие;
	ТипыПродукции.Вставить(Перечисления.ТипыПродукцииЕГАИС.ПустаяСсылка(), "Packed");
	ТипыПродукции.Вставить(Перечисления.ТипыПродукцииЕГАИС.Упакованная, "Packed");
	ТипыПродукции.Вставить(Перечисления.ТипыПродукцииЕГАИС.Неупакованная, "Unpacked");
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "UnitType",  ТипыПродукции[Реквизиты.ТипПродукции], СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "Type",      ТипыАП[Реквизиты.ВидЛицензии],         СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "FullName",  Реквизиты.НаименованиеПолное,          СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "ShortName", Реквизиты.Наименование,                СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "AlcCode",   Реквизиты.Код,                         СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "Capacity",  Реквизиты.Объем,                       СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "AlcVolume", Реквизиты.Крепость,                    СообщениеXML);
	
	ИнтеграцияЕГАИС.ЗаполнитьВXDTOОрганизацию_v2(ОбъектXDTO, "Producer", Источник, "Производитель", СообщениеXML);
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO, "ProductVCode", Реквизиты.КодВидаПродукции, СообщениеXML);
	
КонецПроцедуры

// Функция возвращает объект XDTO, соответствующий транспортному разделу ТТН.
//
Процедура ЗаполнитьВXDTOТранспортныйРазделТТН(ДоставкаXDTO, Источник, СообщениеXML) Экспорт
	
	ПоляДоставки = Новый Соответствие;
	ПоляДоставки.Вставить("TRAN_TYPE",        "ТипДоставки");
	ПоляДоставки.Вставить("TRAN_COMPANY",     "Перевозчик");
	ПоляДоставки.Вставить("TRAN_CAR",         "Автомобиль");
	ПоляДоставки.Вставить("TRAN_TRAILER",     "Прицеп");
	ПоляДоставки.Вставить("TRAN_CUSTOMER",    "Заказчик");
	ПоляДоставки.Вставить("TRAN_DRIVER",      "Водитель");
	ПоляДоставки.Вставить("TRAN_LOADPOINT",   "ПунктПогрузки");
	ПоляДоставки.Вставить("TRAN_UNLOADPOINT", "ПунктРазгрузки");
	ПоляДоставки.Вставить("TRAN_REDIRECT",    "Перенаправление");
	ПоляДоставки.Вставить("TRAN_FORWARDER",   "Экспедитор");
	
	Для Каждого СвойствоДоставкиXDTO Из ДоставкаXDTO.Свойства() Цикл
		ИмяПоляДанных = ПоляДоставки[СвойствоДоставкиXDTO.Имя];
		Если НЕ ИмяПоляДанных = Неопределено И НЕ Источник[ИмяПоляДанных] = Неопределено Тогда
			МаксДлина = 0;
			Для Каждого Фасет Из СвойствоДоставкиXDTO.Тип.Фасеты Цикл
				Если Фасет.Вид = ВидФасетаXDTO.МаксДлина Тогда
					МаксДлина = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(Фасет.Значение);
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если МаксДлина > 0 Тогда
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ДоставкаXDTO, СвойствоДоставкиXDTO.Имя, Лев(Источник[ИмяПоляДанных], МаксДлина), СообщениеXML);
			Иначе
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ДоставкаXDTO, СвойствоДоставкиXDTO.Имя, Источник[ИмяПоляДанных], СообщениеXML);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Функция возвращает объект XDTO, соответствующий переданным акцизным маркам.
//
Процедура ЗаполнитьВXDTOАкцизныеМаркиПоОтбору(ОбъектXDTO, ПараметрыОтбора, ИсточникАкцизныеМарки, ИмяПоля, СообщениеXML) Экспорт
	
	КодыАкцизныхМарок = Новый Массив;
	НайденныеСтроки = ИсточникАкцизныеМарки.НайтиСтроки(ПараметрыОтбора);
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		КодыАкцизныхМарок.Добавить(НайденнаяСтрока.КодАкцизнойМарки);
	КонецЦикла;
	
	Если КодыАкцизныхМарок.Количество() > 0 Тогда
		
		ОбъектXDTO.MarkCodeInfo = ИнтеграцияЕГАИС.ОбъектXDTOПоИмениТипа(ОбъектXDTO, "MarkCodeInfo");
		
		Для Каждого КодАкцизнойМарки Из КодыАкцизныхМарок Цикл
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO.MarkCodeInfo, ИмяПоля, КодАкцизнойМарки, СообщениеXML);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает объект XDTO, соответствующий переданным акцизным маркам.
//
Процедура ЗаполнитьВXDTOАкцизныеМаркиПоСправке2_v2(ОбъектXDTO, Источник, ИсточникАкцизныеМарки, ИмяПоля, СообщениеXML) Экспорт
	
	КодыАкцизныхМарок = Новый Массив;
	НайденныеСтроки = ИсточникАкцизныеМарки.НайтиСтроки(Новый Структура("Справка2", Источник.Справка2));
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		КодыАкцизныхМарок.Добавить(НайденнаяСтрока.КодАкцизнойМарки);
	КонецЦикла;
	
	Если КодыАкцизныхМарок.Количество() > 0 Тогда
		
		ОбъектXDTO.MarkCodeInfo = ИнтеграцияЕГАИС.ОбъектXDTOПоИмениТипа(ОбъектXDTO, "MarkCodeInfo");
		
		Для Каждого КодАкцизнойМарки Из КодыАкцизныхМарок Цикл
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ОбъектXDTO.MarkCodeInfo, ИмяПоля, КодАкцизнойМарки, СообщениеXML);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает объект XDTO, соответствующий переданным акцизным маркам.
//
Процедура ЗаполнитьВXDTOАкцизныеМаркиПоСправке2_v3(ОбъектXDTO, Источник, ИсточникАкцизныеМарки, СообщениеXML) Экспорт
	
	КодыАкцизныхМарок = Новый Массив;
	НайденныеСтроки = ИсточникАкцизныеМарки.НайтиСтроки(Новый Структура("Справка2", Источник.Справка2));
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		КодыАкцизныхМарок.Добавить(НайденнаяСтрока.КодАкцизнойМарки);
	КонецЦикла;
	
	Если КодыАкцизныхМарок.Количество() > 0 Тогда
		
		MarkInfo = ИнтеграцияЕГАИС.ОбъектXDTOПоИмениТипа(ОбъектXDTO, "MarkInfo");
		
		Для Каждого КодАкцизнойМарки Из КодыАкцизныхМарок Цикл
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(MarkInfo, "amc", КодАкцизнойМарки, СообщениеXML);
		КонецЦикла;
		
		ОбъектXDTO.MarkInfo.Добавить(MarkInfo);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаДанных

Функция НайтиОбъектПоИдентификатору(МетаданныеОбъекта, ИмяРеквизита, Значение, ВидКвитанции = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ИмяРеквизита = "ИдентификаторЗапроса" Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПротоколОбмена.Документ            КАК Ссылка,
		|	ТаблицаДокументы.ДокументОснование КАК ДокументОснование,
		|	ПротоколОбмена.Операция            КАК Операция,
		|	ПротоколОбмена.Ссылка              КАК ИсходящееСообщение,
		|	ПротоколОбмена.ФорматОбмена        КАК ФорматОбмена
		|ИЗ
		|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ПротоколОбмена
		|		ЛЕВОЕ СОЕДИНЕНИЕ " + МетаданныеОбъекта.ПолноеИмя() + " КАК ТаблицаДокументы
		|		ПО ТаблицаДокументы.Ссылка = ПротоколОбмена.Документ
		|ГДЕ
		|	ПротоколОбмена.ИдентификаторЗапроса = &ИдентификаторЗапроса
		|	И ПротоколОбмена.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовЕГАИС.Исходящий)");
		
	ИначеЕсли ВидКвитанции = Неопределено Тогда
		
		Если МетаданныеОбъекта = Метаданные.Документы.ТТНВходящаяЕГАИС Тогда
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Таблица.Ссылка            КАК Ссылка,
			|	Таблица.ДокументОснование КАК ДокументОснование,
			|	Неопределено              КАК Операция,
			|	Неопределено              КАК ИсходящееСообщение,
			|	Таблица.ФорматОбмена      КАК ФорматОбмена
			|ИЗ
			|	Документ.ТТНВходящаяЕГАИС КАК Таблица
			|ГДЕ
			|	Таблица." + ИмяРеквизита + " = &Значение");
		Иначе
			Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Таблица.Ссылка                                      КАК Ссылка,
			|	Таблица.ДокументОснование                           КАК ДокументОснование,
			|	Неопределено                                        КАК Операция,
			|	Неопределено                                        КАК ИсходящееСообщение,
			|	ЕСТЬNULL(ПротоколОбмена.ФорматОбмена, НЕОПРЕДЕЛЕНО) КАК ФорматОбмена
			|ИЗ
			|	" + МетаданныеОбъекта.ПолноеИмя() + " КАК Таблица
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕГАИСПрисоединенныеФайлы КАК ПротоколОбмена
			|		ПО Таблица.Ссылка = ПротоколОбмена.Документ
			|ГДЕ
			|	Таблица." + ИмяРеквизита + " = &Значение
			|УПОРЯДОЧИТЬ ПО
			|	ПротоколОбмена.ДатаСоздания ВОЗР
			|");
		КонецЕсли;
		
	Иначе
		
		Если МетаданныеОбъекта = Метаданные.Документы.ТТНВходящаяЕГАИС Тогда
			
			Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Таблица.Ссылка                       КАК Ссылка,
			|	Таблица.ДокументОснование            КАК ДокументОснование,
			|	Таблица.ФорматОбмена                 КАК ФорматОбмена,
			|	ПротоколОбменаИсходящий.Ссылка       КАК ИсходящееСообщение,
			|	ПротоколОбменаИсходящий.Операция     КАК Операция,
			|	ПротоколОбменаИсходящий.ДатаСоздания КАК ДатаСоздания
			|ИЗ
			|	" + МетаданныеОбъекта.ПолноеИмя() + " КАК Таблица
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЕГАИСПрисоединенныеФайлы КАК ПротоколОбменаИсходящий
			|			ПО Таблица.Ссылка = ПротоколОбменаИсходящий.Документ
			|			И ПротоколОбменаИсходящий.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовЕГАИС.Исходящий)
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕГАИСПрисоединенныеФайлы КАК ПротоколОбменаВходящий
			|			ПО Таблица.Ссылка = ПротоколОбменаВходящий.Документ
			|			И ПротоколОбменаВходящий.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовЕГАИС.Входящий)
			|			И ПротоколОбменаВходящий.СообщениеОснование = ПротоколОбменаИсходящий.Ссылка
			|			И ПротоколОбменаВходящий.Операция = &ВидКвитанции
			|ГДЕ
			|	Таблица." + ИмяРеквизита + " = &Значение
			|	И ПротоколОбменаВходящий.Ссылка ЕСТЬ NULL
			|
			|УПОРЯДОЧИТЬ ПО
			|	ПротоколОбменаИсходящий.ДатаСоздания УБЫВ
			|");
			
		Иначе
			
			Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Таблица.Ссылка                       КАК Ссылка,
			|	Таблица.ДокументОснование            КАК ДокументОснование,
			|	ПротоколОбменаИсходящий.Ссылка       КАК ИсходящееСообщение,
			|	ПротоколОбменаИсходящий.Операция     КАК Операция,
			|	ПротоколОбменаИсходящий.ФорматОбмена КАК ФорматОбмена,
			|	ПротоколОбменаИсходящий.ДатаСоздания КАК ДатаСоздания
			|ИЗ
			|	" + МетаданныеОбъекта.ПолноеИмя() + " КАК Таблица
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЕГАИСПрисоединенныеФайлы КАК ПротоколОбменаИсходящий
			|			ПО Таблица.Ссылка = ПротоколОбменаИсходящий.Документ
			|			И ПротоколОбменаИсходящий.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовЕГАИС.Исходящий)
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕГАИСПрисоединенныеФайлы КАК ПротоколОбменаВходящий
			|			ПО Таблица.Ссылка = ПротоколОбменаВходящий.Документ
			|			И ПротоколОбменаВходящий.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовЕГАИС.Входящий)
			|			И ПротоколОбменаВходящий.СообщениеОснование = ПротоколОбменаИсходящий.Ссылка
			|			И ПротоколОбменаВходящий.Операция = &ВидКвитанции
			|ГДЕ
			|	Таблица." + ИмяРеквизита + " = &Значение
			|	И ПротоколОбменаВходящий.Ссылка ЕСТЬ NULL
			|
			|УПОРЯДОЧИТЬ ПО
			|	ПротоколОбменаИсходящий.ДатаСоздания УБЫВ
			|");
			
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ВидКвитанции", ВидКвитанции);
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Значение", Значение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ВозвращаемоеЗначение = Новый Структура;
		ВозвращаемоеЗначение.Вставить("Ссылка",             Выборка.Ссылка);
		ВозвращаемоеЗначение.Вставить("ДокументОснование",  Выборка.ДокументОснование);
		ВозвращаемоеЗначение.Вставить("ИсходящееСообщение", Выборка.ИсходящееСообщение);
		ВозвращаемоеЗначение.Вставить("Операция",           Выборка.Операция);
		ВозвращаемоеЗначение.Вставить("ФорматОбмена",       Выборка.ФорматОбмена);
		
		Возврат ВозвращаемоеЗначение;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает данные последнего исходящего запроса по идентификатору.
//
// Параметры:
//  ИдентификаторЗапроса - Строка - идентификатор исходящего запроса.
//
// Возвращаемое значение:
//   Структура - данные последнего исходящего запроса. Неопределено - если запрос не найден.
//
Функция НайтиОбъектПоИдентификаторуЗапроса(ИдентификаторЗапроса, ИскатьДокументОснование = Истина) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПротоколОбмена.Документ     КАК Ссылка,
	|	ПротоколОбмена.Операция     КАК Операция,
	|	ПротоколОбмена.Ссылка       КАК ИсходящееСообщение,
	|	ПротоколОбмена.ФорматОбмена КАК ФорматОбмена
	|ИЗ
	|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ПротоколОбмена
	|ГДЕ
	|	ПротоколОбмена.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовЕГАИС.Исходящий)
	|	И ПротоколОбмена.ИдентификаторЗапроса = &ИдентификаторЗапроса");
	
	Запрос.УстановитьПараметр("ИдентификаторЗапроса", ИдентификаторЗапроса);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ДокументОснование = Неопределено;
		Если ИскатьДокументОснование
			И ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			МетаданныеДокумента = Выборка.Ссылка.Метаданные();
			Если Метаданные.Документы.Содержит(МетаданныеДокумента)
				И МетаданныеДокумента.Реквизиты.Найти("ДокументОснование") <> Неопределено Тогда
				ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Ссылка, "ДокументОснование");
			КонецЕсли;
		КонецЕсли;
		
		ВозвращаемоеЗначение = Новый Структура;
		ВозвращаемоеЗначение.Вставить("Ссылка",             Выборка.Ссылка);
		ВозвращаемоеЗначение.Вставить("ДокументОснование",  ДокументОснование);
		ВозвращаемоеЗначение.Вставить("Операция",           Выборка.Операция);
		ВозвращаемоеЗначение.Вставить("ИсходящееСообщение", Выборка.ИсходящееСообщение);
		ВозвращаемоеЗначение.Вставить("ФорматОбмена",       Выборка.ФорматОбмена);
		
		Возврат ВозвращаемоеЗначение;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

// Возвращает вид документа и ссылку на документ-основание, выгруженного ранее в ЕГАИС
//
Функция НайтиОбъектПоИдентификаторуТипаЕГАИСПриЗагрузкеКвитанции(Идентификатор, ТипЕГАИС, ВидКвитанции)
	
	Операция = Перечисления.ВидыДокументовЕГАИС.ДанныеДокументаПоТипуЕГАИС(ТипЕГАИС).ВидДокументаЕГАИС;
	
	Если Не ЗначениеЗаполнено(Операция) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Операция = Перечисления.ВидыДокументовЕГАИС.ТТН Тогда
		
		РезультатПоиска = НайтиОбъектПоИдентификатору(
			Метаданные.Документы.ТТНИсходящаяЕГАИС,
			"Идентификатор",
			Идентификатор,
			ВидКвитанции);
		
		Если РезультатПоиска = Неопределено Тогда
			
			РезультатПоиска = НайтиОбъектПоИдентификатору(
				Метаданные.Документы.ТТНВходящаяЕГАИС,
				"Идентификатор",
				Идентификатор,
				ВидКвитанции);
			
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.АктТТН Тогда
		
		РезультатПоиска = НайтиОбъектПоИдентификатору(
			Метаданные.Документы.ТТНИсходящаяЕГАИС,
			"Идентификатор",
			Идентификатор,
			ВидКвитанции);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр1
		ИЛИ Операция = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр2 Тогда
		
		РезультатПоиска = НайтиОбъектПоИдентификатору(
			Метаданные.Документы.АктПостановкиНаБалансЕГАИС,
			"Идентификатор",
			Идентификатор,
			ВидКвитанции);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра1
		ИЛИ Операция = Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра2 Тогда
		
		РезультатПоиска = НайтиОбъектПоИдентификатору(
			Метаданные.Документы.АктСписанияЕГАИС,
			"Идентификатор",
			Идентификатор,
			ВидКвитанции);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.ВозвратИзРегистра2 Тогда
		
		РезультатПоиска = НайтиОбъектПоИдентификатору(
			Метаданные.Документы.ВозвратИзРегистра2ЕГАИС,
			"Идентификатор",
			Идентификатор,
			ВидКвитанции);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.ПередачаВРегистр2 Тогда
		
		РезультатПоиска = НайтиОбъектПоИдентификатору(
			Метаданные.Документы.ПередачаВРегистр2ЕГАИС,
			"Идентификатор",
			Идентификатор,
			ВидКвитанции);
		
	КонецЕсли;
	
	Возврат РезультатПоиска;
	
КонецФункции

Функция НайтиУведомлениеОРегистрацииСправок(ДанныеТТН, ДанныеДокументовРегистрацияСправок) Экспорт
	
	ШапкаТТН = ДанныеТТН.Объект.Header;
	
	Для Каждого ЭлементМассива Из ДанныеДокументовРегистрацияСправок Цикл
		
		ШапкаРегистрация = ЭлементМассива.Объект.Header;
		
		Если ШапкаРегистрация.Identity = ДанныеТТН.Объект.Identity
			И ШапкаРегистрация.WBNUMBER = ШапкаТТН.NUMBER
			И ШапкаРегистрация.WBDate = ШапкаТТН.Date
			И ДанныеОрганизации(ШапкаРегистрация.Shipper).ClientRegId = ДанныеОрганизации(ШапкаТТН.Shipper).ClientRegId
			И ДанныеОрганизации(ШапкаРегистрация.Consignee).ClientRegId = ДанныеОрганизации(ШапкаТТН.Consignee).ClientRegId Тогда
			
			Возврат ЭлементМассива;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция ИсходящееСообщение(ИдентификаторЗапроса) Экспорт
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗапроса) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПротоколОбмена.СообщениеОснование КАК Ссылка
	|ИЗ
	|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ПротоколОбмена
	|ГДЕ
	|	ПротоколОбмена.ИдентификаторЗапроса = &ИдентификаторЗапроса
	|	И ПротоколОбмена.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовЕГАИС.Исходящий)");
	
	Запрос.УстановитьПараметр("ИдентификаторЗапроса", ИдентификаторЗапроса);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Создает ТТН на основании переданных данных.
//
Функция ЗагрузитьТТНВходящуюЕГАИС(ДанныеДокументаТТН, ДанныеДокументаУведомлениеОРегистрации, ОрганизацияЕГАИС) Экспорт
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	Ссылка = Документы.ТТНВходящаяЕГАИС.ПолучитьСсылку();
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокументаТТН.Операция);
	Реквизиты.Вставить("Документ",             Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   Неопределено);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получена ТТН ЕГАИС (входящая)'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокументаТТН.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокументаТТН.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	РезультатДобавленияЗаписиТТН = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокументаТТН.ТекстXML,
		Реквизиты, Истина);
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокументаУведомлениеОРегистрации.Операция);
	Реквизиты.Вставить("Документ",             Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   Неопределено);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получено уведомление о регистрации движения ТТН ЕГАИС (входящая)'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокументаУведомлениеОРегистрации.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокументаУведомлениеОРегистрации.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	РезультатДобавленияЗаписиУведомлениеОРегистрации = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокументаУведомлениеОРегистрации.ТекстXML,
		Реквизиты, Истина);
	
	Если РезультатДобавленияЗаписиТТН.НовоеСообщение Тогда
	
		ДокументОбъект = Документы.ТТНВходящаяЕГАИС.СоздатьДокумент();
		ДокументОбъект.УстановитьСсылкуНового(Ссылка);
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		
		ДокументОбъект.Идентификатор      = Строка(ДанныеДокументаТТН.Объект.Identity);
		ДокументОбъект.ИдентификаторЕГАИС = Строка(ДанныеДокументаУведомлениеОРегистрации.Объект.Header.WBRegId);
		ДокументОбъект.ФорматОбмена       = ДанныеДокументаТТН.ФорматОбмена;
		
		Если СтрНайти(ВРег(ДанныеДокументаТТН.Объект.Header.Type), ВРег("Return")) > 0 Тогда
			ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийТТНВходящейЕГАИС.ВозвратОтПокупателя;
		Иначе
			ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийТТНВходящейЕГАИС.ПриходнаяНакладная;
		КонецЕсли;
		
		Если ДанныеДокументаТТН.ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
			ДокументОбъект.Упакована = (ДанныеДокументаТТН.Объект.Header.UnitType = "Packed");
		КонецЕсли;
		
		ДокументОбъект.НомерТТН         = ДанныеДокументаТТН.Объект.Header.NUMBER;
		ДокументОбъект.ДатаТТН          = ДанныеДокументаТТН.Объект.Header.Date;
		ДокументОбъект.ДатаОтгрузки     = ДанныеДокументаТТН.Объект.Header.ShippingDate;
		ДокументОбъект.Грузоотправитель = ЗагрузитьОрганизацию(ДанныеДокументаТТН.Объект.Header.Shipper, ДанныеДокументаТТН.ФорматОбмена);
		ДокументОбъект.Грузополучатель  = ЗагрузитьОрганизацию(ДанныеДокументаТТН.Объект.Header.Consignee);
		
		Если ДанныеДокументаТТН.ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1
			И ДанныеДокументаТТН.Объект.Header.Свойство("Supplier")
			И ДанныеДокументаТТН.Объект.Header.Supplier <> Неопределено Тогда
			ДокументОбъект.Поставщик = ЗагрузитьОрганизацию(ДанныеДокументаТТН.Объект.Header.Supplier);
		КонецЕсли;
		
		ДокументОбъект.Основание   = ДанныеДокументаТТН.Объект.Header.Base;
		ДокументОбъект.Комментарий = ДанныеДокументаТТН.Объект.Header.Note;
		
		Если ДанныеДокументаТТН.Объект.Header.Transport <> Неопределено Тогда
			ДокументОбъект.ТипДоставки     = ДанныеДокументаТТН.Объект.Header.Transport.TRAN_TYPE;
			ДокументОбъект.Перевозчик      = ДанныеДокументаТТН.Объект.Header.Transport.TRAN_COMPANY;
			ДокументОбъект.Автомобиль      = ДанныеДокументаТТН.Объект.Header.Transport.TRAN_CAR;
			ДокументОбъект.Прицеп          = ДанныеДокументаТТН.Объект.Header.Transport.TRAN_TRAILER;
			ДокументОбъект.Заказчик        = ДанныеДокументаТТН.Объект.Header.Transport.TRAN_CUSTOMER;
			ДокументОбъект.Водитель        = ДанныеДокументаТТН.Объект.Header.Transport.TRAN_DRIVER;
			ДокументОбъект.ПунктПогрузки   = ДанныеДокументаТТН.Объект.Header.Transport.TRAN_LOADPOINT;
			ДокументОбъект.ПунктРазгрузки  = ДанныеДокументаТТН.Объект.Header.Transport.TRAN_UNLOADPOINT;
			ДокументОбъект.Перенаправление = ДанныеДокументаТТН.Объект.Header.Transport.TRAN_REDIRECT;
			ДокументОбъект.Экспедитор      = ДанныеДокументаТТН.Объект.Header.Transport.TRAN_FORWARDER;
		КонецЕсли;
		
		СписокАлкогольнойПродукции = Новый Массив;
		Для Каждого ЭлементДанных Из ДанныеДокументаТТН.Объект.Content.Position Цикл
			Если СписокАлкогольнойПродукции.Найти(ЭлементДанных.Product) = Неопределено Тогда
				СписокАлкогольнойПродукции.Добавить(ЭлементДанных.Product);
			КонецЕсли;
		КонецЦикла;
		
		Товары         = Новый Соответствие;
		НомераСправки1 = Новый Соответствие;
		ДиапазоныМарок = Новый Соответствие;
		
		СоответствиеЗагруженнаяАлкогольнаяПродукция = ЗагрузитьАлкогольнуюПродукцию(СписокАлкогольнойПродукции);
		
		Для Каждого ЭлементДанных Из ДанныеДокументаТТН.Объект.Content.Position Цикл
			
			ДанныеАлкогольнойПродукции = СоответствиеЗагруженнаяАлкогольнаяПродукция[ЭлементДанных.Product.AlcCode];
			
			НоваяСтрока = ДокументОбъект.Товары.Добавить();
			НоваяСтрока.ИдентификаторУпаковки = ЭлементДанных.Pack_ID;
			НоваяСтрока.Количество            = ЭлементДанных.Quantity;
			НоваяСтрока.КоличествоФакт        = 0;
			НоваяСтрока.Цена                  = ЭлементДанных.Price;
			НоваяСтрока.НомерПартии           = ЭлементДанных.Party;
			НоваяСтрока.ИдентификаторСтроки   = ЭлементДанных.Identity;
			
			Если ДанныеАлкогольнойПродукции <> Неопределено Тогда
				НоваяСтрока.АлкогольнаяПродукция = ДанныеАлкогольнойПродукции.АлкогольнаяПродукция;
				
				СтрокиСопоставления = ДанныеАлкогольнойПродукции.ТаблицаСопоставления[НоваяСтрока.ИдентификаторУпаковки];
				Если СтрокиСопоставления <> Неопределено И СтрокиСопоставления.Количество() = 1 Тогда
					НоваяСтрока.Номенклатура = СтрокиСопоставления[0].Номенклатура;
					НоваяСтрока.Характеристика = СтрокиСопоставления[0].Характеристика;
				КонецЕсли;
			КонецЕсли;
			
			Если ДанныеДокументаТТН.ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
				MarkInfoRanges = ЭлементДанных.InformB.InformBItem.MarkInfo;
				НомераСправки1.Вставить(ЭлементДанных.Identity, ЭлементДанных.InformA.RegId);
				НоваяСтрока.НомерСправки2Поставщика = ЭлементДанных.InformB.InformBItem.BRegId;
			ИначеЕсли ДанныеДокументаТТН.ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V2 Тогда
				MarkInfoRanges = ЭлементДанных.InformF2.InformF2Item.MarkInfo;
				НомераСправки1.Вставить(ЭлементДанных.Identity, ЭлементДанных.InformF1.RegId);
				НоваяСтрока.НомерСправки2Поставщика = ЭлементДанных.InformF2.InformF2Item.F2RegId;
			ИначеЕсли ДанныеДокументаТТН.ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V3 Тогда
				MarkInfo = ЭлементДанных.InformF2.MarkInfo;
				НомераСправки1.Вставить(ЭлементДанных.Identity, ЭлементДанных.FARegId);
				НоваяСтрока.НомерСправки2Поставщика = ЭлементДанных.InformF2.F2RegId;
			КонецЕсли;
			
			Диапазоны = Новый Массив;
			Если MarkInfoRanges <> Неопределено Тогда
				
				Для Каждого ДанныеДиапазона Из MarkInfoRanges.Ranges.Range Цикл
					
					Диапазон = Новый Структура;
					Диапазон.Вставить("ТипМарки",       MarkInfoRanges.Type);
					Диапазон.Вставить("Идентификатор",  ДанныеДиапазона.Identity);
					Диапазон.Вставить("СерияМарки",     ДанныеДиапазона.Rank);
					Диапазон.Вставить("НачальныйНомер", ДанныеДиапазона.Start);
					Диапазон.Вставить("КонечныйНомер",  ДанныеДиапазона.Last);
					
					Диапазоны.Добавить(Диапазон);
					
				КонецЦикла;
				
			КонецЕсли;
			ДиапазоныМарок.Вставить(ЭлементДанных.Identity, Диапазоны);
			
			НоваяСтрока.Сумма = НоваяСтрока.Количество * НоваяСтрока.Цена;
			
			Товары.Вставить(ЭлементДанных.Identity, НоваяСтрока);
			
		КонецЦикла;
		
		ДокументОбъект.СуммаДокумента = ДокументОбъект.Товары.Итог("Сумма");
		
		Для Каждого ЭлементДанных Из ДанныеДокументаУведомлениеОРегистрации.Объект.Content.Position Цикл
			
			ДанныеСправки2 = ИнтеграцияЕГАИСКлиентСервер.СтруктураДанныхСправки2();
			
			Если ДанныеДокументаУведомлениеОРегистрации.ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
				РегистрационныйНомер = ЭлементДанных.InformBRegId;
			Иначе
				РегистрационныйНомер = ЭлементДанных.InformF2RegId;
			КонецЕсли;
			
			ДанныеСправки2.НомерПодтвержденияЕГАИС = ДанныеДокументаУведомлениеОРегистрации.Объект.Header.EGAISFixNumber;
			ДанныеСправки2.ДатаПодтвержденияЕГАИС  = ДанныеДокументаУведомлениеОРегистрации.Объект.Header.EGAISFixDate;
			ДанныеСправки2.РегистрационныйНомер    = РегистрационныйНомер;
			ДанныеСправки2.Наименование            = РегистрационныйНомер;
			
			СтрокаТовара = Товары.Получить(ЭлементДанных.Identity);
			
			ДанныеСправки2.АлкогольнаяПродукция = СтрокаТовара.АлкогольнаяПродукция;
			ДанныеСправки2.Количество           = СтрокаТовара.Количество;
			ДанныеСправки2.НомерСправки1        = НомераСправки1.Получить(ЭлементДанных.Identity);
			ДанныеСправки2.ДокументОснование    = Ссылка;
			ДанныеСправки2.ДиапазоныМарок       = ДиапазоныМарок.Получить(ЭлементДанных.Identity);
			
			НоваяСправка2 = СоздатьСправку(ДанныеСправки2, Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросСправки2);
			
			СтрокаТовара.Справка2 = НоваяСправка2;
			
		КонецЦикла;
		
		ИнтеграцияЕГАИСПереопределяемый.ПриЗагрузкеТТНВходящаяЕГАИС(ДокументОбъект);
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = ДанныеДокументаТТН.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = ДанныеДокументаТТН.ФорматОбмена;
		
		ПолноеИмя = Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			Ссылка, ДанныеДокументаТТН.Операция,
			ПараметрыОбновленияСтатуса);
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		ОбъектИзменен = Истина;
		
		ДокументОснование = ДокументОбъект.ДокументОснование;
		ДокументСсылка    = Ссылка;
		
	Иначе
		
		ДокументОснование = РезультатДобавленияЗаписиТТН.ДокументОснование;
		ДокументСсылка    = РезультатДобавленияЗаписиТТН.Документ;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокументаТТН.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокументаТТН.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = Неопределено;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписиТТН.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументСсылка;
	ВозвращаемоеЗначение.ДокументОснование = ДокументОснование;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокументаУведомлениеОРегистрации.Операция,
			ДанныеДокументаУведомлениеОРегистрации.АдресЗапроса));
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокументаТТН.Операция,
			ДанныеДокументаТТН.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Регистрирует новые справки 1 и 2 и заполняет их в табличной части ТТН.
//
Функция ЗагрузитьУведомлениеОРегистрацииДвиженияТТН(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификатору(
		Метаданные.Документы.ТТНИсходящаяЕГАИС,
		"Идентификатор",
		ДанныеДокумента.Объект.Header.Identity);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке уведомления о регистрации движения ТТН:
			           |Не найден документ ТТН ЕГАИС (исходящая) с идентификатором %1.'"),
			ДанныеДокумента.Объект.Header.Identity);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получение уведомления о регистрации движения ТТН ЕГАИС (исходящая)'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
		
		// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
		// Документ будет получен в следующий итерации загрузки данных.
		ДокументОбъект.Заблокировать();
		
		ТоварыИтоги = Новый ТаблицаЗначений;
		ТоварыИтоги.Колонки.Добавить("АлкогольнаяПродукция");
		ТоварыИтоги.Колонки.Добавить("ИдентификаторУпаковки");
		ТоварыИтоги.Колонки.Добавить("НомерПартии");
		ТоварыИтоги.Колонки.Добавить("Справка2");
		ТоварыИтоги.Колонки.Добавить("Цена");
		
		Для Каждого СтрокаТЧ Из ДокументОбъект.Товары Цикл
			
			НоваяСтрока = ТоварыИтоги.Добавить();
			НоваяСтрока.АлкогольнаяПродукция  = СтрокаТЧ.АлкогольнаяПродукция;
			НоваяСтрока.ИдентификаторУпаковки = СтрокаТЧ.ИдентификаторУпаковки;
			НоваяСтрока.НомерПартии           = СтрокаТЧ.НомерПартии;
			НоваяСтрока.Справка2              = СтрокаТЧ.Справка2;
			НоваяСтрока.Цена                  = СтрокаТЧ.Цена;
			
		КонецЦикла;
		
		ТоварыИтоги.Свернуть(
			"АлкогольнаяПродукция,
			|ИдентификаторУпаковки,
			|НомерПартии,
			|Справка2,
			|Цена");
		
		Для Каждого ЭлементДанных Из ДанныеДокумента.Объект.Content.Position Цикл
			
			ИндексСтроки = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ЭлементДанных.Identity) - 1;
			
			Если ДанныеДокумента.ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
				НомерСправки2 = ЭлементДанных.InformBRegId;
			Иначе
				НомерСправки2 = ЭлементДанных.InformF2RegId;
			КонецЕсли;
			
			Если ТоварыИтоги.Количество() > ИндексСтроки Тогда
				
				СтрокаТЧИтоги = ТоварыИтоги[ИндексСтроки];
				
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("АлкогольнаяПродукция",  СтрокаТЧИтоги.АлкогольнаяПродукция);
				ПараметрыОтбора.Вставить("ИдентификаторУпаковки", СтрокаТЧИтоги.ИдентификаторУпаковки);
				ПараметрыОтбора.Вставить("НомерПартии",           СтрокаТЧИтоги.НомерПартии);
				ПараметрыОтбора.Вставить("Справка2",              СтрокаТЧИтоги.Справка2);
				ПараметрыОтбора.Вставить("Цена",                  СтрокаТЧИтоги.Цена);
				
				НайденныеСтрокиТЧТовары = ДокументОбъект.Товары.НайтиСтроки(ПараметрыОтбора);
				Для Каждого СтрокаТЧ Из НайденныеСтрокиТЧТовары Цикл
					СтрокаТЧ.НомерСправки2Покупателя = НомерСправки2;
				КонецЦикла;
				
			Иначе
				
				ВызватьИсключение СтрШаблон(
					НСтр("ru = 'При загрузке уведомления о регистрации движения ТТН:
						|Не найдена строка документа ТТН ЕГАИС (исходящая) по идентификатору %1 (Поиск выполняется по набору ключевых реквизитов).'"),
					ЭлементДанных.Identity);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЗаписыватьДокумент = ДокументОбъект.Модифицированность();
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Не ЗаписыватьДокумент;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
		
		ПолноеИмя = РезультатПоиска.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		Если ЗаписыватьДокумент Тогда
			ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
			ОбъектИзменен = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = РезультатПоиска.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = РезультатПоиска.ДокументОснование;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Регистрирует новые справки 1 и 2 и заполняет их в табличной части акта.
//
Функция ЗагрузитьУведомлениеОРегистрацииДвиженияАктаПостановкиНаБаланс(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификатору(
		Метаданные.Документы.АктПостановкиНаБалансЕГАИС,
		"Идентификатор",
		ДанныеДокумента.Объект.Header.Identity);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке уведомления о регистрации движения акта постановки на баланс ЕГАИС:
			           |Не найден документ Акт постановки на баланс ЕГАИС с идентификатором %1.'"),
			ДанныеДокумента.Объект.Header.Identity);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получено уведомление о регистрации движения акта постановки на баланс ЕГАИС'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
		
		// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
		// Документ будет получен в следующий итерации загрузки данных.
		ДокументОбъект.Заблокировать();
		
		ТоварыИтоги = Новый ТаблицаЗначений;
		ТоварыИтоги.Колонки.Добавить("АлкогольнаяПродукция");
		ТоварыИтоги.Колонки.Добавить("КоличествоПоСправке1");
		ТоварыИтоги.Колонки.Добавить("ДатаРозлива");
		ТоварыИтоги.Колонки.Добавить("НомерТТН");
		ТоварыИтоги.Колонки.Добавить("ДатаТТН");
		ТоварыИтоги.Колонки.Добавить("НомерПодтвержденияЕГАИС");
		ТоварыИтоги.Колонки.Добавить("ДатаПодтвержденияЕГАИС");
		ТоварыИтоги.Колонки.Добавить("Количество");
		
		Для Каждого СтрокаТЧ Из ДокументОбъект.Товары Цикл
			
			НоваяСтрока = ТоварыИтоги.Добавить();
			НоваяСтрока.АлкогольнаяПродукция    = СтрокаТЧ.АлкогольнаяПродукция;
			НоваяСтрока.КоличествоПоСправке1    = СтрокаТЧ.КоличествоПоСправке1;
			НоваяСтрока.ДатаРозлива             = СтрокаТЧ.ДатаРозлива;
			НоваяСтрока.НомерТТН                = СтрокаТЧ.НомерТТН;
			НоваяСтрока.ДатаТТН                 = СтрокаТЧ.ДатаТТН;
			НоваяСтрока.НомерПодтвержденияЕГАИС = СтрокаТЧ.НомерПодтвержденияЕГАИС;
			НоваяСтрока.ДатаПодтвержденияЕГАИС  = СтрокаТЧ.ДатаПодтвержденияЕГАИС;
			
			НоваяСтрока.Количество = СтрокаТЧ.Количество;
			
		КонецЦикла;
		
		ТоварыИтоги.Свернуть(
			"АлкогольнаяПродукция,
			|КоличествоПоСправке1,
			|ДатаРозлива,
			|НомерТТН,
			|ДатаТТН,
			|НомерПодтвержденияЕГАИС,
			|ДатаПодтвержденияЕГАИС",
			"Количество");
		
		Для Каждого СтрокаКвитанцииXDTO Из ДанныеДокумента.Объект.Content.Position Цикл
			
			ИндексСтроки = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрокаКвитанцииXDTO.Identity) - 1;
			
			Если ДанныеДокумента.ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
				НомерНовойСправки1 = СтрокаКвитанцииXDTO.InformARegId;
			Иначе
				НомерНовойСправки1 = СтрокаКвитанцииXDTO.InformF1RegId;
			КонецЕсли;
			
			ДанныеСправки1 = ИнтеграцияЕГАИСКлиентСервер.СтруктураДанныхСправки1();
			ДанныеСправки1.РегистрационныйНомер = НомерНовойСправки1;
			ДанныеСправки1.Наименование         = НомерНовойСправки1;
			
			СтрокаТЧИтоги = Неопределено;
			Если ТоварыИтоги.Количество() > ИндексСтроки Тогда
				
				СтрокаТЧИтоги = ТоварыИтоги[ИндексСтроки];
				
				ДанныеСправки1.АлкогольнаяПродукция    = СтрокаТЧИтоги.АлкогольнаяПродукция;
				ДанныеСправки1.НомерТТН                = СтрокаТЧИтоги.НомерТТН;
				ДанныеСправки1.ДатаТТН                 = СтрокаТЧИтоги.ДатаТТН;
				ДанныеСправки1.ДатаРозлива             = СтрокаТЧИтоги.ДатаРозлива;
				ДанныеСправки1.Количество              = СтрокаТЧИтоги.КоличествоПоСправке1;
				ДанныеСправки1.НомерПодтвержденияЕГАИС = СтрокаТЧИтоги.НомерПодтвержденияЕГАИС;
				ДанныеСправки1.ДатаПодтвержденияЕГАИС  = СтрокаТЧИтоги.ДатаПодтвержденияЕГАИС;
				
			Иначе
				
				ВызватьИсключение СтрШаблон(
					НСтр("ru = 'При загрузке уведомления о регистрации движения Акта постановки на баланс:
						|Не найдена строка документа Акт постаноки на баланс по идентификатору %1.'"),
					СтрокаКвитанцииXDTO.Identity);
				
			КонецЕсли;
			
			НоваяСправка1 = СоздатьСправку(ДанныеСправки1, Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросСправки1);
			
			Если ДанныеДокумента.ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
				СписокСправок2XDTO = СтрокаКвитанцииXDTO.InformB.InformBItem;
			Иначе
				СписокСправок2XDTO = СтрокаКвитанцииXDTO.InformF2.InformF2Item;
			КонецЕсли;
			
			Для Каждого ЭлементДанных Из СписокСправок2XDTO Цикл
				
				Если ДанныеДокумента.ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
					РегистрационныйНомер = ЭлементДанных.BRegId;
				Иначе
					РегистрационныйНомер = ЭлементДанных.F2RegId;
				КонецЕсли;
				
				ДанныеСправки2 = ИнтеграцияЕГАИСКлиентСервер.СтруктураДанныхСправки2();
				ДанныеСправки2.РегистрационныйНомер = РегистрационныйНомер;
				ДанныеСправки2.Наименование         = РегистрационныйНомер;
				ДанныеСправки2.НомерСправки1        = НомерНовойСправки1;
				ДанныеСправки2.Справка1             = НоваяСправка1;
				ДанныеСправки2.ДокументОснование    = ДокументОбъект.Ссылка;
				
				ДанныеСправки2.АлкогольнаяПродукция = СтрокаТЧИтоги.АлкогольнаяПродукция;
				ДанныеСправки2.Количество           = СтрокаТЧИтоги.КоличествоПоСправке1;
				
				НоваяСправка2 = СоздатьСправку(ДанныеСправки2, Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросСправки2);
			
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("АлкогольнаяПродукция",    СтрокаТЧИтоги.АлкогольнаяПродукция);
				ПараметрыОтбора.Вставить("КоличествоПоСправке1",    СтрокаТЧИтоги.КоличествоПоСправке1);
				ПараметрыОтбора.Вставить("ДатаРозлива",             СтрокаТЧИтоги.ДатаРозлива);
				ПараметрыОтбора.Вставить("НомерТТН",                СтрокаТЧИтоги.НомерТТН);
				ПараметрыОтбора.Вставить("ДатаТТН",                 СтрокаТЧИтоги.ДатаТТН);
				ПараметрыОтбора.Вставить("НомерПодтвержденияЕГАИС", СтрокаТЧИтоги.НомерПодтвержденияЕГАИС);
				ПараметрыОтбора.Вставить("ДатаПодтвержденияЕГАИС",  СтрокаТЧИтоги.ДатаПодтвержденияЕГАИС);
				
				НайденныеСтрокиТЧТовары = ДокументОбъект.Товары.НайтиСтроки(ПараметрыОтбора);
				Для Каждого СтрокаТЧ Из НайденныеСтрокиТЧТовары Цикл
					СтрокаТЧ.Справка2 = НоваяСправка2;
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
		
		ПолноеИмя = ДокументОбъект.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
		ОбъектИзменен = Истина;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументОбъект.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = ДокументОбъект.ДокументОснование;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Выполняет действия при получении подтверждения акта расхождений ТТН.
//
Функция ЗагрузитьКвитанциюАктаРасхожденийТТН(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификатору(
		Метаданные.Документы.ТТНВходящаяЕГАИС,
		"ИдентификаторЕГАИС",
		ДанныеДокумента.Объект.Header.WBRegId);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке квитанции акта расхождений по ТТН ЕГАИС (входящая):
			           |Не найден документ с идентификатором %1.'"),
			ДанныеДокумента.Объект.Header.WBRegId);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	Если ВРег(ДанныеДокумента.Объект.Header.IsConfirm) = ВРег("Rejected") Тогда
		ДанныеДокумента.Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийОтказ;
	Иначе
		ДанныеДокумента.Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийПодтверждение;
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получение квитанции акта расхождений по ТТН ЕГАИС (входящая)'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ЗаписыватьДокумент = (ДанныеДокумента.Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияАктаРасхожденийПодтверждение);
		
		ПолноеИмя = РезультатПоиска.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		
		Если ЗаписыватьДокумент Тогда
			
			ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
			
			// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
			// Документ будет получен в следующий итерации загрузки данных.
			ДокументОбъект.Заблокировать();
			
			ДокументОбъект.ДатаРегистрацииДвижений = ДанныеДокумента.Объект.Header.TicketDate;
			
			ИнтеграцияЕГАИСПереопределяемый.ПриЗагрузкеПодтвержденияАктаРасхожденийТТН(
				РезультатПоиска.Ссылка,
				НовыйСтатус = Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.ОтмененПоставщиком);
			
			ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
			ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
			ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
			ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
			ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
			ПараметрыОбновленияСтатуса.ФорматОбмена         = ДанныеДокумента.ФорматОбмена;
			
			НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
				РезультатПоиска.Ссылка, ДанныеДокумента.Операция,
				ПараметрыОбновленияСтатуса);
			
			ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
			ОбъектИзменен = Истина;
			
		Иначе
			
			ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
			ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
			ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
			ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
			ПараметрыОбновленияСтатуса.ФорматОбмена         = ДанныеДокумента.ФорматОбмена;
			
			НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
				РезультатПоиска.Ссылка, ДанныеДокумента.Операция,
				ПараметрыОбновленияСтатуса);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = РезультатПоиска.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = РезультатПоиска.ДокументОснование;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Выполняет действия при получении подтверждения акта расхождений ТТН.
//
Функция ЗагрузитьКвитанциюЗапросаНаОтменуПроведенияТТН(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификатору(
		Метаданные.Документы.ТТНВходящаяЕГАИС,
		"ИдентификаторЕГАИС",
		ДанныеДокумента.Объект.Header.WBRegId);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке квитанции запроса на отмену проведения ТТН ЕГАИС:
			           |Не найден документ с идентификатором %1.'"),
			ДанныеДокумента.Объект.Header.WBRegId);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	Если ВРег(ДанныеДокумента.Объект.Header.IsConfirm) = ВРег("Rejected") Тогда
		ДанныеДокумента.Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНОтказ;
	Иначе
		ДанныеДокумента.Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияЗапросаНаОтменуПроведенияТТНПодтверждение;
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получение квитанции запроса на отмену проведения ТТН ЕГАИС'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ПолноеИмя = РезультатПоиска.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			РезультатПоиска.Ссылка, ДанныеДокумента.Операция);
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = РезультатПоиска.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = РезультатПоиска.ДокументОснование;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Выполняет действия при получении акта подтверждения ТТН.
//
Функция ЗагрузитьАктПоИсходящейТТНЕГАИС(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификатору(
		Метаданные.Документы.ТТНИсходящаяЕГАИС,
		"ИдентификаторЕГАИС",
		ДанныеДокумента.Объект.Header.WBRegId);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке акта по ТТН ЕГАИС (исходящей):
			           |Не найден документ с идентификатором %1.'"),
			ДанныеДокумента.Объект.Header.WBRegId);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	Если ВРег(ДанныеДокумента.Объект.Header.IsAccept) = ВРег("Rejected") Тогда
		ДанныеДокумента.Операция = Перечисления.ВидыДокументовЕГАИС.АктТТНОтказ;
	ИначеЕсли ВРег(ДанныеДокумента.Объект.Header.IsAccept) = ВРег("Differences") Тогда
		ДанныеДокумента.Операция = Перечисления.ВидыДокументовЕГАИС.АктТТНРасхождения;
	Иначе
		Если ДанныеДокумента.Объект.Content <> Неопределено
			И ДанныеДокумента.Объект.Content.Position.Количество() > 0 Тогда
			ДанныеДокумента.Операция = Перечисления.ВидыДокументовЕГАИС.АктТТНРасхождения;
		Иначе
			ДанныеДокумента.Операция = Перечисления.ВидыДокументовЕГАИС.АктТТНПодтверждение;
		КонецЕсли;
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получение акта по документу ТТН ЕГАИС (входящая)'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ЕстьРасхождения = Ложь;
		ДокументОбъект  = Неопределено;
		
		Если ДанныеДокумента.Объект.Content <> Неопределено Тогда
			
			Для Каждого СтрокаРасхожденияXDTO Из ДанныеДокумента.Объект.Content.Position Цикл
				
				Если ДокументОбъект = Неопределено Тогда
					
					ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
					
					// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
					// Документ будет получен в следующий итерации загрузки данных.
					ДокументОбъект.Заблокировать();
					
				КонецЕсли;
				
				ИндексСтроки = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрокаРасхожденияXDTO.Identity) - 1;
				
				ДокументОбъект.Товары[ИндексСтроки].КоличествоФакт = СтрокаРасхожденияXDTO.RealQuantity;
				
				ЕстьРасхождения = Истина;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если ЕстьРасхождения Тогда
			ДокументОбъект.ЕстьРасхождения = Истина;
		КонецЕсли;
		
		ИнтеграцияЕГАИСПереопределяемый.ПриЗагрузкеАктаПодтвержденияТТН(
			ДокументОбъект,
			ДанныеДокумента.Операция = Перечисления.ВидыДокументовЕГАИС.АктТТНОтказ,
			ДанныеДокумента.Операция = Перечисления.ВидыДокументовЕГАИС.АктТТНРасхождения);
		
		ПолноеИмя = РезультатПоиска.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Не ЕстьРасхождения;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = ДанныеДокумента.ФорматОбмена;
		
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			РезультатПоиска.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		Если ЕстьРасхождения Тогда
			ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
			ОбъектИзменен = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = РезультатПоиска.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = РезультатПоиска.ДокументОснование;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Выполняет действия при получении запроса на отмену проведения ТТН.
//
Функция ЗагрузитьЗапросНаОтменуПроведенияТТН(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификатору(
		Метаданные.Документы.ТТНИсходящаяЕГАИС,
		"ИдентификаторЕГАИС",
		ДанныеДокумента.Объект.WBRegId);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке запроса на отмену проведения ТТН ЕГАИС (исходящей):
			           |Не найден документ с идентификатором %1.'"),
			ДанныеДокумента.Объект.WBRegId);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получение запроса на отмену проведения ТТН ЕГАИС (исходящая)'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ПолноеИмя = РезультатПоиска.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			РезультатПоиска.Ссылка, ДанныеДокумента.Операция);
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = РезультатПоиска.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = РезультатПоиска.ДокументОснование;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает список организаций ЕГАИС в базу.
//
Функция ЗагрузитьОтветНаЗапросДанныхОрганизации(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	ИсходящееСообщение = ИсходящееСообщение(ДанныеДокумента.ИдентификаторЗапроса);
	
	Если ИсходящееСообщение = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке ответа на запрос данных организации:
			           |Не найден исходящий запрос с идентификатором %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	Для Каждого ДанныеОрганизацииXDTO Из ДанныеДокумента.Объект.Clients.Client Цикл
		Организация = ЗагрузитьОрганизацию(ДанныеОрганизацииXDTO);
	КонецЦикла;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             Неопределено);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получены данные организации ЕГАИС'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = Организация;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = Неопределено;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает список организаций ЕГАИС в базу.
//
Функция ЗагрузитьОтветНаЗапросАлкогольнойПродукции(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	ИсходящееСообщение = ИсходящееСообщение(ДанныеДокумента.ИдентификаторЗапроса);
	
	Если ИсходящееСообщение = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке ответа на запрос алкогольной продукции:
			           |Не найден исходящий запрос с идентификатором %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	СоответствиеЗагруженнаяАлкогольнаяПродукция = ЗагрузитьАлкогольнуюПродукцию(ДанныеДокумента.Объект.Products.Product);
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             Неопределено);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получена алкогольная продукция'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = СоответствиеЗагруженнаяАлкогольнаяПродукция;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = Неопределено;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает организацию в базу.
//
Функция ЗагрузитьОрганизацию(ДанныеОрганизацииXDTO, ФорматОбмена = Неопределено) Экспорт
	
	Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ДанныеОрганизацииXDTO, "OrgInfoV2") Тогда
		Если НЕ ЗначениеЗаполнено(ФорматОбмена) Тогда
			Если ВРег(ДанныеОрганизацииXDTO.VersionWB) = ВРег("WayBill") Тогда
				ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1;
			ИначеЕсли ВРег(ДанныеОрганизацииXDTO.VersionWB) = ВРег("WayBill_v2") Тогда
				ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V2;
			ИначеЕсли ВРег(ДанныеОрганизацииXDTO.VersionWB) = ВРег("WayBill_v3") Тогда
				ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V3;
			КонецЕсли;
		КонецЕсли;
		ТипОрганизации = ТипОрганизации(ДанныеОрганизацииXDTO.OrgInfoV2);
		ЗагружаемаяОрганизацияXDTO = ДанныеОрганизации(ДанныеОрганизацииXDTO.OrgInfoV2);
	Иначе
		ТипОрганизации = ТипОрганизации(ДанныеОрганизацииXDTO);
		ЗагружаемаяОрганизацияXDTO = ДанныеОрганизации(ДанныеОрганизацииXDTO);
	КонецЕсли;
	
	НайденнаяОрганизация = Справочники.КлассификаторОрганизацийЕГАИС.НайтиПоКоду(ЗагружаемаяОрганизацияXDTO.ClientRegId);
	
	Если НЕ ЗначениеЗаполнено(НайденнаяОрганизация) Тогда
		СправочникОбъект = Справочники.КлассификаторОрганизацийЕГАИС.СоздатьЭлемент();
		СправочникОбъект.Код = ЗагружаемаяОрганизацияXDTO.ClientRegId;
	Иначе
		СправочникОбъект = НайденнаяОрганизация.ПолучитьОбъект();
	КонецЕсли;
	
	Если ПустаяСтрока(СправочникОбъект.Наименование) Тогда
		СправочникОбъект.Наименование = ?(ПустаяСтрока(ЗагружаемаяОрганизацияXDTO.ShortName), ЗагружаемаяОрганизацияXDTO.FullName, ЗагружаемаяОрганизацияXDTO.ShortName);
	КонецЕсли;
	
	Если ПустаяСтрока(СправочникОбъект.НаименованиеПолное) Тогда
		СправочникОбъект.НаименованиеПолное = ЗагружаемаяОрганизацияXDTO.FullName;
	КонецЕсли;
	
	Если СправочникОбъект.ТипОрганизации <> ТипОрганизации И ЗначениеЗаполнено(ТипОрганизации) Тогда
		СправочникОбъект.ТипОрганизации = ТипОрганизации;
	КонецЕсли;
	
	Если СправочникОбъект.ФорматОбмена <> ФорматОбмена И ЗначениеЗаполнено(ФорматОбмена) Тогда
		СправочникОбъект.ФорматОбмена = ФорматОбмена;
	КонецЕсли;
	
	Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ЗагружаемаяОрганизацияXDTO, "INN") Тогда
		Если НЕ ПустаяСтрока(ЗагружаемаяОрганизацияXDTO.INN) И СокрЛП(СправочникОбъект.ИНН) <> СокрЛП(ЗагружаемаяОрганизацияXDTO.INN) Тогда
			СправочникОбъект.ИНН = СокрЛП(ЗагружаемаяОрганизацияXDTO.INN);
		КонецЕсли;
	КонецЕсли;
	
	Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ЗагружаемаяОрганизацияXDTO, "KPP") Тогда
		Если НЕ ПустаяСтрока(ЗагружаемаяОрганизацияXDTO.KPP) И СокрЛП(СправочникОбъект.КПП) <> СокрЛП(ЗагружаемаяОрганизацияXDTO.KPP) Тогда
			СправочникОбъект.КПП = СокрЛП(ЗагружаемаяОрганизацияXDTO.KPP);
		КонецЕсли;
	КонецЕсли;
	
	Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ЗагружаемаяОрганизацияXDTO, "TSNUM") Тогда
		Если НЕ ПустаяСтрока(ЗагружаемаяОрганизацияXDTO.TSNUM) И СокрЛП(СправочникОбъект.ИдентификаторОрганизацииТС) <> СокрЛП(ЗагружаемаяОрганизацияXDTO.TSNUM) Тогда
			СправочникОбъект.ИдентификаторОрганизацииТС = СокрЛП(ЗагружаемаяОрганизацияXDTO.TSNUM);
		КонецЕсли;
	КонецЕсли;
	
	КодСтраны = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ЗагружаемаяОрганизацияXDTO.address.Country);
	
	Если КодСтраны <> 0 И СправочникОбъект.КодСтраны <> КодСтраны Тогда
		СправочникОбъект.КодСтраны = КодСтраны;
	КонецЕсли;
	
	Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ЗагружаемаяОрганизацияXDTO.address, "RegionCode") Тогда
		КодРегиона = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ЗагружаемаяОрганизацияXDTO.address.RegionCode);
		Если КодРегиона <> 0 И СправочникОбъект.КодРегиона <> КодРегиона Тогда
			СправочникОбъект.КодРегиона = КодРегиона;
		КонецЕсли;
	КонецЕсли;
	
	Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ЗагружаемаяОрганизацияXDTO.address, "Index") Тогда
		ПочтовыйИндекс = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ЗагружаемаяОрганизацияXDTO.address.Index);
		Если ПочтовыйИндекс <> 0 И СправочникОбъект.ПочтовыйИндекс <> ПочтовыйИндекс Тогда
			СправочникОбъект.ПочтовыйИндекс = ПочтовыйИндекс;
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(СправочникОбъект.ПредставлениеАдреса) Тогда
		СправочникОбъект.ПредставлениеАдреса = СокрЛП(ЗагружаемаяОрганизацияXDTO.address.description);
	КонецЕсли;
	
	СправочникОбъект.Адрес = "";
	
	Если Не ЗначениеЗаполнено(СправочникОбъект.ТипОрганизации) Тогда
		Если НЕ ПустаяСтрока(СправочникОбъект.ИдентификаторОрганизацииТС) Тогда
			СправочникОбъект.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.КонтрагентТаможенногоСоюза;
		ИначеЕсли ПустаяСтрока(СправочникОбъект.ИНН) И ПустаяСтрока(СправочникОбъект.КПП) Тогда
			СправочникОбъект.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ИностранныйКонтрагент;
		ИначеЕсли ПустаяСтрока(СправочникОбъект.КПП) И СтрДлина(СокрЛП(СправочникОбъект.ИНН)) = 12 Тогда
			СправочникОбъект.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ИндивидуальныйПредпринимательРФ;
		Иначе
			СправочникОбъект.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ЮридическоеЛицоРФ;
		КонецЕсли;
	КонецЕсли;
	
	Если СправочникОбъект.Модифицированность() Тогда
		ИнтеграцияЕГАИСПереопределяемый.ПриЗагрузкеОрганизации(СправочникОбъект);
		СправочникОбъект.Записать();
	КонецЕсли;
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

// Загружает список алкогольной продукции в базу.
//
Функция ЗагрузитьАлкогольнуюПродукцию(СписокАлкогольнойПродукцииXDTO)
	
	Результат = Новый Соответствие;
	
	КоличествоЭлементов = СписокАлкогольнойПродукцииXDTO.Количество();
	Если КоличествоЭлементов = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	КлассификаторВидовПродукции = КлассификаторВидовАлкогольнойПродукции();
	ИмяКлассификатора = СправочникВидовАлкогольнойПродукции();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КлассификаторВидовПродукции", КлассификаторВидовПродукции);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(КлассификаторВидовПродукции.Код КАК Строка(3)) КАК Код,
	|	КлассификаторВидовПродукции.Наименование КАК Наименование,
	|	КлассификаторВидовПродукции.Маркируемый КАК Маркируемый
	|ПОМЕСТИТЬ КлассификаторВидовПродукции
	|ИЗ
	|	&КлассификаторВидовПродукции КАК КлассификаторВидовПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КлассификаторВидовПродукции.Код,
	|	КлассификаторВидовПродукции.Наименование,
	|	КлассификаторВидовПродукции.Маркируемый,
	|	ЕСТЬNULL(ВидыАлкогольнойПродукцииЕГАИС.Ссылка, ЗНАЧЕНИЕ(Справочник." + ИмяКлассификатора + ".ПустаяСсылка)) КАК Ссылка
	|ИЗ
	|	КлассификаторВидовПродукции КАК КлассификаторВидовПродукции
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник." + ИмяКлассификатора + " КАК ВидыАлкогольнойПродукцииЕГАИС
	|		ПО КлассификаторВидовПродукции.Код = ВидыАлкогольнойПродукцииЕГАИС.Код";
	
	КлассификаторВидовПродукции = Запрос.Выполнить().Выгрузить();
	КлассификаторВидовПродукции.Индексы.Добавить("Код");
	
	ТаблицаАлкогольнойПродукции = Новый ТаблицаЗначений;
	ТаблицаАлкогольнойПродукции.Колонки.Добавить("Код"               , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
	ТаблицаАлкогольнойПродукции.Колонки.Добавить("Наименование"      , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(100)));
	ТаблицаАлкогольнойПродукции.Колонки.Добавить("НаименованиеПолное", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(1000)));
	ТаблицаАлкогольнойПродукции.Колонки.Добавить("Объем"             , Новый ОписаниеТипов("Число"));
	ТаблицаАлкогольнойПродукции.Колонки.Добавить("Крепость"          , Новый ОписаниеТипов("Число"));
	ТаблицаАлкогольнойПродукции.Колонки.Добавить("Производитель"     , Новый ОписаниеТипов("СправочникСсылка.КлассификаторОрганизацийЕГАИС"));
	ТаблицаАлкогольнойПродукции.Колонки.Добавить("Импортер"          , Новый ОписаниеТипов("СправочникСсылка.КлассификаторОрганизацийЕГАИС"));
	ТаблицаАлкогольнойПродукции.Колонки.Добавить("ВидПродукции"      , Новый ОписаниеТипов("СправочникСсылка." + ИмяКлассификатора));
	ТаблицаАлкогольнойПродукции.Колонки.Добавить("ТипПродукции"      , Новый ОписаниеТипов("ПеречислениеСсылка.ТипыПродукцииЕГАИС"));
	
	ВидыЛицензий = Новый Соответствие;
	ВидыЛицензий.Вставить("АП"   , Перечисления.ВидыЛицензийАлкогольнойПродукции.АлкогольнаяПродукция);
	ВидыЛицензий.Вставить("ССП"  , Перечисления.ВидыЛицензийАлкогольнойПродукции.СпиртосодержащаяПищеваяПродукция);
	ВидыЛицензий.Вставить("ССНП" , Перечисления.ВидыЛицензийАлкогольнойПродукции.СпиртосодержащаяНеПищеваяПродукция);
	ВидыЛицензий.Вставить("Спирт", Перечисления.ВидыЛицензийАлкогольнойПродукции.Спирт);
	
	МассивКодов = Новый Массив;
	
	Для Каждого ДанныеАлкогольнойПродукцииXDTO Из СписокАлкогольнойПродукцииXDTO Цикл
		
		Если МассивКодов.Найти(ДанныеАлкогольнойПродукцииXDTO.AlcCode) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		МассивКодов.Добавить(ДанныеАлкогольнойПродукцииXDTO.AlcCode);
		
		СтрокаТаблицы = ТаблицаАлкогольнойПродукции.Добавить();
		СтрокаТаблицы.Код                = ДанныеАлкогольнойПродукцииXDTO.AlcCode;
		СтрокаТаблицы.Наименование       = ДанныеАлкогольнойПродукцииXDTO.ShortName;
		СтрокаТаблицы.НаименованиеПолное = ДанныеАлкогольнойПродукцииXDTO.FullName;
		СтрокаТаблицы.Объем              = ДанныеАлкогольнойПродукцииXDTO.Capacity;
		СтрокаТаблицы.Крепость           = ДанныеАлкогольнойПродукцииXDTO.AlcVolume;
		
		Если ПустаяСтрока(СтрокаТаблицы.Наименование) И НЕ ПустаяСтрока(СтрокаТаблицы.НаименованиеПолное) Тогда
			СтрокаТаблицы.Наименование = СтрокаТаблицы.НаименованиеПолное;
		КонецЕсли;
		
		Если ПустаяСтрока(СтрокаТаблицы.НаименованиеПолное) И НЕ ПустаяСтрока(СтрокаТаблицы.Наименование) Тогда
			СтрокаТаблицы.НаименованиеПолное = СтрокаТаблицы.Наименование;
		КонецЕсли;
		
		Если ДанныеАлкогольнойПродукцииXDTO.Producer <> Неопределено Тогда
			СтрокаТаблицы.Производитель = ЗагрузитьОрганизацию(ДанныеАлкогольнойПродукцииXDTO.Producer);
		КонецЕсли;
		
		Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ДанныеАлкогольнойПродукцииXDTO, "Importer") Тогда
			Если ДанныеАлкогольнойПродукцииXDTO.Importer <> Неопределено Тогда
				СтрокаТаблицы.Импортер = ЗагрузитьОрганизацию(ДанныеАлкогольнойПродукцииXDTO.Importer);
			КонецЕсли;
		КонецЕсли;
		
		Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ДанныеАлкогольнойПродукцииXDTO, "UnitType") Тогда
			СтрокаТаблицы.ТипПродукции = ?(ДанныеАлкогольнойПродукцииXDTO.UnitType = "Unpacked",
				Перечисления.ТипыПродукцииЕГАИС.Неупакованная,
				Перечисления.ТипыПродукцииЕГАИС.Упакованная);
		КонецЕсли;
		
		СтрокаКлассификатора = КлассификаторВидовПродукции.Найти(ДанныеАлкогольнойПродукцииXDTO.ProductVCode, "Код");
		Если СтрокаКлассификатора <> Неопределено Тогда
			
			Если Не ЗначениеЗаполнено(СтрокаКлассификатора.Ссылка) Тогда
				ВидПродукцииОбъект = Справочники[ИмяКлассификатора].СоздатьЭлемент();
				ВидПродукцииОбъект.Код          = СтрокаКлассификатора.Код;
				ВидПродукцииОбъект.Наименование = СтрокаКлассификатора.Наименование;
				ВидПродукцииОбъект.ВидЛицензии  = ВидыЛицензий[ДанныеАлкогольнойПродукцииXDTO.Type];
				ВидПродукцииОбъект.Маркируемый  = СтрокаКлассификатора.Маркируемый;
				ВидПродукцииОбъект.Записать();
				
				СтрокаКлассификатора.Ссылка = ВидПродукцииОбъект.Ссылка;
			КонецЕсли;
			
			СтрокаТаблицы.ВидПродукции = СтрокаКлассификатора.Ссылка;
		КонецЕсли;
	
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаАлкогольнойПродукции", ТаблицаАлкогольнойПродукции);
	Запрос.УстановитьПараметр("ПустойВидПродукции", Справочники[ИмяКлассификатора].ПустаяСсылка());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаАлкогольнойПродукции.Код                КАК Код,
	|	ТаблицаАлкогольнойПродукции.Наименование       КАК Наименование,
	|	ТаблицаАлкогольнойПродукции.НаименованиеПолное КАК НаименованиеПолное,
	|	ТаблицаАлкогольнойПродукции.Объем              КАК Объем,
	|	ТаблицаАлкогольнойПродукции.Крепость           КАК Крепость,
	|	ТаблицаАлкогольнойПродукции.Производитель      КАК Производитель,
	|	ТаблицаАлкогольнойПродукции.Импортер           КАК Импортер,
	|	ТаблицаАлкогольнойПродукции.ВидПродукции       КАК ВидПродукции,
	|	ТаблицаАлкогольнойПродукции.ТипПродукции       КАК ТипПродукции
	|ПОМЕСТИТЬ ТаблицаАлкогольнойПродукции
	|ИЗ
	|	&ТаблицаАлкогольнойПродукции КАК ТаблицаАлкогольнойПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаАлкогольнойПродукции.Код                КАК Код,
	|	ТаблицаАлкогольнойПродукции.Наименование       КАК Наименование,
	|	ТаблицаАлкогольнойПродукции.НаименованиеПолное КАК НаименованиеПолное,
	|	ТаблицаАлкогольнойПродукции.Объем              КАК Объем,
	|	ТаблицаАлкогольнойПродукции.Крепость           КАК Крепость,
	|	ТаблицаАлкогольнойПродукции.Производитель      КАК Производитель,
	|	ТаблицаАлкогольнойПродукции.Импортер           КАК Импортер,
	|	ТаблицаАлкогольнойПродукции.ВидПродукции       КАК ВидПродукции,
	|	ТаблицаАлкогольнойПродукции.ТипПродукции       КАК ТипПродукции,
	|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)) КАК Ссылка,
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка ЕСТЬ NULL
	|		ИЛИ (ВЫРАЗИТЬ(ТаблицаАлкогольнойПродукции.Наименование КАК СТРОКА(100))) <> (ВЫРАЗИТЬ(КлассификаторАлкогольнойПродукцииЕГАИС.Наименование КАК СТРОКА(100)))
	|			И ТаблицаАлкогольнойПродукции.Наименование <> """"
	|		ИЛИ (ВЫРАЗИТЬ(ТаблицаАлкогольнойПродукции.НаименованиеПолное КАК СТРОКА(1000))) <> (ВЫРАЗИТЬ(КлассификаторАлкогольнойПродукцииЕГАИС.НаименованиеПолное КАК СТРОКА(1000)))
	|			И ТаблицаАлкогольнойПродукции.НаименованиеПолное <> """"
	|		ИЛИ ТаблицаАлкогольнойПродукции.Объем <> КлассификаторАлкогольнойПродукцииЕГАИС.Объем
	|			И ТаблицаАлкогольнойПродукции.Объем <> 0
	|		ИЛИ ТаблицаАлкогольнойПродукции.Крепость <> КлассификаторАлкогольнойПродукцииЕГАИС.Крепость
	|			И ТаблицаАлкогольнойПродукции.Крепость <> 0
	|		ИЛИ ТаблицаАлкогольнойПродукции.Производитель <> КлассификаторАлкогольнойПродукцииЕГАИС.Производитель
	|			И ТаблицаАлкогольнойПродукции.Производитель <> ЗНАЧЕНИЕ(Справочник.КлассификаторОрганизацийЕГАИС.ПустаяСсылка)
	|		ИЛИ ТаблицаАлкогольнойПродукции.Импортер <> КлассификаторАлкогольнойПродукцииЕГАИС.Импортер
	|			И ТаблицаАлкогольнойПродукции.Импортер <> ЗНАЧЕНИЕ(Справочник.КлассификаторОрганизацийЕГАИС.ПустаяСсылка)
	|		ИЛИ ТаблицаАлкогольнойПродукции.ВидПродукции <> КлассификаторАлкогольнойПродукцииЕГАИС.ВидПродукции
	|			И ТаблицаАлкогольнойПродукции.ВидПродукции <> &ПустойВидПродукции
	|		ИЛИ ТаблицаАлкогольнойПродукции.ТипПродукции <> КлассификаторАлкогольнойПродукцииЕГАИС.ТипПродукции
	|			И ТаблицаАлкогольнойПродукции.ТипПродукции <> ЗНАЧЕНИЕ(Перечисление.ТипыПродукцииЕГАИС.ПустаяСсылка) КАК ЕстьИзменения
	|ПОМЕСТИТЬ ТаблицаСКлассификатором
	|ИЗ
	|	ТаблицаАлкогольнойПродукции КАК ТаблицаАлкогольнойПродукции
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
	|		ПО ТаблицаАлкогольнойПродукции.Код = КлассификаторАлкогольнойПродукцииЕГАИС.Код
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура          КАК Номенклатура,
	|	СоответствиеНоменклатурыЕГАИС.Характеристика        КАК Характеристика,
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция  КАК АлкогольнаяПродукция,
	|	СоответствиеНоменклатурыЕГАИС.ИдентификаторУпаковки КАК ИдентификаторУпаковки
	|ИЗ
	|	ТаблицаСКлассификатором КАК ТаблицаСКлассификатором
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО ТаблицаСКлассификатором.Ссылка = СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция
	|ГДЕ
	|	ТаблицаСКлассификатором.Ссылка <> ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСКлассификатором.Код                КАК Код,
	|	ТаблицаСКлассификатором.Наименование       КАК Наименование,
	|	ТаблицаСКлассификатором.НаименованиеПолное КАК НаименованиеПолное,
	|	ТаблицаСКлассификатором.Объем              КАК Объем,
	|	ТаблицаСКлассификатором.Крепость           КАК Крепость,
	|	ТаблицаСКлассификатором.Производитель      КАК Производитель,
	|	ТаблицаСКлассификатором.Импортер           КАК Импортер,
	|	ТаблицаСКлассификатором.ВидПродукции       КАК ВидПродукции,
	|	ТаблицаСКлассификатором.ТипПродукции       КАК ТипПродукции,
	|	ТаблицаСКлассификатором.Ссылка             КАК Ссылка,
	|	ТаблицаСКлассификатором.ЕстьИзменения      КАК ЕстьИзменения
	|ИЗ
	|	ТаблицаСКлассификатором КАК ТаблицаСКлассификатором";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаСопоставления = МассивРезультатов[МассивРезультатов.Количество() - 2].Выгрузить();
	РезультатЗапроса = МассивРезультатов[МассивРезультатов.Количество() - 1];
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДанныеАлкогольнойПродукции = Новый Структура;
		ДанныеАлкогольнойПродукции.Вставить("АлкогольнаяПродукция");
		ДанныеАлкогольнойПродукции.Вставить("ТаблицаСопоставления", Новый Соответствие);
		
		Если Не Выборка.ЕстьИзменения Тогда
			ДанныеАлкогольнойПродукции.АлкогольнаяПродукция = Выборка.Ссылка;
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("АлкогольнаяПродукция", Выборка.Ссылка);
			
			МассивСтрок = ТаблицаСопоставления.НайтиСтроки(ПараметрыОтбора);
			Для Каждого СтрокаТаблицы Из МассивСтрок Цикл
				СписокНоменклатуры = ДанныеАлкогольнойПродукции.ТаблицаСопоставления[СтрокаТаблицы.ИдентификаторУпаковки];
				Если СписокНоменклатуры = Неопределено Тогда
					ДанныеАлкогольнойПродукции.ТаблицаСопоставления.Вставить(СтрокаТаблицы.ИдентификаторУпаковки, Новый Массив);
				КонецЕсли;
				
				СопоставленнаяНоменклатура = Новый Структура;
				СопоставленнаяНоменклатура.Вставить("Номенклатура", СтрокаТаблицы.Номенклатура);
				СопоставленнаяНоменклатура.Вставить("Характеристика", СтрокаТаблицы.Характеристика);
				ДанныеАлкогольнойПродукции.ТаблицаСопоставления[СтрокаТаблицы.ИдентификаторУпаковки].Добавить(СопоставленнаяНоменклатура);
			КонецЦикла;
			
			Результат.Вставить(Выборка.Код, ДанныеАлкогольнойПродукции);
			Продолжить;
		КонецЕсли;
		
		Если Выборка.Ссылка.Пустая() Тогда
			АлкогольнаяПродукцияОбъект = Справочники.КлассификаторАлкогольнойПродукцииЕГАИС.СоздатьЭлемент();
		Иначе
			АлкогольнаяПродукцияОбъект = Выборка.Ссылка.ПолучитьОбъект();
		КонецЕсли;
		
		Для Каждого Колонка Из ТаблицаАлкогольнойПродукции.Колонки Цикл
			Если ЗначениеЗаполнено(Выборка[Колонка.Имя]) Тогда
				АлкогольнаяПродукцияОбъект[Колонка.Имя] = Выборка[Колонка.Имя];
			КонецЕсли;
		КонецЦикла;
		
		АлкогольнаяПродукцияОбъект.Записать();
		
		ДанныеАлкогольнойПродукции.АлкогольнаяПродукция = АлкогольнаяПродукцияОбъект.Ссылка;
		
		Результат.Вставить(Выборка.Код, ДанныеАлкогольнойПродукции);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Загружает остатки ЕГАИС по регистру №1 в базу данных.
//
Функция ЗагрузитьОтветНаЗапросОстатковВРегистре1(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификаторуЗапроса(
		ДанныеДокумента.ИдентификаторЗапроса, Ложь);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке остатков ЕГАИС (регистр №1):
			           |Не найден документ с идентификатором запроса %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получены остатки в регистре №1'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
		
		// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
		// Документ будет получен в следующий итерации загрузки данных.
		ДокументОбъект.Заблокировать();
		
		ДокументОбъект.Дата = ДанныеДокумента.Объект.RestsDate;
		
		ДокументОбъект.ОстаткиПоДаннымЕГАИС.Очистить();
		
		Если ДанныеДокумента.Объект.Products <> Неопределено
			И ДанныеДокумента.Объект.Products.StockPosition.Количество() <> 0 Тогда
			
			АлкогольнаяПродукция = Новый Массив;
			Для Каждого ЭлементДанных Из ДанныеДокумента.Объект.Products.StockPosition Цикл
				АлкогольнаяПродукция.Добавить(ЭлементДанных.Product);
			КонецЦикла;
			
			СоответствиеЗагруженнаяАлкогольнаяПродукция = ЗагрузитьАлкогольнуюПродукцию(АлкогольнаяПродукция);
			
			Для Каждого ЭлементДанных Из ДанныеДокумента.Объект.Products.StockPosition Цикл
				
				СтрокаТЧ = ДокументОбъект.ОстаткиПоДаннымЕГАИС.Добавить();
				Если СоответствиеЗагруженнаяАлкогольнаяПродукция[ЭлементДанных.Product.AlcCode] <> Неопределено Тогда
					СтрокаТЧ.АлкогольнаяПродукция = СоответствиеЗагруженнаяАлкогольнаяПродукция[ЭлементДанных.Product.AlcCode].АлкогольнаяПродукция;
				КонецЕсли;
				
				СтрокаТЧ.Количество = ЭлементДанных.Quantity;
				
				Если ДанныеДокумента.ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
					НомерСправки1 = ЭлементДанных.InformARegId;
					НомерСправки2 = ЭлементДанных.InformBRegId;
				Иначе
					НомерСправки1 = ЭлементДанных.InformF1RegId;
					НомерСправки2 = ЭлементДанных.InformF2RegId;
				КонецЕсли;
				
				ДанныеСправки2 = ИнтеграцияЕГАИСКлиентСервер.СтруктураДанныхСправки2();
				ДанныеСправки2.РегистрационныйНомер = НомерСправки2;
				ДанныеСправки2.Наименование         = НомерСправки2;
				ДанныеСправки2.АлкогольнаяПродукция = СтрокаТЧ.АлкогольнаяПродукция;
				ДанныеСправки2.НомерСправки1        = НомерСправки1;
				
				СтрокаТЧ.Справка2 = СоздатьСправку(ДанныеСправки2, Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросСправки2);
				
			КонецЦикла;
			
		КонецЕсли;
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
		
		ПолноеИмя = РезультатПоиска.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
		ОбъектИзменен = Истина;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументОбъект.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает остатки ЕГАИС по регистру №2 в базу данных.
//
Функция ЗагрузитьОтветНаЗапросОстатковВРегистре2(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификаторуЗапроса(
		ДанныеДокумента.ИдентификаторЗапроса, Ложь);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке остатков ЕГАИС (регистр №2):
			           |Не найден документ с идентификатором запроса %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получены остатки в регистре №2'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
		
		// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
		// Документ будет получен в следующий итерации загрузки данных.
		ДокументОбъект.Заблокировать();
		
		ДокументОбъект.Дата = ДанныеДокумента.Объект.RestsDate;
		
		ДокументОбъект.ОстаткиПоДаннымЕГАИС.Очистить();
		
		Если ДанныеДокумента.Объект.Products <> Неопределено
			И ДанныеДокумента.Объект.Products.ShopPosition.Количество() <> 0 Тогда
			
			АлкогольнаяПродукция = Новый Массив;
			Для Каждого ЭлементДанных Из ДанныеДокумента.Объект.Products.ShopPosition Цикл
				АлкогольнаяПродукция.Добавить(ЭлементДанных.Product);
			КонецЦикла;
			
			СоответствиеЗагруженнаяАлкогольнаяПродукция = ЗагрузитьАлкогольнуюПродукцию(АлкогольнаяПродукция);
			
			Для Каждого ЭлементДанных Из ДанныеДокумента.Объект.Products.ShopPosition Цикл
				
				СтрокаТЧ = ДокументОбъект.ОстаткиПоДаннымЕГАИС.Добавить();
				Если СоответствиеЗагруженнаяАлкогольнаяПродукция[ЭлементДанных.Product.AlcCode] <> Неопределено Тогда
					СтрокаТЧ.АлкогольнаяПродукция = СоответствиеЗагруженнаяАлкогольнаяПродукция[ЭлементДанных.Product.AlcCode].АлкогольнаяПродукция;
				КонецЕсли;
				СтрокаТЧ.Количество = ЭлементДанных.Quantity;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
		
		ПолноеИмя = ДокументОбъект.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
		ОбъектИзменен = Истина;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументОбъект.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает новые штрихкоды акцизных марок (для замены испорченных) в базу данных.
//
Функция ЗагрузитьОтветНаЗапросАкцизныхМарок(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификаторуЗапроса(
		ДанныеДокумента.ИдентификаторЗапроса, Ложь);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке данных акцизных марок ЕГАИС:
			           |Не найден документ с идентификатором запроса %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получены новые акцизные марки (для замены испорченных)'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = Неопределено;
		
		Для Каждого ЭлементДанных Из ДанныеДокумента.Объект.Marks.Mark Цикл
			
			Если ДокументОбъект = Неопределено Тогда
				
				ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
				
				// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
				// Документ будет получен в следующий итерации загрузки данных.
				ДокументОбъект.Заблокировать();
				
			КонецЕсли;
			
			НомерСтроки = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ЭлементДанных.Identity);
			ДокументОбъект.Товары[НомерСтроки - 1].КодАкцизнойМарки = ЭлементДанных.Barcode;
			
		КонецЦикла;
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = (ДокументОбъект = Неопределено);
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
		
		ПолноеИмя = РезультатПоиска.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			РезультатПоиска.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		Если ДокументОбъект <> Неопределено Тогда
			ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
			ОбъектИзменен = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументОбъект.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#Область Отчеты

// Загружает данные отчета Движения между регистрами в базу данных.
//
Функция ЗагрузитьОтветНаЗапросОтчетаДвиженияМеждуРегистрами(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификаторуЗапроса(
		ДанныеДокумента.ИдентификаторЗапроса, Ложь);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке данных отчета ЕГАИС (Движения между регистрами):
			           |Не найден документ с идентификатором запроса %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получены данные отчета ""Движения между регистрами""'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
		
		// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
		// Документ будет получен в следующий итерации загрузки данных.
		ДокументОбъект.Заблокировать();
		
		ДокументОбъект.Дата = ДанныеДокумента.Объект.ReplyDate;
		ДокументОбъект.ДвиженияМеждуРегистрами.Очистить();
		
		Для Каждого ЭлементДанных Из ДанныеДокумента.Объект.History.DocData Цикл
			
			ДанныеДокументаПоТипуЕГАИС = Перечисления.ВидыДокументовЕГАИС.ДанныеДокументаПоТипуЕГАИС(ЭлементДанных.DocType);
			
			СтрокаТЧ = ДокументОбъект.ДвиженияМеждуРегистрами.Добавить();
			СтрокаТЧ.ВидДокумента            = ДанныеДокументаПоТипуЕГАИС.ВидДокументаЕГАИС;
			СтрокаТЧ.ИдентификаторЕГАИС      = ЭлементДанных.DocId;
			СтрокаТЧ.Количество              = ЭлементДанных.Quantity;
			СтрокаТЧ.ОписаниеОперации        = ЭлементДанных.DocType;
			СтрокаТЧ.ДатаРегистрацииДвижений = ЭлементДанных.OperDate;
			СтрокаТЧ.НомерСправки2           = ЭлементДанных.RegForm2;
			
		КонецЦикла;
		
		ВидыДокументовТТН = Новый Массив;
		ВидыДокументовТТН.Добавить(Перечисления.ВидыДокументовЕГАИС.АктТТНОтказ);
		ВидыДокументовТТН.Добавить(Перечисления.ВидыДокументовЕГАИС.АктТТНПодтверждение);
		ВидыДокументовТТН.Добавить(Перечисления.ВидыДокументовЕГАИС.АктТТНРасхождения);
		ВидыДокументовТТН.Добавить(Перечисления.ВидыДокументовЕГАИС.ТТН);
		ВидыДокументовТТН.Добавить(Перечисления.ВидыДокументовЕГАИС.ПустаяСсылка());
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияМеждуРегистрами.ВидДокумента КАК ВидДокумента,
		|	ДвиженияМеждуРегистрами.ИдентификаторЕГАИС КАК ИдентификаторЕГАИС,
		|	ДвиженияМеждуРегистрами.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ДвиженияМеждуРегистрами
		|ИЗ
		|	&ДвиженияМеждуРегистрами КАК ДвиженияМеждуРегистрами
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияМеждуРегистрами.НомерСтроки КАК НомерСтроки,
		|	АктПостановкиНаБалансЕГАИС.Ссылка КАК ДокументОснование
		|ИЗ
		|	ДвиженияМеждуРегистрами КАК ДвиженияМеждуРегистрами
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктПостановкиНаБалансЕГАИС КАК АктПостановкиНаБалансЕГАИС
		|		ПО ((ВЫРАЗИТЬ(ДвиженияМеждуРегистрами.ИдентификаторЕГАИС КАК СТРОКА(50))) = (ВЫРАЗИТЬ(АктПостановкиНаБалансЕГАИС.ИдентификаторЕГАИС КАК СТРОКА(50))))
		|			И (ДвиженияМеждуРегистрами.ВидДокумента В (АктПостановкиНаБалансЕГАИС.ВидДокумента, ЗНАЧЕНИЕ(Перечисление.ВидыДокументовЕГАИС.ПустаяСсылка)))
		|ГДЕ
		|	НЕ АктПостановкиНаБалансЕГАИС.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвиженияМеждуРегистрами.НомерСтроки,
		|	АктСписанияЕГАИС.Ссылка
		|ИЗ
		|	ДвиженияМеждуРегистрами КАК ДвиженияМеждуРегистрами
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктСписанияЕГАИС КАК АктСписанияЕГАИС
		|		ПО ((ВЫРАЗИТЬ(ДвиженияМеждуРегистрами.ИдентификаторЕГАИС КАК СТРОКА(50))) = (ВЫРАЗИТЬ(АктСписанияЕГАИС.ИдентификаторЕГАИС КАК СТРОКА(50))))
		|			И (ДвиженияМеждуРегистрами.ВидДокумента В (АктСписанияЕГАИС.ВидДокумента, ЗНАЧЕНИЕ(Перечисление.ВидыДокументовЕГАИС.ПустаяСсылка)))
		|ГДЕ
		|	НЕ АктСписанияЕГАИС.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвиженияМеждуРегистрами.НомерСтроки,
		|	ВозвратИзРегистра2ЕГАИС.Ссылка
		|ИЗ
		|	ДвиженияМеждуРегистрами КАК ДвиженияМеждуРегистрами
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратИзРегистра2ЕГАИС КАК ВозвратИзРегистра2ЕГАИС
		|		ПО ((ВЫРАЗИТЬ(ДвиженияМеждуРегистрами.ИдентификаторЕГАИС КАК СТРОКА(50))) = (ВЫРАЗИТЬ(ВозвратИзРегистра2ЕГАИС.ИдентификаторЕГАИС КАК СТРОКА(50))))
		|			И (ДвиженияМеждуРегистрами.ВидДокумента В (ЗНАЧЕНИЕ(Перечисление.ВидыДокументовЕГАИС.ВозвратИзРегистра2), ЗНАЧЕНИЕ(Перечисление.ВидыДокументовЕГАИС.ПустаяСсылка)))
		|ГДЕ
		|	НЕ ВозвратИзРегистра2ЕГАИС.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвиженияМеждуРегистрами.НомерСтроки,
		|	ПередачаВРегистр2ЕГАИС.Ссылка
		|ИЗ
		|	ДвиженияМеждуРегистрами КАК ДвиженияМеждуРегистрами
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПередачаВРегистр2ЕГАИС КАК ПередачаВРегистр2ЕГАИС
		|		ПО ((ВЫРАЗИТЬ(ДвиженияМеждуРегистрами.ИдентификаторЕГАИС КАК СТРОКА(50))) = (ВЫРАЗИТЬ(ПередачаВРегистр2ЕГАИС.ИдентификаторЕГАИС КАК СТРОКА(50))))
		|			И (ДвиженияМеждуРегистрами.ВидДокумента В (ЗНАЧЕНИЕ(Перечисление.ВидыДокументовЕГАИС.ПередачаВРегистр2), ЗНАЧЕНИЕ(Перечисление.ВидыДокументовЕГАИС.ПустаяСсылка)))
		|ГДЕ
		|	НЕ ПередачаВРегистр2ЕГАИС.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвиженияМеждуРегистрами.НомерСтроки,
		|	ТТНВходящаяЕГАИС.Ссылка
		|ИЗ
		|	ДвиженияМеждуРегистрами КАК ДвиженияМеждуРегистрами
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
		|		ПО ((ВЫРАЗИТЬ(ДвиженияМеждуРегистрами.ИдентификаторЕГАИС КАК СТРОКА(50))) = (ВЫРАЗИТЬ(ТТНВходящаяЕГАИС.ИдентификаторЕГАИС КАК СТРОКА(50))))
		|			И (ДвиженияМеждуРегистрами.ВидДокумента В (&ВидыДокументовТТН))
		|ГДЕ
		|	НЕ ТТНВходящаяЕГАИС.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвиженияМеждуРегистрами.НомерСтроки,
		|	ТТНИсходящаяЕГАИС.Ссылка
		|ИЗ
		|	ДвиженияМеждуРегистрами КАК ДвиженияМеждуРегистрами
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТТНИсходящаяЕГАИС КАК ТТНИсходящаяЕГАИС
		|		ПО ((ВЫРАЗИТЬ(ДвиженияМеждуРегистрами.ИдентификаторЕГАИС КАК СТРОКА(50))) = (ВЫРАЗИТЬ(ТТНИсходящаяЕГАИС.ИдентификаторЕГАИС КАК СТРОКА(50))))
		|			И (ДвиженияМеждуРегистрами.ВидДокумента В (&ВидыДокументовТТН))
		|ГДЕ
		|	НЕ ТТНИсходящаяЕГАИС.ПометкаУдаления
		|");
		
		Запрос.УстановитьПараметр("ДвиженияМеждуРегистрами", ДокументОбъект.ДвиженияМеждуРегистрами);
		Запрос.УстановитьПараметр("ВидыДокументовТТН",       ВидыДокументовТТН);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ДокументОбъект.ДвиженияМеждуРегистрами[Выборка.НомерСтроки - 1].ДокументОснование = Выборка.ДокументОснование;
		КонецЦикла;
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = Реквизиты.ФорматОбмена;
		
		ПолноеИмя = ДокументОбъект.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
		ОбъектИзменен = Истина;
		
		ЕстьОшибки = Документы.ОтчетЕГАИС.ЕстьРасхожденияВПолученныхДанных(ДокументОбъект.ВидДокумента, ДокументОбъект.Ссылка);
		Если ЕстьОшибки Тогда
			
			ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
			ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
			ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
			ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = ДанныеДокумента.ИдентификаторЗапроса;
			ПараметрыОбновленияСтатуса.СтатусОбработки      = Перечисления.СтатусыОбработкиСообщенийЕГАИС.Ошибка;
			ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
			
			НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
				ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
				ПараметрыОбновленияСтатуса);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументОбъект.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает данные отчета Движения между регистрами в базу данных.
//
Функция ЗагрузитьОтветНаЗапросОтчетаДвиженияПоСправке2(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификаторуЗапроса(
		ДанныеДокумента.ИдентификаторЗапроса, Ложь);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке данных отчета ЕГАИС (Движения по справке 2):
			           |Не найден документ с идентификатором запроса %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получены данные отчета ""Движения по справке 2""'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
		
		// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
		// Документ будет получен в следующий итерации загрузки данных.
		ДокументОбъект.Заблокировать();
		
		Если ДанныеДокумента.ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
			ДокументОбъект.Период = ДанныеДокумента.Объект.HistFormBDate;
			ДанныеДвижений = ДанныеДокумента.Объект.HistoryB.OperationB;
		Иначе
			ДокументОбъект.Период = ДанныеДокумента.Объект.HistForm2Date;
			ДанныеДвижений = ДанныеДокумента.Объект.HistoryF2.OperationB;
		Конецесли;
		
		ДокументОбъект.ДвиженияПоСправке2.Очистить();
		
		Для Каждого ЭлементДанных Из ДанныеДвижений Цикл
			
			ДанныеДокументаПоТипуЕГАИС = Перечисления.ВидыДокументовЕГАИС.ДанныеДокументаПоТипуЕГАИС(ЭлементДанных.DocType);
			
			СтрокаТЧ = ДокументОбъект.ДвиженияПоСправке2.Добавить();
			СтрокаТЧ.ВидДокумента            = ДанныеДокументаПоТипуЕГАИС.ВидДокументаЕГАИС;
			СтрокаТЧ.ИдентификаторЕГАИС      = ЭлементДанных.DocId;
			СтрокаТЧ.Количество              = ЭлементДанных.Quantity;
			СтрокаТЧ.ОписаниеОперации        = ЭлементДанных.Operation;
			СтрокаТЧ.ДатаРегистрацииДвижений = ЭлементДанных.OperDate;
			
		КонецЦикла;
		
		ВидыДокументовТТН = Новый Массив;
		ВидыДокументовТТН.Добавить(Перечисления.ВидыДокументовЕГАИС.АктТТНОтказ);
		ВидыДокументовТТН.Добавить(Перечисления.ВидыДокументовЕГАИС.АктТТНПодтверждение);
		ВидыДокументовТТН.Добавить(Перечисления.ВидыДокументовЕГАИС.АктТТНРасхождения);
		ВидыДокументовТТН.Добавить(Перечисления.ВидыДокументовЕГАИС.ТТН);
		ВидыДокументовТТН.Добавить(Перечисления.ВидыДокументовЕГАИС.ПустаяСсылка());
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияПоСправке2.ВидДокумента КАК ВидДокумента,
		|	ДвиженияПоСправке2.ИдентификаторЕГАИС КАК ИдентификаторЕГАИС,
		|	ДвиженияПоСправке2.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ДвиженияПоСправке2
		|ИЗ
		|	&ДвиженияПоСправке2 КАК ДвиженияПоСправке2
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияПоСправке2.НомерСтроки КАК НомерСтроки,
		|	АктПостановкиНаБалансЕГАИС.Ссылка КАК ДокументОснование
		|ИЗ
		|	ДвиженияПоСправке2 КАК ДвиженияПоСправке2
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктПостановкиНаБалансЕГАИС КАК АктПостановкиНаБалансЕГАИС
		|		ПО ((ВЫРАЗИТЬ(ДвиженияПоСправке2.ИдентификаторЕГАИС КАК СТРОКА(50))) = (ВЫРАЗИТЬ(АктПостановкиНаБалансЕГАИС.ИдентификаторЕГАИС КАК СТРОКА(50))))
		|			И ДвиженияПоСправке2.ВидДокумента = АктПостановкиНаБалансЕГАИС.ВидДокумента
		|ГДЕ
		|	НЕ АктПостановкиНаБалансЕГАИС.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвиженияПоСправке2.НомерСтроки,
		|	АктСписанияЕГАИС.Ссылка
		|ИЗ
		|	ДвиженияПоСправке2 КАК ДвиженияПоСправке2
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктСписанияЕГАИС КАК АктСписанияЕГАИС
		|		ПО ((ВЫРАЗИТЬ(ДвиженияПоСправке2.ИдентификаторЕГАИС КАК СТРОКА(50))) = (ВЫРАЗИТЬ(АктСписанияЕГАИС.ИдентификаторЕГАИС КАК СТРОКА(50))))
		|			И ДвиженияПоСправке2.ВидДокумента = АктСписанияЕГАИС.ВидДокумента
		|ГДЕ
		|	НЕ АктСписанияЕГАИС.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвиженияПоСправке2.НомерСтроки,
		|	ВозвратИзРегистра2ЕГАИС.Ссылка
		|ИЗ
		|	ДвиженияПоСправке2 КАК ДвиженияПоСправке2
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратИзРегистра2ЕГАИС КАК ВозвратИзРегистра2ЕГАИС
		|		ПО ((ВЫРАЗИТЬ(ДвиженияПоСправке2.ИдентификаторЕГАИС КАК СТРОКА(50))) = (ВЫРАЗИТЬ(ВозвратИзРегистра2ЕГАИС.ИдентификаторЕГАИС КАК СТРОКА(50))))
		|			И (ДвиженияПоСправке2.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыДокументовЕГАИС.ВозвратИзРегистра2))
		|ГДЕ
		|	НЕ ВозвратИзРегистра2ЕГАИС.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвиженияПоСправке2.НомерСтроки,
		|	ПередачаВРегистр2ЕГАИС.Ссылка
		|ИЗ
		|	ДвиженияПоСправке2 КАК ДвиженияПоСправке2
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПередачаВРегистр2ЕГАИС КАК ПередачаВРегистр2ЕГАИС
		|		ПО ((ВЫРАЗИТЬ(ДвиженияПоСправке2.ИдентификаторЕГАИС КАК СТРОКА(50))) = (ВЫРАЗИТЬ(ПередачаВРегистр2ЕГАИС.ИдентификаторЕГАИС КАК СТРОКА(50))))
		|			И (ДвиженияПоСправке2.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыДокументовЕГАИС.ПередачаВРегистр2))
		|ГДЕ
		|	НЕ ПередачаВРегистр2ЕГАИС.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвиженияПоСправке2.НомерСтроки,
		|	ТТНВходящаяЕГАИС.Ссылка
		|ИЗ
		|	ДвиженияПоСправке2 КАК ДвиженияПоСправке2
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
		|		ПО ((ВЫРАЗИТЬ(ДвиженияПоСправке2.ИдентификаторЕГАИС КАК СТРОКА(50))) = (ВЫРАЗИТЬ(ТТНВходящаяЕГАИС.ИдентификаторЕГАИС КАК СТРОКА(50))))
		|			И (ДвиженияПоСправке2.ВидДокумента В (&ВидыДокументовТТН))
		|ГДЕ
		|	НЕ ТТНВходящаяЕГАИС.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвиженияПоСправке2.НомерСтроки,
		|	ТТНИсходящаяЕГАИС.Ссылка
		|ИЗ
		|	ДвиженияПоСправке2 КАК ДвиженияПоСправке2
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТТНИсходящаяЕГАИС КАК ТТНИсходящаяЕГАИС
		|		ПО ((ВЫРАЗИТЬ(ДвиженияПоСправке2.ИдентификаторЕГАИС КАК СТРОКА(50))) = (ВЫРАЗИТЬ(ТТНИсходящаяЕГАИС.ИдентификаторЕГАИС КАК СТРОКА(50))))
		|			И (ДвиженияПоСправке2.ВидДокумента В (&ВидыДокументовТТН))
		|ГДЕ
		|	НЕ ТТНИсходящаяЕГАИС.ПометкаУдаления
		|");
		
		Запрос.УстановитьПараметр("ДвиженияПоСправке2", ДокументОбъект.ДвиженияПоСправке2);
		Запрос.УстановитьПараметр("ВидыДокументовТТН" , ВидыДокументовТТН);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ДокументОбъект.ДвиженияПоСправке2[Выборка.НомерСтроки - 1].ДокументОснование = Выборка.ДокументОснование;
		КонецЦикла;
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
		
		ПолноеИмя = ДокументОбъект.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
		ОбъектИзменен = Истина;
		
		ЕстьОшибки = Документы.ОтчетЕГАИС.ЕстьРасхожденияВПолученныхДанных(ДокументОбъект.ВидДокумента, ДокументОбъект.Ссылка);
		Если ЕстьОшибки Тогда
			
			ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
			ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
			ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
			ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = ДанныеДокумента.ИдентификаторЗапроса;
			ПараметрыОбновленияСтатуса.СтатусОбработки      = Перечисления.СтатусыОбработкиСообщенийЕГАИС.Ошибка;
			ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
			
			НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
				ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
				ПараметрыОбновленияСтатуса);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументОбъект.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает данные отчета Информация об организации в базу данных.
//
Функция ЗагрузитьОтветНаЗапросОтчетаИнформацияОбОрганизации(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификаторуЗапроса(
		ДанныеДокумента.ИдентификаторЗапроса, Ложь);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке данных отчета ЕГАИС (Информация об организации):
			           |Не найден документ с идентификатором запроса %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получены данные отчета ""Информация об организации""'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
		
		// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
		// Документ будет получен в следующий итерации загрузки данных.
		ДокументОбъект.Заблокировать();
		
		ДокументОбъект.Период = ДанныеДокумента.Объект.VersionDate;
		ДокументОбъект.ИнформацияОбОрганизацииЕГАИС.Очистить();
		
		СтрокаТЧ = ДокументОбъект.ИнформацияОбОрганизацииЕГАИС.Добавить();
		
		Если ДанныеДокумента.Объект.Client <> Неопределено Тогда
			
			ОрганизацияXDTO = ДанныеОрганизации(ДанныеДокумента.Объект.Client);
			
			СтрокаТЧ.ТипОрганизации     = ТипОрганизации(ДанныеДокумента.Объект.Client);
			СтрокаТЧ.Наименование       = ОрганизацияXDTO.ShortName;
			СтрокаТЧ.НаименованиеПолное = ОрганизацияXDTO.FullName;
			
			Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ОрганизацияXDTO, "INN") Тогда
				СтрокаТЧ.ИНН = СокрЛП(ОрганизацияXDTO.INN);
			КонецЕсли;
			
			Если  ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ОрганизацияXDTO, "KPP") Тогда
				СтрокаТЧ.КПП = СокрЛП(ОрганизацияXDTO.KPP);
			КонецЕсли;
			
			СтрокаТЧ.КодСтраны = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ОрганизацияXDTO.address.Country);
			
			Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ОрганизацияXDTO.address, "RegionCode") Тогда
				СтрокаТЧ.КодРегиона = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ОрганизацияXDTO.address.RegionCode);
			КонецЕсли;
			
			Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ОрганизацияXDTO.address, "Index") Тогда
				СтрокаТЧ.ПочтовыйИндекс = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ОрганизацияXDTO.address.Index);
			КонецЕсли;
			
			СтрокаТЧ.ПредставлениеАдреса = СокрЛП(ОрганизацияXDTO.address.description);
			
			Если Не ЗначениеЗаполнено(СтрокаТЧ.ТипОрганизации) Тогда
				Если ПустаяСтрока(СтрокаТЧ.ИНН) И ПустаяСтрока(СтрокаТЧ.КПП) Тогда
					СтрокаТЧ.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ИностранныйКонтрагент;
				ИначеЕсли ПустаяСтрока(СтрокаТЧ.КПП) И СтрДлина(СокрЛП(СтрокаТЧ.ИНН)) = 12 Тогда
					СтрокаТЧ.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ИндивидуальныйПредпринимательРФ;
				Иначе
					СтрокаТЧ.ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ЮридическоеЛицоРФ;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
		
		ПолноеИмя = ДокументОбъект.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
		ОбъектИзменен = Истина;
		
		ЕстьОшибки = Документы.ОтчетЕГАИС.ЕстьРасхожденияВПолученныхДанных(ДокументОбъект.ВидДокумента, ДокументОбъект.Ссылка);
		Если ЕстьОшибки Тогда
			
			ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
			ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
			ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
			ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = ДанныеДокумента.ИдентификаторЗапроса;
			ПараметрыОбновленияСтатуса.СтатусОбработки      = Перечисления.СтатусыОбработкиСообщенийЕГАИС.Ошибка;
			ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
			
			НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
				ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
				ПараметрыОбновленияСтатуса);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументОбъект.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает данные отчета Необработанные ТТН в базу данных.
//
Функция ЗагрузитьОтветНаЗапросОтчетаНеобработанныеТТН(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификаторуЗапроса(
		ДанныеДокумента.ИдентификаторЗапроса, Ложь);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке данных отчета ЕГАИС (Необработанные ТТН):
			           |Не найден документ с идентификатором запроса %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получены данные отчета ""Необработанные ТТН""'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
		
		// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
		// Документ будет получен в следующий итерации загрузки данных.
		ДокументОбъект.Заблокировать();
		
		ДокументОбъект.Период = ДанныеДокумента.Объект.ReplyDate;
		ДокументОбъект.НеобработанныеТТН.Очистить();
		
		Для Каждого ЭлементДанных Из ДанныеДокумента.Объект.ttnlist.NoAnswer Цикл
			
			СтрокаТЧ = ДокументОбъект.НеобработанныеТТН.Добавить();
			СтрокаТЧ.ИдентификаторЕГАИС  = ЭлементДанных.WbRegID;
			СтрокаТЧ.НомерТТН            = ЭлементДанных.ttnNumber;
			СтрокаТЧ.ДатаТТН             = ЭлементДанных.ttnDate;
			СтрокаТЧ.КодГрузоотправителя = ЭлементДанных.Shipper;
			
		КонецЦикла;
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = Реквизиты.ФорматОбмена;
		
		ПолноеИмя = ДокументОбъект.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
		ОбъектИзменен = Истина;
		
		ЕстьОшибки = Документы.ОтчетЕГАИС.ЕстьРасхожденияВПолученныхДанных(ДокументОбъект.ВидДокумента, ДокументОбъект.Ссылка);
		Если ЕстьОшибки Тогда
			
			ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
			ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
			ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
			ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = ДанныеДокумента.ИдентификаторЗапроса;
			ПараметрыОбновленияСтатуса.СтатусОбработки      = Перечисления.СтатусыОбработкиСообщенийЕГАИС.Ошибка;
			ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
			
			НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
				ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
				ПараметрыОбновленияСтатуса);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументОбъект.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает данные отчета Обработанные чеки в базу данных.
//
Функция ЗагрузитьОтветНаЗапросОтчетаОбработанныеЧеки(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификаторуЗапроса(
		ДанныеДокумента.ИдентификаторЗапроса, Ложь);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке данных отчета ЕГАИС (Обработанные чеки):
			           |Не найден документ с идентификатором запроса %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получены данные отчета ""Обработанные чеки""'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
		
		// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
		// Документ будет получен в следующий итерации загрузки данных.
		ДокументОбъект.Заблокировать();
		
		ДокументОбъект.Период = ДанныеДокумента.Объект.ReplyDate;
		ДокументОбъект.ОбработанныеЧеки.Очистить();
		
		СтрокаТЧ = ДокументОбъект.ОбработанныеЧеки.Добавить();
		СтрокаТЧ.КоличествоЧековПродаж    = ДанныеДокумента.Объект.WriteOffCh;
		СтрокаТЧ.КоличествоЧековНаВозврат = ДанныеДокумента.Объект.ReturnCh;
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
		
		ПолноеИмя = ДокументОбъект.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
		ОбъектИзменен = Истина;
		
		ЕстьОшибки = Документы.ОтчетЕГАИС.ЕстьРасхожденияВПолученныхДанных(ДокументОбъект.ВидДокумента, ДокументОбъект.Ссылка);
		Если ЕстьОшибки Тогда
			
			ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
			ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
			ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
			ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = ДанныеДокумента.ИдентификаторЗапроса;
			ПараметрыОбновленияСтатуса.СтатусОбработки      = Перечисления.СтатусыОбработкиСообщенийЕГАИС.Ошибка;
			ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
			
			НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
				ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
				ПараметрыОбновленияСтатуса);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументОбъект.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает данные отчета Остатки в регистре №1 в базу данных.
//
Функция ЗагрузитьОтветНаЗапросОтчетаОстаткиВРегистре1(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификаторуЗапроса(
		ДанныеДокумента.ИдентификаторЗапроса, Ложь);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке данных отчета ЕГАИС (Остатки в регистре №1):
			           |Не найден документ с идентификатором запроса %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получены данные отчета ""Остатки в регистре №1""'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
		
		// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
		// Документ будет получен в следующий итерации загрузки данных.
		ДокументОбъект.Заблокировать();
		
		ДокументОбъект.Дата = ДанныеДокумента.Объект.RestsDate;
		ДокументОбъект.ОстаткиАлкогольнойПродукции.Очистить();
		
		Если ДанныеДокумента.Объект.Products <> Неопределено Тогда
			
			Для Каждого ЭлементДанных Из ДанныеДокумента.Объект.Products.StockPosition Цикл
				
				СтрокаТЧ = ДокументОбъект.ОстаткиАлкогольнойПродукции.Добавить();
				СтрокаТЧ.КодАлкогольнойПродукции = ЭлементДанных.AlcCode;
				СтрокаТЧ.Количество              = ЭлементДанных.Quantity;
				СтрокаТЧ.НомерСправки2           = ЭлементДанных.Inform2RegId;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
		
		ПолноеИмя = ДокументОбъект.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
		ОбъектИзменен = Истина;
		
		ЕстьОшибки = Документы.ОтчетЕГАИС.ЕстьРасхожденияВПолученныхДанных(ДокументОбъект.ВидДокумента, ДокументОбъект.Ссылка);
		Если ЕстьОшибки Тогда
			
			ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
			ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
			ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
			ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = ДанныеДокумента.ИдентификаторЗапроса;
			ПараметрыОбновленияСтатуса.СтатусОбработки      = Перечисления.СтатусыОбработкиСообщенийЕГАИС.Ошибка;
			ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
			
			НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
				ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
				ПараметрыОбновленияСтатуса);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументОбъект.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает данные отчета Остатки в регистре №2 в базу данных.
//
Функция ЗагрузитьОтветНаЗапросОтчетаОстаткиВРегистре2(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификаторуЗапроса(
		ДанныеДокумента.ИдентификаторЗапроса, Ложь);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке данных отчета ЕГАИС (Остатки в регистре №2):
			           |Не найден документ с идентификатором запроса %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получены данные отчета ""Остатки в регистре №2""'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
		
		// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
		// Документ будет получен в следующий итерации загрузки данных.
		ДокументОбъект.Заблокировать();
		
		ДокументОбъект.Дата = ДанныеДокумента.Объект.RestsDate;
		ДокументОбъект.ОстаткиАлкогольнойПродукции.Очистить();
		
		Если ДанныеДокумента.Объект.Products <> Неопределено Тогда
			
			Для Каждого ЭлементДанных Из ДанныеДокумента.Объект.Products.ShopPosition Цикл
				
				СтрокаТЧ = ДокументОбъект.ОстаткиАлкогольнойПродукции.Добавить();
				СтрокаТЧ.КодАлкогольнойПродукции = ЭлементДанных.AlcCode;
				СтрокаТЧ.Количество              = ЭлементДанных.Quantity;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
		
		ПолноеИмя = ДокументОбъект.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
		ОбъектИзменен = Истина;
		
		ЕстьОшибки = Документы.ОтчетЕГАИС.ЕстьРасхожденияВПолученныхДанных(ДокументОбъект.ВидДокумента, ДокументОбъект.Ссылка);
		Если ЕстьОшибки Тогда
			
			ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
			ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
			ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
			ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = ДанныеДокумента.ИдентификаторЗапроса;
			ПараметрыОбновленияСтатуса.СтатусОбработки      = Перечисления.СтатусыОбработкиСообщенийЕГАИС.Ошибка;
			ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
			
			НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
				ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
				ПараметрыОбновленияСтатуса);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументОбъект.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает данные отчета Остатки в регистре №3 в базу данных.
//
Функция ЗагрузитьОтветНаЗапросОтчетаОстаткиВРегистре3(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификаторуЗапроса(
		ДанныеДокумента.ИдентификаторЗапроса, Ложь);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке данных отчета ЕГАИС (Остатки в регистре №3):
			           |Не найден документ с идентификатором запроса %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получены данные отчета ""Остатки в регистре №3""'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
		
		// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
		// Документ будет получен в следующий итерации загрузки данных.
		ДокументОбъект.Заблокировать();
		
		ДокументОбъект.Дата = ДанныеДокумента.Объект.RestsDate;
		ДокументОбъект.АкцизныеМарки.Очистить();
		
		Если ДанныеДокумента.Объект.MarkInfo <> Неопределено Тогда
			
			Для Каждого ЭлементДанных Из ДанныеДокумента.Объект.MarkInfo Цикл
				
				Для Каждого КодАкцизнойМарки Из ЭлементДанных.amc Цикл
					
					СтрокаТЧ = ДокументОбъект.АкцизныеМарки.Добавить();
					СтрокаТЧ.КодАкцизнойМарки = КодАкцизнойМарки;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
		
		ПолноеИмя = ДокументОбъект.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
		ОбъектИзменен = Истина;
		
		ЕстьОшибки = Документы.ОтчетЕГАИС.ЕстьРасхожденияВПолученныхДанных(ДокументОбъект.ВидДокумента, ДокументОбъект.Ссылка);
		Если ЕстьОшибки Тогда
			
			ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
			ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
			ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
			ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = ДанныеДокумента.ИдентификаторЗапроса;
			ПараметрыОбновленияСтатуса.СтатусОбработки      = Перечисления.СтатусыОбработкиСообщенийЕГАИС.Ошибка;
			ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
			
			НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
				ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
				ПараметрыОбновленияСтатуса);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументОбъект.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает данные отчета История справок 2 в базу данных.
//
Функция ЗагрузитьОтветНаЗапросОтчетаИсторияСправок2(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификаторуЗапроса(
		ДанныеДокумента.ИдентификаторЗапроса, Ложь);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке данных отчета ЕГАИС (История справок 2):
			           |Не найден документ с идентификатором запроса %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получены данные отчета ""История справок 2""'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
		
		// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
		// Документ будет получен в следующий итерации загрузки данных.
		ДокументОбъект.Заблокировать();
		
		ДокументОбъект.Дата = ДанныеДокумента.Объект.HistForm2Date;
		ДокументОбъект.ИсторияСправок2.Очистить();
		
		Если ДанныеДокумента.Объект.ParentHist <> Неопределено Тогда
			
			Для Каждого ЭлементДанных Из ДанныеДокумента.Объект.ParentHist.step Цикл
				
				СтрокаТЧ = ДокументОбъект.ИсторияСправок2.Добавить();
				СтрокаТЧ.Шаг                            = ЭлементДанных.lev;
				СтрокаТЧ.РегистрационныйНомер           = ЭлементДанных.Form2;
				СтрокаТЧ.РегистрационныйНомерПоставщика = ЭлементДанных.parentForm2;
				СтрокаТЧ.ГрузоотправительКод            = ЭлементДанных.Shipper;
				СтрокаТЧ.ГрузополучательКод             = ЭлементДанных.Consignee;
				СтрокаТЧ.НомерТТН                       = ЭлементДанных.WBRegId;
				СтрокаТЧ.Количество                     = ЭлементДанных.amount;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
		ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
		ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
		ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = Реквизиты.ИдентификаторЗапроса;
		ПараметрыОбновленияСтатуса.СтатусОбработки      = Реквизиты.СтатусОбработки;
		ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
		
		ПолноеИмя = ДокументОбъект.Ссылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
			ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
			ПараметрыОбновленияСтатуса);
		
		ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
		ОбъектИзменен = Истина;
		
		ЕстьОшибки = Документы.ОтчетЕГАИС.ЕстьРасхожденияВПолученныхДанных(ДокументОбъект.ВидДокумента, ДокументОбъект.Ссылка);
		Если ЕстьОшибки Тогда
			
			ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
			ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Ложь;
			ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
			ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = ДанныеДокумента.ИдентификаторЗапроса;
			ПараметрыОбновленияСтатуса.СтатусОбработки      = Перечисления.СтатусыОбработкиСообщенийЕГАИС.Ошибка;
			ПараметрыОбновленияСтатуса.ФорматОбмена         = РезультатПоиска.ФорматОбмена;
			
			НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
				ДокументОбъект.Ссылка, ДанныеДокумента.Операция,
				ПараметрыОбновленияСтатуса);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументОбъект.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает историю справок 2 по ТТН в базу данных.
//
Функция ЗагрузитьИсториюСправок2ПоТТН(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПоиска = НайтиОбъектПоИдентификатору(
		Метаданные.Документы.ТТНВходящаяЕГАИС,
		"ИдентификаторЕГАИС",
		ДанныеДокумента.Объект.Header.WBRegId);
	
	Если РезультатПоиска = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке данных истории справок 2 по ТТН:
			           |Не найден документ ТТН ЕГАИС (входящая) с идентификатором ЕГАИС %1.'"),
			ДанныеДокумента.Объект.Header.WBRegId);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИсходящееСообщение = Неопределено;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             РезультатПоиска.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получение истории справок 2 по ТТН'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         РезультатПоиска.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ДокументОбъект = РезультатПоиска.Ссылка.ПолучитьОбъект();
		
		// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
		// Документ будет получен в следующий итерации загрузки данных.
		ДокументОбъект.Заблокировать();
		
		Для Каждого ЭлементДанных Из ДанныеДокумента.Объект.Content.Position Цикл
			
			Для Каждого ЭлементДанныхШаг Из ЭлементДанных.HistF2.step Цикл
				
				СтрокаТЧ = ДокументОбъект.ИсторияСправок2.Добавить();
				СтрокаТЧ.ИдентификаторСтроки = ЭлементДанных.Identity;
				
				СтрокаТЧ.Шаг                            = ЭлементДанныхШаг.lev;
				СтрокаТЧ.РегистрационныйНомер           = ЭлементДанныхШаг.Form2;
				СтрокаТЧ.РегистрационныйНомерПоставщика = ЭлементДанныхШаг.parentForm2;
				СтрокаТЧ.ГрузоотправительКод            = ЭлементДанныхШаг.Shipper;
				СтрокаТЧ.ГрузополучательКод             = ЭлементДанныхШаг.Consignee;
				СтрокаТЧ.НомерТТН                       = ЭлементДанныхШаг.WBRegId;
				СтрокаТЧ.Количество                     = ЭлементДанныхШаг.amount;
				
			КонецЦикла;
			
		КонецЦикла;
		
		ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
		ОбъектИзменен = Истина;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = РезультатПоиска.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДокументОбъект.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

// Загружает справки по формам А и Б в базу данных.
//
Функция ЗагрузитьСправку(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт
	
	ИсходящееСообщение = ИсходящееСообщение(ДанныеДокумента.ИдентификаторЗапроса);
	
	Если ИсходящееСообщение = Неопределено Тогда
		
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'При загрузке ответа на запрос справок:
			           |Не найден исходящий запрос с идентификатором %1.'"),
			ДанныеДокумента.ИдентификаторЗапроса);
		
	КонецЕсли;
	
	Ссылка = СоздатьСправку(
		ДанныеСправки(ДанныеДокумента, ДанныеДокумента.Операция),
		ДанныеДокумента.Операция);
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             Неопределено);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             НСтр("ru = 'Получены данные справки №1 (справки №2)'"));
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС);
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = Неопределено;
	ВозвращаемоеЗначение.НовыйСтатус       = Неопределено;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает и обрабатывает квитанцию из ЕГАИС.
//
Функция ЗагрузитьКвитанцию(ДанныеДокумента, ОрганизацияЕГАИС, ДополнительныеПараметры = Неопределено) Экспорт

	Если ДанныеДокумента.Объект.Result <> Неопределено Тогда
		ДанныеДокумента.Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияПолученЕГАИС;
	ИначеЕсли ДанныеДокумента.Объект.OperationResult <> Неопределено Тогда
		ДанныеДокумента.Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияПроведенЕГАИС;
	КонецЕсли;
	
	РезультатПоиска = НайтиОбъектПоИдентификаторуЗапроса(ДанныеДокумента.ИдентификаторЗапроса);
	Если РезультатПоиска = Неопределено
		И ЗначениеЗаполнено(ДанныеДокумента.Объект.Identity)
		И ЗначениеЗаполнено(ДанныеДокумента.Объект.DocType) Тогда
		РезультатПоиска = НайтиОбъектПоИдентификаторуТипаЕГАИСПриЗагрузкеКвитанции(ДанныеДокумента.Объект.Identity, ДанныеДокумента.Объект.DocType, ДанныеДокумента.Операция);
	КонецЕсли;
	
	Если РезультатПоиска = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.СтопЛист.Получить(РезультатПоиска.Ссылка) <> Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ДополнительныеПараметры.ТекущийОбъект = РезультатПоиска.Ссылка;
		
	КонецЕсли;
	
	ИдентификаторЕГАИС = "";
	Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ДанныеДокумента.Объект, "RegID") Тогда
		ИдентификаторЕГАИС = Строка(ДанныеДокумента.Объект.RegID);
	КонецЕсли;
	
	Если ДанныеДокумента.Объект.Result <> Неопределено Тогда
		
		Возврат ЗагрузитьКвитанциюПолученЕГАИС(
			ДанныеДокумента,
			РезультатПоиска,
			ИдентификаторЕГАИС,
			ОрганизацияЕГАИС);
		
	ИначеЕсли ДанныеДокумента.Объект.OperationResult <> Неопределено Тогда
		
		Возврат ЗагрузитьКвитанциюПроведенЕГАИС(
			ДанныеДокумента,
			РезультатПоиска,
			ИдентификаторЕГАИС,
			ОрганизацияЕГАИС);
		
	Иначе
		
		ВызватьИсключение НСтр("ru = 'Неизвестный тип загружаемой квитанции.'");
		
	КонецЕсли;
	
КонецФункции

// Загружает информацию о фиксации документа в ЕГАИС.
//
Функция ЗагрузитьКвитанциюПолученЕГАИС(ДанныеДокумента, ДанныеОснованияКвитанции, ИдентификаторЕГАИС, ОрганизацияЕГАИС) Экспорт
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	Если ВРег(ДанныеДокумента.Объект.Result.Conclusion) = ВРег("Rejected") Тогда
		СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЕГАИС.Ошибка;
	Иначе
		СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЕГАИС.ОбрабатываетсяЕГАИС;
	КонецЕслИ;
	
	Если ДанныеОснованияКвитанции.ИсходящееСообщение = Неопределено Тогда
		ИсходящееСообщение = ИсходящееСообщение(ДанныеДокумента.ИдентификаторЗапроса);
	Иначе
		ИсходящееСообщение = ДанныеОснованияКвитанции.ИсходящееСообщение;
	КонецЕсли;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             ДанныеОснованияКвитанции.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             ДанныеДокумента.Объект.Result.Comments);
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      СтатусОбработки);
	Реквизиты.Вставить("ОперацияКвитанции",    ДанныеОснованияКвитанции.Операция);
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
		
	Если ДанныеОснованияКвитанции.Ссылка <> Неопределено Тогда
		ЭтоДокумент = Метаданные.Документы.Содержит(ДанныеОснованияКвитанции.Ссылка.Метаданные());
	Иначе
		ЭтоДокумент = Ложь;
	КонецЕсли;
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		ОтразитьФиксацию = ЭтоДокумент;
		
		Если ОтразитьФиксацию И ЗначениеЗаполнено(ДанныеОснованияКвитанции.Ссылка) Тогда
			
			ЗаписыватьДокумент = ЗначениеЗаполнено(ИдентификаторЕГАИС);
			
			ПолноеИмя = ДанныеОснованияКвитанции.Ссылка.Метаданные().ПолноеИмя();
			МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
			
			Если ЗаписыватьДокумент Тогда
				
				ДокументОбъект = ДанныеОснованияКвитанции.Ссылка.ПолучитьОбъект();
				
				// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
				// Документ будет получен в следующий итерации загрузки данных.
				ДокументОбъект.Заблокировать();
				
				Если Не ЗначениеЗаполнено(ДокументОбъект.ИдентификаторЕГАИС) Тогда
					ДокументОбъект.ИдентификаторЕГАИС = ИдентификаторЕГАИС;
				КонецЕсли;
				
				ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
				ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Не ЗаписыватьДокумент;
				ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
				ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = ДанныеДокумента.ИдентификаторЗапроса;
				ПараметрыОбновленияСтатуса.СтатусОбработки      = СтатусОбработки;
				ПараметрыОбновленияСтатуса.ОперацияКвитанции    = ДанныеОснованияКвитанции.Операция;
				ПараметрыОбновленияСтатуса.ФорматОбмена         = ДанныеОснованияКвитанции.ФорматОбмена;
				
				НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
					ДанныеОснованияКвитанции.Ссылка,
					ДанныеДокумента.Операция,
					ПараметрыОбновленияСтатуса);
				
				ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
				ОбъектИзменен = Истина;
				
			Иначе
				
				ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
				ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Не ЗаписыватьДокумент;
				ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = ДанныеДокумента.ИдентификаторЗапроса;
				ПараметрыОбновленияСтатуса.СтатусОбработки      = СтатусОбработки;
				ПараметрыОбновленияСтатуса.ОперацияКвитанции    = ДанныеОснованияКвитанции.Операция;
				ПараметрыОбновленияСтатуса.ФорматОбмена         = ДанныеОснованияКвитанции.ФорматОбмена;
				
				НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
					ДанныеОснованияКвитанции.Ссылка,
					ДанныеДокумента.Операция,
					ПараметрыОбновленияСтатуса);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ДанныеОснованияКвитанции.ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ?(ЭтоДокумент, ДанныеОснованияКвитанции.Ссылка, Неопределено);
	ВозвращаемоеЗначение.ДокументОснование = ДанныеОснованияКвитанции.ДокументОснование;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Загружает информацию о проведении документа в ЕГАИС.
//
Функция ЗагрузитьКвитанциюПроведенЕГАИС(ДанныеДокумента, ДанныеОснованияКвитанции, ИдентификаторЕГАИС, ОрганизацияЕГАИС) Экспорт
	
	ОперацияВыполнена = (ВРег(ДанныеДокумента.Объект.OperationResult.OperationResult) <> ВРег("Rejected"));
	
	ДатаРегистрацииДвижений = Неопределено;
	
	Если ВРег(ДанныеДокумента.Объект.OperationResult.OperationName) = ВРег("Confirm")
		И ОперацияВыполнена Тогда
		
		СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЕГАИС.ДокументПроведен;
		
		Если ДокументСоответствуетТипуЕГАИС(ДанныеОснованияКвитанции.Ссылка, ДанныеДокумента.Объект.DocType) Тогда
			ДатаРегистрацииДвижений = ДанныеДокумента.Объект.OperationResult.OperationDate;
		КонецЕсли;
		
	ИначеЕсли ВРег(ДанныеДокумента.Объект.OperationResult.OperationName) = ВРег("UnConfirm")
		И ОперацияВыполнена Тогда
		
		СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЕГАИС.ДокументРаспроведен;
		ДатаРегистрацииДвижений = '00010101'
		
	Иначе
		
		СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЕГАИС.Ошибка;
		
	КонецЕсли;
	
	Если ДанныеОснованияКвитанции.ИсходящееСообщение = Неопределено Тогда
		ИсходящееСообщение = ИсходящееСообщение(ДанныеДокумента.ИдентификаторЗапроса);
	Иначе
		ИсходящееСообщение = ДанныеОснованияКвитанции.ИсходящееСообщение;
	КонецЕсли;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТипСообщения",         Перечисления.ТипыЗапросовЕГАИС.Входящий);
	Реквизиты.Вставить("Операция",             ДанныеДокумента.Операция);
	Реквизиты.Вставить("Документ",             ДанныеОснованияКвитанции.Ссылка);
	Реквизиты.Вставить("СообщениеОснование",   ИсходящееСообщение);
	Реквизиты.Вставить("Описание",             ДанныеДокумента.Объект.OperationResult.OperationComment);
	Реквизиты.Вставить("ИдентификаторЗапроса", ДанныеДокумента.ИдентификаторЗапроса);
	Реквизиты.Вставить("ФорматОбмена",         ДанныеДокумента.ФорматОбмена);
	Реквизиты.Вставить("ОрганизацияЕГАИС",     ОрганизацияЕГАИС);
	Реквизиты.Вставить("СтатусОбработки",      СтатусОбработки);
	Реквизиты.Вставить("ОперацияКвитанции",    ДанныеОснованияКвитанции.Операция);
	
	НовыйСтатус   = Неопределено;
	ОбъектИзменен = Ложь;
	
	РезультатДобавленияЗаписи = ИнтеграцияЕГАИС.ДобавитьЗаписьВПротоколОбмена(
		ДанныеДокумента.ТекстXML,
		Реквизиты);
	
	Если РезультатДобавленияЗаписи.НовоеСообщение Тогда
		
		Если ЗначениеЗаполнено(ДанныеОснованияКвитанции.Ссылка) Тогда
			
			Если СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЕГАИС.ДокументПроведен Тогда
				ЗаписыватьДокумент = ЗначениеЗаполнено(ИдентификаторЕГАИС)
					Или ДатаРегистрацииДвижений <> Неопределено;
			Иначе
				ЗаписыватьДокумент = Ложь;
			КонецЕсли;
			
			ПолноеИмя = ДанныеОснованияКвитанции.Ссылка.Метаданные().ПолноеИмя();
			МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
			
			Если ЗаписыватьДокумент Тогда
				
				ДокументОбъект = ДанныеОснованияКвитанции.Ссылка.ПолучитьОбъект();
				
				// Если выполнить блокировку объекта не удалось, то будет выдано исключение.
				// Документ будет получен в следующий итерации загрузки данных.
				ДокументОбъект.Заблокировать();
				
				Если ЗначениеЗаполнено(ИдентификаторЕГАИС)
					И Не ЗначениеЗаполнено(ДокументОбъект.ИдентификаторЕГАИС) Тогда
					ДокументОбъект.ИдентификаторЕГАИС = ИдентификаторЕГАИС;
				КонецЕсли;
				Если ДатаРегистрацииДвижений <> Неопределено Тогда
					ДокументОбъект.ДатаРегистрацииДвижений = ДатаРегистрацииДвижений;
				КонецЕсли;
				
				ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
				ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Не ЗаписыватьДокумент;
				ПараметрыОбновленияСтатуса.ДокументОбъект       = ДокументОбъект;
				ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = ДанныеДокумента.ИдентификаторЗапроса;
				ПараметрыОбновленияСтатуса.СтатусОбработки      = СтатусОбработки;
				ПараметрыОбновленияСтатуса.ОперацияКвитанции    = ДанныеОснованияКвитанции.Операция;
				ПараметрыОбновленияСтатуса.ФорматОбмена         = ДанныеОснованияКвитанции.ФорматОбмена;
				
				НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
					ДанныеОснованияКвитанции.Ссылка,
					ДанныеДокумента.Операция,
					ПараметрыОбновленияСтатуса);
				
				ДокументОбъект.Записать(РежимЗаписи(ДокументОбъект));
				ОбъектИзменен = Истина;
				
			Иначе
				
				ПараметрыОбновленияСтатуса = ПараметрыОбновленияСтатуса();
				ПараметрыОбновленияСтатуса.ОбновлятьДвижения    = Не ЗаписыватьДокумент;
				ПараметрыОбновленияСтатуса.ИдентификаторЗапроса = ДанныеДокумента.ИдентификаторЗапроса;
				ПараметрыОбновленияСтатуса.СтатусОбработки      = СтатусОбработки;
				ПараметрыОбновленияСтатуса.ОперацияКвитанции    = ДанныеОснованияКвитанции.Операция;
				ПараметрыОбновленияСтатуса.ФорматОбмена         = ДанныеОснованияКвитанции.ФорматОбмена;
				
				НовыйСтатус = МенеджерОбъекта.ОбновитьСтатусПослеПолученияДанных(
					ДанныеОснованияКвитанции.Ссылка,
					ДанныеДокумента.Операция,
					ПараметрыОбновленияСтатуса);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение = ИнтеграцияЕГАИСКлиентСервер.СтруктураИзменения();
	ВозвращаемоеЗначение.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
	ВозвращаемоеЗначение.Операция         = ДанныеДокумента.Операция;
	ВозвращаемоеЗначение.ФорматОбмена     = ДанныеДокумента.ФорматОбмена;
	
	ВозвращаемоеЗначение.ТекстОшибки           = "";
	ВозвращаемоеЗначение.ПодготовленоКПередаче = Ложь;
	ВозвращаемоеЗначение.ПереданоВУТМ          = Ложь;
	ВозвращаемоеЗначение.Принято               = Истина;
	
	ВозвращаемоеЗначение.ИсходящееСообщение = ДанныеОснованияКвитанции.ИсходящееСообщение;
	ВозвращаемоеЗначение.ВходящееСообщение  = РезультатДобавленияЗаписи.Ссылка;
	
	ВозвращаемоеЗначение.Объект            = ДанныеОснованияКвитанции.Ссылка;
	ВозвращаемоеЗначение.ДокументОснование = ДанныеОснованияКвитанции.ДокументОснование;
	ВозвращаемоеЗначение.НовыйСтатус       = НовыйСтатус;
	ВозвращаемоеЗначение.ОбъектИзменен     = ОбъектИзменен;
	
	СлужебныеДанные = Новый Массив;
	СлужебныеДанные.Добавить(
		СлужебныеДанные(
			ОрганизацияЕГАИС,
			ДанныеДокумента.Операция,
			ДанныеДокумента.АдресЗапроса));
	ВозвращаемоеЗначение.Вставить("СлужебныеДанные", СлужебныеДанные);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Проверяет соответствие типа ЕГАИС и передаваемого документа.
//
Функция ДокументСоответствуетТипуЕГАИС(ДокументСсылка, ТипЕГАИС)
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ТТНВходящаяЕГАИС")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ТТНИсходящаяЕГАИС") Тогда
		
		Возврат ВРег(ТипЕГАИС) = ВРег("WayBill")
			ИЛИ ВРег(ТипЕГАИС) = ВРег("WayBill_v2")
			ИЛИ ВРег(ТипЕГАИС) = ВРег("WayBill_v3");
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АктПостановкиНаБалансЕГАИС") Тогда
		
		Возврат ВРег(ТипЕГАИС) = ВРег("ActChargeOn")
			ИЛИ ВРег(ТипЕГАИС) = ВРег("ActChargeOn_v2")
			ИЛИ ВРег(ТипЕГАИС) = ВРег("ActChargeOnShop_v2");
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АктСписанияЕГАИС") Тогда
		
		Возврат ВРег(ТипЕГАИС) = ВРег("ActWriteOff")
			ИЛИ ВРег(ТипЕГАИС) = ВРег("ActWriteOff_v2")
			ИЛИ ВРег(ТипЕГАИС) = ВРег("ActWriteOff_v3")
			ИЛИ ВРег(ТипЕГАИС) = ВРег("ActWriteOffShop_v2");
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратИзРегистра2ЕГАИС") Тогда
		
		Возврат ВРег(ТипЕГАИС) = ВРег("TransferFromShop");
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПередачаВРегистр2ЕГАИС") Тогда
		
		Возврат ВРег(ТипЕГАИС) = ВРег("TransferToShop");
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

// Получает данные справок А и Б в виде структуры для последующей загрузки в базу.
//
Функция ДанныеСправки(ДанныеДокумента, ВидСправки)
	
	// Соответствие между реквизитами справки (Ключ) и свойствами объекта XDTO (Значение)
	СоответствияРеквизитов = Новый Соответствие;
	СоответствияРеквизитов.Вставить("Количество", "Quantity");
	
	Если ВидСправки = Перечисления.ВидыДокументовЕГАИС.ОтветНаЗапросСправки1 Тогда
		
		ДанныеСправки = ИнтеграцияЕГАИСКлиентСервер.СтруктураДанныхСправки1();
		
		Если ДанныеДокумента.ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
			
			СоответствияРеквизитов.Вставить("РегистрационныйНомер", "InformARegId");
			СоответствияРеквизитов.Вставить("НомерТТН", "TTNNumber");
			СоответствияРеквизитов.Вставить("ДатаТТН", "TTNDate");
			СоответствияРеквизитов.Вставить("ДатаОтгрузки", "ShippingDate");
			
			ДанныеСправки.Грузоотправитель = ЗагрузитьОрганизацию(ДанныеДокумента.Объект.Shipper);
			ДанныеСправки.Грузополучатель = ЗагрузитьОрганизацию(ДанныеДокумента.Объект.Consignee);
			
		Иначе
			
			СоответствияРеквизитов.Вставить("РегистрационныйНомер", "InformF1RegId");
			СоответствияРеквизитов.Вставить("НомерТТН", "OriginalDocNumber");
			СоответствияРеквизитов.Вставить("ДатаТТН", "OriginalDocDate");
			
			Если ДанныеДокумента.Объект.OriginalClient <> Неопределено Тогда
				ДанныеСправки.Грузоотправитель = ЗагрузитьОрганизацию(ДанныеДокумента.Объект.OriginalClient);
			КонецЕсли;
			
		КонецЕсли;
		
		СоответствияРеквизитов.Вставить("ДатаРозлива", "BottlingDate");
		СоответствияРеквизитов.Вставить("НомерПодтвержденияЕГАИС", "EGAISNumber");
		СоответствияРеквизитов.Вставить("ДатаПодтвержденияЕГАИС", "EGAISDate");
		
	Иначе
		
		ДанныеСправки = ИнтеграцияЕГАИСКлиентСервер.СтруктураДанныхСправки2();
		
		Если ДанныеДокумента.ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
			СоответствияРеквизитов.Вставить("РегистрационныйНомер", "InformBRegId");
		Иначе
			СоответствияРеквизитов.Вставить("РегистрационныйНомер", "InformF2RegId");
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого КлючЗначение Из СоответствияРеквизитов Цикл
		ДанныеСправки[КлючЗначение.Ключ] = ДанныеДокумента.Объект[КлючЗначение.Значение];
	КонецЦикла;
	
	АлкогольнаяПродукция = Новый Массив;
	АлкогольнаяПродукция.Добавить(ДанныеДокумента.Объект.Product);
	
	СоответствиеЗагруженнаяАлкогольнаяПродукция = ЗагрузитьАлкогольнуюПродукцию(АлкогольнаяПродукция);
	
	Если СоответствиеЗагруженнаяАлкогольнаяПродукция[ДанныеДокумента.Объект.Product.AlcCode] <> Неопределено Тогда
		ДанныеСправки.АлкогольнаяПродукция = СоответствиеЗагруженнаяАлкогольнаяПродукция[ДанныеДокумента.Объект.Product.AlcCode].АлкогольнаяПродукция;
	КонецЕсли;
	
	Возврат ДанныеСправки;
	
КонецФункции

// Возвращает объект XDTO организации, которую требуется загрузить.
//
Функция ДанныеОрганизации(ДанныеОрганизации)
	
	Объект = Неопределено;
	
	Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ДанныеОрганизации, "UL") И ДанныеОрганизации.UL <> Неопределено Тогда
		Объект = ДанныеОрганизации.UL;
	ИначеЕсли ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ДанныеОрганизации, "FL") И ДанныеОрганизации.FL <> Неопределено Тогда
		Объект = ДанныеОрганизации.FL;
	ИначеЕсли ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ДанныеОрганизации, "FO") И ДанныеОрганизации.FO <> Неопределено Тогда
		Объект = ДанныеОрганизации.FO;
	ИначеЕсли ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ДанныеОрганизации, "TS") И ДанныеОрганизации.TS <> Неопределено Тогда
		Объект = ДанныеОрганизации.TS;
	Иначе
		Объект = ДанныеОрганизации;
	КонецЕсли;
	
	Возврат Объект;
	
КонецФункции

// Возвращает тип организации ЕГАИС.
//
// Параметры:
//  ДанныеОрганизации - ОбъектXDTO - Данные организации ЕГАИС.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыОрганизацийЕГАИС - Тип организации ЕГАИС.
//
Функция ТипОрганизации(ДанныеОрганизации)
	
	ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ПустаяСсылка();
	
	Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ДанныеОрганизации, "UL") И ДанныеОрганизации.UL <> Неопределено Тогда
		ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ЮридическоеЛицоРФ;
	ИначеЕсли ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ДанныеОрганизации, "FL") И ДанныеОрганизации.FL <> Неопределено Тогда
		ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ИндивидуальныйПредпринимательРФ;
	ИначеЕсли ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ДанныеОрганизации, "FO") И ДанныеОрганизации.FO <> Неопределено Тогда
		ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.ИностранныйКонтрагент;
	ИначеЕсли ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ДанныеОрганизации, "TS") И ДанныеОрганизации.TS <> Неопределено Тогда
		ТипОрганизации = Перечисления.ТипыОрганизацийЕГАИС.КонтрагентТаможенногоСоюза;
	КонецЕсли;
	
	Возврат ТипОрганизации;
	
КонецФункции

// Возвращает структура данных для удаления запросов из УТМ.
//
// Параметры:
//  ОрганизацияЕГАИС - СправочникСсылка.КлассификаторОрганизацийЕГАИС - Организация ЕГАИС.
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция.
//  АдресЗапроса - Строка - Адрес запроса.
// 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * ОрганизацияЕГАИС - СправочникСсылка.КлассификаторОрганизацийЕГАИС - Организация ЕГАИС.
//   * Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция.
//   * АдресЗапроса - Строка - Адрес запроса.
//
Функция СлужебныеДанные(ОрганизацияЕГАИС, Операция, АдресЗапроса)
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ОрганизацияЕГАИС", ОрганизацияЕГАИС);
	ВозвращаемоеЗначение.Вставить("Операция",         Операция);
	ВозвращаемоеЗначение.Вставить("АдресЗапроса",     АдресЗапроса);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Осуществляет поиск элементов справочника ШтрихкодыУпаковокТоваров по значениям штрихкодов акцизных марок.
//
Функция ШтрихкодыУпаковок(ЗначенияШтрихкодов) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.ЗначениеШтрихкода КАК ЗначениеШтрихкода
	|ПОМЕСТИТЬ Штрихкоды
	|ИЗ
	|	&ЗначенияШтрихкодов КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Штрихкоды.ЗначениеШтрихкода КАК ЗначениеШтрихкода,
	|	ШтрихкодыУпаковокТоваров.Ссылка КАК Ссылка,
	|	ШтрихкодыУпаковокТоваров.ХешСумма КАК ХешСумма
	|ИЗ
	|	Штрихкоды КАК Штрихкоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	|		ПО (ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода = Штрихкоды.ЗначениеШтрихкода)
	|");
	
	Запрос.УстановитьПараметр("ЗначенияШтрихкодов", ЗначенияШтрихкодов);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

// Выполняет поиск акцизных марок в дереве упаковок.
//
// Параметры:
//  ДеревоУпаковок - ДеревоЗначений - Дерево упаковок.
//  ЗначенияШтрихкодов - Массив - Массив найденных штрихкодов.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Найденные штрихкоды.
Функция ЗначенияШтрихкодовИзДереваУпаковок(ДеревоУпаковок, ЗначенияШтрихкодов = Неопределено) Экспорт
	
	Если ЗначенияШтрихкодов = Неопределено Тогда
		Результат = Новый ТаблицаЗначений;
		Результат.Колонки.Добавить("ЗначениеШтрихкода", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(200)));
	Иначе
		Результат = ЗначенияШтрихкодов;
	КонецЕсли;
	
	Для Каждого СтрокаДерева Из ДеревоУпаковок.Строки Цикл
		
		ЗначенияШтрихкодовИзДереваУпаковок(
			СтрокаДерева,
			Результат);
		
		Если Не ПустаяСтрока(СтрокаДерева.ЗначениеШтрихкода) Тогда
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.ЗначениеШтрихкода = СтрокаДерева.ЗначениеШтрихкода;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Статусы

// Обработчик подписки на событие "При записи" для документов, влияющий на расчет статуса Акта постановки на баланс ЕГАИС.
//
// Параметры:
//  Источник - ДокументОбъект - источник события.
//  Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияАктПостановкиНаБалансЕГАИС(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусОформленияАктПостановкиНаБалансЕГАИС(Источник, Отказ);
	
КонецПроцедуры

// Обработчик подписки на событие "При записи" для документов, влияющий на расчет статуса Акта списания ЕГАИС.
//
// Параметры:
//  Источник - ДокументОбъект - источник события.
//  Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияАктСписанияЕГАИС(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусОформленияАктСписанияЕГАИС(Источник, Отказ);
	
КонецПроцедуры

// Обработчик подписки на событие "При записи" для документов, влияющий на расчет статуса Возврат из регистра №2 ЕГАИС.
//
// Параметры:
//  Источник - ДокументОбъект - источник события.
//  Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияВозвратИзРегистра2ЕГАИС(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусОформленияВозвратИзРегистра2ЕГАИС(Источник, Отказ);
	
КонецПроцедуры

// Обработчик подписки на событие "При записи" для документов, влияющий на расчет статуса Передача в регистр №2 ЕГАИС.
//
// Параметры:
//  Источник - ДокументОбъект - источник события.
//  Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияПередачаВРегистр2ЕГАИС(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусОформленияПередачаВРегистр2ЕГАИС(Источник, Отказ);
	
КонецПроцедуры

// Обработчик подписки на событие "При записи" для документов, влияющий на расчет статуса ТТН ЕГАИС (исходящая).
//
// Параметры:
//  Источник - ДокументОбъект - источник события.
//  Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияТТНИсходящаяЕГАИС(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусОформленияТТНИсходящаяЕГАИС(Источник, Отказ);
	
КонецПроцедуры

// Обработчик подписки на событие "При записи" для документов, влияющий на расчет статуса ТТН ЕГАИС (входящая).
//
// Параметры:
//  Источник - ДокументОбъект - источник события.
//  Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияТТНВходящаяЕГАИС(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусОформленияТТНВходящаяЕГАИС(Источник, Отказ);
	
КонецПроцедуры

// Обработчик подписки на событие "При записи" для документов, влияющий на расчет статуса Чек ЕГАИС.
//
// Параметры:
//  Источник - ДокументОбъект - источник события.
//  Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияЧекЕГАИС(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусОформленияЧекЕГАИС(Источник, Отказ);
	
КонецПроцедуры

// Обработчик подписки на событие "При записи" для документов, влияющий на расчет статуса Чек ЕГАИС на возврат.
//
// Параметры:
//  Источник - ДокументОбъект - источник события.
//  Отказ    - см. описание в синтаксис помощнике к обработчику события объекта ПриЗаписи.
//
Процедура РассчитатьСтатусОформленияЧекЕГАИСВозврат(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЕГАИСПереопределяемый.РассчитатьСтатусОформленияЧекЕГАИСВозврат(Источник, Отказ);
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусИнформированияГИСМДляДокументаСРаспоряжением
//
Процедура ЗаписатьСтатусДокументаЕГАИСПоУмолчанию(Источник) Экспорт

	ЗаписьНового = Источник.ДополнительныеСвойства.Свойство("ЭтоНовый")
	             И Источник.ДополнительныеСвойства.ЭтоНовый;
	
	Если Не ЗаписьНового Тогда
		Возврат;
	КонецЕсли;
	
	Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(Источник, "ДокументОснование") Тогда
		ДокументОснование = ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(Источник, "ДокументОснование");
	Иначе
		ДокументОснование = Неопределено;
	КонецЕсли;
	
	ДанныеЗаписи = РегистрыСведений.СтатусыДокументовЕГАИС.ЗначенияПоУмолчанию(Источник.Ссылка);
	
	РегистрыСведений.СтатусыДокументовЕГАИС.ВыполнитьЗаписьВРегистр(ДанныеЗаписи);
	
КонецПроцедуры

// Текст исключения обработки статуса
//
// Параметры:
//  Документ - ДокументСсылка - Документ.
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция.
// 
// Возвращаемое значение:
//  Строка - Текст исключения.
//
Функция ТекстИсключенияОбработкиСтатуса(Документ, Операция) Экспорт
	
	Возврат СтрШаблон(
		НСтр("ru = 'При изменении статуса документа %1 не обработана операция %2'"),
		Документ,
		Операция);
	
КонецФункции

#КонецОбласти

#Область ОтборПоОрганизацииЕГАИС

Процедура ОтборПоОрганизацииПриСозданииНаСервере(Форма, Знач ЗначениеПрефиксы = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеПрефиксы = Неопределено Тогда
		Префиксы = Новый Массив;
		Префиксы.Добавить("Оформлено");
		Префиксы.Добавить("КОформлению");
		Префиксы.Добавить("ВРегистр3");
	Иначе
		Если ТипЗнч(ЗначениеПрефиксы) = Тип("Строка") Тогда
			Префиксы = Новый Массив();
			Префиксы.Добавить(ЗначениеПрефиксы);
		Иначе
			Префиксы = ЗначениеПрефиксы;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого Значение Из Префиксы Цикл
		ЭлементОтбораОрганизацияЕГАИС = Форма.Элементы.Найти(Значение + "ОрганизацияЕГАИС");
		Если ЭлементОтбораОрганизацияЕГАИС <> Неопределено Тогда
			ЭлементОтбораОрганизацияЕГАИС.СписокВыбора.Очистить();
		КонецЕсли;
		
		ЭлементОтбораОрганизацииЕГАИС = Форма.Элементы.Найти(Значение + "ОрганизацииЕГАИС");
		Если ЭлементОтбораОрганизацииЕГАИС <> Неопределено Тогда
			ЭлементОтбораОрганизацииЕГАИС.СписокВыбора.Очистить();
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыОтбора = Новый Структура("КлючОбъекта", "Справочник.КлассификаторОрганизацийЕГАИС.Форма.ФормаВыбораСпискаОрганизаций");
	Выборка = ХранилищеНастроекДанныхФорм.Выбрать(ПараметрыОтбора);
	Пока Выборка.Следующий() Цикл
		
		Данные = Новый Массив;
		Значение = Выборка.Настройки.Получить("ТаблицаОрганизацииЕГАИС");
		Если Значение <> Неопределено Тогда
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("Выбрана", Истина);
			НайденныеСтроки = Значение.НайтиСтроки(ПараметрыОтбора);
			
			Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
				Данные.Добавить(СтрокаТЧ.ОрганизацияЕГАИС);
			КонецЦикла;
			
			Для Каждого Значение Из Префиксы Цикл
				ЭлементОтбораОрганизацияЕГАИС = Форма.Элементы.Найти(Значение + "ОрганизацияЕГАИС");
				Если ЭлементОтбораОрганизацияЕГАИС <> Неопределено Тогда
					ЭлементОтбораОрганизацияЕГАИС.СписокВыбора.Добавить(Данные, СтрСоединить(Данные, "; "));
				КонецЕсли;
		
				ЭлементОтбораОрганизацииЕГАИС = Форма.Элементы.Найти(Значение + "ОрганизацииЕГАИС");
				Если ЭлементОтбораОрганизацииЕГАИС <> Неопределено Тогда
					ЭлементОтбораОрганизацииЕГАИС.СписокВыбора.Добавить(Данные, СтрСоединить(Данные, "; "));
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ИнтеграцияЕГАИСКлиентСервер.НастроитьОтборПоОрганизацииЕГАИС(Форма, Форма.ОрганизацииЕГАИС, Неопределено, Префиксы);
	
КонецПроцедуры

#КонецОбласти

#Область ОтборДальнейшиеДействия

// Формирует массив дальнейших действий, которые не отображаются при выводе статуса обмена с ЕГАИС в форме документа.
// 
// Возвращаемое значение:
//  Массив - содержит неотображаемые дальнейшие действия.
//
Функция НеотображаемыеВДокументахДальнейшиеДействия() Экспорт
	
	Действия = Новый Массив;
	
	Действия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПередачуДанныхРегламентнымЗаданием);
	Действия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПолученЕГАИС);
	Действия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПроведенЕГАИС);
	Действия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеУведомленияОРегистрацииДвижения);
	Действия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеОстатков);
	Действия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.НеТребуется);
	
	Возврат Действия;
	
КонецФункции

// Заполняет список дальнейших действий для быстрого отбора динамического списка "Дальнейшее действие"
//
// Параметры:
//  СписокВыбора         - СписокЗначений - формируемый список значений.
//  ВсеТребующиеДействия - Массив - действия, которые необходимо выполнить пользователю.
//  ВсеТребующиеОжидания - Массив - действия, выполнения которых ожидает пользователь.
//
Процедура ЗаполнитьСписокВыбораДальнейшееДействие(СписокВыбора, ВсеТребующиеДействия, ВсеТребующиеОжидания) Экспорт
	
	СписокВыбора.Очистить();
	СписокВыбора.Добавить("ВсеТребующиеДействия", НСтр("ru='Все требующие действия'"));
	СписокВыбора.Добавить("ВсеТребующиеОжидания", НСтр("ru='Все требующие ожидания'"));
	СписокВыбора.Добавить("ВсеТребующиеДействияИлиОжидания", НСтр("ru='Все требующие действия или ожидания'"));
	
	Для Каждого Значение Из ВсеТребующиеДействия Цикл
		СписокВыбора.Добавить(Значение);
	КонецЦикла;
	
	Для Каждого Значение Из ВсеТребующиеОжидания Цикл
		СписокВыбора.Добавить(Значение);
	КонецЦикла;
	
	СписокВыбора.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.НеТребуется);
	
КонецПроцедуры

// Устанавливает отбор в динамическом списке по полю "Дальнейшее действие".
//
// Параметры:
//  ДинамическийСписок     - ДинамическийСписок - список, в котором устанавливается отбор.
//  ДальнейшееДействиеЕГАИС - Перечисление.ДальнейшееДействиеЕГАИС, Строка - значение устанавливаемого отбора.
//  ВсеТребующиеДействия - Массив - действия, которые необходимо выполнить пользователю.
//  ВсеТребующиеОжидания - Массив - действия, выполнения которых ожидает пользователь.
//
Процедура УстановитьОтборПоДальнейшемуДействию(ДинамическийСписок, ДальнейшееДействие, ВсеТребующиеДействия, ВсеТребующиеОжидания) Экспорт
	
	ИмяПоля = "ДальнейшееДействиеЕГАИС1";
	
	Если ДальнейшееДействие = "ВсеТребующиеДействия" Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля, ВсеТребующиеДействия, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
		
	ИначеЕсли ДальнейшееДействие = "ВсеТребующиеОжидания" Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля, ВсеТребующиеОжидания, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
		
	ИначеЕсли ДальнейшееДействие = "ВсеТребующиеДействияИлиОжидания" Тогда
		
		ВсеТребующиеДействияИлиОжидания = Новый Массив;
		Для Каждого Элемент Из ВсеТребующиеДействия Цикл
			ВсеТребующиеДействияИлиОжидания.Добавить(Элемент);
		КонецЦикла;
		Для Каждого Элемент Из ВсеТребующиеОжидания Цикл
			ВсеТребующиеДействияИлиОжидания.Добавить(Элемент);
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля, ВсеТребующиеДействияИлиОжидания, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, ИмяПоля, ДальнейшееДействие, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(ДальнейшееДействие));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Проверить наличие движений в зависимости от статусов.
//
// Параметры:
//  СтатусыДвижений - Массив - Статусы.
//  НовыйСтатус - ПеречислениеСсылка - Новый статус.
//  ПредыдущийСтатус - ПеречислениеСсылка - Новый статус.
// 
// Возвращаемое значение:
//  Булево - Истина, если движения есть.
//
Функция ЕстьДвижения(СтатусыДвижений, ПредыдущийСтатус, НовыйСтатус) Экспорт
	
	Возврат СтатусыДвижений.Найти(ПредыдущийСтатус) <> Неопределено
		И СтатусыДвижений.Найти(НовыйСтатус) <> Неопределено;
	
КонецФункции

// Проверить отсутствие движений в зависимости от статусов.
//
// Параметры:
//  СтатусыДвижений - Массив - Статусы.
//  НовыйСтатус - ПеречислениеСсылка - Новый статус.
//  ПредыдущийСтатус - ПеречислениеСсылка - Новый статус.
// 
// Возвращаемое значение:
//  Булево - Истина, если движений нет.
//
Функция НетДвижений(СтатусыДвижений, ПредыдущийСтатус, НовыйСтатус) Экспорт
	
	Возврат СтатусыДвижений.Найти(ПредыдущийСтатус) = Неопределено
		И СтатусыДвижений.Найти(НовыйСтатус) = Неопределено;
	
КонецФункции

// Рассчитать необходимость обновления движений в зависимости от статуса.
//
// Параметры:
//  СтатусыДвижений - Массив - Статусы.
//  НовыйСтатус - ПеречислениеСсылка - Новый статус.
//  ПредыдущийСтатус - ПеречислениеСсылка - Новый статус.
// 
// Возвращаемое значение:
//  Булево - Необходимость обновления движений.
//
Функция СтатусТребуетОбновленияДвижений(СтатусыДвижений, ПредыдущийСтатус, НовыйСтатус) Экспорт
	
	Возврат СтатусТребуетДобавленияДвижений(СтатусыДвижений, ПредыдущийСтатус, НовыйСтатус)
	    Или СтатусТребуетУдаленияДвижений(СтатусыДвижений, ПредыдущийСтатус, НовыйСтатус);
	
КонецФункции

// Рассчитать необходимость добавления движений в зависимости от статуса.
//
// Параметры:
//  СтатусыДвижений - Массив - Статусы.
//  НовыйСтатус - ПеречислениеСсылка - Новый статус.
//  ПредыдущийСтатус - ПеречислениеСсылка - Новый статус.
// 
// Возвращаемое значение:
//  Булево - Необходимость добавления движений.
//
Функция СтатусТребуетДобавленияДвижений(СтатусыДвижений, ПредыдущийСтатус, НовыйСтатус) Экспорт
	
	Возврат СтатусыДвижений.Найти(ПредыдущийСтатус) = Неопределено
	      И СтатусыДвижений.Найти(НовыйСтатус) <> Неопределено;
	
КонецФункции

// Рассчитать необходимость удаления движений в зависимости от статуса.
//
// Параметры:
//  СтатусыДвижений - Массив - Статусы.
//  НовыйСтатус - ПеречислениеСсылка - Новый статус.
//  ПредыдущийСтатус - ПеречислениеСсылка - Новый статус.
// 
// Возвращаемое значение:
//  Булево - Необходимость удаления движений.
//
Функция СтатусТребуетУдаленияДвижений(СтатусыДвижений, ПредыдущийСтатус, НовыйСтатус) Экспорт
	
	Возврат СтатусыДвижений.Найти(ПредыдущийСтатус) <> Неопределено
	      И СтатусыДвижений.Найти(НовыйСтатус) = Неопределено;
	
КонецФункции

// Заполняет информацию о сопоставлении алкогольной продукции в табличной части
//
// Параметры:
//  ТабличнаяЧасть	 - ДанныеФормыСтруктура - Табличная часть документа
//
Процедура ЗаполнитьАлкогольнуюПродукцию(ТабличнаяЧасть) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Серия КАК Серия,
	|	Товары.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Товары КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО (СоответствиеНоменклатурыЕГАИС.Номенклатура = ТабличнаяЧасть.Номенклатура)
	|			И (СоответствиеНоменклатурыЕГАИС.Характеристика = ТабличнаяЧасть.Характеристика)
	|			И (СоответствиеНоменклатурыЕГАИС.Серия = ТабличнаяЧасть.Серия ИЛИ СоответствиеНоменклатурыЕГАИС.Серия = &ПустаяСерия)
	|ГДЕ
	|	НЕ СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция ЕСТЬ NULL
	|ИТОГИ ПО
	|	ТабличнаяЧасть.НомерСтроки
	|;");
	
	Запрос.УстановитьПараметр("Товары", ТабличнаяЧасть.Выгрузить(,"НомерСтроки,АлкогольнаяПродукция, Номенклатура, Характеристика, Серия"));
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	ВыборкаСтроки = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСтроки.Следующий() Цикл
		
		СтрокаТЧ = ТабличнаяЧасть.Получить(ВыборкаСтроки.НомерСтроки - 1);
		
		ВыборкаАлкогольнаяПродукция = ВыборкаСтроки.Выбрать();
		КоличествоСопоставлено = ВыборкаАлкогольнаяПродукция.Количество();
		Если КоличествоСопоставлено > 1 Тогда
			СтрокаТЧ.СопоставлениеАлкогольнаяПродукция = СтрШаблон(НСтр("ru = '<Несколько позиций (%1)>'"), КоличествоСопоставлено);
		ИначеЕсли КоличествоСопоставлено = 1 Тогда
			СтрокаТЧ.СопоставлениеАлкогольнаяПродукция = "";
		Иначе
			СтрокаТЧ.СопоставлениеАлкогольнаяПродукция = НСтр("ru = '<Не сопоставлено>'");
		КонецЕсли;
		Пока ВыборкаАлкогольнаяПродукция.Следующий() Цикл
			СтрокаТЧ.НоменклатураДляВыбора.Добавить(ВыборкаАлкогольнаяПродукция.АлкогольнаяПродукция);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Сверачивает остатки по сериям и справкам 2 регистра соответствие номенклатуры ЕГАИС
//
Процедура СверткаРегистраСоответствиеНоменклатурыЕГАИС() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.СверткаРегистраСоответствиеНоменклатурыЕГАИС);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции") Тогда
		Возврат
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.Справка2 КАК Справка2,
	|	ОстаткиАлкогольнойПродукцииЕГАИСОстатки.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ВТОстатки
	|ИЗ
	|	РегистрНакопления.ОстаткиАлкогольнойПродукцииЕГАИС.Остатки(, ) КАК ОстаткиАлкогольнойПродукцииЕГАИСОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура,
	|	СоответствиеНоменклатурыЕГАИС.Характеристика КАК Характеристика,
	|	СоответствиеНоменклатурыЕГАИС.ИдентификаторУпаковки КАК ИдентификаторУпаковки,
	|	СоответствиеНоменклатурыЕГАИС.Справка2 КАК Справка2,
	|	МИНИМУМ(СоответствиеНоменклатурыЕГАИС.Порядок) КАК Порядок
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстатки КАК ВТОстатки
	|		ПО СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция = ВТОстатки.АлкогольнаяПродукция
	|			И СоответствиеНоменклатурыЕГАИС.Справка2 = ВТОстатки.Справка2
	|ГДЕ
	|	СоответствиеНоменклатурыЕГАИС.Справка2 <> ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)
	|	И ВТОстатки.Справка2 ЕСТЬ NULL
	|СГРУППИРОВАТЬ ПО
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура,
	|	СоответствиеНоменклатурыЕГАИС.Характеристика,
	|	СоответствиеНоменклатурыЕГАИС.Справка2,
	|	СоответствиеНоменклатурыЕГАИС.ИдентификаторУпаковки
	|УПОРЯДОЧИТЬ ПО
	|	СоответствиеНоменклатурыЕГАИС.Справка2,
	|	МИНИМУМ(СоответствиеНоменклатурыЕГАИС.Порядок) ВОЗР
	|;";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Свертка регистра Соответствие номенклатуры ЕГАИС'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru = 'Начато регламентное задание ""Свертка регистра Соответствие номенклатуры ЕГАИС"".'"));
			
		Выборка = Результат.Выбрать();
		ТекСправка2 = Неопределено;
		
		Пока Выборка.Следующий() Цикл
			
			Если ТекСправка2 = Выборка.Справка2 Тогда
				Продолжить;
			КонецЕсли;
			
			НаборЗаписей = РегистрыСведений.СоответствиеНоменклатурыЕГАИС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура, Истина);
			НаборЗаписей.Отбор.Характеристика.Установить(Выборка.Характеристика, Истина);
			НаборЗаписей.Отбор.АлкогольнаяПродукция.Установить(Выборка.АлкогольнаяПродукция, Истина);
			НаборЗаписей.Отбор.ИдентификаторУпаковки.Установить(Выборка.ИдентификаторУпаковки, Истина);
			НаборЗаписей.Отбор.Справка2.Установить(Выборка.Справка2, Истина);
			
			Попытка
				НаборЗаписей.Записать();
			Исключение
				
				ТекстОшибки = НСтр("ru = 'При записи соответствия номенклатуры произошла ошибка:
				                         |%1'");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					СтрШаблон(ТекстОшибки, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
				
				ИнтеграцияЕГАИСВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
					СтрШаблон(ТекстОшибки, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
				
			КонецПопытки;
			
			НаборЗаписей = РегистрыСведений.СоответствиеНоменклатурыЕГАИС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура, Истина);
			НаборЗаписей.Отбор.Характеристика.Установить(Выборка.Характеристика, Истина);
			НаборЗаписей.Отбор.АлкогольнаяПродукция.Установить(Выборка.АлкогольнаяПродукция, Истина);
			НаборЗаписей.Отбор.ИдентификаторУпаковки.Установить(Выборка.ИдентификаторУпаковки, Истина);
			НаборЗаписей.Отбор.Справка2.Установить(Справочники.Справки2ЕГАИС.ПустаяСсылка(), Истина);
			
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Номенклатура          = Выборка.Номенклатура;
			НоваяЗапись.Характеристика        = Выборка.Характеристика;
			НоваяЗапись.АлкогольнаяПродукция  = Выборка.АлкогольнаяПродукция;
			НоваяЗапись.ИдентификаторУпаковки = Выборка.ИдентификаторУпаковки;
			НоваяЗапись.Порядок               = Выборка.Порядок;
			
			ТекСправка2 = Выборка.Справка2;
			
			Попытка
				НаборЗаписей.Записать();
			Исключение
				
				ТекстОшибки = НСтр("ru = 'При записи соответствия номенклатуры произошла ошибка:
				                         |%1'");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					СтрШаблон(ТекстОшибки, КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
				
				ИнтеграцияЕГАИСВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
					СтрШаблон(ТекстОшибки, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
				
			КонецПопытки;
			
		КонецЦикла;
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Свертка регистра Соответствие номенклатуры ЕГАИС'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Информация, , ,
			НСтр("ru = 'Завершено регламентное задание ""Свертка регистра Соответствие номенклатуры ЕГАИС"".'"));
		
	КонецЕсли;
	
КонецПроцедуры

Функция СоответствиеАлкогольнойПродукции(КодыАлкогольнойПродукции) Экспорт
	
	АлкогольнаяПродукция = Новый Соответствие;
	
	Если КодыАлкогольнойПродукции.Количество() > 0 Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка КАК Ссылка,
		|	КлассификаторАлкогольнойПродукцииЕГАИС.Код    КАК Код
		|ИЗ
		|	Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
		|ГДЕ
		|	КлассификаторАлкогольнойПродукцииЕГАИС.Код В(&КодыАлкогольнойПродукции)");
		Запрос.УстановитьПараметр("КодыАлкогольнойПродукции", КодыАлкогольнойПродукции);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			АлкогольнаяПродукция.Вставить(Выборка.Код, Выборка.Ссылка);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат АлкогольнаяПродукция;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает Объект XDTO, получаемый из текста сообщения XML
//
// Параметры:
//  ТекстСообщенияXML - Строка - Текст сообщения XML
// 
// Возвращаемое значение:
//  ОбъектXDTO - Объект XDTO
//
Функция ПроизвольныйОбъектXDTOПоТекстуСообщенияXML(ТекстСообщенияXML) Экспорт
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ТекстСообщенияXML);
	
	ОбъектXDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
	
	Возврат ОбъектXDTO;
	
КонецФункции

// Преобразует содержимое произвольного объекта XDTO источника (без типов) в содержимое объекта XDTO приемника (с типами).
//
Функция ПреобразоватьПроизвольныйОбъектXDTOВОбъектXDTO(ОбъектXDTOИсточник, ОбъектXDTOПриемник) Экспорт
	
	Для Каждого СвойствоИсточника Из ОбъектXDTOИсточник.Свойства() Цикл
		
		ИмяСвойства      = СвойствоИсточника.Имя;
		ЗначениеСвойства = ОбъектXDTOИсточник[ИмяСвойства];
		
		СвойствоПриемника = ОбъектXDTOПриемник.Свойства().Получить(ИмяСвойства);
		Если СвойствоПриемника = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеСвойства = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ЗначениеСвойства) = Тип("ОбъектXDTO") Тогда
			
			Если ТипЗнч(СвойствоПриемника.Тип) = Тип("ТипОбъектаXDTO") Тогда
				
				ЗначениеСвойстваПриемника = ОбъектXDTOПоИмениСвойства(СвойствоПриемника.URIПространстваИмен, ИмяСвойства, ОбъектXDTOПриемник);
				
				Если ТипЗнч(ЗначениеСвойстваПриемника) = Тип("ОбъектXDTO") Тогда
					ПреобразоватьПроизвольныйОбъектXDTOВОбъектXDTO(ЗначениеСвойства, ЗначениеСвойстваПриемника);
					
					Если ТипЗнч(ОбъектXDTOПриемник[ИмяСвойства]) = Тип("СписокXDTO") Тогда
						ОбъектXDTOПриемник[ИмяСвойства].Добавить(ЗначениеСвойстваПриемника);
					Иначе
						ОбъектXDTOПриемник[ИмяСвойства] = ЗначениеСвойстваПриемника;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ЗначениеСвойства) = Тип("СписокXDTO") Тогда
			
			Для Индекс = 0 По ЗначениеСвойства.Количество() - 1 Цикл
				
				ЭлементСпискаИсточника = ЗначениеСвойства.Получить(Индекс);
				Если ТипЗнч(ЭлементСпискаИсточника) = Тип("ОбъектXDTO") Тогда
					
					Если ТипЗнч(СвойствоПриемника.Тип) = Тип("ТипОбъектаXDTO") Тогда
						
						ЭлементаСпискаПриемника = ОбъектXDTOПоИмениСвойства(СвойствоПриемника.URIПространстваИмен, ИмяСвойства, ОбъектXDTOПриемник);
						
						Если ТипЗнч(ЭлементаСпискаПриемника) = Тип("ОбъектXDTO") Тогда
							ПреобразоватьПроизвольныйОбъектXDTOВОбъектXDTO(ЭлементСпискаИсточника, ЭлементаСпискаПриемника);
							ОбъектXDTOПриемник[ИмяСвойства].Добавить(ЭлементаСпискаПриемника);
						КонецЕсли;
						
					КонецЕсли;
					
				Иначе
					ОбъектXDTOПриемник[ИмяСвойства].Добавить(ЭлементСпискаИсточника);
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			Попытка
				
				Если ТипЗнч(ОбъектXDTOПриемник[ИмяСвойства]) = Тип("СписокXDTO") Тогда
					ОбъектXDTOПриемник[ИмяСвойства].Добавить(ЗначениеСвойства);
				Иначе
					ОбъектXDTOПриемник[ИмяСвойства] = ЗначениеСвойства;
				КонецЕсли;
				
			Исключение
				Продолжить;
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОбъектXDTOПриемник;
	
КонецФункции

// Создает новый объект XDTO.
//
// Параметры:
//  ПространствоИмен - Строка - Пространство имен.
//  ИмяТипа - Строка - Имя типа в пространстве имен.
// 
// Возвращаемое значение:
//  ОбъектXDTO - Созданный объект XDTO/
//
Функция ОбъектXDTO(ПространствоИмен, ИмяТипа) Экспорт
	
	Возврат ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, ИмяТипа));
	
КонецФункции

// Преобразует объект XDTO в структуру
//
// Параметры:
//  ОбъектXDTO - ОбъектXDTO - Объект XDTO
// 
// Возвращаемое значение:
//  Структура - Структура объекта
//
Функция ОбъектXDTOВСтруктуру(ОбъектXDTO) Экспорт
	
	Структура = Новый Структура;
	
	Для Каждого Свойство Из ОбъектXDTO.Свойства() Цикл
		
		ИмяСвойства      = Свойство.Имя;
		ЗначениеСвойства = ОбъектXDTO[ИмяСвойства];
		
		Если ТипЗнч(ЗначениеСвойства) = Тип("ОбъектXDTO") Тогда
			
			Структура.Вставить(ИмяСвойства, ОбъектXDTOВСтруктуру(ЗначениеСвойства));
			
		ИначеЕсли ТипЗнч(ЗначениеСвойства) = Тип("СписокXDTO") Тогда
			
			Структура.Вставить(ИмяСвойства, Новый Массив);
			Для Индекс = 0 По ЗначениеСвойства.Количество() - 1 Цикл
				
				ЭлементСписка = ЗначениеСвойства.Получить(Индекс);
				Если ТипЗнч(ЭлементСписка) = Тип("ОбъектXDTO") Тогда
					Структура[ИмяСвойства].Добавить(ОбъектXDTOВСтруктуру(ЭлементСписка));
				Иначе
					Структура[ИмяСвойства].Добавить(ЭлементСписка);
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			Структура.Вставить(ИмяСвойства, ЗначениеСвойства);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Структура;
	
КонецФункции

// Создает ОбъектXDTO из пространства имен по имени свойства.
//
Функция ОбъектXDTOПоИмениСвойства(ПространствоИмен, ИмяСвойства, ОбъектXDTOРодитель = Неопределено) Экспорт
	
	Если ОбъектXDTOРодитель = Неопределено Тогда
		ТипОбъекта = ФабрикаXDTO.Пакеты.Получить(ПространствоИмен).КорневыеСвойства.Получить(ИмяСвойства).Тип;
	Иначе
		ТипОбъекта = ОбъектXDTOРодитель.Тип().Свойства.Получить(ИмяСвойства).Тип;
	КонецЕсли;
	
	Возврат ФабрикаXDTO.Создать(ТипОбъекта);
	
КонецФункции

// Создает ОбъектXDTO из пространства имен по имени свойства.
//
Функция ОбъектXDTOПоИмениТипа(ОбъектXDTOРодитель, ИмяТипа) Экспорт
	
	ТипОбъекта = ОбъектXDTOРодитель.Тип().Свойства.Получить(ИмяТипа).Тип;
	
	Возврат ФабрикаXDTO.Создать(ТипОбъекта);
	
КонецФункции

// Возвращает Объект XDTO, получаемый из текста сообщения XML
//
// Параметры:
//  ТекстСообщенияXML - Строка - Текст сообщения XML
//  Тип - Строка, Неопределено, ТипОбъектаXDTO - Тип объекта
//  Версия - Строка - Требуемая версия
// 
// Возвращаемое значение:
//  ОбъектXDTO - Объект XDTO
//
Функция ОбъектXDTOПоТекстуСообщенияXML(ТекстСообщенияXML, Тип) Экспорт
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ТекстСообщенияXML);
	
	Если ТипЗнч(Тип) = Тип("Строка") Тогда
		СоздаваемыйТип = ОбъектXDTOПоИмениСвойства(КорневоеПространствоИмен(), Тип).Тип();
	Иначе
		СоздаваемыйТип = Тип;
	КонецЕсли;
	
	ОбъектXDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML, СоздаваемыйТип);
	
	Возврат ОбъектXDTO;
	
КонецФункции

// Устанавливает значение свойства объекта XDTO.
//
Процедура ЗаполнитьСвойствоXDTO(ОбъектXDTO, ИмяСвойства, ЗначениеСвойства = Неопределено, КешОшибок = Неопределено, Глубина = Неопределено) Экспорт
	
	Если Не ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(ОбъектXDTO, ИмяСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	РазрешеноНеЗаполнять = ОбъектXDTO.Тип().Свойства.Получить(ИмяСвойства).НижняяГраница = 0;
	Если Не ЗначениеЗаполнено(ЗначениеСвойства)
		И РазрешеноНеЗаполнять Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Если ТипЗнч(ОбъектXDTO[ИмяСвойства]) = Тип("СписокXDTO") Тогда
			ОбъектXDTO[ИмяСвойства].Добавить(ЗначениеСвойства);
		Иначе
			ОбъектXDTO[ИмяСвойства] = ЗначениеСвойства;
		КонецЕсли;
	Исключение
		ЧтениеXML = Новый Структура;
		ЧтениеXML.Вставить("Имя"                , ИмяСвойства);
		ЧтениеXML.Вставить("ЛокальноеИмя"       , ИмяСвойства);
		ЧтениеXML.Вставить("Значение"           , ЗначениеСвойства);
		ЧтениеXML.Вставить("ТипУзла"            , ТипУзлаXML.КонецЭлемента);
		ЧтениеXML.Вставить("URIПространстваИмен", ОбъектXDTO.Тип().URIПространстваИмен);
		
		ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстОшибки = ПредставлениеОшибкиXDTO(ТекстОшибки, ЧтениеXML, Глубина);
		
		Если КешОшибок <> Неопределено Тогда
			ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(КешОшибок, ТекстОшибки);
		КонецЕсли;
		
	КонецПопытки;
	
КонецПроцедуры

// Возвращает пользовательское представление ошибки при чтении/записи xml-файла.
//
Функция ПредставлениеОшибкиXDTO(ПредставлениеОшибки, ЧтениеXML, Глубина = Неопределено) Экспорт
	
	Если СтрНайти(ПредставлениеОшибки, НСтр("ru = 'Ошибка проверки данных XDTO'")) = 0
		И СтрНайти(ПредставлениеОшибки, НСтр("ru = 'Несоответствие типов XDTO'")) = 0 Тогда
		Возврат ПредставлениеОшибки;
	КонецЕсли;
	
	ЗначениеПоля = ЧтениеXML.Значение;
	Если ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
		ЧтениеXML.Прочитать();
	КонецЕсли;
	
	ТаблицаПредставлений = ПредставленияПолейЕГАИС();
	
	Отбор = Новый Структура;
	Отбор.Вставить("ПространствоИмен", ЧтениеXML.URIПространстваИмен);
	Отбор.Вставить("ЛокальноеИмя", ЧтениеXML.ЛокальноеИмя);
	
	МассивСтрок = ТаблицаПредставлений.НайтиСтроки(Отбор);
	
	СтрокаТаблицы = Неопределено;
	Если МассивСтрок.Количество() = 1 Тогда
		СтрокаТаблицы = МассивСтрок[0];
	ИначеЕсли МассивСтрок.Количество() > 1 И НЕ Глубина = Неопределено Тогда
		Отбор.Вставить("Глубина", Глубина);
		МассивСтрокПоГлубине = ТаблицаПредставлений.НайтиСтроки(Отбор);
		Если МассивСтрокПоГлубине.Количество() = 1 Тогда
			СтрокаТаблицы = МассивСтрокПоГлубине[0];
		Иначе
			СтрокаТаблицы = МассивСтрок[0];
		КонецЕсли;
	КонецЕсли;
	
	Если СтрокаТаблицы <> Неопределено Тогда
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			Если НЕ ЗначениеЗаполнено(ЗначениеПоля) Тогда
				ПредставлениеОшибки = НСтр("ru = 'Не заполнено значение поля ""%1"" (%2)'");
				ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеОшибки, СтрокаТаблицы.Представление, ЧтениеXML.Имя);
			Иначе
				ПредставлениеОшибки = НСтр("ru = 'Некорректное значение ""%1"" поля ""%2"" (%3)'");
				ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеОшибки, ЗначениеПоля, СтрокаТаблицы.Представление, ЧтениеXML.Имя);
			КонецЕсли;
		Иначе
			ИндексСтроки = ТаблицаПредставлений.Индекс(СтрокаТаблицы);
			
			Пока ИндексСтроки > 0 Цикл
				ПредыдущееПоле = ТаблицаПредставлений[ИндексСтроки - 1];
				Если ПредыдущееПоле.ПространствоИмен <> СтрокаТаблицы.ПространствоИмен Тогда
					Прервать;
				КонецЕсли;
				Если ПредыдущееПоле.Обязательное Тогда
					ПредставлениеОшибки = НСтр("ru = 'Отсутствует обязательное поле ""%1"" (%2)'");
					ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеОшибки, ПредыдущееПоле.Представление, ПредыдущееПоле.ЛокальноеИмя);
					Прервать;
				КонецЕсли;
				ИндексСтроки = ИндексСтроки - 1;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПредставлениеОшибки;
	
КонецФункции

// Возвращает корневое пространство имен для документов ЕГАИС.
//
Функция КорневоеПространствоИмен() Экспорт
	
	Возврат "http://fsrar.ru/WEGAIS/WB_DOC_SINGLE_01";
	
КонецФункции

// Возвращает все пространства зависимые пространства имен из коллекции пакетов.
//
Функция ЗависимыеПространстваИмен(ЗависимыеПакеты, Результат)
	
	Для Каждого ПакетXDTO Из ЗависимыеПакеты Цикл
		Если Результат.Найти(ПакетXDTO.URIПространстваИмен) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(ПакетXDTO.URIПространстваИмен);
		
		ЗависимыеПространстваИмен(ПакетXDTO.Зависимости, Результат);
	КонецЦикла;
	
КонецФункции

// Функция возвращает таблицу значений представлений полей файла обмена с УТМ.
//
Функция ПредставленияПолейЕГАИС()
	
	ТаблицаПредставлений = Новый ТаблицаЗначений;
	
	Макет = ПолучитьОбщийМакет("ПредставленияПолейЕГАИС");
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(Макет.ПолучитьТекст());
	
	Если Не ЧтениеXML.Прочитать() Тогда
		ВызватьИсключение НСтр("ru = 'Пустой XML'");
	ИначеЕсли ЧтениеXML.Имя <> "Items" Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка в структуре XML'");
	КонецЕсли;
	
	ИменаКолонок = СтрЗаменить(ЧтениеXML.ПолучитьАтрибут("Columns"), ",", Символы.ПС);
	КоличествоКолонок = СтрЧислоСтрок(ИменаКолонок);
	
	Для Сч = 1 По КоличествоКолонок Цикл
		ИмяКолонки = СтрПолучитьСтроку(ИменаКолонок, Сч);
		
		Если ИмяКолонки = "Глубина" Тогда
			ТаблицаПредставлений.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Число"));
		ИначеЕсли ИмяКолонки = "Обязательное" Тогда
			ТаблицаПредставлений.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Булево"));
		Иначе
			ТаблицаПредставлений.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Строка"));
		КонецЕсли;
	КонецЦикла;
	
	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.Имя = "Items" Тогда
			Прервать;
		ИначеЕсли ЧтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента Тогда
			Продолжить;
		ИначеЕсли ЧтениеXML.Имя <> "Item" Тогда
			ВызватьИсключение НСтр("ru = 'Ошибка в структуре XML'");
		КонецЕсли;
		
		новСтр = ТаблицаПредставлений.Добавить();
		Для Сч = 1 По КоличествоКолонок Цикл
			ИмяКолонки = СтрПолучитьСтроку(ИменаКолонок, Сч);
			
			Если ИмяКолонки = "Глубина" Тогда
				новСтр[Сч-1] = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ЧтениеXML.ПолучитьАтрибут(ИмяКолонки));
			ИначеЕсли ИмяКолонки = "Обязательное" Тогда
				новСтр[Сч-1] = Булево(СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ЧтениеXML.ПолучитьАтрибут(ИмяКолонки)));
			Иначе
				новСтр[Сч-1] = ЧтениеXML.ПолучитьАтрибут(ИмяКолонки);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаПредставлений.Сортировать(ТаблицаПредставлений.Колонки[0].Имя + " Возр");
	
	Возврат ТаблицаПредставлений;
	
КонецФункции

// Формирует уникальный идентификатор для нового элемента справочника.
//
// Параметры:
//  Источник - СправочникОбъект - записываемый элемент справочника,
//  Отказ - Булево - признак отказа от записи.
//
Процедура СформироватьИдентификаторОбъектаРИБПередЗаписью(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ СтандартныеПодсистемыПовтИсп.ИспользуетсяРИБ() Тогда
		Возврат;
	КонецЕсли;
	
	ИмяСправочника = Источник.Метаданные().Имя;
	
	Если ИмяСправочника = "Справки1ЕГАИС"
		ИЛИ ИмяСправочника = "Справки2ЕГАИС" Тогда
		ИмяРеквизита = "РегистрационныйНомер";
		ПредставлениеРеквизита = Источник.Метаданные().Реквизиты.РегистрационныйНомер.Синоним;
	Иначе
		ИмяРеквизита = "Код";
		ПредставлениеРеквизита = Источник.Метаданные().СтандартныеРеквизиты.Код.Синоним;
	КонецЕсли;
	
	Если Источник.ЭтоНовый() Тогда
		НовыйИдентификатор = Неопределено;
		
		Если ЗначениеЗаполнено(Источник[ИмяРеквизита]) Тогда
			НовыйИдентификатор = СформироватьУникальныйИдентификатор(Источник[ИмяРеквизита], ИмяСправочника);
		КонецЕсли;
		
		Если НовыйИдентификатор <> Неопределено Тогда
			НоваяСсылка = Справочники[ИмяСправочника].ПолучитьСсылку(НовыйИдентификатор);
			Если НЕ ПустаяСтрока(НоваяСсылка.ВерсияДанных) Тогда
				СтрокаСообщения = НСтр("ru='Значение ""%1"" поля ""%2"" не уникально'");
				СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаСообщения, Источник.Код, ПредставлениеРеквизита);
				ВызватьИсключение СтрокаСообщения;
			КонецЕсли;
			
			Источник.УстановитьСсылкуНового(НоваяСсылка);
		КонецЕсли;
	Иначе
		ПредыдущийКод = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, ИмяРеквизита);
		Если ПредыдущийКод <> Источник[ИмяРеквизита] Тогда
			СтрокаСообщения = НСтр("ru = 'Изменение реквизита ""%1"" для существующих элементов запрещено'");
			СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаСообщения, ПредставлениеРеквизита);
			ВызватьИсключение СтрокаСообщения;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Формирует уникальный идентификатор из переданной строки добавлением лидирующих нулей.
//
Функция СформироватьУникальныйИдентификатор(ИсходнаяСтрока, ИмяСправочника)
	
	Префиксы = Новый Соответствие;
	Префиксы.Вставить("КлассификаторАлкогольнойПродукцииЕГАИС", "00");
	Префиксы.Вставить("КлассификаторОрганизацийЕГАИС"         , "01");
	Префиксы.Вставить("Справки1ЕГАИС"                         , "02");
	Префиксы.Вставить("Справки2ЕГАИС"                         , "03");
	Префиксы.Вставить(СправочникВидовАлкогольнойПродукции()   , "04");
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
	ХешированиеДанных.Добавить(Префиксы[ИмяСправочника] + ИсходнаяСтрока);
	
	ХешСумма = Строка(ХешированиеДанных.ХешСумма);
	
	Строка32 = "";
	Для Сч = 1 По СтрДлина(ХешСумма) Цикл
		ТекСимвол = Сред(ХешСумма, Сч, 1);
		Если Найти("0123456789abcdef", НРег(ТекСимвол)) <> 0 Тогда
			Строка32 = Строка32 + ТекСимвол;
		КонецЕсли;
	КонецЦикла;
	
	Если СтрДлина(Строка32) < 32 Тогда
		Строка32 = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(Строка32, 32, "0");
	КонецЕсли;
	
	СтрокаGUID = Лев(Строка32, 8)
		+ "-" + Сред(Строка32, 9, 4)
		+ "-" + Сред(Строка32, 13, 4)
		+ "-" + Сред(Строка32, 17, 4)
		+ "-" + Сред(Строка32, 21, 12);
	
	НовыйИдентификатор = Новый УникальныйИдентификатор(СтрокаGUID);
	
	Возврат НовыйИдентификатор;
	
КонецФункции

// Возвращает структуру параметров обновления статуса.
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * ОбновлятьДвижения- Булево - Признак необходимости обновления движений документа.
//   * ОперацияКвитанции - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция.
//   * СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийЕГАИС - Статус обработки сообщения.
//   * ДокументОбъект - ДокументОбъект - Документ-объект.
//   * ИдентификаторЗапроса - Строка - Идентификатор запроса.
//   * ФорматОбмена - ПеречислениеСсылка.ФорматыОбменаЕГАИС - Формат обмена.
//
Функция ПараметрыОбновленияСтатуса() Экспорт
	
	ПараметрыОбновленияСтатуса = Новый Структура;
	ПараметрыОбновленияСтатуса.Вставить("ОбновлятьДвижения", Истина);
	ПараметрыОбновленияСтатуса.Вставить("ОперацияКвитанции");
	ПараметрыОбновленияСтатуса.Вставить("ТекущееСостояние");
	ПараметрыОбновленияСтатуса.Вставить("СтатусОбработки");
	ПараметрыОбновленияСтатуса.Вставить("ДокументОбъект");
	ПараметрыОбновленияСтатуса.Вставить("ИдентификаторЗапроса");
	ПараметрыОбновленияСтатуса.Вставить("ФорматОбмена");
	
	Возврат ПараметрыОбновленияСтатуса;
	
КонецФункции

// Возвращает режим записи документа.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - Документ.
// 
// Возвращаемое значение:
//  РежимЗаписиДокумента - Режим записи документа.
//
Функция РежимЗаписи(ДокументОбъект)
	
	Возврат ?(ДокументОбъект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись);
	
КонецФункции

// Возвращает новый идентификатор документа
//
// Параметры:
//  Ссылка   - ЛюбаяСсылка - Ссылка.
//  Постфикс - Строка - Максимум 3 символа.
// 
// Возвращаемое значение:
//  Строка - Идентификатор документа.
//
Функция НовыйИдентификаторДокумента(Ссылка = Неопределено, Постфикс = "") Экспорт
	
	Префикс = "";
	Если ОбщегоНазначенияКлиентСервер.РежимОтладки() Тогда
		Префикс = ПрефиксИдентификатораРежимаОтладки();
	КонецЕсли;
	
	Если Ссылка = Неопределено Тогда
		Возврат Префикс + ?(Постфикс = "", "", СокрЛП(Лев(Постфикс, 3)) + "-") + Строка(Новый УникальныйИдентификатор());
	Иначе
		Возврат Префикс + ?(Постфикс = "", "", СокрЛП(Лев(Постфикс, 3)) + "-") + Строка(Ссылка.УникальныйИдентификатор());
	КонецЕсли;
	
КонецФункции

// Возвращает общий префикс идентификатора для режима отладки.
// 
// Возвращаемое значение:
//  Строка - Общий префикс идентификатора режима отладки.
//
Функция ПрефиксИдентификатораРежимаОтладкиОбщий() Экспорт
	
	Возврат "1c-";
	
КонецФункции

// Возвращает префикс идентификатора для режима отладки.
// 
// Возвращаемое значение:
//  Строка - Префикс идентификатора режима отладки.
//
Функция ПрефиксИдентификатораРежимаОтладки() Экспорт
	
	ПрефиксИдентификатора = НРег(СокрЛП(ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("РежимОтладкиЕГАИС", "ПрефиксИдентификатора", "")));
	Если СтрДлина(ПрефиксИдентификатора) > 6 Тогда
		ПрефиксИдентификатора = Лев(ПрефиксИдентификатора, 6);
	КонецЕсли;
	Префикс = ПрефиксИдентификатораРежимаОтладкиОбщий() + ПрефиксИдентификатора + "-";
	
	Возврат Префикс;
	
КонецФункции

// Сообщать об ошибках при загрузке данных.
// 
// Возвращаемое значение:
//  Булево - Необходимость сообщения об ошибках при загрузке данных.
//
Функция СообщатьОбОшибкахПриЗагрузкеДанных() Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.РежимОтладки() Тогда
		
		СообщатьОбОшибкахПриЗагрузкеДанных = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"РежимОтладкиЕГАИС", "СообщатьОбОшибкахПриЗагрузкеДанных", Истина);
		
		Если СообщатьОбОшибкахПриЗагрузкеДанных = Истина Тогда
			Возврат Истина;
		ИначеЕсли СообщатьОбОшибкахПриЗагрузкеДанных = Ложь Тогда
			Возврат Ложь;
		Иначе
			Возврат Истина;
		КонецЕсли;
		
	Иначе
		
		Возврат Истина;
		
	КонецЕсли;
	
КонецФункции

// Помечает/снимает пометку удаления у приложенных файлов.
Процедура ПометитьНаУдалениеПрисоединенныеФайлыЕГАИС(Знач Источник, ДляОрганизацииЕГАИС = Ложь)
	
	Если Источник.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИсточникСсылкаПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "ПометкаУдаления");
	
	Если Источник.ПометкаУдаления = ИсточникСсылкаПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
	
	Если ДляОрганизацииЕГАИС Тогда
		ТекстЗапроса =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Файлы.Ссылка КАК Ссылка,
			|	Файлы.Редактирует КАК Редактирует
			|ИЗ
			|	Справочник.ЕГАИСПрисоединенныеФайлы КАК Файлы
			|ГДЕ
			|	Файлы.ВладелецФайла = &Ссылка
			|	И Файлы.Документ = НЕОПРЕДЕЛЕНО";
	Иначе
		ТекстЗапроса =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Файлы.Ссылка КАК Ссылка,
			|	Файлы.Редактирует КАК Редактирует
			|ИЗ
			|	Справочник.ЕГАИСПрисоединенныеФайлы КАК Файлы
			|ГДЕ
			|	Файлы.Документ = &Ссылка";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Источник.ПометкаУдаления И ЗначениеЗаполнено(Выборка.Редактирует) Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '""%1"" не может быть удален,
				           |т.к. содержит присоединенный файл ""%2"",
				           |занятый для редактирования.'"),
				Строка(Источник.Ссылка),
				Строка(Выборка.Ссылка));
		КонецЕсли;
		ФайлОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ФайлОбъект.Заблокировать();
		ФайлОбъект.УстановитьПометкуУдаления(Источник.ПометкаУдаления);
	КонецЦикла;
	
КонецПроцедуры

// Рассчитать статус документа ЕГАИС.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка - Документ ЕГАИС.
//  ЕстьЗаписиВПротоколеОбмена - Булево - Признак наличия записей в протоколе обмена (Возвращаемый параметр).
// 
// Возвращаемое значение:
//  НаборЗаписей.СтатусыДокументовЕГАИС - Набор записей регистра сведений СтатусыДокументовЕГАИС.
//
Функция РассчитатьСтатус(ДокументСсылка, ЕстьЗаписиВПротоколеОбмена = Неопределено, ЭтоОбновлениеИБ = Ложь) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕГАИСПрисоединенныеФайлы.Ссылка КАК Ссылка,
	|	ЕГАИСПрисоединенныеФайлы.Операция КАК Операция,
	|	ЕГАИСПрисоединенныеФайлы.ТипСообщения КАК ТипСообщения,
	|	ЕГАИСПрисоединенныеФайлы.ДатаСоздания КАК ДатаСоздания,
	|	ЕГАИСПрисоединенныеФайлы.ОперацияКвитанции КАК ОперацияКвитанции,
	|	ВЫБОР
	|		КОГДА ОчередьПередачиДанныхЕГАИС.Сообщение ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК КПередаче,
	|	ВЫБОР
	|		КОГДА ЕГАИСПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовЕГАИС.Исходящий)
	|				И ОтветНаПередачуДанных.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПереданВУТМ,
	|	ВЫБОР
	|		КОГДА ЕГАИСПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовЕГАИС.Исходящий)
	|			ТОГДА ОтветНаПередачуДанных.СтатусОбработки
	|		ИНАЧЕ ЕГАИСПрисоединенныеФайлы.СтатусОбработки
	|	КОНЕЦ КАК СтатусОбработки,
	|	ВЫБОР
	|		КОГДА ЕГАИСПрисоединенныеФайлы.Операция В (&Квитанции)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоКвитанция
	|ИЗ
	|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕГАИСПрисоединенныеФайлы КАК ОтветНаПередачуДанных
	|		ПО ЕГАИСПрисоединенныеФайлы.Ссылка = ОтветНаПередачуДанных.СообщениеОснование
	|			И ЕГАИСПрисоединенныеФайлы.Операция = ОтветНаПередачуДанных.Операция
	|			И (ОтветНаПередачуДанных.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовЕГАИС.Входящий))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОчередьПередачиДанныхЕГАИС КАК ОчередьПередачиДанныхЕГАИС
	|		ПО (ОчередьПередачиДанныхЕГАИС.Сообщение = ЕГАИСПрисоединенныеФайлы.Ссылка)
	|ГДЕ
	|	ЕГАИСПрисоединенныеФайлы.Документ = &ДокументСсылка
	|	И ВЫБОР
	|			КОГДА ЕГАИСПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовЕГАИС.Входящий)
	|					И ЕГАИСПрисоединенныеФайлы.СообщениеОснование <> ЗНАЧЕНИЕ(Справочник.ЕГАИСПрисоединенныеФайлы.ПустаяСсылка)
	|					И НЕ ЕГАИСПрисоединенныеФайлы.Операция В (&Квитанции)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЕГАИСПрисоединенныеФайлы.ДатаСоздания ВОЗР,
	|	ВЫБОР
	|		КОГДА ЕГАИСПрисоединенныеФайлы.Операция В (ЗНАЧЕНИЕ(Перечисление.ВидыДокументовЕГАИС.КвитанцияПолученЕГАИС))
	|			ТОГДА 3
	|		КОГДА ЕГАИСПрисоединенныеФайлы.Операция В (ЗНАЧЕНИЕ(Перечисление.ВидыДокументовЕГАИС.КвитанцияПроведенЕГАИС))
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ УБЫВ
	|");
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Квитанции = Новый Массив;
	Квитанции.Добавить(Перечисления.ВидыДокументовЕГАИС.КвитанцияПолученЕГАИС);
	Квитанции.Добавить(Перечисления.ВидыДокументовЕГАИС.КвитанцияПроведенЕГАИС);
	Запрос.УстановитьПараметр("Квитанции", Квитанции);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаборЗаписей = РегистрыСведений.СтатусыДокументовЕГАИС.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(ДокументСсылка);
	
	ЗначенияПоУмолчанию = РегистрыСведений.СтатусыДокументовЕГАИС.ЗначенияПоУмолчанию(ДокументСсылка);
	
	ЗаписьНабора = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗначенияПоУмолчанию);
	
	Если Выборка.Количество() > 0 Тогда
		
		ПолноеИмя = ДокументСсылка.Метаданные().ПолноеИмя();
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
		
		ЕстьОшибки = Ложь;
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ТипСообщения = Перечисления.ТипыЗапросовЕГАИС.Исходящий Тогда
				
				Если Выборка.КПередаче Тогда
					
					Если Не ЭтоОбновлениеИБ Тогда
						ПараметрыОбновления = МенеджерОбъекта.СтатусПослеПодготовкиКПередачеДанных(
							ДокументСсылка,
							Выборка.Операция);
					Иначе
						Попытка
							ПараметрыОбновления = МенеджерОбъекта.СтатусПослеПодготовкиКПередачеДанных(
								ДокументСсылка,
								Выборка.Операция);
						Исключение
							ЕстьОшибки = Истина;
							Прервать;
						КонецПопытки;
					КонецЕсли;
					
				ИначеЕсли Выборка.ПереданВУТМ Тогда
					
					Если Не ЭтоОбновлениеИБ Тогда
						ПараметрыОбновления = МенеджерОбъекта.СтатусПослеПередачиДанных(
							ДокументСсылка,
							Выборка.Операция, Выборка.СтатусОбработки);
					Иначе
						Попытка
							ПараметрыОбновления = МенеджерОбъекта.СтатусПослеПередачиДанных(
								ДокументСсылка,
								Выборка.Операция, Выборка.СтатусОбработки);
						Исключение
							ЕстьОшибки = Истина;
							Прервать;
						КонецПопытки;
					КонецЕсли;
					
				Иначе
					Продолжить;
				КонецЕсли;
				
			Иначе
				
				Если Не ЭтоОбновлениеИБ Тогда
					ПараметрыОбновления = ПараметрыОбновленияПослеПолученияДанных(
						ЗаписьНабора, МенеджерОбъекта, ДокументСсылка, Выборка.Операция,
						Выборка.ЭтоКвитанция, Выборка.ОперацияКвитанции, Выборка.СтатусОбработки);
				Иначе
					Попытка
						ПараметрыОбновления = ПараметрыОбновленияПослеПолученияДанных(
							ЗаписьНабора, МенеджерОбъекта, ДокументСсылка, Выборка.Операция,
							Выборка.ЭтоКвитанция, Выборка.ОперацияКвитанции, Выборка.СтатусОбработки);
					Исключение
						ЕстьОшибки = Истина;
						Прервать;
					КонецПопытки;
				КонецЕсли;
				
			КонецЕсли;
			
			Если ПараметрыОбновления <> Неопределено Тогда
				РегистрыСведений.СтатусыДокументовЕГАИС.ОбработатьНаборЗаписей(НаборЗаписей, ПараметрыОбновления);
			КонецЕсли;
			
		КонецЦикла;
		
		ЕстьЗаписиВПротоколеОбмена = Не ЕстьОшибки;
		
		Если ЕстьОшибки Тогда
			
			НаборЗаписей.Очистить();
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, ЗначенияПоУмолчанию);
			
		КонецЕсли;
		
	Иначе
		
		ЕстьЗаписиВПротоколеОбмена = Ложь;
		
	КонецЕсли;
	
	Возврат НаборЗаписей;
	
КонецФункции

// Возвращает параметры обновления после получения данных.
//
// Параметры:
//  ЗаписьНабора - ЗаписьНабора - Запись набора.
//  МенеджерОбъекта - ДокументМенеджер - Менеджер документа.
//  ДокументСсылка - ДокументСсылка - Ссылка на документ.
//  Операция - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция.
//  ЭтоКвитанция - Булево - Признак квитанции.
//  ОперацияКвитанции - ПеречислениеСсылка.ВидыДокументовЕГАИС - Операция квитанции.
//  СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийЕГАИС - Статус обработки сообщения.
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * НовыйСтатус - ПеречисленияСсылка.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие 3.
//
Функция ПараметрыОбновленияПослеПолученияДанных(ЗаписьНабора, МенеджерОбъекта, ДокументСсылка, Операция,
	ЭтоКвитанция = Ложь, ОперацияКвитанции = Неопределено, СтатусОбработки = Неопределено) Экспорт
	
	ТекущееСостояние = РегистрыСведений.СтатусыДокументовЕГАИС.ЗначенияПоУмолчанию(ДокументСсылка);
	ЗаполнитьЗначенияСвойств(ТекущееСостояние, ЗаписьНабора);
	
	Если ЭтоКвитанция Тогда
		ДополнительныеПараметры = ПараметрыОбновленияСтатуса();
		ДополнительныеПараметры.ОперацияКвитанции = ОперацияКвитанции;
		ДополнительныеПараметры.СтатусОбработки   = СтатусОбработки;
		ДополнительныеПараметры.ТекущееСостояние  = ТекущееСостояние;
	Иначе
		ДополнительныеПараметры = ПараметрыОбновленияСтатуса();
		ДополнительныеПараметры.ТекущееСостояние  = ТекущееСостояние;
	КонецЕсли;
	
	ПараметрыОбновления = МенеджерОбъекта.СтатусПослеПолученияДанных(
		ДокументСсылка,
		Операция, ДополнительныеПараметры);
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Устанавливает видимость команды "Оформить" в формах списка документов.
//
Процедура УстановитьВидимостьКомандыОформленияДокумента(Форма, МетаданныеДокумента, ИмяЭлемента) Экспорт
	
	Если НЕ ПравоДоступа("Добавление", МетаданныеДокумента) Тогда
		Кнопка = Форма.Элементы.Найти(ИмяЭлемента);
		Если Кнопка <> Неопределено Тогда
			Кнопка.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


#Область ПодборСправок2

// Инициализирует начальные данные для подбора справок 2 для списания по LIFO.
//
Функция ПодготовитьДанныеДляПодбораСправок2(Товары)
	
	ТаблицаДляЗаполнения = Новый ТаблицаЗначений;
	ТаблицаДляЗаполнения.Колонки.Добавить("АлкогольнаяПродукция", Новый ОписаниеТипов("СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС"));
	ТаблицаДляЗаполнения.Колонки.Добавить("Номенклатура",         Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
	ТаблицаДляЗаполнения.Колонки.Добавить("Характеристика",       Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
	ТаблицаДляЗаполнения.Колонки.Добавить("Серия",                Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип);
	ТаблицаДляЗаполнения.Колонки.Добавить("Количество",           Новый ОписаниеТипов("Число"));
	
	ТаблицаДляСписания = Новый ТаблицаЗначений;
	ТаблицаДляСписания.Колонки.Добавить("АлкогольнаяПродукция", Новый ОписаниеТипов("СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС"));
	ТаблицаДляСписания.Колонки.Добавить("Номенклатура",         Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
	ТаблицаДляСписания.Колонки.Добавить("Характеристика",       Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
	ТаблицаДляСписания.Колонки.Добавить("Серия",                Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип);
	ТаблицаДляСписания.Колонки.Добавить("Справка2",             Новый ОписаниеТипов("СправочникСсылка.Справки2ЕГАИС"));
	ТаблицаДляСписания.Колонки.Добавить("Количество",           Новый ОписаниеТипов("Число"));
	
	Для Каждого СтрокаТовары Из Товары Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТовары.Справка2)Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаДляЗаполнения.Добавить(), СтрокаТовары);
		Иначе
			ЗаполнитьЗначенияСвойств(ТаблицаДляСписания.Добавить(), СтрокаТовары);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТаблицаДляЗаполнения.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТаблицаДляСписания.Свернуть("АлкогольнаяПродукция, Номенклатура, Характеристика, Серия, Справка2", "Количество");
	ТаблицаДляЗаполнения.Свернуть("АлкогольнаяПродукция, Номенклатура, Характеристика, Серия", "Количество");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДляЗаполнения.АлкогольнаяПродукция,
	|	ТаблицаДляЗаполнения.Номенклатура,
	|	ТаблицаДляЗаполнения.Характеристика,
	|	ТаблицаДляЗаполнения.Серия,
	|	ТаблицаДляЗаполнения.Количество
	|ПОМЕСТИТЬ втТаблицаДляЗаполненияПодготовка
	|ИЗ
	|	&ТаблицаДляЗаполнения КАК ТаблицаДляЗаполнения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДляСписания.АлкогольнаяПродукция,
	|	ТаблицаДляСписания.Номенклатура,
	|	ТаблицаДляСписания.Характеристика,
	|	ТаблицаДляСписания.Серия,
	|	ТаблицаДляСписания.Справка2,
	|	ТаблицаДляСписания.Количество
	|ПОМЕСТИТЬ втТаблицаДляСписанияПодготовка
	|ИЗ
	|	&ТаблицаДляСписания КАК ТаблицаДляСписания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Т.Номенклатура КАК Номенклатура,
	|	Т.Характеристика КАК Характеристика,
	|	Т.Серия КАК Серия,
	|	СоответствиеНоменклатурыЕГАИС.Справка2 КАК Справка2,
	|	Т.Количество
	|ПОМЕСТИТЬ втТаблицаСоответствияБезАлкоПродукции
	|ИЗ
	|	втТаблицаДляЗаполненияПодготовка КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|ПО
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура = Т.Номенклатура
	|	И СоответствиеНоменклатурыЕГАИС.Характеристика = Т.Характеристика
	|	И СоответствиеНоменклатурыЕГАИС.Серия = Т.Серия
	|ГДЕ
	|	Т.АлкогольнаяПродукция = ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)
	|;
	|
	|";
	
	ТекстЗапроса = ТекстЗапроса
		+ ИнтеграцияЕГАИС.ТекстЗапросаВТКоэффициентыПересчетаВЕдиницыЕГАИС(
			"втТаблицаДляСписанияПодготовка",   "ВТТаблицаДляСписанияКоэффициентыПересчетаВЕдиницыЕГАИС", Истина);
		
	ТекстЗапроса = ТекстЗапроса
		+ ИнтеграцияЕГАИС.ТекстЗапросаВТКоэффициентыПересчетаВЕдиницыЕГАИС(
			"втТаблицаДляЗаполненияПодготовка", "ВТТаблицаДляЗаполненияКоэффициентыПересчетаВЕдиницыЕГАИС", Истина);
		
	ТекстЗапроса = ТекстЗапроса
		+ ИнтеграцияЕГАИС.ТекстЗапросаВТКоэффициентыПересчетаВЕдиницыЕГАИС(
			"втТаблицаСоответствияБезАлкоПродукции", "втТаблицаСоответствияБезАлкоПродукцииКоэффициентыПересчетаВЕдиницыЕГАИС", Истина);
		
	ТекстЗапроса = ТекстЗапроса +
	"ВЫБРАТЬ
	|	ТаблицаДляСписания.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТаблицаДляСписания.Справка2             КАК Справка2,
	|	СУММА(ТаблицаДляСписания.Количество
	|	* ЕСТЬNULL(ЕдиницыЕГАИС.Коэффициент, 1)) КАК Количество
	|ПОМЕСТИТЬ втТаблицаДляСписания
	|ИЗ
	|	втТаблицаДляСписанияПодготовка КАК ТаблицаДляСписания
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаДляСписанияКоэффициентыПересчетаВЕдиницыЕГАИС КАК ЕдиницыЕГАИС
	|		ПО ЕдиницыЕГАИС.АлкогольнаяПродукция = ТаблицаДляСписания.АлкогольнаяПродукция
	|		 И ЕдиницыЕГАИС.Номенклатура = ТаблицаДляСписания.Номенклатура
	|		 И ЕдиницыЕГАИС.Характеристика = ТаблицаДляСписания.Характеристика
	|		 И ЕдиницыЕГАИС.Серия = ТаблицаДляСписания.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДляСписания.АлкогольнаяПродукция,
	|	ТаблицаДляСписания.Справка2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДляЗаполнения.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТаблицаДляЗаполнения.Количество
	|	* ЕСТЬNULL(ЕдиницыЕГАИС.Коэффициент, 1)) КАК Количество
	|ПОМЕСТИТЬ втТаблицаДляЗаполнения
	|ИЗ
	|	втТаблицаДляЗаполненияПодготовка КАК ТаблицаДляЗаполнения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаДляЗаполненияКоэффициентыПересчетаВЕдиницыЕГАИС КАК ЕдиницыЕГАИС
	|		ПО ЕдиницыЕГАИС.АлкогольнаяПродукция = ТаблицаДляЗаполнения.АлкогольнаяПродукция
	|		 И ЕдиницыЕГАИС.Номенклатура = ТаблицаДляЗаполнения.Номенклатура
	|		 И ЕдиницыЕГАИС.Характеристика = ТаблицаДляЗаполнения.Характеристика
	|		 И ЕдиницыЕГАИС.Серия = ТаблицаДляЗаполнения.Серия
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДляЗаполнения.АлкогольнаяПродукция
	|;
	//|
	//|////////////////////////////////////////////////////////////////////////////////
	//|ВЫБРАТЬ
	//|	Т.Номенклатура КАК Номенклатура,
	//|	Т.Характеристика КАК Характеристика,
	//|	Т.Серия КАК Серия,
	//|	Т.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	//|	Т.Справка2 КАК Справка2
	//|ПОМЕСТИТЬ втТаблицаСоответствияБезАлкоПродукции
	//|ИЗ
	//|	втТаблицаДляЗаполненияПодготовка КАК Т
	//|ЛЕВОЕ СОЕДИНЕНИЕ
	//|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	//|ПО
	//|	СоответствиеНоменклатурыЕГАИС.Номенклатура = Т.Номенклатура
	//|	И СоответствиеНоменклатурыЕГАИС.Характеристика = Т.Характеристика
	//|	И СоответствиеНоменклатурыЕГАИС.Серия = Т.Серия
	//|ГДЕ
	//|	Т.АлкогольнаяПродукция = ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)
	//|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Т.Номенклатура         КАК Номенклатура,
	|	Т.Характеристика       КАК Характеристика,
	|	Т.Серия                КАК Серия,
	|	Т.ПроверятьОбъемДАЛ    КАК ПроверятьОбъемДАЛ,
	|	Т.ОбъемДАЛ             КАК ОбъемДАЛ,
	|	Т.Коэффициент КАК      Коэффициент
	|ИЗ
	|	ВТТаблицаДляЗаполненияКоэффициентыПересчетаВЕдиницыЕГАИС КАК Т
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Т.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Т.Номенклатура         КАК Номенклатура,
	|	Т.Характеристика       КАК Характеристика,
	|	Т.Серия                КАК Серия,
	|	Т.ПроверятьОбъемДАЛ    КАК ПроверятьОбъемДАЛ,
	|	Т.ОбъемДАЛ             КАК ОбъемДАЛ,
	|	Т.Коэффициент КАК      Коэффициент
	|ИЗ
	|	ВТТаблицаДляСписанияКоэффициентыПересчетаВЕдиницыЕГАИС КАК Т
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Т.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Т.Номенклатура         КАК Номенклатура,
	|	Т.Характеристика       КАК Характеристика,
	|	Т.Серия                КАК Серия,
	|	Т.ПроверятьОбъемДАЛ    КАК ПроверятьОбъемДАЛ,
	|	Т.ОбъемДАЛ             КАК ОбъемДАЛ,
	|	Т.Коэффициент КАК      Коэффициент
	|ИЗ
	|	втТаблицаСоответствияБезАлкоПродукцииКоэффициентыПересчетаВЕдиницыЕГАИС КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	втТаблицаДляЗаполнения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	втТаблицаДляСписания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Номенклатура КАК Номенклатура,
	|	Т.Характеристика КАК Характеристика,
	|	Т.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Т.Серия КАК Серия,
	|	СоответствиеНоменклатурыЕГАИС.Справка2 КАК Справка2
	|ИЗ
	|	втТаблицаДляЗаполненияПодготовка КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|ПО
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура = Т.Номенклатура
	|	И СоответствиеНоменклатурыЕГАИС.Характеристика = Т.Характеристика
	|	И СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция = Т.АлкогольнаяПродукция
	|	И СоответствиеНоменклатурыЕГАИС.Серия = Т.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	втТаблицаСоответствияБезАлкоПродукции
	|;
	|";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаДляЗаполнения", ТаблицаДляЗаполнения);
	Запрос.УстановитьПараметр("ТаблицаДляСписания",   ТаблицаДляСписания);
	Запрос.УстановитьПараметр("ПустаяСерия", ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	Результат = Запрос.ВыполнитьПакет();
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("МенеджерВременныхТаблиц",            МенеджерВременныхТаблиц);
	ВозвращаемоеЗначение.Вставить("ТаблицаСоответствияБезАлкоПродукции",Результат[Результат.Количество() - 1].Выгрузить());
	ВозвращаемоеЗначение.Вставить("ТаблицаСоответствия",                Результат[Результат.Количество() - 2].Выгрузить());
	ВозвращаемоеЗначение.Вставить("ТаблицаДляСписания",                 Результат[Результат.Количество() - 3].Выгрузить());
	ВозвращаемоеЗначение.Вставить("ТаблицаДляЗаполнения",               Результат[Результат.Количество() - 4].Выгрузить());
	ВозвращаемоеЗначение.Вставить("КоэффициентыПересчетаВЕдиницыЕГАИС", Результат[Результат.Количество() - 5].Выгрузить());
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Заполняет справку 2 в тех строках таблицы Товары, где справка 2 не заполнена.
// Если в строке таблицы Товары документа указана серия, то справка 2 будет заполнена только в том случае,
// если в регистре сведений "Соответствие номенклатуры ЕГАИС" есть запись для этой серии и на остатках есть справка 2.
// Если в строке таблицы Товары документа не указана серия, то справка 2 будет подобрана по следующему алгоритму.
// Сначала будут подобраны справки 2, для которых есть записи в регистре сведений "Соответствие номенклатуры ЕГАИС".
// Затем будут подобраны справки 2, для которых в регистре сведений "Соответствие номенклатуры ЕГАИС" записей нет.
// Если в строке не заполнена алкогольная продукция, то она заполняется по данным регистра сведений
// "Соответствие номенклатуры ЕГАИС" и остаткам алкогольной продукции
//
Функция ЗаполнитьСправку2ВТабличнойЧасти(Товары, АлкогольнаяПродукция, Справка2, ОстатокПоСправке, СтруктураПересчетаСуммы, КоэффициентыПересчетаВЕдиницыЕГАИС, ТаблицаСоответствия, ТаблицаСоответствияБезАлкоПродукции = Неопределено)
	
	ЕстьСтрокиДляЗаполнения = Истина;
	
	Отбор1 = Новый Структура;
	Отбор1.Вставить("АлкогольнаяПродукция", АлкогольнаяПродукция);
	Отбор1.Вставить("Справка2",             Справочники.Справки2ЕГАИС.ПустаяСсылка());
	
	Отбор2 = Новый Структура;
	Отбор2.Вставить("АлкогольнаяПродукция", АлкогольнаяПродукция);
	Отбор2.Вставить("Справка2",             Справочники.Справки2ЕГАИС.ПустаяСсылка());
	Отбор2.Вставить("Серия",                ИнтеграцияЕГАИС.ПустоеЗначениеОпределяемогоТипа(Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Имя));
	
	Отбор3 = Новый Структура;
	Отбор3.Вставить("АлкогольнаяПродукция", Справочники.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка());
	Отбор2.Вставить("Справка2",             Справочники.Справки2ЕГАИС.ПустаяСсылка());
	
	Пока ОстатокПоСправке > 0 Цикл
		
		МассивСтрок1 = Товары.НайтиСтроки(Отбор1);
		МассивСтрок2 = Товары.НайтиСтроки(Отбор2);
		МассивСтрок3 = Товары.НайтиСтроки(Отбор3);
		
		Если МассивСтрок1.Количество() = 0 И
			МассивСтрок2.Количество() = 0 И
			МассивСтрок3.Количество() = 0 Тогда
			
			ЕстьСтрокиДляЗаполнения = Ложь;
			
		КонецЕсли;
		
		КоличествоНеподходящихСтрокТЧ = 0;
		
		Если МассивСтрок1.Количество() > 0 Тогда
		
			Для Каждого СтрокаТЧ Из МассивСтрок1 Цикл
				
				Если Не ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						СтрШаблон(НСтр("ru = 'Не заполнена номенклатура в строке %1 табличной части ""Товары"".'"), СтрокаТЧ.НомерСтроки));
						
					КоличествоНеподходящихСтрокТЧ = КоличествоНеподходящихСтрокТЧ + 1;
					Продолжить;
					
				КонецЕсли;
				
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("АлкогольнаяПродукция", СтрокаТЧ.АлкогольнаяПродукция);
				ПараметрыОтбора.Вставить("Номенклатура",         СтрокаТЧ.Номенклатура);
				ПараметрыОтбора.Вставить("Характеристика",       СтрокаТЧ.Характеристика);
				ПараметрыОтбора.Вставить("Серия",                СтрокаТЧ.Серия);
				
				НайденныеСтрокиКоэффициенты = КоэффициентыПересчетаВЕдиницыЕГАИС.НайтиСтроки(ПараметрыОтбора);
				
				Если НайденныеСтрокиКоэффициенты.Количество() > 0 Тогда
				
					ПараметрыОтбораСоответствиеСправка2 = Новый Структура;
					ПараметрыОтбораСоответствиеСправка2.Вставить("АлкогольнаяПродукция", СтрокаТЧ.АлкогольнаяПродукция);
					ПараметрыОтбораСоответствиеСправка2.Вставить("Номенклатура",         СтрокаТЧ.Номенклатура);
					ПараметрыОтбораСоответствиеСправка2.Вставить("Характеристика",       СтрокаТЧ.Характеристика);
					ПараметрыОтбораСоответствиеСправка2.Вставить("Серия",                СтрокаТЧ.Серия);
					ПараметрыОтбораСоответствиеСправка2.Вставить("Справка2",             Справка2);
					
					НайденныеСтрокиСоответствиеСправка2 = ТаблицаСоответствия.НайтиСтроки(ПараметрыОтбораСоответствиеСправка2);
					
					Если НайденныеСтрокиСоответствиеСправка2.Количество() = 0 Тогда
						КоличествоНеподходящихСтрокТЧ = КоличествоНеподходящихСтрокТЧ + 1;
						Продолжить;
					Иначе
						СтрокаТаблицыКоэффициентов = НайденныеСтрокиКоэффициенты[0];
					КонецЕсли;
				
				Иначе
					
					КоличествоНеподходящихСтрокТЧ = КоличествоНеподходящихСтрокТЧ + 1;
					Продолжить;
					
				КонецЕсли;
				
				Если СтрокаТаблицыКоэффициентов.Коэффициент = 0 Тогда
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						СтрШаблон(
							НСтр("ru = 'Для номенклатуры %1 в строке %2 табличной части ""Товары"" не заполнен объем в дал .'"),
							СтрокаТЧ.Номенклатура,
							СтрокаТЧ.НомерСтроки));
					
					КоличествоНеподходящихСтрокТЧ = КоличествоНеподходящихСтрокТЧ + 1;
					Продолжить;
					
				КонецЕсли;
				
				ОстатокПоСправкеПриведенный = ОстатокПоСправке / СтрокаТаблицыКоэффициентов.Коэффициент;
				Если ОстатокПоСправкеПриведенный = 0 Тогда
					
					КоличествоНеподходящихСтрокТЧ = КоличествоНеподходящихСтрокТЧ + 1;
					Продолжить;
					
				КонецЕсли;
				
				СтрокаТЧ.Справка2 = Справка2;
				
				Если СтрокаТЧ.Количество > ОстатокПоСправкеПриведенный Тогда
					
					ИнтеграцияЕГАИСКлиентСервер.ЗаполнитьСтруктуруПересчетаСуммы(СтруктураПересчетаСуммы, СтрокаТЧ);
					
					РазницаПоКоличеству = СтрокаТЧ.Количество - ОстатокПоСправкеПриведенный;
					СтрокаТЧ.Количество = ОстатокПоСправкеПриведенный;
					
					НоваяСтрока = Товары.Вставить(Товары.Индекс(СтрокаТЧ) + 1);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
					НоваяСтрока.Количество = РазницаПоКоличеству;
					НоваяСтрока.Справка2   = Справочники.Справки2ЕГАИС.ПустаяСсылка();
					
					ИнтеграцияЕГАИСКлиентСервер.ДобавитьСтрокуДляПересчетаСуммы(СтруктураПересчетаСуммы, СтрокаТЧ);
					ИнтеграцияЕГАИСКлиентСервер.ДобавитьСтрокуДляПересчетаСуммы(СтруктураПересчетаСуммы, НоваяСтрока);
					ИнтеграцияЕГАИСКлиентСервер.ПересчитатьСуммы(СтруктураПересчетаСуммы);
					
				КонецЕсли;
				
				ОстатокПоСправке = ОстатокПоСправке - СтрокаТЧ.Количество * СтрокаТаблицыКоэффициентов.Коэффициент;
				Если ОстатокПоСправке <= 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		// Дозаполним справки 2 в строках таблицы, для которых нет записей в регистре сведений "Соответствие номенклатуры ЕГАИС"
		
		КоличествоНеподходящихСтрокТЧ = 0;
		Если МассивСтрок2.Количество() > 0 Тогда
		
			КоличествоНеподходящихСтрокТЧ = 0;
			
			Для Каждого СтрокаТЧ Из МассивСтрок2 Цикл
				
				Если Не ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
					
					КоличествоНеподходящихСтрокТЧ = КоличествоНеподходящихСтрокТЧ + 1;
					Продолжить;
					
				КонецЕсли;
				
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("АлкогольнаяПродукция", СтрокаТЧ.АлкогольнаяПродукция);
				ПараметрыОтбора.Вставить("Номенклатура",         СтрокаТЧ.Номенклатура);
				ПараметрыОтбора.Вставить("Характеристика",       СтрокаТЧ.Характеристика);
				ПараметрыОтбора.Вставить("Серия",                СтрокаТЧ.Серия);
				
				НайденныеСтрокиКоэффициенты = КоэффициентыПересчетаВЕдиницыЕГАИС.НайтиСтроки(ПараметрыОтбора);
				
				Если НайденныеСтрокиКоэффициенты.Количество() > 0 Тогда
					СтрокаТаблицыКоэффициентов = НайденныеСтрокиКоэффициенты[0];
				Иначе
					
					КоличествоНеподходящихСтрокТЧ = КоличествоНеподходящихСтрокТЧ + 1;
					Продолжить;
					
				КонецЕсли;
				
				Если СтрокаТаблицыКоэффициентов.Коэффициент = 0 Тогда
					
					КоличествоНеподходящихСтрокТЧ = КоличествоНеподходящихСтрокТЧ + 1;
					Продолжить;
					
				КонецЕсли;
				
				ОстатокПоСправкеПриведенный = ОстатокПоСправке / СтрокаТаблицыКоэффициентов.Коэффициент;
				Если ОстатокПоСправкеПриведенный = 0 Тогда
					
					КоличествоНеподходящихСтрокТЧ = КоличествоНеподходящихСтрокТЧ + 1;
					Продолжить;
					
				КонецЕсли;
				
				СтрокаТЧ.Справка2 = Справка2;
				
				Если СтрокаТЧ.Количество > ОстатокПоСправкеПриведенный Тогда
					
					ИнтеграцияЕГАИСКлиентСервер.ЗаполнитьСтруктуруПересчетаСуммы(СтруктураПересчетаСуммы, СтрокаТЧ);
					
					РазницаПоКоличеству = СтрокаТЧ.Количество - ОстатокПоСправкеПриведенный;
					СтрокаТЧ.Количество = ОстатокПоСправкеПриведенный;
					
					НоваяСтрока = Товары.Вставить(Товары.Индекс(СтрокаТЧ) + 1);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
					НоваяСтрока.Количество = РазницаПоКоличеству;
					НоваяСтрока.Справка2   = Справочники.Справки2ЕГАИС.ПустаяСсылка();
					
					ИнтеграцияЕГАИСКлиентСервер.ДобавитьСтрокуДляПересчетаСуммы(СтруктураПересчетаСуммы, СтрокаТЧ);
					ИнтеграцияЕГАИСКлиентСервер.ДобавитьСтрокуДляПересчетаСуммы(СтруктураПересчетаСуммы, НоваяСтрока);
					ИнтеграцияЕГАИСКлиентСервер.ПересчитатьСуммы(СтруктураПересчетаСуммы);
					
				КонецЕсли;
				
				ОстатокПоСправке = ОстатокПоСправке - СтрокаТЧ.Количество * СтрокаТаблицыКоэффициентов.Коэффициент;
				Если ОстатокПоСправке <= 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		// Дозаполним справки 2 и алкогольную продукцию в строках таблицы с незаполненной алкогольной продукцией
		КоличествоНеподходящихСтрокТЧ = 0;
		Если МассивСтрок3.Количество() > 0 Тогда
		
			Для Каждого СтрокаТЧ Из МассивСтрок3 Цикл
				
				Если Не ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
					
					КоличествоНеподходящихСтрокТЧ = КоличествоНеподходящихСтрокТЧ + 1;
					Продолжить;
					
				КонецЕсли;
				
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("АлкогольнаяПродукция", АлкогольнаяПродукция);
				ПараметрыОтбора.Вставить("Справка2",             Справка2);
				ПараметрыОтбора.Вставить("Номенклатура",         СтрокаТЧ.Номенклатура);
				ПараметрыОтбора.Вставить("Характеристика",       СтрокаТЧ.Характеристика);
				ПараметрыОтбора.Вставить("Серия",                СтрокаТЧ.Серия);
				
				НайденныеСтрокиСоответствиеБезАлкоПродукции = ТаблицаСоответствияБезАлкоПродукции.НайтиСтроки(ПараметрыОтбора);
				
				Если НайденныеСтрокиСоответствиеБезАлкоПродукции.Количество() > 0 Тогда
					
					ПараметрыОтбора = Новый Структура;
					ПараметрыОтбора.Вставить("АлкогольнаяПродукция", АлкогольнаяПродукция);
					ПараметрыОтбора.Вставить("Номенклатура",         СтрокаТЧ.Номенклатура);
					ПараметрыОтбора.Вставить("Характеристика",       СтрокаТЧ.Характеристика);
					ПараметрыОтбора.Вставить("Серия",                СтрокаТЧ.Серия);
					НайденныеСтрокиКоэффициенты = КоэффициентыПересчетаВЕдиницыЕГАИС.НайтиСтроки(ПараметрыОтбора);
					
					Если НайденныеСтрокиКоэффициенты.Количество() > 0 Тогда
						СтрокаТаблицыКоэффициентов = НайденныеСтрокиКоэффициенты[0];
					Иначе
						
						КоличествоНеподходящихСтрокТЧ = КоличествоНеподходящихСтрокТЧ + 1;
						Продолжить;
						
					КонецЕсли;
					
					Если СтрокаТаблицыКоэффициентов.Коэффициент = 0 Тогда
						
						КоличествоНеподходящихСтрокТЧ = КоличествоНеподходящихСтрокТЧ + 1;
						Продолжить;
						
					КонецЕсли;
					
					СтрокаТЧ.АлкогольнаяПродукция = АлкогольнаяПродукция;
					СтрокаТЧ.Справка2 = Справка2;
					
					ОстатокПоСправкеПриведенный = ОстатокПоСправке / СтрокаТаблицыКоэффициентов.Коэффициент;
					Если ОстатокПоСправкеПриведенный = 0 Тогда
						
						КоличествоНеподходящихСтрокТЧ3 = КоличествоНеподходящихСтрокТЧ3 + 1;
						Продолжить;
						
					КонецЕсли;
					
					Если СтрокаТЧ.Количество > ОстатокПоСправкеПриведенный Тогда
						
						ИнтеграцияЕГАИСКлиентСервер.ЗаполнитьСтруктуруПересчетаСуммы(СтруктураПересчетаСуммы, СтрокаТЧ);
						
						РазницаПоКоличеству = СтрокаТЧ.Количество - ОстатокПоСправкеПриведенный;
						СтрокаТЧ.Количество = ОстатокПоСправкеПриведенный;
						
						НоваяСтрока = Товары.Вставить(Товары.Индекс(СтрокаТЧ) + 1);
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
						НоваяСтрока.Количество = РазницаПоКоличеству;
						НоваяСтрока.Справка2   = Справочники.Справки2ЕГАИС.ПустаяСсылка();
						
						ИнтеграцияЕГАИСКлиентСервер.ДобавитьСтрокуДляПересчетаСуммы(СтруктураПересчетаСуммы, СтрокаТЧ);
						ИнтеграцияЕГАИСКлиентСервер.ДобавитьСтрокуДляПересчетаСуммы(СтруктураПересчетаСуммы, НоваяСтрока);
						ИнтеграцияЕГАИСКлиентСервер.ПересчитатьСуммы(СтруктураПересчетаСуммы);
						
					КонецЕсли;
					
					ОстатокПоСправке = ОстатокПоСправке - СтрокаТЧ.Количество * СтрокаТаблицыКоэффициентов.Коэффициент;
					Если ОстатокПоСправке <= 0 Тогда
						Прервать;
					КонецЕсли;
					
				Иначе
					
					КоличествоНеподходящихСтрокТЧ = КоличествоНеподходящихСтрокТЧ + 1;
					Продолжить;
					
				КонецЕсли;
				
			КонецЦикла;
		
		КонецЕсли;
		
		Если КоличествоНеподходящихСтрокТЧ = МассивСтрок3.Количество() Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЕстьСтрокиДляЗаполнения;
	
КонецФункции

// Заполняет таблицы, по которым будет сформирован возврат из регистра 2.
//
Функция ОбработатьПорциюДанныхДляПодбораСправок2ДляВозвратаИзРегистра2(МенеджерВременныхТаблиц, ОрганизацияЕГАИС, Период, Таблицы)
	
	Запрос = Новый Запрос(
	"////////////////////////////////////////////////////////////////////////////////0
	|ВЫБРАТЬ 
	|	ОстатокВРегистре2.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ОстатокВРегистре2.Справка2             КАК Справка2,
	|	ОстатокВРегистре2.Количество           КАК Количество
	|ПОМЕСТИТЬ втОстатокВРегистре2
	|ИЗ
	|	&ОстатокВРегистре2 КАК ОстатокВРегистре2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////1
	|ВЫБРАТЬ ПЕРВЫЕ 500
	|	Таблица.Период КАК Период
	|ПОМЕСТИТЬ ГраницыНачало
	|ИЗ
	|	РегистрНакопления.ОстаткиАлкогольнойПродукцииЕГАИС КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаДляЗаполнения КАК втТаблицаДляЗаполнения
	|		ПО (втТаблицаДляЗаполнения.АлкогольнаяПродукция = Таблица.АлкогольнаяПродукция)
	|ГДЕ
	|	(   Таблица.Регистратор ССЫЛКА Документ.ВозвратИзРегистра2ЕГАИС
	|	ИЛИ Таблица.Регистратор ССЫЛКА Документ.ПередачаВРегистр2ЕГАИС)
	|	И Таблица.Количество <> 0
	|	И Таблица.ОрганизацияЕГАИС = &ОрганизацияЕГАИС
	|	" + ?(ЗначениеЗаполнено(Период), "И Таблица.Период < &Период", "") + "
	|УПОРЯДОЧИТЬ ПО
	|	Таблица.Период УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////2
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МИНИМУМ(Таблица.Период), ДатаВремя(1,1,1)) КАК Период
	|ПОМЕСТИТЬ Границы
	|ИЗ
	|	ГраницыНачало КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////3
	|ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Таблица.Справка2 КАК Справка2,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -Таблица.Количество
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА Таблица.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ втПорцияДанных
	|ИЗ
	|	РегистрНакопления.ОстаткиАлкогольнойПродукцииЕГАИС КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаДляЗаполнения КАК втТаблицаДляЗаполнения
	|		ПО (втТаблицаДляЗаполнения.АлкогольнаяПродукция = Таблица.АлкогольнаяПродукция)
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Границы КАК Границы
	|		ПО ИСТИНА
	|ГДЕ
	|	(   Таблица.Регистратор ССЫЛКА Документ.ВозвратИзРегистра2ЕГАИС
	|	ИЛИ Таблица.Регистратор ССЫЛКА Документ.ПередачаВРегистр2ЕГАИС)
	|	И Таблица.Количество <> 0
	|	И Таблица.ОрганизацияЕГАИС = &ОрганизацияЕГАИС
	|	И Таблица.Период >= Границы.Период
	|	" + ?(ЗначениеЗаполнено(Период), "И Таблица.Период < &Период", "") + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////4
	|ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	Таблица.Справка2 КАК Справка2,
	|	Таблица.Количество КАК Количество
	|ИЗ
	|	втПорцияДанных КАК Таблица
	|УПОРЯДОЧИТЬ ПО
	|	Таблица.АлкогольнаяПродукция,
	|	Таблица.Период УБЫВ,
	|	Таблица.Справка2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////5
	|ВЫБРАТЬ
	|	Таблица.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(Таблица.Количество)    КАК Количество,
	|	0                            КАК Требуется
	|ПОМЕСТИТЬ ТаблицаДляГруппировки
	|ИЗ
	|	втПорцияДанных КАК Таблица
	|СГРУППИРОВАТЬ ПО
	|	Таблица.АлкогольнаяПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.АлкогольнаяПродукция,
	|	-Таблица.Количество,
	|	0
	|ИЗ
	|	втТаблицаДляСписания КАК Таблица
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.АлкогольнаяПродукция,
	|	Таблица.Количество,
	|	0
	|ИЗ
	|	втОстатокВРегистре2 КАК Таблица
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.АлкогольнаяПродукция,
	|	0,
	|	Таблица.Количество
	|ИЗ
	|	втТаблицаДляЗаполнения КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////6
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаДляГруппировки.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТаблицаДляГруппировки.Количество)    КАК Количество,
	|	СУММА(ТаблицаДляГруппировки.Требуется)     КАК Требуется
	|ИЗ
	|	ТаблицаДляГруппировки КАК ТаблицаДляГруппировки
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДляГруппировки.АлкогольнаяПродукция
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаДляГруппировки.Требуется) - СУММА(ТаблицаДляГруппировки.Количество) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////7
	|ВЫБРАТЬ
	|	Границы.Период
	|ИЗ
	|	Границы КАК Границы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втОстатокВРегистре2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втПорцияДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ГраницыНачало
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Границы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаДляГруппировки
	|");
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ОрганизацияЕГАИС",  ОрганизацияЕГАИС);
	Запрос.УстановитьПараметр("Период",            Период);
	Запрос.УстановитьПараметр("ОстатокВРегистре2", Таблицы.ОстатокВРегистре2);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	НоваяПорция = РезультатЗапроса[4].Выгрузить();
	
	Для Каждого СтрокаТЧ Из НоваяПорция Цикл
		
		Если СтрокаТЧ.Количество < 0 Тогда
			НоваяСтрока = Таблицы.ТаблицаДляСписания.Добавить();
			НоваяСтрока.АлкогольнаяПродукция =  СтрокаТЧ.АлкогольнаяПродукция;
			НоваяСтрока.Справка2             =  СтрокаТЧ.Справка2;
			НоваяСтрока.Количество           = -СтрокаТЧ.Количество;
			Продолжить;
		КонецЕсли;
		
		Если Таблицы.ТаблицаДляСписания.Количество() > 0 Тогда
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("АлкогольнаяПродукция", СтрокаТЧ.АлкогольнаяПродукция);
			ПараметрыОтбора.Вставить("Справка2",             СтрокаТЧ.Справка2);
			НайденныеСтроки = Таблицы.ТаблицаДляСписания.НайтиСтроки(ПараметрыОтбора);
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				
				Если НайденнаяСтрока.Количество > 0 И СтрокаТЧ.Количество > 0 Тогда
					
					Если СтрокаТЧ.Количество <= НайденнаяСтрока.Количество Тогда
						НайденнаяСтрока.Количество = НайденнаяСтрока.Количество - СтрокаТЧ.Количество;
						СтрокаТЧ.Количество = 0;
					Иначе
						Разница = СтрокаТЧ.Количество - НайденнаяСтрока.Количество;
						НайденнаяСтрока.Количество = 0;
						СтрокаТЧ.Количество = СтрокаТЧ.Количество - Разница;
					КонецЕсли;
					
				КонецЕсли;
				
				Если НайденнаяСтрока.Количество = 0 Тогда
					Таблицы.ТаблицаДляСписания.Удалить(НайденнаяСтрока);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если СтрокаТЧ.Количество <> 0 Тогда
		
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("АлкогольнаяПродукция", СтрокаТЧ.АлкогольнаяПродукция);
			ПараметрыОтбора.Вставить("Последняя",            Истина);
			НайденныеСтроки = Таблицы.ОстатокВРегистре2.НайтиСтроки(ПараметрыОтбора);
			Если НайденныеСтроки.Количество() > 0 Тогда
				
				Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					
					Если НайденнаяСтрока.Справка2 = СтрокаТЧ.Справка2 Тогда
						
						НайденнаяСтрока.Количество = НайденнаяСтрока.Количество + СтрокаТЧ.Количество;
						
					Иначе
						
						НайденнаяСтрока.Последняя = Ложь;
						
						НоваяСтрока = Таблицы.ОстатокВРегистре2.Добавить();
						НоваяСтрока.АлкогольнаяПродукция = СтрокаТЧ.АлкогольнаяПродукция;
						НоваяСтрока.Справка2             = СтрокаТЧ.Справка2;
						НоваяСтрока.Количество           = СтрокаТЧ.Количество;
						НоваяСтрока.Последняя            = Истина;
						
					КонецЕсли;
					
				КонецЦикла;
				
			Иначе
				
				НоваяСтрока = Таблицы.ОстатокВРегистре2.Добавить();
				НоваяСтрока.АлкогольнаяПродукция = СтрокаТЧ.АлкогольнаяПродукция;
				НоваяСтрока.Справка2             = СтрокаТЧ.Справка2;
				НоваяСтрока.Количество           = СтрокаТЧ.Количество;
				НоваяСтрока.Последняя            = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если РезультатЗапроса[6].Пустой() Тогда
		
		Возврат Истина;
		
	Иначе
		
		Границы = РезультатЗапроса[7].Выгрузить();
		Если Границы.Количество() = 1 Тогда
			
			НовыйПериод = Границы[0].Период;
			
			Если ЗначениеЗаполнено(НовыйПериод) Тогда
				
				Возврат ОбработатьПорциюДанныхДляПодбораСправок2ДляВозвратаИзРегистра2(
					МенеджерВременныхТаблиц,
					ОрганизацияЕГАИС,
					НовыйПериод,
					Таблицы);
			Иначе
				
				Возврат Ложь;
				
			КонецЕсли;
			
		Иначе
			
			Возврат Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

// Все строки табличной части имеют заполненные справки 2.
//
// Параметры:
//  Товары - ТабличнаяЧасть - Табличная часть.
//
// Возвращаемое значение:
//  Булево - Истина, если в табличной части нет строк с незаполненными справками 2.
//
Функция Справки2ЗаполненыВТабличнойЧасти(Товары) Экспорт
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Справка2", Справочники.Справки2ЕГАИС.ПустаяСсылка());
	НайденныеСтроки = Товары.НайтиСтроки(ПараметрыОтбора);
	
	Возврат НайденныеСтроки.Количество() = 0;
	
КонецФункции

#КонецОбласти

// Дополнить параметры обновления статуса.
//
// Параметры:
//  ПараметрыОбновленияСтатуса - Структура - см. функцию ПараметрыОбновленияСтатуса().
// 
// Возвращаемое значение:
//  Структура - см. функцию ПараметрыОбновленияСтатуса().
//
Функция ДополнитьПараметрыОбновленияСтатуса(ПараметрыОбновленияСтатуса = Неопределено) Экспорт
	
	Если ПараметрыОбновленияСтатуса <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(ПараметрыОбновленияСтатуса.ИдентификаторЗапроса)
			И Не ЗначениеЗаполнено(ПараметрыОбновленияСтатуса.ФорматОбмена) Тогда
			РезультатПоиска = ИнтеграцияЕГАИС.НайтиОбъектПоИдентификаторуЗапроса(ПараметрыОбновленияСтатуса.ИдентификаторЗапроса);
			ПараметрыОбновленияСтатуса.ФорматОбмена = РезультатПоиска.ФорматОбмена;
		КонецЕсли;
		
	Иначе
		
		ПараметрыОбновленияСтатуса = ИнтеграцияЕГАИС.ПараметрыОбновленияСтатуса();
		
	КонецЕсли;
	
	Возврат ПараметрыОбновленияСтатуса;
	
КонецФункции

// Получить таблицу справок 2 по документу.
//
// Параметры:
//  ДокументСсылка- ДокументСсылка - Документ.
//  ИспользоватьИдентификаторСтроки - Булево - Использовать идентификаторы строки.
//  ИмяТабличнойЧасти - Строка - Имя табличной части.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Справки 2 по документу.
//
Функция Справки2ПоДокументу(ДокументСсылка, ИспользоватьИдентификаторСтроки = Истина, ИмяТабличнойЧасти = "Товары") Экспорт
	
	ПолноеИмяДокумента = ДокументСсылка.Метаданные().ПолноеИмя();
	
	Если ИспользоватьИдентификаторСтроки Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТабличнаяЧасть.ИдентификаторСтроки  КАК ИдентификаторСтроки,
		|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	ТабличнаяЧасть.Справка2             КАК Справка2
		|ИЗ
		|	&ТабличнаяЧасть КАК ТабличнаяЧасть
		|ГДЕ
		|	ТабличнаяЧасть.Ссылка = &ДокументСсылка
		|";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Неопределено                        КАК ИдентификаторСтроки,
		|	ТабличнаяЧасть.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	ТабличнаяЧасть.Справка2             КАК Справка2
		|ИЗ
		|	&ТабличнаяЧасть КАК ТабличнаяЧасть
		|ГДЕ
		|	ТабличнаяЧасть.Ссылка = &ДокументСсылка
		|";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТабличнаяЧасть", ПолноеИмяДокумента + "." + ИмяТабличнойЧасти);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает массив доступных для использования в документе статусов акцизных марок.
//
// Параметры:
//  ДокументСсылка   - ДокументСсылка - Ссылка на документ, для которого необходимо получить доступные статусы.
//
// Возвращаемое значение:
//  Массив - Массив статусов доступных акцизных марок.
//
Функция СтатусыДоступныхАкцизныхМарок(ДокументСсылка) Экспорт

	ПолноеИмя = ДокументСсылка.Метаданные().ПолноеИмя();
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
	
	Возврат МенеджерОбъекта.СтатусыДоступныхАкцизныхМарок();

КонецФункции

#КонецОбласти