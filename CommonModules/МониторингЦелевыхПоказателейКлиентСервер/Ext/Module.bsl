#Область ПрограммныйИнтерфейс

// Возвращает стандартные настройки формирования монитора целевых покзателей
//
// Возвращаемое значение:
//	Структура - стандартные настройки:
//		* ВариантГруппировкиПоказателей - Строка - "ПоКатегориямЦелей", группировка показателей по категориям целей
//		* КоличествоКолонокМонитораПоказателей - Число - 3 колонки, количество колонок
//		* РазмерДиаграмм - Число - крупный (2), высота примерно равна ширине
//		* ПроизвольныйМасштаб - Число - 100 %, стандартное значение масштабирования
//		* ВариантОтображенияДеталей - Строка - "ПоказыватьТолькоТекущееЗначение" - только текущее значение
//		* ПериодАвтообновления - Число - 15 мин. Число минут, через которые следует автоматически обновлять данные на форме
//
Функция СтандартныеНастройки() Экспорт
	
	НастройкиПоУмолчанию = Новый Структура;
	
	// порядок вывода показателей
	НастройкиПоУмолчанию.Вставить("ВариантГруппировкиПоказателей", "ПоКатегориямЦелей");
	НастройкиПоУмолчанию.Вставить("КоличествоКолонокМонитораПоказателей", 3);
	
	// Внешний вид показателей
	НастройкиПоУмолчанию.Вставить("РазмерДиаграмм", 1);
	НастройкиПоУмолчанию.Вставить("ПроизвольныйМасштаб", 100);
	НастройкиПоУмолчанию.Вставить("ВариантОтображенияДеталей", "ПоказыватьТолькоТекущееЗначение");
	
	// Обновление данных
	НастройкиПоУмолчанию.Вставить("ПериодАвтообновления", 15);
	
	Возврат НастройкиПоУмолчанию;
	
КонецФункции

// Возвращает названия перечисления Периодичность в разных склонениях
//
// Параметры
//	ЧислоПериодов - Число - количество периодов, определяет склонение
//	Периодичность - ссылка на значение перечисления "Периодичность"
//
// Возвращаемое значение:
//  ПериодПрописью - Строка - название периода в правильном склонении
//
Функция ПериодПрописью(ЧислоПериодов, Периодичность) Экспорт
	ПериодПрописью = "";
	ФорматнаяСтрока = "L = ru_RU";
	ПараметрыПредметаИсчиления = "";
	
	Если Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.День") Тогда
		ПараметрыПредметаИсчиления = "день, дня, дней, м";
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчиления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='день'")) Тогда
			ПериодПрописью = НСтр("ru='день'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='дня'")) Тогда
			ПериодПрописью = НСтр("ru='дня'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='дней'")) Тогда
			ПериодПрописью = НСтр("ru='дней'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Неделя") Тогда
		ПараметрыПредметаИсчиления = "неделя, недели, недель, ж";
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчиления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='неделя'")) Тогда
			ПериодПрописью = НСтр("ru='неделя'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='недели'")) Тогда
			ПериодПрописью = НСтр("ru='недели'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='недель'")) Тогда
			ПериодПрописью = НСтр("ru='недель'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Декада") Тогда
		ПараметрыПредметаИсчиления = "декада, декады, декад, ж";
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчиления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='декада'")) Тогда
			ПериодПрописью = НСтр("ru='декада'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='декады'")) Тогда
			ПериодПрописью = НСтр("ru='декады'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='декад'")) Тогда
			ПериодПрописью = НСтр("ru='декад'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Месяц") Тогда
		ПараметрыПредметаИсчиления = "месяц, месяца, месяцев, м";
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчиления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='месяцев'")) Тогда
			ПериодПрописью = НСтр("ru='месяцев'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='месяца'")) Тогда
			ПериодПрописью = НСтр("ru='месяца'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='месяц'")) Тогда
			ПериодПрописью = НСтр("ru='месяц'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Квартал") Тогда
		ПараметрыПредметаИсчиления = "квартал, квартала, кварталов, м";
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчиления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='кварталов'")) Тогда
			ПериодПрописью = НСтр("ru='кварталов'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='квартала'")) Тогда
			ПериодПрописью = НСтр("ru='квартала'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='квартал'")) Тогда
			ПериодПрописью = НСтр("ru='квартал'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Полугодие") Тогда
		ПараметрыПредметаИсчиления = "полугодие, полугодия, полугодий, с";
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчиления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='полугодие'")) Тогда
			ПериодПрописью = НСтр("ru='полугодие'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='полугодия'")) Тогда
			ПериодПрописью = НСтр("ru='полугодия'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='полугодий'")) Тогда
			ПериодПрописью = НСтр("ru='полугодий'");
		КонецЕсли;
	ИначеЕсли Периодичность = ПредопределенноеЗначение("Перечисление.Периодичность.Год") Тогда
		ПараметрыПредметаИсчиления = "год, года, лет, м";
		ПериодПрописью = ЧислоПрописью(ЧислоПериодов, ФорматнаяСтрока, ПараметрыПредметаИсчиления);
		
		Если СтрНайти(ПериодПрописью, НСтр("ru='год'")) Тогда
			ПериодПрописью = НСтр("ru='год'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='года'")) Тогда
			ПериодПрописью = НСтр("ru='года'");
		ИначеЕсли СтрНайти(ПериодПрописью, НСтр("ru='лет'")) Тогда
			ПериодПрописью = НСтр("ru='лет'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПериодПрописью;
КонецФункции

// Возвращает структуру настроек для формирования состава монитора
//
// Возвращаемое значение:
//	Структура - незаполненная структура с требуемыми свойствами
//		* АдресаДинамическихПараметров - Строка - содержит указание на временные настройки вывода вариантов анализа в монитор целевых показателей
//		* ВидПоиска                    - ПеречислениеСсылка.ВидыПоискаСвязанныхПоказателей, Строка - значения зависимые, влияющие, взаимосвязанные, все
//		* ДемонстрационныйРежим        - Булево - указание на необходимость отображения демонстрационных данных вариантов анализа
//		* ОтборВариантовАнализа        - СправочникСсылка.ВариантыАнализаЦелевыхПоказателей, Массив - отбор данных на основании ссылки на вариант анализа
//		* ОтборЗонаВнимания            - Число - параметр устарел, оставлен для совместимости
//		* ОтборСтатус                  - Строка - отбор вариантов анализа по их статусу. 
//			Принимает значения: "" (без учета состояния), "ЦельДостигнута", "ПриемлемоеСостояние", "НеприемлемоеСостояние", "СостояниеНеОпределено", "РассчитаноСОшибками" 
//		* ПериодичностьКонтроля        - ПеречислениеСсылка.Периодичность - отбор вариантов анализа по соответствующему реквизиту
//		* ПринудительноОбновитьДанные  - Булево - указание на обновление кэша ранее запланированного времени
//		* РежимАнализаВзаимосвязанных  - Булево - признак, что производится анализ связанных показателей
//		* СвойстваВариантаАнализа      - Структура
//			* Ссылка        - СправочникСсылка.ВариантыАнализаЦелевыхПоказателей - вариант анализа, для которого будут получаться данные
//			* ПериодАнализа - ХранилищеЗначений - содержит упакованны СтандартныйПериод, описывающий период выборки данных
//		* СтруктураНастроек            - Структура - см. функцию МониторингЦелевыхПоказателей.ПолучитьНастройкиМонитораЦелевыхПоказателей
//		* УчитыватьВариантыОтображения - Булево - Истина, подготовка данных с учетом отображения в виде таблицы, диаграммы или текста
//
Функция ИнициализацияПараметровСоставаМонитора() Экспорт
	ПараметрыСоставаМонитора = Новый Структура;
	ПараметрыСоставаМонитора.Вставить("АдресаДинамическихПараметров",);
	ПараметрыСоставаМонитора.Вставить("ВидПоиска",);
	ПараметрыСоставаМонитора.Вставить("ДемонстрационныйРежим",);
	ПараметрыСоставаМонитора.Вставить("ОтборВариантовАнализа",);
	ПараметрыСоставаМонитора.Вставить("ОтборЗонаВнимания",);
	ПараметрыСоставаМонитора.Вставить("ОтборСтатус",);
	ПараметрыСоставаМонитора.Вставить("ПериодичностьКонтроля",);
	ПараметрыСоставаМонитора.Вставить("ПринудительноОбновитьДанные",);
	ПараметрыСоставаМонитора.Вставить("РежимАнализаВзаимосвязанных",);
	ПараметрыСоставаМонитора.Вставить("СвойстваВариантаАнализа",);
	ПараметрыСоставаМонитора.Вставить("СтруктураНастроек",);
	ПараметрыСоставаМонитора.Вставить("УчитыватьВариантыОтображения",);
	
	Возврат ПараметрыСоставаМонитора;
КонецФункции

#КонецОбласти
