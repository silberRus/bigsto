////////////////////////////////////////////////////////////////////////////////
// Подсистема «Аудит состояния системы».
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПодключитьДлительнуюОперацию(Форма, Результат, ПараметрыОбработчикаОжидания, ФормаДлительнойОперации) Экспорт
	
	Если Не Результат.ЗаданиеВыполнено Тогда
		
		Форма.ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		Форма.АдресХранилища	   = Результат.АдресХранилища;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		Форма.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(Форма, Форма.ИдентификаторЗадания);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПроверитьВыполнениеЗадания(Форма, ПараметрыОбработчикаОжидания, ФормаДлительнойОперации) Экспорт
	
	ЗаданиеВыполнено = Ложь;
	
	Попытка
		Если ФормаДлительнойОперации.Открыта()
			И ФормаДлительнойОперации.ИдентификаторЗадания = Форма.ИдентификаторЗадания Тогда
			Если Форма.ЗаданиеВыполнено(Форма.ИдентификаторЗадания) Тогда
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
				ЗаданиеВыполнено = Истина;
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				Форма.ПодключитьОбработчикОжидания(
					"Подключаемый_ПроверитьВыполнениеЗадания",
					ПараметрыОбработчикаОжидания.ТекущийИнтервал,
					Истина);
			КонецЕсли;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ЗаданиеВыполнено;
	
КонецФункции

#КонецОбласти
