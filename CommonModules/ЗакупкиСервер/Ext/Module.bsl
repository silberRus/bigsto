////////////////////////////////////////////////////////////////////////////////
// Модуль "ЗакупкиСервер", содержит процедуры и функции для 
// получения значений реквизитов для подстановки в документы закупки
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ПроцедурыИнициализацииПравилЗакупок

// Возвращает структуру условий закупок по торговому соглашению.
//
// Параметры:
//  Соглашение - СправочникСсылка.СоглашенияСКлиентами - соглашение,по которому необходимо получить стандартные условия закупок
//
// Возвращаемое значение:
//  Структура - структура, включающая условия продаж.
//
Функция ПолучитьУсловияЗакупок(Знач Соглашение,
	                           Знач УчитыватьГруппыСкладов=Ложь,
	                           Знач ИсключитьГруппыСкладовДоступныеВЗаказах=Ложь) Экспорт
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Соглашение);
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоглашениеСПоставщиком.Ссылка             КАК Соглашение,
	|	СоглашениеСПоставщиком.Партнер            КАК Партнер,
	|	СоглашениеСПоставщиком.Контрагент         КАК Контрагент,
	|	СоглашениеСПоставщиком.Организация        КАК Организация,
	|	СоглашениеСПоставщиком.ВидЦеныПоставщика  КАК ВидЦеныПоставщика,
	|	СоглашениеСПоставщиком.Валюта             КАК Валюта,
	|	ВЫБОР
	|		КОГДА СоглашениеСПоставщиком.ИспользуютсяДоговорыКонтрагентов
	|			ТОГДА СоглашениеСПоставщиком.Валюта
	|		ИНАЧЕ СоглашениеСПоставщиком.ВалютаВзаиморасчетов
	|	КОНЕЦ                                     КАК ВалютаВзаиморасчетов,
	|	СоглашениеСПоставщиком.ЦенаВключаетНДС    КАК ЦенаВключаетНДС,
	|	СоглашениеСПоставщиком.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ВЫБОР
	|		КОГДА
	|			НЕ СоглашениеСПоставщиком.Склад.ЭтоГруппа
	|		ТОГДА
	|			СоглашениеСПоставщиком.Склад
	|		КОГДА
	|			СоглашениеСПоставщиком.Склад.ЭтоГруппа
	|			И &УчитыватьГруппыСкладов
	|			И СоглашениеСПоставщиком.Склад.ВыборГруппы В (&ВыборГруппыСкладов)
	|		ТОГДА
	|			СоглашениеСПоставщиком.Склад
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|	КОНЕЦ                                               КАК Склад,
	|	СоглашениеСПоставщиком.ФормаОплаты                  КАК ФормаОплаты,
	|	СоглашениеСПоставщиком.ПорядокОплаты                КАК ПорядокОплаты,
	|	СоглашениеСПоставщиком.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	СоглашениеСПоставщиком.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	СоглашениеСПоставщиком.СрокПоставки                 КАК СрокПоставки,
	|	СоглашениеСПоставщиком.ХозяйственнаяОперация        КАК ХозяйственнаяОперация,
	|	СоглашениеСПоставщиком.РегистрироватьЦеныПоставщика КАК РегистрироватьЦеныПоставщика,
	|	СоглашениеСПоставщиком.СпособРасчетаВознаграждения  КАК СпособРасчетаВознаграждения,
	|	СоглашениеСПоставщиком.ПроцентВознаграждения        КАК ПроцентВознаграждения,
	|	СоглашениеСПоставщиком.УдержатьВознаграждение       КАК УдержатьВознаграждение,
	|	
	|	СоглашениеСПоставщиком.ИспользуютсяДоговорыКонтрагентов            КАК ИспользуютсяДоговорыКонтрагентов,
	|	СоглашениеСПоставщиком.ПорядокРасчетов                             КАК ПорядокРасчетов,
	|	СоглашениеСПоставщиком.ВозвращатьМногооборотнуюТару                КАК ВозвращатьМногооборотнуюТару,
	|	СоглашениеСПоставщиком.СрокВозвратаМногооборотнойТары              КАК СрокВозвратаМногооборотнойТары,
	|	СоглашениеСПоставщиком.РассчитыватьДатуВозвратаТарыПоКалендарю     КАК РассчитыватьДатуВозвратаТарыПоКалендарю,
	|	СоглашениеСПоставщиком.КалендарьВозвратаТары                       КАК КалендарьВозвратаТары,
	|	СоглашениеСПоставщиком.ТребуетсяЗалогЗаТару                        КАК ТребуетсяЗалогЗаТару,
	|	СоглашениеСПоставщиком.НаправлениеДеятельности                     КАК НаправлениеДеятельности
	|
	|ИЗ
	|	Справочник.СоглашенияСПоставщиками КАК СоглашениеСПоставщиком
	|ГДЕ
	|	СоглашениеСПоставщиком.Ссылка = &Соглашение";
	
	Запрос.УстановитьПараметр("Соглашение",             Соглашение);
	Запрос.УстановитьПараметр("ВыборГруппыСкладов",     Справочники.Склады.ВариантыВыбораГруппыСкладов(ИсключитьГруппыСкладовДоступныеВЗаказах));
	Запрос.УстановитьПараметр("УчитыватьГруппыСкладов", УчитыватьГруппыСкладов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	СтруктураРеквизитов = ШаблонУсловияЗакупок();
	ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, Выборка);
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСПоставщиками") Тогда
		СтруктураРеквизитов.Вставить("ИспользуютсяДоговорыКонтрагентов", Ложь);
	КонецЕсли;
		
	Возврат СтруктураРеквизитов;
	
КонецФункции

// Возвращает дату последнего этапа по графику.
//
// Параметры:
// ДатаДокумента - Дата - дата, от которой необходимо получить последнюю дату по графику
// ГрафикОплаты  - СправочникСсылка.ГрафикиОплаты -ссылка на график оплаты
//
// Возвращаемое значение:
// Число.
// Последняя дата по графику с учетом календаря
//
Функция ПолучитьПоследнююДатуПоГрафику(Знач ДатаДокумента, Знач СоглашениеСПоставщиком) Экспорт
	
	Если Не ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МАКСИМУМ(ЭтапыГрафикаОплаты.Сдвиг) КАК Сдвиг,
	|	ЭтапыГрафикаОплаты.Ссылка.Календарь КАК Календарь,
	|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&ДатаДокумента, ДЕНЬ, ЕСТЬNULL(МАКСИМУМ(ЭтапыГрафикаОплаты.Сдвиг), 0)), ДЕНЬ) КАК ДатаПлатежа
	|ИЗ
	|	Справочник.СоглашенияСПоставщиками.ЭтапыГрафикаОплаты КАК ЭтапыГрафикаОплаты
	|ГДЕ
	|	ЭтапыГрафикаОплаты.Ссылка = &СоглашениеСПоставщиком
	|	И ЭтапыГрафикаОплаты.ВариантОплаты = ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыПоставщику.КредитПослеПоступления)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭтапыГрафикаОплаты.Ссылка,
	|	ЭтапыГрафикаОплаты.Ссылка.Календарь");
	
	Запрос.УстановитьПараметр("СоглашениеСПоставщиком",  СоглашениеСПоставщиком);
	Запрос.УстановитьПараметр("ДатаДокумента", ДатаДокумента);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Календарь = Выборка.Календарь;
	Если ЗначениеЗаполнено(Календарь) Тогда
		ДатаПлатежа = КалендарныеГрафики.ДатаПоКалендарю(Календарь, ДатаДокумента, Выборка.Сдвиг);
	Иначе
		ДатаПлатежа = Выборка.ДатаПлатежа;
	КонецЕсли;
		
	Возврат ДатаПлатежа;
	
КонецФункции

// Возвращает структуру условий закупок по партнеру.
//
// Параметры:
//  Партнер           - СправочникСсылка.Партнеры - партнер, для которого необходимо получить условия закупок,
//  ПараметрыОтбора   - Структура - содержит параметры отбора соглашения.
//
// Возвращаемое значение:
//  Структура - структура, включающая условия закупок.
//
Функция ПолучитьУсловияЗакупокПоУмолчанию(Знач Партнер, Знач ПараметрыОтбора = Неопределено) Экспорт
	
	ВсеПараметрыОтбора = Новый Структура();
	ВсеПараметрыОтбора.Вставить("ТолькоДействующее",                       Истина);
	ВсеПараметрыОтбора.Вставить("УчитыватьГруппыСкладов",                  Ложь);
	ВсеПараметрыОтбора.Вставить("ИсключитьГруппыСкладовДоступныеВЗаказах", Ложь);
	ВсеПараметрыОтбора.Вставить("ХозяйственныеОперации",                   Неопределено);
	ВсеПараметрыОтбора.Вставить("ВыбранноеСоглашение",   Справочники.СоглашенияСПоставщиками.ПустаяСсылка());
	
	Если ПараметрыОтбора <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ВсеПараметрыОтбора, ПараметрыОтбора);
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	СоглашениеСПоставщиком.Ссылка                       КАК Соглашение,
	|	СоглашениеСПоставщиком.Партнер                      КАК Партнер,
	|	СоглашениеСПоставщиком.Контрагент                   КАК Контрагент,
	|	СоглашениеСПоставщиком.Организация                  КАК Организация,
	|	СоглашениеСПоставщиком.ВидЦеныПоставщика            КАК ВидЦеныПоставщика,
	|	СоглашениеСПоставщиком.Валюта                       КАК Валюта,
	|	ВЫБОР 
	|		КОГДА СоглашениеСПоставщиком.ИспользуютсяДоговорыКонтрагентов
	|			ТОГДА СоглашениеСПоставщиком.Валюта
	|		ИНАЧЕ СоглашениеСПоставщиком.ВалютаВзаиморасчетов
	|	КОНЕЦ                                               КАК ВалютаВзаиморасчетов,
	|	СоглашениеСПоставщиком.ЦенаВключаетНДС              КАК ЦенаВключаетНДС,
	|	СоглашениеСПоставщиком.НалогообложениеНДС           КАК НалогообложениеНДС,
	|	СоглашениеСПоставщиком.ХозяйственнаяОперация        КАК ХозяйственнаяОперация,
	|	СоглашениеСПоставщиком.РегистрироватьЦеныПоставщика КАК РегистрироватьЦеныПоставщика,
	|	ВЫБОР
	|		КОГДА
	|			НЕ СоглашениеСПоставщиком.Склад.ЭтоГруппа
	|		ТОГДА
	|			СоглашениеСПоставщиком.Склад
	|		КОГДА
	|			СоглашениеСПоставщиком.Склад.ЭтоГруппа
	|			И &УчитыватьГруппыСкладов
	|			И СоглашениеСПоставщиком.Склад.ВыборГруппы В (&ВыборГруппыСкладов)
	|		ТОГДА
	|			СоглашениеСПоставщиком.Склад
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|	КОНЕЦ                                               КАК Склад,
	|	СоглашениеСПоставщиком.ФормаОплаты                  КАК ФормаОплаты,
	|	СоглашениеСПоставщиком.ПорядокОплаты                КАК ПорядокОплаты,
	|	СоглашениеСПоставщиком.СрокПоставки                 КАК СрокПоставки,
	|	СоглашениеСПоставщиком.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	СоглашениеСПоставщиком.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	СоглашениеСПоставщиком.СпособРасчетаВознаграждения  КАК СпособРасчетаВознаграждения,
	|	СоглашениеСПоставщиком.ПроцентВознаграждения        КАК ПроцентВознаграждения,
	|	СоглашениеСПоставщиком.УдержатьВознаграждение       КАК УдержатьВознаграждение,
	|	
	|	СоглашениеСПоставщиком.ИспользуютсяДоговорыКонтрагентов КАК ИспользуютсяДоговорыКонтрагентов,
	|	СоглашениеСПоставщиком.ПорядокРасчетов                  КАК ПорядокРасчетов,
	|	СоглашениеСПоставщиком.ВозвращатьМногооборотнуюТару                КАК ВозвращатьМногооборотнуюТару,
	|	СоглашениеСПоставщиком.СрокВозвратаМногооборотнойТары              КАК СрокВозвратаМногооборотнойТары,
	|	СоглашениеСПоставщиком.РассчитыватьДатуВозвратаТарыПоКалендарю     КАК РассчитыватьДатуВозвратаТарыПоКалендарю,
	|	СоглашениеСПоставщиком.КалендарьВозвратаТары                       КАК КалендарьВозвратаТары,
	|	СоглашениеСПоставщиком.ТребуетсяЗалогЗаТару                        КАК ТребуетсяЗалогЗаТару,
	|	СоглашениеСПоставщиком.НаправлениеДеятельности                     КАК НаправлениеДеятельности
	|ИЗ
	|	Справочник.СоглашенияСПоставщиками КАК СоглашениеСПоставщиком
	|ГДЕ
	|	НЕ СоглашениеСПоставщиком.ПометкаУдаления
	|	И ((НЕ &ОтборХозяйственныеОперации
	|		И СоглашениеСПоставщиком.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОказаниеАгентскихУслуг)
	|		И СоглашениеСПоставщиком.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи))
	|			ИЛИ СоглашениеСПоставщиком.ХозяйственнаяОперация В (&ХозяйственныеОперации)) И
	|" + ?(ВсеПараметрыОтбора.ТолькоДействующее," СоглашениеСПоставщиком.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийСПоставщиками.Действует) И","") + "
	|	СоглашениеСПоставщиком.Партнер = &Партнер;
	|
	|////////////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ 
	|	СоглашениеСПоставщиком.Ссылка                       КАК Соглашение,
	|	СоглашениеСПоставщиком.Партнер                      КАК Партнер,
	|	СоглашениеСПоставщиком.Контрагент                   КАК Контрагент,
	|	СоглашениеСПоставщиком.Организация                  КАК Организация,
	|	СоглашениеСПоставщиком.Валюта                       КАК Валюта,
	|	ВЫБОР 
	|		КОГДА СоглашениеСПоставщиком.ИспользуютсяДоговорыКонтрагентов
	|			ТОГДА СоглашениеСПоставщиком.Валюта
	|		ИНАЧЕ СоглашениеСПоставщиком.ВалютаВзаиморасчетов
	|	КОНЕЦ                                               КАК ВалютаВзаиморасчетов,
	|	СоглашениеСПоставщиком.ЦенаВключаетНДС              КАК ЦенаВключаетНДС,
	|	СоглашениеСПоставщиком.НалогообложениеНДС           КАК НалогообложениеНДС,
	|	СоглашениеСПоставщиком.ХозяйственнаяОперация        КАК ХозяйственнаяОперация,
	|	СоглашениеСПоставщиком.ВидЦеныПоставщика            КАК ВидЦеныПоставщика,
	|	СоглашениеСПоставщиком.РегистрироватьЦеныПоставщика КАК РегистрироватьЦеныПоставщика,
	|	ВЫБОР
	|		КОГДА
	|			НЕ СоглашениеСПоставщиком.Склад.ЭтоГруппа
	|		ТОГДА
	|			СоглашениеСПоставщиком.Склад
	|		КОГДА
	|			СоглашениеСПоставщиком.Склад.ЭтоГруппа
	|			И &УчитыватьГруппыСкладов
	|			И СоглашениеСПоставщиком.Склад.ВыборГруппы В (&ВыборГруппыСкладов)
	|		ТОГДА
	|			СоглашениеСПоставщиком.Склад
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|	КОНЕЦ                                               КАК Склад,
	|	СоглашениеСПоставщиком.ФормаОплаты                  КАК ФормаОплаты,
	|	СоглашениеСПоставщиком.ПорядокОплаты                КАК ПорядокОплаты,
	|	СоглашениеСПоставщиком.СрокПоставки                 КАК СрокПоставки,
	|	СоглашениеСПоставщиком.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	СоглашениеСПоставщиком.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	СоглашениеСПоставщиком.СпособРасчетаВознаграждения  КАК СпособРасчетаВознаграждения,
	|	СоглашениеСПоставщиком.ПроцентВознаграждения        КАК ПроцентВознаграждения,
	|	СоглашениеСПоставщиком.УдержатьВознаграждение       КАК УдержатьВознаграждение,
	|	
	|	СоглашениеСПоставщиком.ИспользуютсяДоговорыКонтрагентов КАК ИспользуютсяДоговорыКонтрагентов,
	|	СоглашениеСПоставщиком.ПорядокРасчетов                  КАК ПорядокРасчетов,
	|	СоглашениеСПоставщиком.ВозвращатьМногооборотнуюТару                КАК ВозвращатьМногооборотнуюТару,
	|	СоглашениеСПоставщиком.СрокВозвратаМногооборотнойТары              КАК СрокВозвратаМногооборотнойТары,
	|	СоглашениеСПоставщиком.РассчитыватьДатуВозвратаТарыПоКалендарю     КАК РассчитыватьДатуВозвратаТарыПоКалендарю,
	|	СоглашениеСПоставщиком.КалендарьВозвратаТары                       КАК КалендарьВозвратаТары,
	|	СоглашениеСПоставщиком.ТребуетсяЗалогЗаТару                        КАК ТребуетсяЗалогЗаТару,
	|	СоглашениеСПоставщиком.НаправлениеДеятельности                     КАК НаправлениеДеятельности
	|ИЗ
	|	Справочник.СоглашенияСПоставщиками КАК СоглашениеСПоставщиком
	|ГДЕ
	|	НЕ СоглашениеСПоставщиком.ПометкаУдаления И
	|	((НЕ &ОтборХозяйственныеОперации И СоглашениеСПоставщиком.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОказаниеАгентскихУслуг))
	|		ИЛИ СоглашениеСПоставщиком.ХозяйственнаяОперация В (&ХозяйственныеОперации)) И
	|	СоглашениеСПоставщиком.Ссылка = &ВыбранноеСоглашение И
	|" + ?(ВсеПараметрыОтбора.ТолькоДействующее," СоглашениеСПоставщиком.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийСПоставщиками.Действует) И","") + "
	|	СоглашениеСПоставщиком.Партнер = &Партнер
	|";
	
	Запрос.УстановитьПараметр("Партнер",                    Партнер);
	Запрос.УстановитьПараметр("ВыборГруппыСкладов",         Справочники.Склады.ВариантыВыбораГруппыСкладов(ВсеПараметрыОтбора.ИсключитьГруппыСкладовДоступныеВЗаказах));
	Запрос.УстановитьПараметр("УчитыватьГруппыСкладов",     ВсеПараметрыОтбора.УчитыватьГруппыСкладов);
	Запрос.УстановитьПараметр("ОтборХозяйственныеОперации", ВсеПараметрыОтбора.ХозяйственныеОперации <> Неопределено);
	Запрос.УстановитьПараметр("ХозяйственныеОперации",      ВсеПараметрыОтбора.ХозяйственныеОперации);
	Запрос.УстановитьПараметр("ВыбранноеСоглашение",        ВсеПараметрыОтбора.ВыбранноеСоглашение);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Если РезультатЗапроса[0].Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса[0].Выбрать();
	
	// Если в выборке одно соглашение - возвращаем его
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
	ИначеЕсли Выборка.Количество() > 1 Тогда
		Если НЕ РезультатЗапроса[1].Пустой() Тогда
			Выборка = РезультатЗапроса[1].Выбрать();
			Выборка.Следующий();
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	СтруктураРеквизитов = ШаблонУсловияЗакупок();
	ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, Выборка);
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

// Возвращает массив хоз. операций — переданная хоз. операция и операции для использования в схемах "Товары в пути" и
// и "Неотфактурованные поставки".
//
// Параметры:
//  ОсновнаяХозяйственнаяОперация	 - ПеречислениеСсылка.ХозяйственныеОперации	 - основная хозяйственная операция
// 
// Возвращаемое значение:
//  Массив - массив хозяйственных операций включая основную
//
Функция ХозяйственныеОперацииПоОсновной(ОсновнаяХозяйственнаяОперация) Экспорт
	
	ИспользоватьТоварыВПутиОтПоставщиков  = ПолучитьФункциональнуюОпцию("ИспользоватьТоварыВПутиОтПоставщиков");
	ИспользоватьНеотфактурованныеПоставки = ПолучитьФункциональнуюОпцию("ИспользоватьНеотфактурованныеПоставки");
	
	ХозОперации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОсновнаяХозяйственнаяОперация);
	Если ОсновнаяХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика Тогда
		Если ИспользоватьТоварыВПутиОтПоставщиков Тогда
			ХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути);
			ХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути);
		КонецЕсли;
		Если ИспользоватьНеотфактурованныеПоставки Тогда
			ХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки);
			ХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаНеотфактурованнаяПоставка);
		КонецЕсли;
	ИначеЕсли ОсновнаяХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС Тогда
		Если ИспользоватьТоварыВПутиОтПоставщиков Тогда
			ХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути);
			ХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСПоступлениеИзТоваровВПути);
		КонецЕсли;
		Если ИспользоватьНеотфактурованныеПоставки Тогда
			ХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки);
			ХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСНеотфактурованнаяПоставка);
		КонецЕсли;
	ИначеЕсли ОсновнаяХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту Тогда
		Если ИспользоватьТоварыВПутиОтПоставщиков Тогда
			ХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
			ХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ХозОперации;
	
КонецФункции

// Возвращает хозяйственные операции, которые могут использоваться в документе. Если передать пустую ссылку на
// договор или соглашение, то будет возвращено несколько хозяйственных операций, которые могут быть использованы.
//
// Параметры:
//	ПолноеИмяДокумента	 - Строка 									 - имя документа, для которого нужно получить список хозяйственных операций.
//	Соглашение			 - СправочникСсылка.СоглашенияСПоставщиками	 - соглашение, которое используется в документе.
//	Договор				 - СправочникСсылка.ДоговорыСКонтрагентами	 - договор, который используется в документе.
//
// Возвращаемое значение:
//	Массив - массив элементов с типом ПеречислениеСсылка.ХозяйственныеОперации
//
Функция ХозяйственныеОперацииРаздельнойЗакупки(ПолноеИмяДокумента, Соглашение = Неопределено, Договор = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ВложенныйЗапрос.ВариантОформленияЗакупок) КАК ВариантОформленияЗакупок,
	|	МАКСИМУМ(ВложенныйЗапрос.ХозяйственнаяОперация)    КАК ХозяйственнаяОперация
	|ПОМЕСТИТЬ ДанныеПараметров
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДоговорыКонтрагентов.ВариантОформленияЗакупок КАК ВариантОформленияЗакупок,
	|		ЕСТЬNULL(ДоговорыКонтрагентов.ХозяйственнаяОперация, СоглашенияСПоставщиками.ХозяйственнаяОперация) КАК ХозяйственнаяОперация
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ДоговорыКонтрагентов.ВариантОформленияЗакупок КАК ВариантОформленияЗакупок,
	|			ДоговорыКонтрагентов.ХозяйственнаяОперация    КАК ХозяйственнаяОперация
	|		ИЗ
	|			Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ГДЕ
	|			ДоговорыКонтрагентов.Ссылка = &Договор) КАК ДоговорыКонтрагентов
	|			ПОЛНОЕ СОЕДИНЕНИЕ
	|			(ВЫБРАТЬ
	|				СоглашенияСПоставщиками.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|			ИЗ
	|				Справочник.СоглашенияСПоставщиками КАК СоглашенияСПоставщиками
	|			ГДЕ
	|				СоглашенияСПоставщиками.Ссылка = &Соглашение) КАК СоглашенияСПоставщиками
	|			ПО (ИСТИНА)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		NULL,
	|		NULL) КАК ВложенныйЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозяйственныеОперации.Ссылка КАК ХозОперация
	|ПОМЕСТИТЬ ХозОперацииПоСоглашению
	|ИЗ
	|	ДанныеПараметров КАК ДанныеПараметров,
	|	Перечисление.ХозяйственныеОперации КАК ХозяйственныеОперации
	|ГДЕ
	|	ХозяйственныеОперации.Ссылка В(&ХозОперацииЗакупкаУПоставщика)
	|	И (ДанныеПараметров.ХозяйственнаяОперация В (&ХозОперацииЗакупкаУПоставщика)
	|			ИЛИ ДанныеПараметров.ХозяйственнаяОперация ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозяйственныеОперации.Ссылка
	|ИЗ
	|	Перечисление.ХозяйственныеОперации КАК ХозяйственныеОперации,
	|	ДанныеПараметров КАК ДанныеПараметров
	|ГДЕ
	|	ХозяйственныеОперации.Ссылка В(&ХозОперацииЗакупкаПоИмпорту)
	|	И (ДанныеПараметров.ХозяйственнаяОперация В (&ХозОперацииЗакупкаПоИмпорту)
	|			ИЛИ ДанныеПараметров.ХозяйственнаяОперация ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозяйственныеОперации.Ссылка
	|ИЗ
	|	Перечисление.ХозяйственныеОперации КАК ХозяйственныеОперации,
	|	ДанныеПараметров КАК ДанныеПараметров
	|ГДЕ
	|	ХозяйственныеОперации.Ссылка В(&ХозОперацииЗакупкаВСтранахЕАЭС)
	|	И (ДанныеПараметров.ХозяйственнаяОперация В (&ХозОперацииЗакупкаВСтранахЕАЭС)
	|			ИЛИ ДанныеПараметров.ХозяйственнаяОперация ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозяйственныеОперации.Ссылка
	|ИЗ
	|	Перечисление.ХозяйственныеОперации КАК ХозяйственныеОперации,
	|	ДанныеПараметров КАК ДанныеПараметров
	|ГДЕ
	|	ХозяйственныеОперации.Ссылка В(&ХозОперацииПриемНаКомиссию)
	|	И (ДанныеПараметров.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию)
	|			ИЛИ ДанныеПараметров.ХозяйственнаяОперация ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозяйственныеОперации.Ссылка
	|ИЗ
	|	Перечисление.ХозяйственныеОперации КАК ХозяйственныеОперации,
	|	ДанныеПараметров КАК ДанныеПараметров
	|ГДЕ
	|	ХозяйственныеОперации.Ссылка В(&ХозОперацииПриемНаОтветственноеХранение)
	|	И (ДанныеПараметров.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи)
	|			ИЛИ ДанныеПараметров.ХозяйственнаяОперация ЕСТЬ NULL)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозяйственныеОперации.Ссылка КАК ХозОперация
	|ПОМЕСТИТЬ ХозОперацииПоДоговору
	|ИЗ
	|	ДанныеПараметров КАК ДанныеПараметров,
	|	Перечисление.ХозяйственныеОперации КАК ХозяйственныеОперации
	|ГДЕ
	|	ХозяйственныеОперации.Ссылка В(&ХозОперацииНеотфактурованныеПоставки)
	|	И (ДанныеПараметров.ВариантОформленияЗакупок = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияЗакупок.НеотфактурованныеПоставки)
	|			ИЛИ ДанныеПараметров.ВариантОформленияЗакупок ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозяйственныеОперации.Ссылка
	|ИЗ
	|	ДанныеПараметров КАК ДанныеПараметров,
	|	Перечисление.ХозяйственныеОперации КАК ХозяйственныеОперации
	|ГДЕ
	|	ХозяйственныеОперации.Ссылка В(&ХозОперацииТоварыВПути)
	|	И (ДанныеПараметров.ВариантОформленияЗакупок = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияЗакупок.ТоварыВПути)
	|			ИЛИ ДанныеПараметров.ВариантОформленияЗакупок ЕСТЬ NULL)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозяйственныеОперации.Ссылка
	|ИЗ
	|	ДанныеПараметров КАК ДанныеПараметров,
	|	Перечисление.ХозяйственныеОперации КАК ХозяйственныеОперации
	|ГДЕ
	|	ХозяйственныеОперации.Ссылка В(&ХозОперацииНеРазделятьОформлениеЗакупок)
	|	И (ДанныеПараметров.ВариантОформленияЗакупок = ЗНАЧЕНИЕ(Перечисление.ВариантыОформленияЗакупок.НеРазделять)
	|			ИЛИ ДанныеПараметров.ВариантОформленияЗакупок ЕСТЬ NULL)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозОперацииПоДоговору.ХозОперация,
	|	ВЫБОР
	|		КОГДА ХозОперацииПоДоговору.ХозОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	ХозОперацииПоДоговору КАК ХозОперацииПоДоговору
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ХозОперацииПоСоглашению.ХозОперация,
	|	ВЫБОР
	|		КОГДА ХозОперацииПоСоглашению.ХозОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	ХозОперацииПоСоглашению КАК ХозОперацииПоСоглашению
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	
	ПараметрПоУмолчанию = Новый Массив;
	
	Запрос.УстановитьПараметр("Договор",    Договор);
	Запрос.УстановитьПараметр("Соглашение", Соглашение);
	
	Запрос.УстановитьПараметр("ХозОперацииЗакупкаУПоставщика",           ПараметрПоУмолчанию);
	Запрос.УстановитьПараметр("ХозОперацииЗакупкаПоИмпорту",             ПараметрПоУмолчанию);
	Запрос.УстановитьПараметр("ХозОперацииПриемНаКомиссию",              ПараметрПоУмолчанию);
	Запрос.УстановитьПараметр("ХозОперацииПриемНаОтветственноеХранение", ПараметрПоУмолчанию);
	Запрос.УстановитьПараметр("ХозОперацииЗакупкаВСтранахЕАЭС",          ПараметрПоУмолчанию);
	Запрос.УстановитьПараметр("ХозОперацииТоварыВПути",                  ПараметрПоУмолчанию);
	Запрос.УстановитьПараметр("ХозОперацииНеотфактурованныеПоставки",    ПараметрПоУмолчанию);
	Запрос.УстановитьПараметр("ХозОперацииНеРазделятьОформлениеЗакупок", ПараметрПоУмолчанию);
	
	МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяДокумента);
	МенеджерДокумента.ЗаполнитьПараметрыХозяйственныхОпераций(Запрос.Параметры);
	
	УстановитьПривилегированныйРежим(Истина);
	ХозяйственныеОперацииРаздельнойЗакупки = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ХозОперация");
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ХозяйственныеОперацииРаздельнойЗакупки;
	
КонецФункции

// Возвращает полный список хозяйственных операций раздленой закупки
// 
// Возвращаемое значение:
//	Массив - массив элементов с типом ПеречислениеСсылка.ХозяйственныеОперации
//
Функция ХозяйственныеОперацииРаздельнойЗакупкиБезОтборов() Экспорт
	
	Массив = Новый Массив;
	
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаНеотфактурованнаяПоставка);
	
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСПоступлениеИзТоваровВПути);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСНеотфактурованнаяПоставка);
	
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути);
	
	Возврат Массив;
	
КонецФункции

// Возвращает хозяйственную операцию, используемую при неразделенном варианте оформления закупок.
//
// Параметры:
//	ХозяйственнаяОперацияРаздельнойЗакупки - ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция документа.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция, используемая при неразделенном варианте 
//		оформления закупок.
//
Функция ОсновнаяХозяйственнаяОперацияРаздельнойЗакупки(ХозяйственнаяОперацияРаздельнойЗакупки) Экспорт
	
	ИспользоватьИмпортныеЗакупки          = ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеЗакупки");
	ИспользоватьТоварыВПутиОтПоставщиков  = ПолучитьФункциональнуюОпцию("ИспользоватьТоварыВПутиОтПоставщиков");
	ИспользоватьНеотфактурованныеПоставки = ПолучитьФункциональнуюОпцию("ИспользоватьНеотфактурованныеПоставки");
	
	Если ИспользоватьТоварыВПутиОтПоставщиков
		Или ИспользоватьНеотфактурованныеПоставки Тогда
		
		ХозяйственныеОперацииЗакупки = Новый Массив;
		ХозяйственныеОперацииЗакупки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
		
		ХозяйственныеОперацииИмпорта = Новый Массив;
		ХозяйственныеОперацииИмпорта.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту);
		
		ХозяйственныеОперацииВвозИзЕАЭС = Новый Массив;
		ХозяйственныеОперацииВвозИзЕАЭС.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС);
		
		Если ИспользоватьТоварыВПутиОтПоставщиков Тогда
			ХозяйственныеОперацииЗакупки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути);
			ХозяйственныеОперацииИмпорта.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
			ХозяйственныеОперацииВвозИзЕАЭС.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути);
		КонецЕсли;
		
		Если ИспользоватьНеотфактурованныеПоставки Тогда
			ХозяйственныеОперацииЗакупки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки);
			ХозяйственныеОперацииВвозИзЕАЭС.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки);
		КонецЕсли;
		
		Если ХозяйственныеОперацииЗакупки.Найти(ХозяйственнаяОперацияРаздельнойЗакупки) <> Неопределено Тогда
			Возврат Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика;
		КонецЕсли;
		
		Если ИспользоватьИмпортныеЗакупки Тогда
			Если ХозяйственныеОперацииИмпорта.Найти(ХозяйственнаяОперацияРаздельнойЗакупки) <> Неопределено Тогда
				Возврат Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту;
			КонецЕсли;
			
			Если ХозяйственныеОперацииВвозИзЕАЭС.Найти(ХозяйственнаяОперацияРаздельнойЗакупки) <> Неопределено Тогда
				Возврат Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС;
			КонецЕсли;
		КонецЕсли;
		
		Возврат ХозяйственнаяОперацияРаздельнойЗакупки;
		
	Иначе
		Возврат ХозяйственнаяОперацияРаздельнойЗакупки;
	КонецЕсли;
	
КонецФункции

// Возвращает признак хозяйственной операции раздельной закупки
//
// Параметры:
//  ХозяйственнаяОперация	 - ПеречислениеСсылка.ХозяйственныеОперации	 - хозяйственная операция, для которой нужно определить признак
// 
// Возвращаемое значение:
//  Булево - Истина, если используется раздельная закупка
//
Функция ЭтоХозяйственнаяОперацияРаздельнойЗакупки(ХозяйственнаяОперация) Экспорт
	
	ХозяйственныеОперацииРаздельнойЗакупки = ХозяйственныеОперацииРаздельнойЗакупкиБезОтборов();
	Возврат ХозяйственныеОперацииРаздельнойЗакупки.Найти(ХозяйственнаяОперация) <> Неопределено;
	
КонецФункции

// Возвращает соответствие, которое определяет пары операций раздельной закупки.
//
// Параметры:
//  КлючомЯвляютсяОперацииПоступления	 - Булево - инвертирует ключи и значения возвращаемого соответствия.
// 
// Возвращаемое значение:
//  Соответствие - ключом и значением являются логически одинаковые операции раздельной закупки,
//              например "ЗакупкаУПоставщикаТоварыВПути" и "ЗакупкаУПоставщикаПоступлениеИзТоваровВПути".
//
Функция ПарыОперацийРаздельнойЗакупки(КлючомЯвляютсяОперацииПоступления = Ложь) Экспорт
	
	ОперацииПереходаПраваСобственности = Новый Массив(5);
	ОперацииПоступления = Новый Массив(5);
	
	ОперацииПереходаПраваСобственности.Вставить(0, Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути);
	ОперацииПоступления.Вставить(0, Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути);
	
	ОперацииПереходаПраваСобственности.Вставить(1, Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки);
	ОперацииПоступления.Вставить(1, Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаНеотфактурованнаяПоставка);
	
	ОперацииПереходаПраваСобственности.Вставить(2, Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути);
	ОперацииПоступления.Вставить(2, Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСПоступлениеИзТоваровВПути);
	
	ОперацииПереходаПраваСобственности.Вставить(3, Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки);
	ОперацииПоступления.Вставить(3, Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСНеотфактурованнаяПоставка);
	
	ОперацииПереходаПраваСобственности.Вставить(4, Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
	ОперацииПоступления.Вставить(4, Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути);
	
	ВозвращаемоеЗначение = Новый Соответствие();
	
	Для Счетчик = 0 По 5 Цикл
		Если КлючомЯвляютсяОперацииПоступления Тогда
			ВозвращаемоеЗначение.Вставить(ОперацииПереходаПраваСобственности[Счетчик], ОперацииПоступления[Счетчик]);
		Иначе
			ВозвращаемоеЗначение.Вставить(ОперацииПоступления[Счетчик], ОперацииПереходаПраваСобственности[Счетчик]);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыПроверкиКорректностиЗаполненияДокументов

// Проверяет корректность заполнения документа закупки в соответствии с массивом проверок
// Вызывается из процедуры документа "ОбработкаПроведения"
//
// Параметры:
// ДокументЗакупки  - ДокументОбъект, для которого необходимо осуществить проверки
// Отказ            - Булево - Флаг отказа от проведения документа
//
Процедура ПроверитьКорректностьЗаполненияДокументаЗакупки(ДокументЗакупки, Отказ) Экспорт
	
	// Если документ уже заполнен некорректно - не будем выполнять запросы	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Проверим доступность ролей
	ОтклонениеОтУсловийЗакупок = ПраваПользователяПовтИсп.ОтклонениеОтУсловийЗакупок();
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Проверим нужно ли проверять диапазон допустимых цен
	ВидМаксимальноДопустимыхЦенЗакупки  = Константы.ВидМаксимальноДопустимыхЦенЗакупки.Получить();
	ПроверятьДиапазонЦен                = ЗначениеЗаполнено(ВидМаксимальноДопустимыхЦенЗакупки);
	ЕстьОшибкиСоглашениеНеУказано       = Ложь;
	ИспользоватьРучныеСкидки            = ПолучитьФункциональнуюОпцию("ИспользоватьРучныеСкидкиВЗакупках");
	ИспользоватьНоменклатуруПоставщиков = ПолучитьФункциональнуюОпцию("ИспользоватьНоменклатуруПоставщиков");
	ИспользоватьСоглашенияСПоставщиками = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСПоставщиками");
	
	
	
	ВсеСтрокиОтменены = Ложь;
	ВозвратМногооборотнойТары          = Ложь;
	
	// Заполним массив необходимых проверок в зависимости от типа документа
	
	МассивПроверок = Новый Массив();
	ИмяТаблицы     = ДокументЗакупки.Метаданные().ПолноеИмя();
	
	Если ИмяТаблицы = "Документ.ЗаказПоставщику" Тогда
		
		ВсеСтрокиОтменены = ОбщегоНазначенияУТ.ВсеСтрокиОтменены(ДокументЗакупки, "Товары", "Отменено");
		
		НужноСогласовать = ?(ВсеСтрокиОтменены, Ложь, Не ДокументЗакупки.Согласован И Не ОтклонениеОтУсловийЗакупок);
	ИначеЕсли ИмяТаблицы = "Документ.ДоверенностьВыданная"
		ИЛИ ИмяТаблицы = "Документ.ВыкупВозвратнойТарыУПоставщика"
		ИЛИ ИмяТаблицы = "Документ.ВозвратТоваровПоставщику"
		ИЛИ ИмяТаблицы = "Документ.ПоступлениеТоваров"
		ИЛИ ИмяТаблицы = "Документ.АктОРасхожденияхПослеОтгрузки"
		ИЛИ ИмяТаблицы = "Документ.ПередачаТоваровМеждуОрганизациями"
		Или ИмяТаблицы = "Документ.АктОРасхожденияхПослеПриемки" Тогда
		НужноСогласовать = Ложь;
	Иначе
		НужноСогласовать = Не ДокументЗакупки.Согласован И Не ОтклонениеОтУсловийЗакупок;
	КонецЕсли;
	
	// СОГЛАШЕНИЕ С ПОСТАВЩИКОМ
	
	Если ИмяТаблицы = "Справочник.СоглашенияСПоставщиками" Тогда
		
		МассивПроверок.Добавить("ВременнаяТаблицаТовары");
		ВозвратМногооборотнойТары = ДокументЗакупки.ВозвращатьМногооборотнуюТару;
		
		Если ПроверятьДиапазонЦен И НужноСогласовать И
			ДокументЗакупки.Статус <> Перечисления.СтатусыСоглашенийСПоставщиками.НеСогласовано Тогда
			
			МассивПроверок.Добавить("ВременнаяТаблицаДокументЗакупки");
			МассивПроверок.Добавить("ВременнаяТаблицаКурсыВалют");
			МассивПроверок.Добавить("ВхождениеЦенВДопустимыйДиапазон");
			
		КонецЕсли;
		
		Если ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаКомиссию
			Или ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи Тогда
			МассивПроверок.Добавить("НаличиеУслуг");
		КонецЕсли;
		
	// ЗАКАЗ ПОСТАВЩИКУ
		
	ИначеЕсли ИмяТаблицы = "Документ.ЗаказПоставщику" Тогда
		
		СоглашениеУказано = ЗначениеЗаполнено(ДокументЗакупки.Соглашение);
		
		МассивПроверок.Добавить("ВременнаяТаблицаДокументЗакупки");
		МассивПроверок.Добавить("ВременнаяТаблицаТовары");
		МассивПроверок.Добавить("КорректностьЗаполненияСклада");
		МассивПроверок.Добавить("КорректностьЗаполненияСкладаВТабличнойЧастиТовары");
		
		МассивПроверок.Добавить("КонтрольЗаполненияЦенСУчетомОтмененных");
		ВозвратМногооборотнойТары = ДокументЗакупки.ВернутьМногооборотнуюТару;
		ТребуетсяЗалогЗаТару = ДокументЗакупки.ТребуетсяЗалогЗаТару;
		
		Если ИспользоватьНоменклатуруПоставщиков Тогда
			МассивПроверок.Добавить("КорректностьЗаполненияНоменклатурыПоставщика");
		КонецЕсли;
		
		Если НужноСогласовать И
			ДокументЗакупки.Статус <> Перечисления.СтатусыЗаказовПоставщикам.НеСогласован Тогда
			
			МассивПроверок.Добавить("ВременнаяТаблицаКурсыВалют");
			
			Если СоглашениеУказано Тогда
				
				МассивПроверок.Добавить("ВременнаяТаблицаСкладыКорректныеПоСоглашению");
				МассивПроверок.Добавить("СоответствиеШапкиУсловиямЗакупок");
				МассивПроверок.Добавить("СоответствиеТоваровУсловиямЗакупок");
				
			Иначе
				
				ЕстьОшибкиСоглашениеНеУказано = ИспользоватьСоглашенияСПоставщиками;
				
			КонецЕсли;
			
			Если ИспользоватьРучныеСкидки
				И ДокументЗакупки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПриемНаКомиссию
				И ДокументЗакупки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи Тогда
				МассивПроверок.Добавить("ДопустимостьРучныхСкидокНаценок");
			КонецЕсли;
			
			Если ПроверятьДиапазонЦен Тогда
				МассивПроверок.Добавить("ВхождениеЦенВДопустимыйДиапазон");
			КонецЕсли;
			
			Если ДокументЗакупки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПриемНаКомиссию
				И ДокументЗакупки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи
				И ДокументЗакупки.СуммаДокумента <> 0
				И ДокументЗакупки.ПорядокРасчетов <> Перечисления.ПорядокРасчетов.ПоНакладным Тогда
				
				МассивПроверок.Добавить("ВременнаяТаблицаЭтапыГрафикаОплаты");
				МассивПроверок.Добавить("КорректностьЭтаповГрафикаОплаты");
				МассивПроверок.Добавить("КорректностьКоличестваЭтаповГрафикаОплаты");
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути
			Или ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки
			Или ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути
			Или ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути
			Или ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки
			Или ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаКомиссию
			Или ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи Тогда
			
			МассивПроверок.Добавить("НаличиеУслуг");
			
		Иначе
			МассивПроверок.Добавить("КорректностьЗаполненияАналитикиУслуг");
		КонецЕсли;
		
	//АКТ ПОСЛЕ ПРИЕМКИ
	ИначеЕсли ИмяТаблицы = "Документ.АктОРасхожденияхПослеПриемки" Тогда

		МассивПроверок.Добавить("ВременнаяТаблицаТовары");
		МассивПроверок.Добавить("КорректностьЗаполненияСкладаВТабличнойЧастиТоварыБезСкладаВШапке");
		
	// ПРИОБРЕТЕНИЕ ТОВАРОВ И УСЛУГ
		
	ИначеЕсли ИмяТаблицы = "Документ.ПриобретениеТоваровУслуг" Тогда
		
		СоглашениеУказано     = ЗначениеЗаполнено(ДокументЗакупки.Соглашение);
		ЕстьОшибкиСоглашениеНеУказано = ИспользоватьСоглашенияСПоставщиками
		                                И Не СоглашениеУказано
		                                И Не ДокументЗакупки.ПоступлениеПоЗаказам
		                                И НужноСогласовать;

		МассивПроверок.Добавить("ВременнаяТаблицаДокументЗакупки");
		
		Если Не ДокументЗакупки.ПоступлениеПоЗаказам И СоглашениеУказано И НужноСогласовать Тогда
			МассивПроверок.Добавить("ВременнаяТаблицаСкладыКорректныеПоСоглашению");
			МассивПроверок.Добавить("СоответствиеШапкиУсловиямЗакупок");
		КонецЕсли;
		
		МассивПроверок.Добавить("ВременнаяТаблицаТовары");
		МассивПроверок.Добавить("КорректностьЗаполненияСклада");
		МассивПроверок.Добавить("КорректностьЗаполненияСкладаВТабличнойЧастиТовары");
		Если ИспользоватьНоменклатуруПоставщиков Тогда
			МассивПроверок.Добавить("КорректностьЗаполненияНоменклатурыПоставщика");
		КонецЕсли;
		
		МассивПроверок.Добавить("КонтрольЗаполненияЦен");
		ВозвратМногооборотнойТары = ДокументЗакупки.ВернутьМногооборотнуюТару;
		ТребуетсяЗалогЗаТару = ДокументЗакупки.ТребуетсяЗалогЗаТару;
		
		Если ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути
			Или ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки
			Или ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути
			Или ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути
			Или ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки
			Или ДокументЗакупки.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаКомиссию Тогда
			
			МассивПроверок.Добавить("НаличиеУслуг");
			
		Иначе
			МассивПроверок.Добавить("КорректностьЗаполненияАналитикиУслуг");
		КонецЕсли;
		
		Если НужноСогласовать Тогда
			
			МассивПроверок.Добавить("ВременнаяТаблицаКурсыВалют");
			
			Если СоглашениеУказано Тогда
				МассивПроверок.Добавить("СоответствиеТоваровУсловиямЗакупок");
			КонецЕсли;
			
			Если СоглашениеУказано
				И ИспользоватьРучныеСкидки
				И ДокументЗакупки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПриемНаКомиссию Тогда
				МассивПроверок.Добавить("ДопустимостьРучныхСкидокНаценок");
			КонецЕсли;
				
			Если ПроверятьДиапазонЦен Тогда
				МассивПроверок.Добавить("ВхождениеЦенВДопустимыйДиапазон");
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ИмяТаблицы = "Документ.ПередачаТоваровМеждуОрганизациями" Тогда
		
		МассивПроверок.Добавить("ВременнаяТаблицаТовары");
		МассивПроверок.Добавить("ВременнаяТаблицаДокументЗакупки");
		МассивПроверок.Добавить("КорректностьЗаполненияСклада");

	ИначеЕсли ИмяТаблицы = "Документ.ВыкупВозвратнойТарыУПоставщика" Тогда
		
		Если ИспользоватьНоменклатуруПоставщиков Тогда
			МассивПроверок.Добавить("ВременнаяТаблицаТовары");
			МассивПроверок.Добавить("КорректностьЗаполненияНоменклатурыПоставщика");
		КонецЕсли;
		
	ИначеЕсли ИмяТаблицы = "Документ.ДоверенностьВыданная" Тогда
		
		Если ИспользоватьНоменклатуруПоставщиков Тогда
			МассивПроверок.Добавить("ВременнаяТаблицаТовары");
			МассивПроверок.Добавить("КорректностьЗаполненияНоменклатурыПоставщика");
		КонецЕсли;
		
		
	// КОРРЕКТИРОВКА ПОСТУПЛЕНИЯ
		
	ИначеЕсли ИмяТаблицы = "Документ.КорректировкаПриобретения" Тогда
		
		ЕстьОшибкиСоглашениеНеУказано = Ложь;
		
		МассивПроверок.Добавить("ВременнаяТаблицаДокументЗакупки");
		МассивПроверок.Добавить("ВременнаяТаблицаТовары");
		МассивПроверок.Добавить("КорректностьЗаполненияСклада");
		МассивПроверок.Добавить("КорректностьЗаполненияСкладаВТабличнойЧастиТовары");
		Если ИспользоватьНоменклатуруПоставщиков Тогда
			МассивПроверок.Добавить("КорректностьЗаполненияНоменклатурыПоставщика");
		КонецЕсли;
		
		МассивПроверок.Добавить("КорректностьЗаполненияАналитикиУслуг");
		
	// ВОЗВРАТ ТОВАРОВ ПОСТАВЩИКУ
		
	ИначеЕсли ИмяТаблицы = "Документ.ВозвратТоваровПоставщику" Тогда
		
		Если ДокументЗакупки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВозвратТоваровКомитенту Тогда
			МассивПроверок.Добавить("ВременнаяТаблицаДокументЗакупки");
			МассивПроверок.Добавить("ВременнаяТаблицаТовары");
			МассивПроверок.Добавить("КонтрольЗаполненияЦен");
			ВозвратМногооборотнойТары = ДокументЗакупки.ВозвратПринятойМногооборотнойТары;
			ТребуетсяЗалогЗаТару = ДокументЗакупки.ПредусмотренЗалогЗаТару;
		КонецЕсли;
		
	КонецЕсли;
	
	// Сформируем текст запроса необходимых проверок в соответствие с массивом проверок
	
	ТекстЗапроса     = "";
	ПараметрыЗапроса = Новый Структура();

	ПараметрыЗапроса.Вставить("Дата",                      ДокументЗакупки.Дата);
	ПараметрыЗапроса.Вставить("Ссылка",                    ДокументЗакупки.Ссылка);
	ПараметрыЗапроса.Вставить("ВозвратМногооборотнойТары", ВозвратМногооборотнойТары);
	ПараметрыЗапроса.Вставить("ТребуетсяЗалогЗаТару",      ТребуетсяЗалогЗаТару);
	
	Для Каждого ТекЭлемент Из МассивПроверок Цикл
		
		Если ТекЭлемент = "ВременнаяТаблицаДокументЗакупки" Тогда
			
			СформироватьЗапросВременнаяТаблицаШапкаДокументаЗакупки(ТекстЗапроса, ПараметрыЗапроса, ДокументЗакупки);
			
		ИначеЕсли ТекЭлемент = "ВременнаяТаблицаТовары" Тогда
			
			СформироватьЗапросВременнаяТаблицаТоварыДокументаЗакупки(ТекстЗапроса, ПараметрыЗапроса, ДокументЗакупки, ИмяТаблицы);
			
		ИначеЕсли ТекЭлемент = "ВременнаяТаблицаСкладыКорректныеПоСоглашению" Тогда
			
			СформироватьЗапросВременнаяТаблицаСкладыКорректныеПоСоглашению(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "СоответствиеШапкиУсловиямЗакупок" Тогда
			
			СформироватьЗапросШапкаДокументаЗакупки(ТекстЗапроса, ИмяТаблицы);
			
		ИначеЕсли ТекЭлемент = "ВременнаяТаблицаЭтапыГрафикаОплаты" Тогда
			
			СформироватьЗапросВременнаяТаблицаЭтапыОплаты(ТекстЗапроса, ПараметрыЗапроса, ДокументЗакупки);
			
		ИначеЕсли ТекЭлемент = "ВременнаяТаблицаКурсыВалют" Тогда
			
			СформироватьЗапросВременнаяТаблицаКурсыВалют(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "КорректностьЗаполненияНоменклатурыПоставщика" Тогда
			
			СформироватьЗапросКорректностьЗаполненияНоменклатурыПоставщика(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "СоответствиеТоваровУсловиямЗакупок" Тогда
			
			СформироватьЗапросТоварыДокументаЗакупки(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "ВхождениеЦенВДопустимыйДиапазон" Тогда
			
			СформироватьЗапросВхождениеЦенВДопустимыйДиапазон(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "КорректностьЗаполненияАналитикиУслуг" Тогда
			
			СформироватьЗапросКорректностьЗаполненияАналитикиУслуг(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "НаличиеУслуг" Тогда
			
			СформироватьЗапросНаличиеУслугВДокументеЗакупки(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "ДопустимостьРучныхСкидокНаценок" Тогда
			
			СформироватьЗапросДопустимостьРучныхСкидокНаценок(ТекстЗапроса);
			
		ИначеЕсли  ТекЭлемент = "КорректностьЗаполненияСклада" Тогда
			
			СформироватьЗапросКорректностьЗаполненияСклада(ТекстЗапроса);
			
		ИначеЕсли  ТекЭлемент = "КорректностьЗаполненияСкладаВТабличнойЧастиТовары" Тогда
			
			СформироватьЗапросКорректностьЗаполненияСкладовВТабличнойЧастиТовары(ТекстЗапроса);
			
		ИначеЕсли  ТекЭлемент = "КорректностьЗаполненияСкладаВТабличнойЧастиТоварыБезСкладаВШапке" Тогда
		
			СформироватьЗапросКорректностьЗаполненияСкладовВТабличнойЧастиТоварыБезСкладаВШапке(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "КорректностьЭтаповГрафикаОплаты" Тогда
			
			СформироватьЗапросКорректностьЭтаповГрафикаОплаты(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "КорректностьКоличестваЭтаповГрафикаОплаты" Тогда
			
			СформироватьЗапросКорректностьКоличестваЭтаповГрафикаОплаты(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "КонтрольЗаполненияЦенСУчетомОтмененных" Тогда
			
			СформироватьЗапросКонтрольЗаполненияЦенСУчетомОтмененных(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "КонтрольЗаполненияЦен" Тогда
			
			СформироватьЗапросКонтрольЗаполненияЦен(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "СоответствиеОрганизацииРаспоряжений" Тогда
			
			СформироватьЗапросСоответствиеОрганизацииРаспоряжений(ТекстЗапроса);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		УстановитьПривилегированныйРежим(Ложь);
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос(ТекстЗапроса);
	
	Для Каждого ПараметрЗапроса Из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ,ПараметрЗапроса.Значение);
	КонецЦикла;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// Сообщим пользователю о результатах проверки для каждого результата запроса
	НевозможноПровестиБезСогласования = Ложь;
	Для ТекИндекс = 0 По МассивРезультатов.Количество()-1 Цикл
		
		Выборка = МассивРезультатов[ТекИндекс].Выбрать();
		
		Если МассивПроверок[ТекИндекс] = "СоответствиеШапкиУсловиямЗакупок" Тогда
			
			СообщитьОбОшибкахШапкаДокументаЗакупки(Выборка, ДокументЗакупки, НевозможноПровестиБезСогласования);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "КорректностьЗаполненияНоменклатурыПоставщика" Тогда
			
			СообщитьОбОшибкахКорректностьЗаполненияНоменклатурыПоставщика(Выборка, ДокументЗакупки, Отказ);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "СоответствиеТоваровУсловиямЗакупок" Тогда
			
			СообщитьОбОшибкахТоварыДокументаЗакупки(Выборка,ДокументЗакупки, НевозможноПровестиБезСогласования);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "ВхождениеЦенВДопустимыйДиапазон" Тогда
			
			СообщитьОбОшибкахВхождениеЦенВДопустимыйДиапазон(Выборка, ДокументЗакупки, Отказ, ИспользоватьРучныеСкидки);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "КорректностьЗаполненияАналитикиУслуг" Тогда
			
			СообщитьОбОшибкахКорректностьЗаполненияАналитикиУслуг(Выборка, ДокументЗакупки, Отказ);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "НаличиеУслуг" Тогда
			
			СообщитьОбОшибкахНаличиеУслугВДокументеЗакупки(Выборка, ДокументЗакупки, Отказ);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "ДопустимостьРучныхСкидокНаценок" Тогда
			
			СообщитьОбОшибкахДопустимостьРучныхСкидокНаценок(Выборка, ДокументЗакупки, Отказ);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "КорректностьЗаполненияСклада" Тогда
			
			СообщитьОбОшибкахКорректностьЗаполненияСклада(Выборка, ДокументЗакупки, Отказ);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "КорректностьЗаполненияСкладаВТабличнойЧастиТовары"
			Или МассивПроверок[ТекИндекс] = "КорректностьЗаполненияСкладаВТабличнойЧастиТоварыБезСкладаВШапке" Тогда
			
			СообщитьОбОшибкахКорректностьЗаполненияСкладаВТабличнойЧастиТовары(Выборка, ДокументЗакупки, Отказ);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "КорректностьЭтаповГрафикаОплаты" Тогда
			
			СообщитьОбОшибкахКорректностьЭтаповГрафикаОплаты(Выборка, ДокументЗакупки, НевозможноПровестиБезСогласования);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "КорректностьКоличестваЭтаповГрафикаОплаты" Тогда
			
			СообщитьОбОшибкахКорректностьКоличестваЭтаповГрафикаОплаты(Выборка, ДокументЗакупки, НевозможноПровестиБезСогласования);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "КонтрольЗаполненияЦен"
			ИЛИ МассивПроверок[ТекИндекс] = "КонтрольЗаполненияЦенСУчетомОтмененных" Тогда
			
			СообщитьОбОшибкахКонтрольЗаполненияЦен(Выборка, ДокументЗакупки, Отказ);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "СоответствиеОрганизацииРаспоряжений" Тогда
			
			СообщитьОбОшибкахСоответствиеОрганизацииРаспоряжений(Выборка, ДокументЗакупки, Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	СообщитьОбОшибкахСоглашениеНеУказано(ЕстьОшибкиСоглашениеНеУказано, ДокументЗакупки, Отказ);
	
	Если НевозможноПровестиБезСогласования Тогда
		ТекстОшибки = НСтр("ru='Недостаточно прав на отклонение от условий закупок.'");
		
		Если ИмяТаблицы = "Документ.ЗаказПоставщику" Тогда 
			ТекстОшибки = ТекстОшибки + НСтр("ru='Документ необходимо провести в статусе ""Не согласован"".'");
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			,
			,
			,
			Отказ);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Проверяет корректность заполнения этапов графика оплаты в документе закупки
// Вызывается из процедуры документа "ОбработкаПроверкиЗаполнения"
//
// Параметры:
// ДокументЗакупки                  - ДокументОбъект, для которого необходимо осуществить проверки
// СуммаОплатыПоДокументу           - Число - сумма документа, в котором необходимо осуществить проверку
// СуммаЗалогаПоДокументу           - Число - сумма залога по документу, в котором необходимо осуществить проверку
// УчитыватьВариантОплаты           - Булево - признак необходимости проверки корректности вариантов оплаты
// Отказ                            - Булево - Флаг отказа от записи документа
// ПривязыватьСообщенияКГиперссылке - Булево - флаг, указывающий на необходимость привязки сообщений к гиперссылке
//
Процедура ПроверитьКорректностьЭтаповГрафикаОплаты(Знач ДокументЗакупки,
	                                               СуммаОплатыПоДокументу,
	                                               СуммаЗалогаПоДокументу,
	                                               УчитыватьВариантОплаты,
	                                               Отказ,
	                                               ПривязыватьСообщенияКГиперссылке) Экспорт
	
	Если ЗначениеЗаполнено(ДокументЗакупки.Дата) Тогда
		ДатаДокумента = НачалоДня(ДокументЗакупки.Дата);
	КонецЕсли;
	
	СуммаОплатыВсего = Окр(СуммаОплатыПоДокументу, 2);
	СуммаЗалогаВсего = Окр(СуммаЗалогаПоДокументу, 2);
	
	ИспользоватьУпрощеннуюСхемуОплаты = ПолучитьФункциональнуюОпцию("ИспользоватьУпрощеннуюСхемуОплатыВЗакупках");
	
	Если ДокументЗакупки.ЭтапыГрафикаОплаты.Количество() = 0 Тогда
		
		Если СуммаОплатыВсего > 0 Тогда
			
			Если ИспользоватьУпрощеннуюСхемуОплаты Тогда
				ТекстОшибки = НСтр("ru='Не заполнены условия оплаты'");
			Иначе
				ТекстОшибки = НСтр("ru='Необходимо заполнить этапы графика оплаты'");
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				,
				?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", "Объект.ЭтапыГрафикаОплаты"),
				,
				Отказ);
				
		КонецЕсли;
		
	Иначе
		
		ИтогПроцентПлатежа = 0;
		ИтогПроцентЗалогаЗаТару = 0;
		ИтогСуммаПлатежа = 0;
		ИтогСуммаЗалогаЗаТару = 0;
		Для ТекИндекс = 0 По ДокументЗакупки.ЭтапыГрафикаОплаты.Количество()-1 Цикл
			
			ДанныеЭтапа = Новый Структура("НомерСтроки, ВариантОплаты, ДатаПлатежа, ПроцентПлатежа, ПроцентЗалогаЗаТару, СуммаПлатежа, СуммаЗалогаЗаТару",
				0, Неопределено, '00010101', 0, 0, 0, 0);
			ЗаполнитьЗначенияСвойств(ДанныеЭтапа, ДокументЗакупки.ЭтапыГрафикаОплаты[ТекИндекс]);
			ИтогПроцентПлатежа = ИтогПроцентПлатежа + ДанныеЭтапа.ПроцентПлатежа;
			ИтогПроцентЗалогаЗаТару = ИтогПроцентЗалогаЗаТару + ДанныеЭтапа.ПроцентЗалогаЗаТару;
			ИтогСуммаПлатежа = ИтогСуммаПлатежа + ДанныеЭтапа.СуммаПлатежа;
			ИтогСуммаЗалогаЗаТару = ИтогСуммаЗалогаЗаТару + ДанныеЭтапа.СуммаЗалогаЗаТару;
			
			АдресОшибки = НСтр("ru='в строке %НомерСтроки% списка ""Этапы графика оплаты""'");
			АдресОшибки = СтрЗаменить(АдресОшибки, "%НомерСтроки%", ДанныеЭтапа.НомерСтроки);
			
			// Все реквизиты этапа оплаты должны быть заполнены
			
			Если УчитыватьВариантОплаты И Не ЗначениеЗаполнено(ДанныеЭтапа.ВариантОплаты) Тогда
				
				ТекстОшибки = НСтр("ru='Не заполнена колонка ""Вариант оплаты""'");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки + " " + АдресОшибки,
					,
					?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", "Объект." + ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "ВариантОплаты")),
					,
					Отказ);
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеЭтапа.ДатаПлатежа) Тогда
				
				Если ИспользоватьУпрощеннуюСхемуОплаты Тогда
					ТекстОшибки = НСтр("ru='Не заполнена ""Дата платежа""'");
				Иначе
					ТекстОшибки = НСтр("ru='Не заполнена колонка ""Дата платежа""'");
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки + " " + АдресОшибки,
					,
					?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", "Объект." + ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "ДатаПлатежа")),
					,
					Отказ);
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеЭтапа.ПроцентПлатежа)
				И Не ЗначениеЗаполнено(ДанныеЭтапа.ПроцентЗалогаЗаТару) Тогда
				
				ТекстОшибки = НСтр("ru='Не заполнена колонка ""Процент платежа""'");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки + " " + АдресОшибки,
					,
					?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", "Объект." + ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "ПроцентПлатежа")),
					,
					Отказ);
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеЭтапа.СуммаПлатежа) И
				ЗначениеЗаполнено(ДанныеЭтапа.ПроцентПлатежа) И 
				СуммаОплатыВсего - СуммаЗалогаВсего > 0 Тогда
				
				ТекстОшибки = НСтр("ru='Не заполнена колонка ""Сумма платежа""'");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки + " " + АдресОшибки,
					,
					?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", "Объект." + ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "СуммаПлатежа")),
					,
					Отказ);
				
			КонецЕсли;
				
			Если Не ЗначениеЗаполнено(ДанныеЭтапа.СуммаЗалогаЗаТару) И
				ЗначениеЗаполнено(ДанныеЭтапа.ПроцентЗалогаЗаТару) И 
				СуммаЗалогаВсего > 0 Тогда
				
				ТекстОшибки = НСтр("ru='Не заполнена колонка ""Сумма залога за тару""'");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки + " " + АдресОшибки,
					,
					?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", "Объект." + ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "СуммаЗалогаЗаТару")),
					,
					Отказ);
				
			КонецЕсли;
			
			// Дата платежа в тч ЭтапыГрафикаОплаты должна быть не меньше даты документа
			Если ЗначениеЗаполнено(ДанныеЭтапа.ДатаПлатежа) И
				ЗначениеЗаполнено(ДокументЗакупки.Дата) И
				ДанныеЭтапа.ДатаПлатежа < ДатаДокумента Тогда
				
				ТекстОшибки = НСтр("ru='Дата платежа должна быть не меньше даты документа %ДатаДокумента%'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ДатаДокумента%", Формат(ДокументЗакупки.Дата, "ДЛФ=DD"));
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					,
					?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", "Объект." + ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "ДатаПлатежа")),
					,
					Отказ);
				
			КонецЕсли;
			
			Если УчитыватьВариантОплаты Тогда
				
				// Дата платежа по авансовому этапу должна быть не больше даты поступления в шапке
				Если ЗначениеЗаполнено(ДанныеЭтапа.ДатаПлатежа) И
					ЗначениеЗаполнено(ДокументЗакупки.ЖелаемаяДатаПоступления) И
					(ДанныеЭтапа.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.АвансДоПодтверждения Или
					ДанныеЭтапа.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления) И
					ДанныеЭтапа.ДатаПлатежа > ДокументЗакупки.ЖелаемаяДатаПоступления Тогда
					
					ТекстОшибки = НСтр("ru='Дата платежа по авансовому этапу должна быть не больше желаемой даты поступления %ДатаПоступления%'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ДатаПоступления%", Формат(ДокументЗакупки.ЖелаемаяДатаПоступления, "ДЛФ=DD")); 
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстОшибки,
						,
						?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "ДатаПлатежа")),
						,
						Отказ);
					
				// Дата платежа по кредитному этапу должна быть не меньше даты поступления в шапке
				ИначеЕсли ЗначениеЗаполнено(ДанныеЭтапа.ДатаПлатежа) И
					ЗначениеЗаполнено(ДокументЗакупки.ЖелаемаяДатаПоступления) И
					ДанныеЭтапа.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.КредитПослеПоступления И
					ДанныеЭтапа.ДатаПлатежа < ДокументЗакупки.ЖелаемаяДатаПоступления Тогда
					
					ТекстОшибки = НСтр("ru='Дата платежа по кредитному этапу должна быть не меньше желаемой даты поступления %ДатаПоступления%'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ДатаПоступления%", Формат(ДокументЗакупки.ЖелаемаяДатаПоступления, "ДЛФ=DD")); 
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстОшибки,
						,
						?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "ДатаПлатежа")),
						,
						Отказ);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Процент платежей в в тч ЭтапыГрафикаОплаты должен равняться 100%
		
		Если ДокументЗакупки.ЭтапыГрафикаОплаты.Количество() > 0 Тогда
			
			Если ИтогПроцентПлатежа <> 100 И СуммаОплатыВсего - СуммаЗалогаВсего > 0 Тогда
				
				ТекстОшибки = НСтр("ru='Процент платежей по всем этапам ""%ПроцентПоЭтапам%%"" должен равняться ""100%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ПроцентПоЭтапам%", ИтогПроцентПлатежа);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					,
					?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", "Объект.ЭтапыГрафикаОплаты[0].ПроцентПлатежа"),
					,
					Отказ);
				
			КонецЕсли;
				
			Если ИтогПроцентЗалогаЗаТару <> 100 И СуммаЗалогаВсего > 0 Тогда
				
				ТекстОшибки = НСтр("ru='Процент залога за тару по всем этапам ""%ПроцентПоЭтапам%%"" должен равняться ""100%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ПроцентПоЭтапам%", ИтогПроцентЗалогаЗаТару);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					,
					?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", "Объект.ЭтапыГрафикаОплаты[0].ПроцентЗалогаЗаТару"),
					,
					Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Сумма платежей в в тч ЭтапыГрафикаОплаты должна равняться сумме заказа с учетом отмененных позиций
		
		Если ДокументЗакупки.ЭтапыГрафикаОплаты.Количество() > 0 Тогда
			
			Если ИтогСуммаПлатежа + СуммаЗалогаВсего <> СуммаОплатыВсего Тогда
				
				ТекстОшибки = НСтр("ru='Сумма платежей по всем этапам ""%СуммаПоЭтапам% %Валюта%"" не должна отличаться от суммы документа ""%СуммаВсего% %Валюта%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СуммаПоЭтапам%", ИтогСуммаПлатежа + СуммаЗалогаВсего);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СуммаВсего%",    СуммаОплатыПоДокументу);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Валюта%",        ДокументЗакупки.Валюта);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					,
					?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", "Объект.ЭтапыГрафикаОплаты[0].СуммаПлатежа"),
					,
					Отказ);
				
			КонецЕсли;
			
			Если ИтогСуммаЗалогаЗаТару <> СуммаЗалогаВсего И СуммаЗалогаВсего <> 0 Тогда
				
				ТекстОшибки = НСтр("ru='Сумма залога за тару по всем этапам ""%СуммаПоЭтапам% %Валюта%"" не должна отличаться от суммы залога по документу ""%СуммаВсего% %Валюта%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СуммаПоЭтапам%", ИтогСуммаЗалогаЗаТару);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СуммаВсего%",    СуммаОплатыПоДокументу);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Валюта%",        ДокументЗакупки.Валюта);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					,
					?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", "Объект.ЭтапыГрафикаОплаты[0].СуммаЗалогаЗаТару"),
					,
					Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
		КоличествоЭтапов = ДокументЗакупки.ЭтапыГрафикаОплаты.Количество();
		
		Для ВнешнийСчетчик = 2 По КоличествоЭтапов Цикл
			
			ИндексПредыдущегоЭтапа = ВнешнийСчетчик - 2;
			ИндексТекущегоЭтапа    = ВнешнийСчетчик - 1;
			ПредыдущееЗначениеДатыПлатежа    = ДокументЗакупки.ЭтапыГрафикаОплаты[ИндексПредыдущегоЭтапа].ДатаПлатежа;
			ТекущееЗначениеДатыПлатежа       = ДокументЗакупки.ЭтапыГрафикаОплаты[ИндексТекущегоЭтапа].ДатаПлатежа;
			
			Если УчитыватьВариантОплаты Тогда
				
				ПредыдущееЗначениеВариантаОплаты = ДокументЗакупки.ЭтапыГрафикаОплаты[ИндексПредыдущегоЭтапа].ВариантОплаты;
				ТекущееЗначениеВариантаОплаты    = ДокументЗакупки.ЭтапыГрафикаОплаты[ИндексТекущегоЭтапа].ВариантОплаты;
				
				// В табличной части Этапы не должно быть строк со значением АвансДоПодтверждения
				// в поле ВариантОплаты, идущих после строк со значением ПредоплатаДоПоступления
				// КредитПослеПоступления
				Если (ТекущееЗначениеВариантаОплаты = Перечисления.ВариантыОплатыПоставщику.АвансДоПодтверждения И 
					(ПредыдущееЗначениеВариантаОплаты = Перечисления.ВариантыОплатыПоставщику.КредитПослеПоступления ИЛИ
					ПредыдущееЗначениеВариантаОплаты = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления)) ИЛИ
					// В табличной части Этапы не должно быть строк со значением ПредоплатаДоПоступления
					// в поле ВариантОплаты, идущих после строк со значением КредитПослеПоступления
					(ТекущееЗначениеВариантаОплаты = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления И 
					ПредыдущееЗначениеВариантаОплаты = Перечисления.ВариантыОплатыПоставщику.КредитПослеПоступления) Тогда
					
					ТекстОшибки = НСтр("ru='Вариант оплаты ""%ТекущееЗначениеВариантаОплаты%"" в строке %ИндексТекущегоЭтапа%
					|не может идти после варианта оплаты ""%ПредыдущееЗначениеВариантаОплаты%"" в строке %ИндексПредыдущегоЭтапа%'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ПредыдущееЗначениеВариантаОплаты%", ПредыдущееЗначениеВариантаОплаты);
					ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ТекущееЗначениеВариантаОплаты%",    ТекущееЗначениеВариантаОплаты);
					ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ИндексТекущегоЭтапа%",              ИндексТекущегоЭтапа + 1);
					ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ИндексПредыдущегоЭтапа%",           ИндексПредыдущегоЭтапа + 1);
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстОшибки,
						,
						?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ЭтапыГрафикаОплаты", ДокументЗакупки.ЭтапыГрафикаОплаты[ИндексТекущегоЭтапа].НомерСтроки, "ВариантОплаты")),
						,
						Отказ);
					
				КонецЕсли;
				
			КонецЕсли;
			
			// Значение поля ДатаПлатежа табличной части ЭтапыГрафикаОплаты должно идти по возрастанию
			Если ТекущееЗначениеДатыПлатежа < ПредыдущееЗначениеДатыПлатежа И
				ЗначениеЗаполнено(ТекущееЗначениеДатыПлатежа) И
				ЗначениеЗаполнено(ПредыдущееЗначениеДатыПлатежа) Тогда
				
				Если ИспользоватьУпрощеннуюСхемуОплаты Тогда
					ТекстОшибки = НСтр("ru='Дата платежа по кредиту не должна быть меньше, чем дата платежа по предоплате'");
				Иначе
					ТекстОшибки = НСтр("ru='Дата платежа в строке %ИндексТекущегоЭтапа%
					|списка ""Этапы оплаты"" должна быть не меньше, чем в строке %ИндексПредыдущегоЭтапа%'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ИндексТекущегоЭтапа%",    ИндексТекущегоЭтапа + 1);
					ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ИндексПредыдущегоЭтапа%", ИндексПредыдущегоЭтапа + 1);
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					,
					?(ПривязыватьСообщенияКГиперссылке, "НадписьЭтапыОплаты", ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ЭтапыГрафикаОплаты", ДокументЗакупки.ЭтапыГрафикаОплаты[ИндексТекущегоЭтапа].НомерСтроки, "ДатаПлатежа")),
					,
					Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Проверяет корректность заполнения документа закупки в соответствии с массивом проверок
// путем выполнения пакета запросов.
//
// Параметры:
//		ДокументЗакупки - ДокументСсылка, для которого необходимо осуществить проверки
//		ЕстьОтклоненияОтЦеновыхУсловий - Булево - Устанавливается в процедуре при необходимости последующего согласования ценовых условий
//		ЕстьОтклоненияОтФинансовыхУсловий - Булево - Устанавливается в процедуре при необходимости последующего согласования финансовых условий
//		ЕстьОтклоненияОтЛогистическихУсловий - Булево - Устанавливается в процедуре при необходимости последующего согласования логистических условий
//
Процедура ПроверитьНеобходимостьСогласованияУсловийЗакупки(Знач ДокументЗакупки,
	                                                       ЕстьОтклоненияОтЦеновыхУсловий,
	                                                       ЕстьОтклоненияОтФинансовыхУсловий,
	                                                       ЕстьОтклоненияОтЛогистическихУсловий) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДокументЗакупки.Соглашение) Тогда
		
		ЕстьОтклоненияОтЦеновыхУсловий = Истина;
		ЕстьОтклоненияОтФинансовыхУсловий = Истина;
		ЕстьОтклоненияОтЛогистическихУсловий= Истина;
		
		Возврат;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТипДокумента   = ТипЗнч(ДокументЗакупки);
	ИмяТаблицы     = ДокументЗакупки.Метаданные().ПолноеИмя();
	
	// Проверим нужно ли проверять диапазон допустимых цен
	ВидМаксимальноДопустимыхЦенЗакупки = Константы.ВидМаксимальноДопустимыхЦенЗакупки.Получить();
	ПроверятьДиапазонЦен = ЗначениеЗаполнено(ВидМаксимальноДопустимыхЦенЗакупки);
	
	// Проверим нужно ли проверять скидки и наценки
	ИспользоватьРучныеСкидки = ПолучитьФункциональнуюОпцию("ИспользоватьРучныеСкидкиВЗакупках");
	
	// Проверим является ли документ приемом на комиссию
	ДокументПриемаНаКомиссию = ДокументЗакупки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПриемНаКомиссию;
	
	// Заполним массив необходимых проверок в зависимости от типа документа
	
	МассивПроверок  = Новый Массив();
	
	Если НЕ ЗначениеЗаполнено(ДокументЗакупки.Соглашение) Тогда
		
		ЕстьОтклоненияОтЦеновыхУсловий = Истина;
		ЕстьОтклоненияОтФинансовыхУсловий = Истина;
		ЕстьОтклоненияОтЛогистическихУсловий= Истина;
		
		УстановитьПривилегированныйРежим(Ложь);
		Возврат;
		
	КонецЕсли;
	
	МассивПроверок.Добавить("ВременнаяТаблицаДокументЗакупки");
	МассивПроверок.Добавить("ВременнаяТаблицаТовары");
	МассивПроверок.Добавить("ВременнаяТаблицаКурсыВалют");
	МассивПроверок.Добавить("ВременнаяТаблицаСкладыКорректныеПоСоглашению");
	
	МассивПроверок.Добавить("СоответствиеШапкиУсловиямЗакупок");
	МассивПроверок.Добавить("СоответствиеТоваровУсловиямЗакупок");
	
	Если ИспользоватьРучныеСкидки И ДокументПриемаНаКомиссию Тогда
		МассивПроверок.Добавить("ДопустимостьРучныхСкидокНаценок");
	КонецЕсли;
	
	Если ПроверятьДиапазонЦен Тогда
		МассивПроверок.Добавить("ВхождениеЦенВДопустимыйДиапазон");
	КонецЕсли;
	
	Если ДокументПриемаНаКомиссию И ДокументЗакупки.СуммаДокумента <> 0 Тогда
		
		МассивПроверок.Добавить("ВременнаяТаблицаЭтапыГрафикаОплаты");
		МассивПроверок.Добавить("КорректностьЭтаповГрафикаОплаты");
		МассивПроверок.Добавить("КорректностьКоличестваЭтаповГрафикаОплаты");
		
	КонецЕсли;
	
	// Сформируем запрос проверки
	
	ТекстЗапроса = "";
	ПараметрыЗапроса = Новый Структура();
	
	ПараметрыЗапроса.Вставить("Дата", ДокументЗакупки.Дата);
	ПараметрыЗапроса.Вставить("ВозвратМногооборотнойТары", ДокументЗакупки.ВернутьМногооборотнуюТару);
	
	Для Каждого ТекЭлемент Из МассивПроверок Цикл
		
		Если ТекЭлемент = "ВременнаяТаблицаДокументЗакупки" Тогда
			
			СформироватьЗапросВременнаяТаблицаШапкаДокументаЗакупки(ТекстЗапроса, ПараметрыЗапроса, ДокументЗакупки);
			
		ИначеЕсли ТекЭлемент = "ВременнаяТаблицаТовары" Тогда
			
			СформироватьЗапросВременнаяТаблицаТоварыДокументаЗакупки(ТекстЗапроса, ПараметрыЗапроса, ДокументЗакупки, ИмяТаблицы);
			
		ИначеЕсли ТекЭлемент = "ВременнаяТаблицаКурсыВалют" Тогда
			
			СформироватьЗапросВременнаяТаблицаКурсыВалют(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "ВременнаяТаблицаСкладыКорректныеПоСоглашению" Тогда
			
			СформироватьЗапросВременнаяТаблицаСкладыКорректныеПоСоглашению(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "СоответствиеШапкиУсловиямЗакупок" Тогда
			
			СформироватьЗапросШапкаДокументаЗакупки(ТекстЗапроса, ИмяТаблицы);
			
		ИначеЕсли ТекЭлемент = "ВременнаяТаблицаЭтапыГрафикаОплаты" Тогда
			
			СформироватьЗапросВременнаяТаблицаЭтапыОплаты(ТекстЗапроса, ПараметрыЗапроса, ДокументЗакупки);
			
		ИначеЕсли ТекЭлемент = "СоответствиеТоваровУсловиямЗакупок" Тогда
			
			СформироватьЗапросТоварыДокументаЗакупки(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "ВхождениеЦенВДопустимыйДиапазон" Тогда
			
			СформироватьЗапросВхождениеЦенВДопустимыйДиапазон(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "ДопустимостьРучныхСкидокНаценок" Тогда
			
			СформироватьЗапросДопустимостьРучныхСкидокНаценок(ТекстЗапроса);
			
		ИначеЕсли  ТекЭлемент = "КорректностьЗаполненияСклада" Тогда
			
			СформироватьЗапросКорректностьЗаполненияСклада(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "КорректностьЭтаповГрафикаОплаты" Тогда
			
			СформироватьЗапросКорректностьЭтаповГрафикаОплаты(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "КорректностьКоличестваЭтаповГрафикаОплаты" Тогда
			
			СформироватьЗапросКорректностьКоличестваЭтаповГрафикаОплаты(ТекстЗапроса);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		
		ЕстьОтклоненияОтЦеновыхУсловий = Истина;
		ЕстьОтклоненияОтФинансовыхУсловий = Истина;
		ЕстьОтклоненияОтЛогистическихУсловий= Истина;
		
		УстановитьПривилегированныйРежим(Ложь);
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Для Каждого ПараметрЗапроса Из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ,ПараметрЗапроса.Значение);
	КонецЦикла;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// Определим результаты проверки для каждого результата запроса
	
	Для ТекИндекс = 0 По МассивРезультатов.Количество()-1 Цикл
		
		// Если имеются отклонения от всех проверяемых условий, дальнейшая проверка не требуется
		Если ЕстьОтклоненияОтЦеновыхУсловий И 
			ЕстьОтклоненияОтФинансовыхУсловий И 
			ЕстьОтклоненияОтЛогистическихУсловий Тогда
			Прервать;
		КонецЕсли;
		
		ТекЭлемент = МассивПроверок[ТекИндекс];
		Выборка = МассивРезультатов[ТекИндекс].Выбрать();
		
		Если ТекЭлемент = "СоответствиеШапкиУсловиямЗакупок" Тогда
			
			Пока Выборка.Следующий() Цикл
				Если ЕстьОтклоненияОтЦеновыхУсловий И ЕстьОтклоненияОтЛогистическихУсловий И ЕстьОтклоненияОтФинансовыхУсловий Тогда
					Прервать;
				КонецЕсли;
				Если Выборка.ЕстьОшибкиЗаполненияВалюта ИЛИ // Валюта не должна отличаться от значения в соглашении
					Выборка.ЕстьОшибкиЗаполненияНалогообложениеНДС ИЛИ // Тип налогообложения НДС не должен отличаться от значения в соглашении
					Выборка.ЕстьОшибкиЗаполненияЦенаВключаетНДС Тогда // Признак ""Цена включает НДС"" не должен отличаться от значения в соглашении
					
					ЕстьОтклоненияОтЦеновыхУсловий = Истина;
				КонецЕсли;
				Если Выборка.ЕстьОшибкиЗаполненияСклад Тогда // Склад не должен отличаться от значения в соглашении
					ЕстьОтклоненияОтЛогистическихУсловий = Истина;
				КонецЕсли;
				Если Выборка.ЕстьОшибкиЗаполненияФормаОплаты Тогда // ФормаОплаты не должна отличаться от значения в соглашении
					ЕстьОтклоненияОтФинансовыхУсловий = Истина;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ТекЭлемент = "СоответствиеТоваровУсловиямЗакупок" Тогда
			
			Пока Выборка.Следующий() Цикл
				Если ЕстьОтклоненияОтЦеновыхУсловий Тогда
					Прервать;
				КонецЕсли;
				Если Выборка.ЕстьОшибкиМаксимальноДопустимаяЦена ИЛИ // Цена не должна превышать максимально допустимую
					Выборка.ЕстьОшибкиЗаполненияСтавкаНДС Тогда // Ставка НДС не должна отличаться от ставки, в соглашении
					
					ЕстьОтклоненияОтЦеновыхУсловий = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ТекЭлемент = "ВхождениеЦенВДопустимыйДиапазон" Тогда
			
			Пока Выборка.Следующий() Цикл
				Если ЕстьОтклоненияОтЦеновыхУсловий Тогда
					Прервать;
				КонецЕсли;
				Если Выборка.ЕстьОшибкиМаксимальноДопустимаяЦена Тогда
					ЕстьОтклоненияОтЦеновыхУсловий = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ТекЭлемент = "ДопустимостьРучныхСкидокНаценок" Тогда
			
			Пока Выборка.Следующий() Цикл
				Если ЕстьОтклоненияОтЦеновыхУсловий Тогда
					Прервать;
				КонецЕсли;
				Если Выборка.ЕстьОшибкиЗаполненияРучнойНаценки Тогда
					ЕстьОтклоненияОтЦеновыхУсловий = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ТекЭлемент = "КорректностьЭтаповГрафикаОплаты" Тогда
			
			МаксДатаАванса = Дата(1,1,1);
			ОдинДень       = 86400;
			СуммаПроцентовПлатежа = 0;
			СуммаПроцентовПлатежаШаблона = 0;
			
			Пока Выборка.Следующий() Цикл
				
				Если ЕстьОтклоненияОтФинансовыхУсловий Тогда
					Прервать;
				КонецЕсли;
				
				Если Не Выборка.ЕстьОшибкиЗаполненияЭтапыГрафикаОплаты ИЛИ // В документе не заполнено соглашение или сумма документа равна 0
					Выборка.ЭтапШаблонаОтсутствует Тогда // График оплаты в соглашении не заполнен или не соответствует по количеству графику документа
					
					Прервать; // Не проверять далее. Если график заполнен, будет выявлена ошибка при проверке по запросу "КорректностьКоличестваЭтаповГрафикаОплаты"
				КонецЕсли;
				
				Если Выборка.ЕстьОшибкиЗаполненияВариантаОплаты Тогда // Вариант оплаты не должен отличаться от значения в графике оплаты
					ЕстьОтклоненияОтФинансовыхУсловий = Истина;
					Прервать;
				КонецЕсли;
				
				Календарь = Выборка.Календарь;
				
				Если ЗначениеЗаполнено(Календарь) Тогда
					
					Если Выборка.ВариантОплатыШаблона = Перечисления.ВариантыОплатыПоставщику.КредитПослеПоступления Тогда
						Если ЗначениеЗаполнено(Выборка.ЖелаемаяДатаПоступления) Тогда
							ДатаПлатежаШаблона = КалендарныеГрафики.ДатаПоКалендарю(Календарь, Выборка.ЖелаемаяДатаПоступления, Выборка.Сдвиг );
						ИначеЕсли ЗначениеЗаполнено(МаксДатаАванса) Тогда
							ДатаПлатежаШаблона = КалендарныеГрафики.ДатаПоКалендарю(Календарь, МаксДатаАванса, Выборка.Сдвиг);
						Иначе
							ДатаПлатежаШаблона = КалендарныеГрафики.ДатаПоКалендарю(Календарь, Выборка.Дата, Выборка.Сдвиг);
						КонецЕсли;
					Иначе
						ДатаПлатежаШаблона = КалендарныеГрафики.ДатаПоКалендарю(Календарь, Выборка.Дата, Выборка.Сдвиг);
					КонецЕсли;
					
				Иначе
					
					Если ЗначениеЗаполнено(Выборка.ЖелаемаяДатаПоступления) Тогда
						ДатаПлатежаШаблона = Выборка.ДатаПлатежаШаблона;
					ИначеЕсли ЗначениеЗаполнено(МаксДатаАванса) Тогда
						ДатаПлатежаШаблона = МаксДатаАванса + Выборка.Сдвиг * ОдинДень;
					Иначе
						ДатаПлатежаШаблона = Выборка.ДатаПлатежаШаблона;
					КонецЕсли;
					
				КонецЕсли;
				
				Если Выборка.ВариантОплатыШаблона = Перечисления.ВариантыОплатыПоставщику.АвансДоПодтверждения Или
					Выборка.ВариантОплатыШаблона = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления Тогда
					
					МаксДатаАванса = ДатаПлатежаШаблона;
					
				КонецЕсли;
				
				Если ДатаПлатежаШаблона > Выборка.ДатаПлатежа Тогда // Дата платежа должна быть не меньше, чем в графике оплаты
					ЕстьОтклоненияОтФинансовыхУсловий = Истина;
					Прервать;
				КонецЕсли;
				
				СуммаПроцентовПлатежаШаблона = СуммаПроцентовПлатежаШаблона + Выборка.ПроцентПлатежаШаблона;
				СуммаПроцентовПлатежа = СуммаПроцентовПлатежа + Выборка.ПроцентПлатежа;
				
				Если Выборка.ЕстьОшибкиЗаполненияПроцентаПлатежа И СуммаПроцентовПлатежа > СуммаПроцентовПлатежаШаблона Тогда // Проценты до следующего платежа по графику не должны превышать проценты по графику соглашения
					
					ЕстьОтклоненияОтФинансовыхУсловий = Истина;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		ИначеЕсли ТекЭлемент = "КорректностьКоличестваЭтаповГрафикаОплаты" Тогда
			
			Если Выборка.Следующий() Тогда
				Если Выборка.КоличествоЭтаповШаблона <> 0 И Выборка.ЕстьОшибкиЗаполненияЭтапыГрафикаОплаты Тогда
					ЕстьОтклоненияОтФинансовыхУсловий = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Проверяет корректность заполнения товаров документа возврата в соответствии 
// с реализованными товарами с учетом корректировок.
// Вызывается из процедуры документа "ОбработкаПроверкиЗаполнения"
//
// Параметры:
// ДокументЗакупки  - ДокументОбъект, для которого необходимо осуществить проверки
// Отказ            - Булево - Флаг отказа от проведения документа
// ТаблицаПроверяемыеТовары - ТаблицаЗначений - Проверяются товары в таблице, если она передана
//
Процедура ПроверитьКорректностьВозвращаемыхТоваров(Знач ДокументЗакупки, Отказ, ТаблицаПроверяемыеТовары = Неопределено) Экспорт
	
	Если ТаблицаПроверяемыеТовары = Неопределено Тогда
		ТаблицаПроверяемыеТовары = ДокументЗакупки.Товары.Выгрузить();
	КонецЕсли;
	
	Если ТаблицаПроверяемыеТовары.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Если ТипЗнч(ДокументЗакупки) = Тип("ДокументОбъект.АктОРасхожденияхПослеОтгрузки") Тогда
			ЗапросТекст = "
				|ВЫБРАТЬ
				|	ВЫРАЗИТЬ(ТаблицаТовары.Номенклатура КАК Справочник.Номенклатура)                 КАК Номенклатура,
				|	ТаблицаТовары.Характеристика                                                     КАК Характеристика,
				|	ВЫРАЗИТЬ(ТаблицаТовары.Назначение КАК Справочник.Назначения)                     КАК Назначение,
				|	ТаблицаТовары.Серия                                                              КАК Серия,
				|	ТаблицаТовары.КоличествоУпаковок - 
				|		ТаблицаТовары.КоличествоУпаковокПоДокументу КАК КоличествоУпаковок,
				|	ТаблицаТовары.Количество - ТаблицаТовары.КоличествоПоДокументу                   КАК Количество,
				|	ВЫРАЗИТЬ(ТаблицаТовары.ДокументПоступления КАК Документ.ПриобретениеТоваровУслуг) КАК ДокументПоступления
				|
				|ПОМЕСТИТЬ ВТТаблицаТовары
				|ИЗ
				|	&ТаблицаПроверяемыеТовары КАК ТаблицаТовары
				|ГДЕ
				|	НЕ ТаблицаТовары.ДокументПоступления = ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка)
				|	И НЕ ТаблицаТовары.ДокументПоступления = НЕОПРЕДЕЛЕНО
				|	И (ТаблицаТовары.Количество-ТаблицаТовары.КоличествоПоДокументу) > 0
				|	И ТаблицаТовары.Действие В (ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного),
				|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного))
				|;";
		ИначеЕсли ТипЗнч(ДокументЗакупки) = Тип("ДокументСсылка.АктОРасхожденияхПослеОтгрузки") Тогда
			ЗапросТекст = "
				|ВЫБРАТЬ
				|	ВЫРАЗИТЬ(ТаблицаТовары.Номенклатура КАК Справочник.Номенклатура)                 КАК Номенклатура,
				|	ТаблицаТовары.Характеристика                                                     КАК Характеристика,
				|	ВЫРАЗИТЬ(ТаблицаТовары.Назначение КАК Справочник.Назначения)                     КАК Назначение,
				|	ТаблицаТовары.Серия                                                              КАК Серия,
				|	ТаблицаТовары.КоличествоУпаковокПоАкту -
				|		ТаблицаТовары.КоличествоУпаковокОснование                                    КАК КоличествоУпаковок,
				|	(ТаблицаТовары.КоличествоУпаковокПоАкту -
				|	ТаблицаТовары.КоличествоУпаковокОснование) * ТаблицаТовары.КоэффициентУпаковки   КАК Количество,
				|	ВЫРАЗИТЬ(ТаблицаТовары.ДокументПоступления КАК Документ.ПриобретениеТоваровУслуг) КАК ДокументПоступления
				|
				|ПОМЕСТИТЬ ВТТаблицаТовары
				|ИЗ
				|	&ТаблицаПроверяемыеТовары КАК ТаблицаТовары
				|ГДЕ
				|	НЕ ТаблицаТовары.ДокументПоступления = ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка)
				|	И НЕ ТаблицаТовары.ДокументПоступления = НЕОПРЕДЕЛЕНО
				|	И ТаблицаТовары.КоличествоУпаковокПоАкту-ТаблицаТовары.КоличествоУпаковокОснование > 0
				|;";
		Иначе
			ЗапросТекст = "ВЫБРАТЬ
				|	ТаблицаТовары.Номенклатура                                   КАК Номенклатура,
				|	ТаблицаТовары.Характеристика                                 КАК Характеристика,
				|	ВЫРАЗИТЬ(ТаблицаТовары.Назначение КАК Справочник.Назначения) КАК Назначение,
				|	ВЫБОР
				|		КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
				|			Тогда ТаблицаТовары.Серия
				|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
				|	КОНЕЦ                                                        КАК Серия,
				|	ТаблицаТовары.КоличествоУпаковок                             КАК КоличествоУпаковок,
				|	ТаблицаТовары.Количество                                     КАК Количество,
				|	ТаблицаТовары.ДокументПоступления                            КАК ДокументПоступления
				|
				|ПОМЕСТИТЬ ВТТаблицаТовары
				|ИЗ
				|	&ТаблицаПроверяемыеТовары КАК ТаблицаТовары
				|ГДЕ
				|	НЕ ТаблицаТовары.ДокументПоступления = ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка)
				|	И НЕ ТаблицаТовары.ДокументПоступления = НЕОПРЕДЕЛЕНО
				|ИНДЕКСИРОВАТЬ ПО
				|	Номенклатура,
				|	Характеристика,
				|	Серия
				|;";
		КонецЕсли;
		Запрос.Текст = ЗапросТекст + "
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ТаблицаТовары.Номенклатура КАК Справочник.Номенклатура)	КАК Номенклатура,
		|	ТаблицаТовары.Характеристика			КАК Характеристика,
		|	ТаблицаТовары.Назначение                КАК Назначение,
		|	ТаблицаТовары.Серия						КАК Серия,
		|	ТаблицаТовары.ДокументПоступления		КАК ДокументПоступления,
		|	СУММА(ТаблицаТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ТаблицаТовары.Количество) 		КАК Количество
		|ПОМЕСТИТЬ ПроверяемыеТовары
		|ИЗ
		|	ВТТаблицаТовары КАК ТаблицаТовары
		|СГРУППИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	ТаблицаТовары.Назначение,
		|	Серия,
		|	ДокументПоступления
		|	
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Серия;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(КорректировкаПриобретения.Дата) КАК Дата,
		|	ПроверяемыеТовары.ДокументПоступления КАК СсылкаПоступления
		|ПОМЕСТИТЬ ДанныеКорректировки
		|ИЗ
		|	ПроверяемыеТовары КАК ПроверяемыеТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
		|		ПО (КорректировкаПриобретения.ДокументОснование = ПроверяемыеТовары.ДокументПоступления)
		|ГДЕ
		|	КорректировкаПриобретения.Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	ПроверяемыеТовары.ДокументПоступления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СсылкаПоступления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(КорректировкаПриобретения.Ссылка) КАК Ссылка,
		|	МАКСИМУМ(КорректировкаПриобретения.Ссылка.Дата) КАК Дата,
		|	КорректировкаПриобретения.Ссылка.ДокументОснование КАК СсылкаПоступления
		|ПОМЕСТИТЬ ДанныеПоследнейКорректировки
		|ИЗ
		|	Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
		|ГДЕ
		|	(КорректировкаПриобретения.Ссылка.ДокументОснование, КорректировкаПриобретения.Ссылка.Дата) В
		|			(ВЫБРАТЬ
		|				ДанныеКорректировки.СсылкаПоступления КАК СсылкаПоступления,
		|				ДанныеКорректировки.Дата КАК Дата
		|			ИЗ
		|				ДанныеКорректировки)
		|	И КорректировкаПриобретения.Ссылка.Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаПриобретения.Ссылка.ДокументОснование
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	СсылкаПоступления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫБОР
		|		КОГДА ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
		|			ТОГДА ПроверяемыеТовары.ДокументПоступления
		|		ИНАЧЕ ДанныеПоследнейКорректировки.Ссылка
		|	КОНЕЦ КАК Ссылка
		|ПОМЕСТИТЬ СсылкиНаПоступления
		|ИЗ
		|	ПроверяемыеТовары КАК ПроверяемыеТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
		|		ПО ПроверяемыеТовары.ДокументПоступления = ДанныеПоследнейКорректировки.СсылкаПоступления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КорректировкаПриобретенияТовары.Номенклатура КАК Номенклатура,
		|	КорректировкаПриобретенияТовары.Характеристика КАК Характеристика,
		|	КорректировкаПриобретенияТовары.Серия КАК Серия,
		|	КорректировкаПриобретенияТовары.Назначение КАК Назначение,
		|	СУММА(КорректировкаПриобретенияТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(КорректировкаПриобретенияТовары.Количество) КАК Количество,
		|	СРЕДНЕЕ(КорректировкаПриобретенияТовары.Цена) КАК Цена,
		|	КорректировкаПриобретенияТовары.Ссылка.ДокументОснование КАК ДокументПоступления
		|ПОМЕСТИТЬ ДанныеПоступления
		|ИЗ
		|	Документ.КорректировкаПриобретения.Товары КАК КорректировкаПриобретенияТовары
		|
		|ГДЕ
		|	КорректировкаПриобретенияТовары.Ссылка.Проведен
		|	И (Номенклатура,Характеристика,Назначение,Серия) В 
		|	(ВЫБРАТЬ 
		|		ПроверяемыеТовары.Номенклатура КАК Номенклатура,
		|		ПроверяемыеТовары.Характеристика КАК Характеристика,
		|		ПроверяемыеТовары.Назначение КАК Назначение,
		|		ПроверяемыеТовары.Серия КАК Серия
		|	ИЗ
		|		ПроверяемыеТовары КАК ПроверяемыеТовары	
		|	) И  (Ссылка) В 
		|	(ВЫБРАТЬ 
		|		СсылкиНаПоступления.Ссылка КАК Ссылка
		|	ИЗ
		|		СсылкиНаПоступления КАК СсылкиНаПоступления
		|	) 
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаПриобретенияТовары.Номенклатура,
		|	КорректировкаПриобретенияТовары.Характеристика,
		|	КорректировкаПриобретенияТовары.Серия,
		|	КорректировкаПриобретенияТовары.Назначение,		
		|	КорректировкаПриобретенияТовары.Ссылка.ДокументОснование
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ПриобретениеТоваровУслугТовары.Номенклатура,
		|	ПриобретениеТоваровУслугТовары.Характеристика,
		|	ПриобретениеТоваровУслугТовары.Серия,
		|	ПриобретениеТоваровУслугТовары.Назначение,
		|	СУММА(ПриобретениеТоваровУслугТовары.КоличествоУпаковок),
		|	СУММА(ПриобретениеТоваровУслугТовары.Количество),
		|	СРЕДНЕЕ(ПриобретениеТоваровУслугТовары.Цена),
		|	ПриобретениеТоваровУслугТовары.Ссылка
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
		|
		|ГДЕ
		|	ПриобретениеТоваровУслугТовары.Ссылка.Проведен
		|	И (Номенклатура,Характеристика,Назначение,Серия) В 
		|	(ВЫБРАТЬ 
		|		ПроверяемыеТовары.Номенклатура КАК Номенклатура,
		|		ПроверяемыеТовары.Характеристика КАК Характеристика,
		|		ПроверяемыеТовары.Назначение КАК Назначение,
		|		ПроверяемыеТовары.Серия КАК Серия
		|	ИЗ
		|		ПроверяемыеТовары КАК ПроверяемыеТовары
		|	) И  (Ссылка) В 
		|	(ВЫБРАТЬ 
		|		СсылкиНаПоступления.Ссылка КАК Ссылка
		|	ИЗ
		|		СсылкиНаПоступления КАК СсылкиНаПоступления
		|	) 
		|СГРУППИРОВАТЬ ПО
		|	ПриобретениеТоваровУслугТовары.Номенклатура,
		|	ПриобретениеТоваровУслугТовары.Характеристика,
		|	ПриобретениеТоваровУслугТовары.Серия,
		|	ПриобретениеТоваровУслугТовары.Назначение,
		|	ПриобретениеТоваровУслугТовары.Ссылка
		|
		|
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	ДокументПоступления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПроверяемыеТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаПроверяемыеТовары.Характеристика КАК Характеристика,
		|	ТаблицаПроверяемыеТовары.Серия КАК Серия,
		|	ТаблицаПроверяемыеТовары.Назначение КАК Назначение,
		|	СУММА(ТаблицаПроверяемыеТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ТаблицаПроверяемыеТовары.Количество) КАК Количество,
		|	ТаблицаПроверяемыеТовары.ДокументПоступления КАК ДокументПоступления
		|
		|ПОМЕСТИТЬ ДанныеДокументовВозврата
		|ИЗ
		|	ПроверяемыеТовары КАК ПроверяемыеТовары
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		Документ.ВозвратТоваровПоставщику.Товары КАК ТаблицаПроверяемыеТовары
		|	ПО
		|		(ТаблицаПроверяемыеТовары.ДокументПоступления = ПроверяемыеТовары.ДокументПоступления)
		|		И ПроверяемыеТовары.Номенклатура = ТаблицаПроверяемыеТовары.Номенклатура
		|		И ПроверяемыеТовары.Характеристика = ТаблицаПроверяемыеТовары.Характеристика
		|		И ПроверяемыеТовары.Серия = ТаблицаПроверяемыеТовары.Серия
		|		И ПроверяемыеТовары.Назначение = ТаблицаПроверяемыеТовары.Назначение
		|
		|ГДЕ
		|	ТаблицаПроверяемыеТовары.Ссылка.Проведен
		|	И ТаблицаПроверяемыеТовары.Ссылка <> &ЭтотВозвратСсылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаПроверяемыеТовары.Номенклатура,
		|	ТаблицаПроверяемыеТовары.Характеристика,
		|	ТаблицаПроверяемыеТовары.Серия,
		|	ТаблицаПроверяемыеТовары.Назначение,
		|	ТаблицаПроверяемыеТовары.ДокументПоступления
		|
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Серия,
		|	ДокументПоступления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПроверяемыеТовары.Номенклатура КАК Номенклатура,
		|	ЕСТЬNULL(ДанныеПоступления.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0) - ЕСТЬNULL(ПроверяемыеТовары.Количество, 0) КАК Количество,
		|	ПроверяемыеТовары.Характеристика КАК Характеристика,
		|	ПроверяемыеТовары.Серия КАК Серия,
		|	ПроверяемыеТовары.Назначение КАК Назначение,
		|	ПроверяемыеТовары.ДокументПоступления КАК ДокументПоступления,
		|	ПроверяемыеТовары.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ПроверяемыеТовары.ДокументПоступления.Номер КАК НомерПоступления
		|ИЗ
		|	ДанныеПоступления КАК ДанныеПоступления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументовВозврата КАК ДанныеДокументовВозврата
		|		ПО ДанныеПоступления.ДокументПоступления = ДанныеДокументовВозврата.ДокументПоступления
		|			И ДанныеПоступления.Номенклатура = ДанныеДокументовВозврата.Номенклатура
		|			И ДанныеПоступления.Характеристика = ДанныеДокументовВозврата.Характеристика
		|			И ДанныеПоступления.Серия = ДанныеДокументовВозврата.Серия
		|			И ДанныеПоступления.Назначение = ДанныеДокументовВозврата.Назначение
		|		ПОЛНОЕ СОЕДИНЕНИЕ ПроверяемыеТовары КАК ПроверяемыеТовары
		|		ПО ДанныеПоступления.ДокументПоступления = ПроверяемыеТовары.ДокументПоступления
		|			И ДанныеПоступления.Номенклатура = ПроверяемыеТовары.Номенклатура
		|			И ДанныеПоступления.Характеристика = ПроверяемыеТовары.Характеристика
		|			И ДанныеПоступления.Серия = ПроверяемыеТовары.Серия
		|			И ДанныеПоступления.Назначение = ПроверяемыеТовары.Назначение
		|		
		|ГДЕ
		|	ЕСТЬNULL(ДанныеПоступления.Количество, 0) - ЕСТЬNULL(ДанныеДокументовВозврата.Количество, 0) - ЕСТЬNULL(ПроверяемыеТовары.Количество, 0) < 0
		|	И (ПроверяемыеТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|	ИЛИ (ПроверяемыеТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) 
		|		И НЕ ПроверяемыеТовары.ДокументПоступления.ВернутьМногооборотнуюТару))";
		
		Запрос.УстановитьПараметр("ТаблицаПроверяемыеТовары", ТаблицаПроверяемыеТовары);
		Запрос.УстановитьПараметр("ЭтотВозвратСсылка", ДокументЗакупки.Ссылка);
		УстановитьПривилегированныйРежим(Истина);
		Результат = Запрос.Выполнить().Выгрузить();
		Если Результат.Количество() > 0 Тогда
			Отказ = Истина;
			СообщитьОбОшибкахКорректностиВозвращаемыхТоваров(Результат);
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

// Формирует отчет "Отклонения от условий закупок" путем выполнения пакета запросов. 
// 
// Параметры:
// ДокументЗакупки  - ДокументОбъект -Документ, для которого необходимо осуществить проверки
// ТаблицаОтчета  - ТабличныйДокумент - Табличный документ отчета
//
Процедура СформироватьОтчетОтклоненияОтУсловийЗакупок(Знач ДокументЗакупки, ТаблицаОтчета) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Проверим нужно ли проверять диапазон допустимых цен
	ВидМаксимальноДопустимыхЦенЗакупки  = Константы.ВидМаксимальноДопустимыхЦенЗакупки.Получить();
	ПроверятьДиапазонЦен                = ЗначениеЗаполнено(ВидМаксимальноДопустимыхЦенЗакупки);
	ЕстьОшибкиСоглашениеНеУказано       = Ложь;
	ИспользоватьРучныеСкидки            = ПолучитьФункциональнуюОпцию("ИспользоватьРучныеСкидкиВЗакупках");
	ИспользоватьНоменклатуруПоставщиков = ПолучитьФункциональнуюОпцию("ИспользоватьНоменклатуруПоставщиков");
	ИспользоватьСоглашенияСПоставщиками = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСПоставщиками");
	
	ВозвратМногооборотнойТары          = Ложь;
	
	// Заполним массив необходимых проверок в зависимости от типа документа
	
	МассивПроверок = Новый Массив();
	ИмяТаблицы     = ДокументЗакупки.Метаданные().ПолноеИмя();
	
	// ЗАКАЗ ПОСТАВЩИКУ
	Если ИмяТаблицы = "Документ.ЗаказПоставщику" Тогда
		
		СоглашениеУказано = ЗначениеЗаполнено(ДокументЗакупки.Соглашение);
		
		МассивПроверок.Добавить("ВременнаяТаблицаДокументЗакупки");
		МассивПроверок.Добавить("ВременнаяТаблицаТовары");
		
		ВозвратМногооборотнойТары = ДокументЗакупки.ВернутьМногооборотнуюТару;
		ТребуетсяЗалогЗаТару = ДокументЗакупки.ТребуетсяЗалогЗаТару;
		
		МассивПроверок.Добавить("ВременнаяТаблицаКурсыВалют");
			
		Если СоглашениеУказано Тогда
			
			МассивПроверок.Добавить("ВременнаяТаблицаСкладыКорректныеПоСоглашению");
			МассивПроверок.Добавить("СоответствиеШапкиУсловиямЗакупок");
			МассивПроверок.Добавить("СоответствиеТоваровУсловиямЗакупок");
			
		Иначе
			
			ЕстьОшибкиСоглашениеНеУказано = ИспользоватьСоглашенияСПоставщиками;
			
		КонецЕсли;
			
		Если ИспользоватьРучныеСкидки И ДокументЗакупки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПриемНаКомиссию Тогда
			МассивПроверок.Добавить("ДопустимостьРучныхСкидокНаценок");
		КонецЕсли;
		
		Если ПроверятьДиапазонЦен Тогда
			МассивПроверок.Добавить("ВхождениеЦенВДопустимыйДиапазон");
		КонецЕсли;
		
		Если ДокументЗакупки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПриемНаКомиссию
			И ДокументЗакупки.СуммаДокумента <> 0
			И ДокументЗакупки.ПорядокРасчетов <> Перечисления.ПорядокРасчетов.ПоНакладным Тогда
			
			МассивПроверок.Добавить("ВременнаяТаблицаЭтапыГрафикаОплаты");
			МассивПроверок.Добавить("КорректностьЭтаповГрафикаОплаты");
			МассивПроверок.Добавить("КорректностьКоличестваЭтаповГрафикаОплаты");
			
		КонецЕсли;
		
	// ПРИОБРЕТЕНИЕ ТОВАРОВ И УСЛУГ
		
	ИначеЕсли ИмяТаблицы = "Документ.ПриобретениеТоваровУслуг" Тогда
		
		СоглашениеУказано     = ЗначениеЗаполнено(ДокументЗакупки.Соглашение);
		ЕстьОшибкиСоглашениеНеУказано = ИспользоватьСоглашенияСПоставщиками
		                                И Не СоглашениеУказано
		                                И Не ДокументЗакупки.ПоступлениеПоЗаказам;
		
		МассивПроверок.Добавить("ВременнаяТаблицаДокументЗакупки");
		
		Если Не ДокументЗакупки.ПоступлениеПоЗаказам И СоглашениеУказано Тогда
			МассивПроверок.Добавить("ВременнаяТаблицаСкладыКорректныеПоСоглашению");
			МассивПроверок.Добавить("СоответствиеШапкиУсловиямЗакупок");
		КонецЕсли;
		
		МассивПроверок.Добавить("ВременнаяТаблицаТовары");
		
		ВозвратМногооборотнойТары = ДокументЗакупки.ВернутьМногооборотнуюТару;
		ТребуетсяЗалогЗаТару = ДокументЗакупки.ТребуетсяЗалогЗаТару;
		
		МассивПроверок.Добавить("ВременнаяТаблицаКурсыВалют");
		
		Если СоглашениеУказано Тогда
			МассивПроверок.Добавить("СоответствиеТоваровУсловиямЗакупок");
		КонецЕсли;
		
		Если СоглашениеУказано И ИспользоватьРучныеСкидки И ДокументЗакупки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПриемНаКомиссию Тогда
			МассивПроверок.Добавить("ДопустимостьРучныхСкидокНаценок");
		КонецЕсли;
			
		Если ПроверятьДиапазонЦен Тогда
			МассивПроверок.Добавить("ВхождениеЦенВДопустимыйДиапазон");
		КонецЕсли;
		
	КонецЕсли;
	
	// Сформируем текст запроса необходимых проверок в соответствие с массивом проверок
	
	ТекстЗапроса     = "";
	ПараметрыЗапроса = Новый Структура();

	ПараметрыЗапроса.Вставить("Дата",                      ДокументЗакупки.Дата);
	ПараметрыЗапроса.Вставить("Ссылка",                    ДокументЗакупки.Ссылка);
	ПараметрыЗапроса.Вставить("ВозвратМногооборотнойТары", ВозвратМногооборотнойТары);
	ПараметрыЗапроса.Вставить("ТребуетсяЗалогЗаТару",      ТребуетсяЗалогЗаТару);
	
	Для Каждого ТекЭлемент Из МассивПроверок Цикл
		
		Если ТекЭлемент = "ВременнаяТаблицаДокументЗакупки" Тогда
			
			СформироватьЗапросВременнаяТаблицаШапкаДокументаЗакупки(ТекстЗапроса, ПараметрыЗапроса, ДокументЗакупки);
			
		ИначеЕсли ТекЭлемент = "ВременнаяТаблицаТовары" Тогда
			
			СформироватьЗапросВременнаяТаблицаТоварыДокументаЗакупки(ТекстЗапроса, ПараметрыЗапроса, ДокументЗакупки, ИмяТаблицы);
			
		ИначеЕсли ТекЭлемент = "ВременнаяТаблицаСкладыКорректныеПоСоглашению" Тогда
			
			СформироватьЗапросВременнаяТаблицаСкладыКорректныеПоСоглашению(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "СоответствиеШапкиУсловиямЗакупок" Тогда
			
			СформироватьЗапросШапкаДокументаЗакупки(ТекстЗапроса, ИмяТаблицы);
			
		ИначеЕсли ТекЭлемент = "ВременнаяТаблицаЭтапыГрафикаОплаты" Тогда
			
			СформироватьЗапросВременнаяТаблицаЭтапыОплаты(ТекстЗапроса, ПараметрыЗапроса, ДокументЗакупки);
			
		ИначеЕсли ТекЭлемент = "ВременнаяТаблицаКурсыВалют" Тогда
			
			СформироватьЗапросВременнаяТаблицаКурсыВалют(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "СоответствиеТоваровУсловиямЗакупок" Тогда
			
			СформироватьЗапросТоварыДокументаЗакупки(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "ВхождениеЦенВДопустимыйДиапазон" Тогда
			
			СформироватьЗапросВхождениеЦенВДопустимыйДиапазон(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "ДопустимостьРучныхСкидокНаценок" Тогда
			
			СформироватьЗапросДопустимостьРучныхСкидокНаценок(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "КорректностьЭтаповГрафикаОплаты" Тогда
			
			СформироватьЗапросКорректностьЭтаповГрафикаОплаты(ТекстЗапроса);
			
		ИначеЕсли ТекЭлемент = "КорректностьКоличестваЭтаповГрафикаОплаты" Тогда
			
			СформироватьЗапросКорректностьКоличестваЭтаповГрафикаОплаты(ТекстЗапроса);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		УстановитьПривилегированныйРежим(Ложь);
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос(ТекстЗапроса);
	
	Для Каждого ПараметрЗапроса Из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ,ПараметрЗапроса.Значение);
	КонецЦикла;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	МассивОтклоненийШапки         = Новый Массив;
	ДеревоОтклоненийТовары        = Отчеты.ОтклоненияОтУсловийЗакупок.ИнициализироватьДеревоОтклонений();
	ДеревоОтклоненийЭтапыОплаты   = Отчеты.ОтклоненияОтУсловийЗакупок.ИнициализироватьДеревоОтклонений();
	
	Для ТекИндекс = 0 По МассивРезультатов.Количество()-1 Цикл
		
		Выборка = МассивРезультатов[ТекИндекс].Выбрать();
		
		Если МассивПроверок[ТекИндекс] = "СоответствиеШапкиУсловиямЗакупок" Тогда
			
			СообщитьОбОшибкахШапкаДокументаЗакупки(Выборка, ДокументЗакупки, Ложь, МассивОтклоненийШапки);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "СоответствиеТоваровУсловиямЗакупок" Тогда
			
			СообщитьОбОшибкахТоварыДокументаЗакупки(Выборка, ДокументЗакупки, Ложь, ДеревоОтклоненийТовары);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "ВхождениеЦенВДопустимыйДиапазон" Тогда
			
			СообщитьОбОшибкахВхождениеЦенВДопустимыйДиапазон(Выборка, ДокументЗакупки, Ложь, ИспользоватьРучныеСкидки, ДеревоОтклоненийТовары);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "ДопустимостьРучныхСкидокНаценок" Тогда
			
			СообщитьОбОшибкахДопустимостьРучныхСкидокНаценок(Выборка, ДокументЗакупки, Ложь, ДеревоОтклоненийТовары);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "КорректностьЭтаповГрафикаОплаты" Тогда
			
			СообщитьОбОшибкахКорректностьЭтаповГрафикаОплаты(Выборка, ДокументЗакупки, Ложь, ДеревоОтклоненийЭтапыОплаты);
			
		ИначеЕсли МассивПроверок[ТекИндекс] = "КорректностьКоличестваЭтаповГрафикаОплаты" Тогда
			
			СообщитьОбОшибкахКорректностьКоличестваЭтаповГрафикаОплаты(Выборка, ДокументЗакупки, Ложь, МассивОтклоненийШапки);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЕстьОтклонения = МассивОтклоненийШапки.Количество() > 0 
		ИЛИ ДеревоОтклоненийТовары.Строки.Количество() > 0 
		ИЛИ ДеревоОтклоненийЭтапыОплаты.Строки.Количество() > 0;
	
	Отчеты.ОтклоненияОтУсловийЗакупок.ВывестиЗаголовокОтчета(ТаблицаОтчета, ЕстьОтклонения, ЕстьОшибкиСоглашениеНеУказано);
	
	Если МассивОтклоненийШапки.Количество() > 0 Тогда
		Отчеты.ОтклоненияОтУсловийЗакупок.ВывестиОтклоненияВОбластьШапки(МассивОтклоненийШапки, ТаблицаОтчета);
	КонецЕсли;
	
	Если ДеревоОтклоненийТовары.Строки.Количество() > 0 Тогда
		Отчеты.ОтклоненияОтУсловийЗакупок.ВывестиОтклоненияТоварыДокументаЗакупки(ДокументЗакупки, ДеревоОтклоненийТовары, ТаблицаОтчета);
	КонецЕсли;
	
	Если ДеревоОтклоненийЭтапыОплаты.Строки.Количество() > 0 Тогда
		Отчеты.ОтклоненияОтУсловийЗакупок.ВывестиОтклоненияЭтапыОплатыДокументаЗакупки(ДокументЗакупки, ДеревоОтклоненийЭтапыОплаты, ТаблицаОтчета);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииРасчетаЦенВДокументахЗакупок

// Заполняет реквизит "Цена" в товарной табличной части.
//
// Параметры:
//   ТабличнаяЧасть - ДанныеФормыКоллекция, ТабличнаяЧасть - Товарная табличная часть документа
//   ВыделенныеСтроки - Массив, Неопределено - Массив выделенных строк
//   ПараметрыЗаполнения - Структура - Структура параметров заполнения
//    Обязательные поля:
//     "Дата" - Дата - Дата документа
//     "Валюта" - СправочникСсылка.Валюты - Валюта документа
//     "Соглашение" - СправочникСсылка.СоглашенияСПоставщиками - Соглашение документа
//    Необязательные поля:
//     "ВидЦеныПоставщика" - СправочникСсылка.ВидыЦенПоставщиков - Вид цены поставщика
//     "ПоляЗаполнения" - Строка - Строка с перечислением заполняемых полей в таблице. Значение по умолчанию: "Цена". Дополнительные поля: "ВидЦеныПоставщика, СтавкаНДС"
//     "КолонкиПоЗначению" - Структура - Структура для передачи в качестве параметра "КолонкиПоЗначению" в функцию "ОбщегоНазначенияУТ.ВыгрузитьТаблицуЗначений"
//     "ДругиеИменаКолонок" - Структура - Структура для передачи в качестве параметра "НовыеИменаКолонок" в функцию "ОбщегоНазначенияУТ.ВыгрузитьТаблицуЗначений"
//   СтруктураДействий - Структура - Стандартная структура действий со строками для вызова "ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ"
//   КэшированныеЗначения - Структура - Структура кэшированных значений
//
// Возвращаемое значение:
//   Булево - Истина, если цены успешно заполнены.
//
Функция ЗаполнитьЦены(ТабличнаяЧасть, ВыделенныеСтроки, ПараметрыЗаполнения, СтруктураДействий = Неопределено, КэшированныеЗначения = Неопределено) Экспорт
	
	ЦеныЗаполнены = Ложь;
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ВызватьИсключение НСтр("ru='Параметры заполнения не указаны'");
		Возврат ЦеныЗаполнены;
	КонецЕсли;
	
	// Получение структуры параметров по умолчанию
	Параметры = Новый Структура(
		"ВидЦеныПоставщика, ПоляЗаполнения, КолонкиПоЗначению, ДругиеИменаКолонок",
		Справочники.ВидыЦенПоставщиков.ПустаяСсылка(), "Цена", Новый Структура, Новый Структура);
	ОбщегоНазначенияУТКлиентСервер.ДополнитьСтруктуру(Параметры, ПараметрыЗаполнения, Истина);
	
	// Проверки входящих данных
	Если Не Параметры.Свойство("Соглашение") И Не Параметры.Свойство("ВидЦеныПоставщика") Тогда
		ВызватьИсключение НСтр("ru='В параметрах заполнения должны быть указаны ""Соглашение"" или ""ВидЦеныПоставщика""'");
		Возврат ЦеныЗаполнены;
	ИначеЕсли Не Параметры.Свойство("Дата") Или Не Параметры.Свойство("Валюта") Тогда
		ВызватьИсключение НСтр("ru='В параметрах заполнения должны быть указаны ""Валюта"" и ""Дата""'");
		Возврат ЦеныЗаполнены;
	КонецЕсли;
	
	ВидЦеныПоставщика = Справочники.ВидыЦенПоставщиков.ПустаяСсылка();
	Если Параметры.Свойство("Соглашение") И Не ПараметрыЗаполнения.Свойство("ВидЦеныПоставщика") Тогда
		ВидЦеныПоставщика = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Соглашение, "ВидЦеныПоставщика");
	Иначе
		ВидЦеныПоставщика = Параметры.ВидЦеныПоставщика;
	КонецЕсли;
	
	Если Параметры.Свойство("Соглашение") Тогда
		Соглашение = Параметры.Соглашение;
	Иначе
		Соглашение = Справочники.СоглашенияСПоставщиками.ПустаяСсылка();
	КонецЕсли;
	
	// Получение выгрузки по табличной части
	Таблица = ОбщегоНазначенияУТ.ВыгрузитьТаблицуЗначений(
		ТабличнаяЧасть,
		ВыделенныеСтроки,
		"НомерСтроки, Номенклатура, Характеристика, Упаковка",
		Параметры.КолонкиПоЗначению,
		Параметры.ДругиеИменаКолонок);
	
	// Получение запроса
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", ?(ЗначениеЗаполнено(Параметры.Дата), Параметры.Дата, ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("Валюта", Параметры.Валюта);
	Запрос.УстановитьПараметр("Соглашение", Соглашение);
	Запрос.УстановитьПараметр("ВидЦеныПоставщика", ВидЦеныПоставщика);
	Запрос.УстановитьПараметр("Таблица", Таблица);
	Запрос.УстановитьПараметр("ЭтоВыкупТары", ?(Параметры.Свойство("ЭтоВыкупТары"), Параметры.ЭтоВыкупТары, Ложь));
	
	Запрос.Текст = ПолучитьТекстЗапросаВременнойТаблицыТоваров("втТаблицаТовары")
	             + ПолучитьТекстЗапросаВременнойТаблицыЦен("втТаблицаЦены", "втТаблицаТовары")
	             + "ВЫБРАТЬ
	               |	втТаблицаЦены.НомерСтроки КАК НомерСтроки,
	               |	втТаблицаЦены.ВидЦеныПоставщика КАК ВидЦеныПоставщика,
	               |	втТаблицаЦены.СтавкаНДС КАК СтавкаНДС,
	               |	втТаблицаЦены.Цена
	               |ИЗ
	               |	втТаблицаЦены КАК втТаблицаЦены";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ЦеныЗаполнены;
	КонецЕсли;
	
	СтруктураЗаполнения = Новый Структура(Параметры.ПоляЗаполнения);
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СтруктураЗаполнения, Выборка);
		СтрокаТЧ = ТабличнаяЧасть[Выборка.НомерСтроки - 1];
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтруктураЗаполнения);
		Если СтруктураДействий <> Неопределено Тогда
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТЧ, СтруктураДействий, КэшированныеЗначения);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Возвращает цену по отбору
//
// Параметры:
//		ПараметрыОтбора - Структура - Структура с обязательными полями
//			"Дата" - Дата - Дата получения цены
// 			"Номенклатура" - СправочникСсылка.Номенклатура
// 			"Характеристика" - СправочникСсылка.ХарактеристикиНоменклатуры
// 			"Упаковка" - СправочникСсылка.УпаковкиЕдиницыИзмерения
// 			"ВидЦены" - СправочникСсылка.ВидыЦен
// 			"Валюта" - СправочникСсылка.Валюты
//
// Возвращаемое значение:
// 		Число - Цена
//
Функция ПолучитьЦенуПоОтбору(ПараметрыОтбора) Экспорт
	
	Запрос = Новый Запрос;
	Для Каждого Параметр Из ПараметрыОтбора Цикл
		Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ЭтоВыкупТары", ?(ПараметрыОтбора.Свойство("ЭтоВыкупТары"), ПараметрыОтбора.ЭтоВыкупТары, Ложь));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	ВЫРАЗИТЬ(&Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	ВЫРАЗИТЬ(&Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения) КАК Упаковка
	|ПОМЕСТИТЬ втТаблицаТовары
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура
	|;" + ПолучитьТекстЗапросаВременнойТаблицыЦен("втТаблицаЦен", "втТаблицаТовары") + "
	|ВЫБРАТЬ
	|	втТаблицаЦен.Цена КАК Цена
	|ИЗ
	|	втТаблицаЦен КАК втТаблицаЦен
	|";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат 0;
	КонецЕсли;
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Цена;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСоСкидкамиВДокументахЗакупки

 // Осуществляет заполнение условий цен и цен в тч Товары по виду цен по выделенным строкам
//
// Параметры:
// 		Объект - ДокументОбъект, для которого необходимо отменить ручные скидки
// 		ИмяТабличнойЧасти  - Строка - имя табличной части объекта, в которой необходимо отменить ручные скидки
// 		ОчищатьСуммыВзаиморасчетов - Булево - Истина, если в табличной части присутствует сумма взаиморасчетов
//
Процедура ОтменитьРучныеСкидки(Объект, ИмяТабличнойЧасти, ОчищатьСуммыВзаиморасчетов = Ложь) Экспорт
	
	СкидкиИзменены = Ложь;
	
	Для Каждого СтрокаТЧ Из Объект[ИмяТабличнойЧасти] Цикл
		
		Если СтрокаТЧ.СуммаРучнойСкидки <> 0 Или СтрокаТЧ.ПроцентРучнойСкидки <> 0 Тогда
			
			СтрокаТЧ.СуммаРучнойСкидки = 0;
			СтрокаТЧ.ПроцентРучнойСкидки = 0;
			Ценообразование.ПересчитатьСуммыВСтроке(СтрокаТЧ, Ложь, Ложь, Истина, Объект.ЦенаВключаетНДС);
			СкидкиИзменены = Истина;
			Если ОчищатьСуммыВзаиморасчетов Тогда
				СтрокаТЧ.СуммаВзаиморасчетов = 0;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если СкидкиИзменены Тогда
		Объект.СуммаДокумента = ЦенообразованиеКлиентСервер.ПолучитьСуммуДокумента(Объект[ИмяТабличнойЧасти], Объект.ЦенаВключаетНДС);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСНоменклатуройПоставщикаВДокументахЗакупки

// Помещает таблицу товаров во временное хранилище
// 
// Параметры:
//  Товары - ДанныеФормыКоллекция - таблица товаров, которую необходимо поместить во временное хранилище.
//
// Возвращаемое значение:
//  Строка - Адрес таблицы товаров во временном хранилище.
//
Функция ПоместитьТоварыВоВременноеХранилище(Товары) Экспорт
	
	МассивСтрок = Новый Массив();
	
	Для Каждого ТекСтрока Из Товары Цикл
		Если ЗначениеЗаполнено(ТекСтрока.НоменклатураПоставщика) И Не ЗначениеЗаполнено(ТекСтрока.Номенклатура) Тогда
			МассивСтрок.Добавить(ТекСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивСтрок.Количество() = 0 Тогда
		АдресТоваровВоВременномХранилище = Неопределено;
	Иначе
		НоменклатураПоставщика = Товары.Выгрузить(МассивСтрок, "НомерСтроки,НоменклатураПоставщика");
		АдресТоваровВоВременномХранилище = ПоместитьВоВременноеХранилище(НоменклатураПоставщика);
	КонецЕсли;
	
	Возврат АдресТоваровВоВременномХранилище;
	
КонецФункции

// Проверяет корректность заполнения этапов графика оплаты в документе закупки
//
// Параметры:
// ЭтапыГрафикаОплаты               - Табличная часть документа
// СуммаОплатыПоДокументу           - Число - сумма документа, в котором необходимо осуществить проверку
// СуммаЗалогаПоДокументу           - Число - сумма залога по документу, в котором необходимо осуществить проверку
// УчитыватьВариантОплаты           - Булево - признак необходимости проверки корректности вариантов оплаты
// Отказ                            - Булево - Флаг отказа от записи документа
// УпрощенныйРежимДиалога           - Булево - флаг, указывающий на использование простого диалога ввода этапов оплаты
//
Процедура ПроверитьКорректностьЭтаповГрафикаОплатыПоТаблицеЗначений(Знач ЭтапыГрафикаОплаты,
	                                               СуммаОплатыПоДокументу,
	                                               СуммаЗалогаПоДокументу,
	                                               УчитыватьВариантОплаты,
	                                               Отказ,
	                                               УпрощенныйРежимДиалога,
	                                               СтруктураПараметров) Экспорт
	
	Если ЗначениеЗаполнено(СтруктураПараметров.Дата) Тогда
		ДатаДокумента = НачалоДня(СтруктураПараметров.Дата);
	КонецЕсли;
	
	СуммаОплатыВсего = Окр(СуммаОплатыПоДокументу, 2);
	СуммаЗалогаВсего = Окр(СуммаЗалогаПоДокументу, 2);
	
	Если ЭтапыГрафикаОплаты.Количество() = 0 Тогда
		
		Если СуммаОплатыВсего <> 0 Или СуммаЗалогаВсего <> 0 Тогда
			
			Если УпрощенныйРежимДиалога Тогда
				ТекстОшибки = НСтр("ru='Необходимо заполнить хотя бы один из этапов'");
				ПолеОшибки  = "ПроцентПлатежаКредит";
			Иначе
				ТекстОшибки = НСтр("ru='Необходимо заполнить этапы графика оплаты'");
				ПолеОшибки  = "ЭтапыГрафикаОплаты";
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , ПолеОшибки, , Отказ);
			
		КонецЕсли;
		
	Иначе
		
		ИтогПроцентПлатежа = 0;
		ИтогПроцентЗалогаЗаТару = 0;
		ИтогСуммаПлатежа = 0;
		ИтогСуммаЗалогаЗаТару = 0;
		Для ТекИндекс = 0 По ЭтапыГрафикаОплаты.Количество()-1 Цикл
			
			ДанныеЭтапа = Новый Структура("НомерСтроки, ВариантОплаты, ДатаПлатежа, ПроцентПлатежа, ПроцентЗалогаЗаТару, СуммаПлатежа, СуммаЗалогаЗаТару",
				0, Неопределено, '00010101', 0, 0, 0, 0);
			ЗаполнитьЗначенияСвойств(ДанныеЭтапа, ЭтапыГрафикаОплаты[ТекИндекс]);
			ИтогПроцентПлатежа = ИтогПроцентПлатежа + ДанныеЭтапа.ПроцентПлатежа;
			ИтогПроцентЗалогаЗаТару = ИтогПроцентЗалогаЗаТару + ДанныеЭтапа.ПроцентЗалогаЗаТару;
			ИтогСуммаПлатежа = ИтогСуммаПлатежа + ДанныеЭтапа.СуммаПлатежа;
			ИтогСуммаЗалогаЗаТару = ИтогСуммаЗалогаЗаТару + ДанныеЭтапа.СуммаЗалогаЗаТару;
			
			АдресОшибки = НСтр("ru='в строке %НомерСтроки% списка ""Этапы графика оплаты""'");
			АдресОшибки = СтрЗаменить(АдресОшибки, "%НомерСтроки%", ДанныеЭтапа.НомерСтроки);
			
			// Все реквизиты этапа оплаты должны быть заполнены
			
			Если УчитыватьВариантОплаты И Не ЗначениеЗаполнено(ДанныеЭтапа.ВариантОплаты) Тогда
				
				ТекстОшибки = НСтр("ru='Не заполнена колонка ""Вариант оплаты""'");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки + " " + АдресОшибки,
					,
					"Объект." + ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "ВариантОплаты"),
					,
					Отказ);
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеЭтапа.ДатаПлатежа) Тогда
				
				Если УпрощенныйРежимДиалога Тогда
					Если ДанныеЭтапа.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.КредитПослеПоступления Тогда
						ТекстОшибки = НСтр("ru='Не заполнена ""Дата платежа"" по кредиту'");
						ПолеОшибки  = "ДатаКредит";
					Иначе
						ТекстОшибки = НСтр("ru='Не заполнена ""Дата платежа"" по предоплате'");
						ПолеОшибки  = "ДатаПредоплата";
					КонецЕсли;
				Иначе
					ТекстОшибки = НСтр("ru='Не заполнена колонка ""Дата платежа""'");
					ПолеОшибки  = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "ДатаПлатежа");
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , ПолеОшибки, , Отказ);
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеЭтапа.ПроцентПлатежа)
				И Не ЗначениеЗаполнено(ДанныеЭтапа.ПроцентЗалогаЗаТару) Тогда
				
				ТекстОшибки = НСтр("ru='Не заполнена колонка ""Процент платежа""'");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки + " " + АдресОшибки,
					,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "ПроцентПлатежа"),
					,
					Отказ);
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеЭтапа.СуммаПлатежа) И
				ЗначениеЗаполнено(ДанныеЭтапа.ПроцентПлатежа) И 
				СуммаОплатыВсего <> 0 Тогда
				
				ТекстОшибки = НСтр("ru='Не заполнена колонка ""Сумма платежа""'");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки + " " + АдресОшибки,
					,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "СуммаПлатежа"),
					,
					Отказ);
				
				КонецЕсли;
				
			Если Не ЗначениеЗаполнено(ДанныеЭтапа.СуммаЗалогаЗаТару) И
				ЗначениеЗаполнено(ДанныеЭтапа.ПроцентЗалогаЗаТару) И 
				СуммаЗалогаВсего > 0 Тогда
				
				ТекстОшибки = НСтр("ru='Не заполнена колонка ""Сумма залога за тару""'");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки + " " + АдресОшибки,
					,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "СуммаЗалогаЗаТару"),
					,
					Отказ);
				
			КонецЕсли;
			
			// Дата платежа в тч ЭтапыГрафикаОплаты должна быть не меньше даты документа
			Если ЗначениеЗаполнено(ДанныеЭтапа.ДатаПлатежа) И
				ЗначениеЗаполнено(СтруктураПараметров.Дата) И
				ДанныеЭтапа.ДатаПлатежа < ДатаДокумента Тогда
				
				Если УпрощенныйРежимДиалога Тогда
					Если ДанныеЭтапа.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.КредитПослеПоступления Тогда
						ПолеОшибки = "ДатаКредит";
					Иначе
						ПолеОшибки = "ДатаПредоплата";
					КонецЕсли;
				Иначе
					ПолеОшибки = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "ДатаПлатежа");
				КонецЕсли;
				
				ТекстОшибки = НСтр("ru='Дата платежа должна быть не меньше даты документа %ДатаДокумента%'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ДатаДокумента%", Формат(СтруктураПараметров.Дата, "ДЛФ=DD"));
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , ПолеОшибки, , Отказ);
				
			КонецЕсли;
			
			Если УчитыватьВариантОплаты Тогда
				
				// Дата платежа по авансовому этапу должна быть не больше даты поступления в шапке
				Если ЗначениеЗаполнено(ДанныеЭтапа.ДатаПлатежа) И
					ЗначениеЗаполнено(СтруктураПараметров.ЖелаемаяДатаПоступления) И
					(ДанныеЭтапа.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.АвансДоПодтверждения Или
					ДанныеЭтапа.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления) И
					ДанныеЭтапа.ДатаПлатежа > СтруктураПараметров.ЖелаемаяДатаПоступления Тогда
					
					ТекстОшибки = НСтр("ru='Дата платежа по авансовому этапу должна быть не больше желаемой даты поступления %ДатаПоступления%'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ДатаПоступления%", Формат(СтруктураПараметров.ЖелаемаяДатаПоступления, "ДЛФ=DD")); 
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстОшибки,
						,
						?(УпрощенныйРежимДиалога, "ДатаПредоплата", ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Объект.ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "ДатаПлатежа")),
						,
						Отказ);
					
				// Дата платежа по кредитному этапу должна быть не меньше даты поступления в шапке
				ИначеЕсли ЗначениеЗаполнено(ДанныеЭтапа.ДатаПлатежа) И
					ЗначениеЗаполнено(СтруктураПараметров.ЖелаемаяДатаПоступления) И
					ДанныеЭтапа.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.КредитПослеПоступления И
					ДанныеЭтапа.ДатаПлатежа < СтруктураПараметров.ЖелаемаяДатаПоступления Тогда
					
					ТекстОшибки = НСтр("ru='Дата платежа по кредитному этапу должна быть не меньше желаемой даты поступления %ДатаПоступления%'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ДатаПоступления%", Формат(СтруктураПараметров.ЖелаемаяДатаПоступления, "ДЛФ=DD")); 
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстОшибки,
						,
						?(УпрощенныйРежимДиалога, "ДатаКредит", ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ДанныеЭтапа.НомерСтроки, "ДатаПлатежа")),
						,
						Отказ);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Процент платежей в в тч ЭтапыГрафикаОплаты должен равняться 100%
		
		Если ЭтапыГрафикаОплаты.Количество() > 0 Тогда
			
			Если ИтогПроцентПлатежа <> 100 И СуммаОплатыВсего <> 0 Тогда
				
				ТекстОшибки = НСтр("ru='Процент платежей по всем этапам ""%ПроцентПоЭтапам%%"" должен равняться ""100%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ПроцентПоЭтапам%", ИтогПроцентПлатежа);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					,
					?(УпрощенныйРежимДиалога, "ПроцентПлатежаКредит", "ЭтапыГрафикаОплаты[0].ПроцентПлатежа"),
					,
					Отказ);
				
			КонецЕсли;
				
			Если ИтогПроцентЗалогаЗаТару <> 100 И СуммаЗалогаВсего > 0 Тогда
				
				ТекстОшибки = НСтр("ru='Процент залога за тару по всем этапам ""%ПроцентПоЭтапам%%"" должен равняться ""100%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ПроцентПоЭтапам%", ИтогПроцентЗалогаЗаТару);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					,
					?(УпрощенныйРежимДиалога, "ПроцентЗалогаЗаТаруКредит", "ЭтапыГрафикаОплаты[0].ПроцентЗалогаЗаТару"),
					,
					Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Сумма платежей в в тч ЭтапыГрафикаОплаты должна равняться сумме заказа с учетом отмененных позиций
		
		Если ЭтапыГрафикаОплаты.Количество() > 0 Тогда
			
			Если СуммаОплатыВсего <> 0 И ИтогСуммаПлатежа <> СуммаОплатыВсего Тогда
				
				ТекстОшибки = НСтр("ru='Сумма платежей по всем этапам ""%СуммаПоЭтапам% %Валюта%"" не должна отличаться от суммы документа ""%СуммаВсего% %Валюта%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СуммаПоЭтапам%", ИтогСуммаПлатежа);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СуммаВсего%",    СуммаОплатыВсего - СуммаЗалогаВсего);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Валюта%",        СтруктураПараметров.Валюта);
				
				Если УпрощенныйРежимДиалога Тогда
					Если ЭтапыГрафикаОплаты[0].ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления Тогда
						ПолеОшибки = "СуммаПлатежаПредоплата";
					Иначе
						ПолеОшибки = "СуммаПлатежаКредит";
					КонецЕсли;
				Иначе
					ПолеОшибки = "ЭтапыГрафикаОплаты[0].СуммаПлатежа";
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , ПолеОшибки, , Отказ);
				
			КонецЕсли;
			
			Если СуммаЗалогаВсего <> 0 И ИтогСуммаЗалогаЗаТару <> СуммаЗалогаВсего Тогда
				
				ТекстОшибки = НСтр("ru='Сумма залога за тару по всем этапам ""%СуммаПоЭтапам% %Валюта%"" не должна отличаться от суммы залога по документу ""%СуммаВсего% %Валюта%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СуммаПоЭтапам%", ИтогСуммаЗалогаЗаТару);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СуммаВсего%",    СуммаЗалогаВсего);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Валюта%",        СтруктураПараметров.Валюта);
				
				Если УпрощенныйРежимДиалога Тогда
					Если ЭтапыГрафикаОплаты[0].ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления Тогда
						ПолеОшибки = "СуммаЗалогаЗаТаруПредоплата";
					Иначе
						ПолеОшибки = "СуммаЗалогаЗаТаруКредит";
					КонецЕсли;
				Иначе
					ПолеОшибки = "ЭтапыГрафикаОплаты[0].СуммаЗалогаЗаТару";
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , ПолеОшибки, , Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
		КоличествоЭтапов = ЭтапыГрафикаОплаты.Количество();
		
		Для ВнешнийСчетчик = 2 По КоличествоЭтапов Цикл
			
			ИндексПредыдущегоЭтапа = ВнешнийСчетчик - 2;
			ИндексТекущегоЭтапа    = ВнешнийСчетчик - 1;
			ПредыдущееЗначениеДатыПлатежа    = ЭтапыГрафикаОплаты[ИндексПредыдущегоЭтапа].ДатаПлатежа;
			ТекущееЗначениеДатыПлатежа       = ЭтапыГрафикаОплаты[ИндексТекущегоЭтапа].ДатаПлатежа;
			
			Если УчитыватьВариантОплаты Тогда
				
				ПредыдущееЗначениеВариантаОплаты = ЭтапыГрафикаОплаты[ИндексПредыдущегоЭтапа].ВариантОплаты;
				ТекущееЗначениеВариантаОплаты    = ЭтапыГрафикаОплаты[ИндексТекущегоЭтапа].ВариантОплаты;
				
				// В табличной части Этапы не должно быть строк со значением АвансДоПодтверждения
				// в поле ВариантОплаты, идущих после строк со значением ПредоплатаДоПоступления
				// КредитПослеПоступления
				Если (ТекущееЗначениеВариантаОплаты = Перечисления.ВариантыОплатыПоставщику.АвансДоПодтверждения И 
					(ПредыдущееЗначениеВариантаОплаты = Перечисления.ВариантыОплатыПоставщику.КредитПослеПоступления ИЛИ
					ПредыдущееЗначениеВариантаОплаты = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления)) ИЛИ
					// В табличной части Этапы не должно быть строк со значением ПредоплатаДоПоступления
					// в поле ВариантОплаты, идущих после строк со значением КредитПослеПоступления
					(ТекущееЗначениеВариантаОплаты = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления И 
					ПредыдущееЗначениеВариантаОплаты = Перечисления.ВариантыОплатыПоставщику.КредитПослеПоступления) Тогда
					
					ТекстОшибки = НСтр("ru='Вариант оплаты ""%ТекущееЗначениеВариантаОплаты%"" в строке %ИндексТекущегоЭтапа%
					|не может идти после варианта оплаты ""%ПредыдущееЗначениеВариантаОплаты%"" в строке %ИндексПредыдущегоЭтапа%'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ПредыдущееЗначениеВариантаОплаты%", ПредыдущееЗначениеВариантаОплаты);
					ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ТекущееЗначениеВариантаОплаты%",    ТекущееЗначениеВариантаОплаты);
					ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ИндексТекущегоЭтапа%",              ИндексТекущегоЭтапа + 1);
					ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ИндексПредыдущегоЭтапа%",           ИндексПредыдущегоЭтапа + 1);
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстОшибки,
						,
						ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ЭтапыГрафикаОплаты[ИндексТекущегоЭтапа].НомерСтроки, "ВариантОплаты"),
						,
						Отказ);
					
				КонецЕсли;
				
			КонецЕсли;
			
			// Значение поля ДатаПлатежа табличной части ЭтапыГрафикаОплаты должно идти по возрастанию
			Если ТекущееЗначениеДатыПлатежа < ПредыдущееЗначениеДатыПлатежа И
				ЗначениеЗаполнено(ТекущееЗначениеДатыПлатежа) И
				ЗначениеЗаполнено(ПредыдущееЗначениеДатыПлатежа) Тогда
				
				Если УпрощенныйРежимДиалога Тогда
					ТекстОшибки = НСтр("ru='Дата платежа по кредиту не должна быть меньше, чем дата платежа по предоплате'");
					ПолеОшибки  = "ДатаПредоплата";
				Иначе
					ТекстОшибки = НСтр("ru='Дата платежа в строке %ИндексТекущегоЭтапа%
					|списка ""Этапы оплаты"" должна быть не меньше, чем в строке %ИндексПредыдущегоЭтапа%'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ИндексТекущегоЭтапа%",    ИндексТекущегоЭтапа + 1);
					ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ИндексПредыдущегоЭтапа%", ИндексПредыдущегоЭтапа + 1);
					ПолеОшибки  = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ЭтапыГрафикаОплаты", ЭтапыГрафикаОплаты[ИндексТекущегоЭтапа].НомерСтроки, "ДатаПлатежа");
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , ПолеОшибки, , Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииПолученияЗначенийРеквизитовПоУмолчанию

// Возвращает структуру по ответственному лицу указанного склада
// В случае если склад является группой, возвращает значения,
// 		использовавшиеся в последнем документе указанного менеджера с указанным складом
//
// Параметры:
// 		Склад - СправочникСсылка.Склады
//
// Возвращаемое значение:
// 		Стуктура - Структура с полями: "Ответственный" и "ОтветственныйДолжность"
//
Функция ПолучитьОтветственногоПоСкладу(Склад, Менеджер) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Склад) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если НЕ Справочники.Склады.ЭтоГруппа(Склад) Тогда
		СтруктураОтветственного = ЗначениеНастроекПовтИсп.ПолучитьОтветственногоПоСкладу(Склад);
		Если СтруктураОтветственного <> Неопределено Тогда
			Возврат СтруктураОтветственного;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПриобретениеТоваровУслуг.Принял КАК Ответственный,
	|	ПриобретениеТоваровУслуг.ПринялДолжность КАК ОтветственныйДолжность,
	|	ПриобретениеТоваровУслуг.МоментВремени КАК МоментВремени
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
	|ГДЕ
	|	ПриобретениеТоваровУслуг.Склад = &Склад
	|	И ПриобретениеТоваровУслуг.Менеджер = &Менеджер
	|	И ПриобретениеТоваровУслуг.Проведен = ИСТИНА
	|	И (ПриобретениеТоваровУслуг.Принял <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|			ИЛИ ПриобретениеТоваровУслуг.ПринялДолжность <> """")
	|
	|УПОРЯДОЧИТЬ ПО
	|	МоментВремени УБЫВ");
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Менеджер", Менеджер);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Новый Структура("Ответственный, ОтветственныйДолжность", Выборка.Ответственный, Выборка.ОтветственныйДолжность);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Выполняет поиск действующего договора с контрагентом по заданным реквизитам.
// Если найден один действующий договор, возвращает ссылку на него, в противном случае - пустую ссылку.
//
// Параметры:
//  Объект — ДокументСсылка, ДокументОбъект, Структура — документ или произвольная структура, для которой нужно получить договор.
//		В объекте должны быть определены свойства:
// 		* Партнер 		— СправочникСсылка.Партнеры 				— партнер, с которым заключен договор.
//		* Договор 		— СправочникСсылка.ДоговорыКонтрагентов 	— договор, который сейчас установлен для объекта. Может быть пустым.
//  	* Контрагент 	— СправочникСсылка.Контрагенты 				— контрагент, с которым заключен договор.
//  	* Организация 	— СправочникСсылка.Организации 				— организация предприятия, от имени которой заключен договор.
//  ХозяйственныеОперации — ПеречислениеСсылка.ХозяйственныеОперации, Массив — одна или несколько хозяйственных операций (тип договора).
//  ДополнительныеПараметрыОтбораДоговоров — Структура — дополнительные параметры для отбора договоров (см. функцию ДополнительныеПараметрыОтбораДоговоров)
//
// Возвращаемое значение:
//  СправочникСсылка.ДоговорыКонтрагентов - договор по умолчанию.
//
Функция ПолучитьДоговорПоУмолчанию(
	Объект,
	ХозяйственныеОперации,
	ДополнительныеПараметрыОтбораДоговоров = Неопределено) Экспорт
	
	Если ДополнительныеПараметрыОтбораДоговоров = Неопределено Тогда
		ДополнительныеПараметрыОтбораДоговоров = ДополнительныеПараметрыОтбораДоговоров();
	КонецЕсли;
	
	СписокПартнеров = Новый СписокЗначений;
	ПартнерыИКонтрагенты.ЗаполнитьСписокПартнераСРодителями(Объект.Партнер, СписокПартнеров);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|
	|ГДЕ
	|	(НЕ ДоговорыКонтрагентов.ПометкаУдаления)
	|	И ДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
	|	И ДоговорыКонтрагентов.Партнер В (&СписокПартнеров)
	|	И ДоговорыКонтрагентов.Контрагент = &Контрагент
	|	И ДоговорыКонтрагентов.Организация = &Организация
	|	И ((НЕ &ОтборХозяйственныеОперации)
	|			ИЛИ ДоговорыКонтрагентов.ХозяйственнаяОперация В (&ХозяйственныеОперации))
	|	И ((НЕ &ОтборВалютаВзаиморасчетов)
	|			ИЛИ ДоговорыКонтрагентов.ВалютаВзаиморасчетов = &ВалютаВзаиморасчетов)
	|	И (ДоговорыКонтрагентов.УчетАгентскогоНДС = &УчетАгентскогоНДС)
	|	И ДоговорыКонтрагентов.Ссылка = &ТекущийДоговор
	|	И ((НЕ &ОтборНаправлениеДеятельности)
	|			ИЛИ ДоговорыКонтрагентов.НаправлениеДеятельности = &НаправлениеДеятельности)
	|	И ((НЕ &ОтборВариантОформленияЗакупок)
	|			ИЛИ ДоговорыКонтрагентов.ВариантОформленияЗакупок В (&ВариантОформленияЗакупок));
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|
	|ГДЕ
	|	(НЕ ДоговорыКонтрагентов.ПометкаУдаления)
	|	И ДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
	|	И ДоговорыКонтрагентов.Партнер В (&СписокПартнеров)
	|	И ДоговорыКонтрагентов.Контрагент = &Контрагент
	|	И ДоговорыКонтрагентов.Организация = &Организация
	|	И ((НЕ &ОтборХозяйственныеОперации)
	|			ИЛИ ДоговорыКонтрагентов.ХозяйственнаяОперация В (&ХозяйственныеОперации))
	|	И ((НЕ &ОтборВалютаВзаиморасчетов)
	|			ИЛИ ДоговорыКонтрагентов.ВалютаВзаиморасчетов = &ВалютаВзаиморасчетов)
	|	И (ДоговорыКонтрагентов.УчетАгентскогоНДС = &УчетАгентскогоНДС)
	|	И ((НЕ &ОтборНаправлениеДеятельности)
	|			ИЛИ ДоговорыКонтрагентов.НаправлениеДеятельности = &НаправлениеДеятельности)
	|	И ((НЕ &ОтборВариантОформленияЗакупок)
	|			ИЛИ ДоговорыКонтрагентов.ВариантОформленияЗакупок В (&ВариантОформленияЗакупок))
	|");
	Запрос.УстановитьПараметр("ТекущийДоговор",					Объект.Договор);
	Запрос.УстановитьПараметр("СписокПартнеров",				СписокПартнеров);
	Запрос.УстановитьПараметр("Контрагент",						Объект.Контрагент);
	Запрос.УстановитьПараметр("Организация",					Объект.Организация);
	Запрос.УстановитьПараметр("ОтборХозяйственныеОперации",		ЗначениеЗаполнено(ХозяйственныеОперации));
	Запрос.УстановитьПараметр("ХозяйственныеОперации",			ХозяйственныеОперации);
	Запрос.УстановитьПараметр("ОтборВалютаВзаиморасчетов",		ЗначениеЗаполнено(ДополнительныеПараметрыОтбораДоговоров.ВалютаВзаиморасчетов));
	Запрос.УстановитьПараметр("ВалютаВзаиморасчетов",			ДополнительныеПараметрыОтбораДоговоров.ВалютаВзаиморасчетов);
	Запрос.УстановитьПараметр("УчетАгентскогоНДС",				ДополнительныеПараметрыОтбораДоговоров.Налогообложение = Перечисления.ТипыНалогообложенияНДС.НалоговыйАгентПоНДС);
	Запрос.УстановитьПараметр("ОтборНаправлениеДеятельности", 	ДополнительныеПараметрыОтбораДоговоров.НаправлениеДеятельности <> Неопределено
		И ПолучитьФункциональнуюОпцию("ИспользоватьУчетЗатратПоНаправлениямДеятельности"));
	Запрос.УстановитьПараметр("НаправлениеДеятельности",		ДополнительныеПараметрыОтбораДоговоров.НаправлениеДеятельности);
	Запрос.УстановитьПараметр("ОтборВариантОформленияЗакупок",	ДополнительныеПараметрыОтбораДоговоров.ВариантОформленияЗакупок <> Неопределено);
	Запрос.УстановитьПараметр("ВариантОформленияЗакупок",		ДополнительныеПараметрыОтбораДоговоров.ВариантОформленияЗакупок);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Если Не МассивРезультатов[0].Пустой() Тогда
		
		Выборка = МассивРезультатов[0].Выбрать();
		Выборка.Следующий();
		
		ДоговорПоУмолчанию = Выборка.Ссылка;
		
	Иначе
		Выборка = МассивРезультатов[1].Выбрать();
	
		Если Не Выборка.Следующий() Тогда
			ДоговорПоУмолчанию = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		ИначеЕсли Выборка.Количество() = 1 Тогда
			ДоговорПоУмолчанию = Выборка.Ссылка;
		Иначе
			ДоговорПоУмолчанию = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

// Устанавливает видимость реквизита договора в зависимости от данных по соглашению
//
// Параметры:
// 		Соглашение - СправочникСсылка.СоглашенияСПоставщиками
// 		ДоступностьЭлемента - Булево
// 		Договор - СправочникСсылка.ДоговорыКонтрагентов
//
Процедура УстановитьДоступностьДоговора(Объект, ДоступностьЭлемента, ВидимостьЭлемента, Договор = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИспользоватьПартнеровКакКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов");
	ИспользуютсяДоговорыКонтрагентов     = ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСПоставщиками");
	
	ЗаполненыВлияющиеПоля = 
		ЗначениеЗаполнено(Объект.Партнер)
		И (ЗначениеЗаполнено(Объект.Контрагент) Или ИспользоватьПартнеровКакКонтрагентов)
		И ЗначениеЗаполнено(Объект.Организация);
		
	Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Объект, "Соглашение")
		И ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСПоставщиками") Тогда
		
		Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
			ИспользуютсяДоговорыКонтрагентов =
				ИспользуютсяДоговорыКонтрагентов
				И ОбщегоНазначенияУТ.ЗначениеРеквизитаОбъектаТипаБулево(Объект.Соглашение, "ИспользуютсяДоговорыКонтрагентов");
		КонецЕсли;
		
	КонецЕсли;
	
	ДоступностьЭлемента = ЗаполненыВлияющиеПоля И ИспользуютсяДоговорыКонтрагентов;
	
	ВидимостьЭлемента = ИспользуютсяДоговорыКонтрагентов;
	
	Если НЕ ДоступностьЭлемента И ЗначениеЗаполнено(Объект.Договор) Тогда
		Объект.Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру параметров представления счета-фактуры в документе.
//
// Параметры:
//  Основание - ДокументСсылка - Документ, на основании которого вводится счет-фактура;
//  Организация - СправочникСсылка.Организации - Организация, на имя которой оформляется счет-фактура;
//	НеТребуется - Булево - Истина - для документа не требуется вводить счет-фактуру.
//
// Возвращаемое значение:
//  Структура - структура с полями:
//   * ТекстСчетФактура - Строка - Представление счета-фактуры;
//   * ТекстСписок - Строка - Представление команды открытия списка счетов-фактур;
//   * ГиперссылкаСчетФактура - Булево - Признак гиперссылки представления счета-фактуры;
//   * РазрешенВводНового - Булево - Истина - Доступен ввод счета-фактуры по команды "Создать счет-фактуру";
//   * РазрешеныИсправления - Булево - Истина - Доступен ввод исправлений счета-фактуры.
//
Функция ПараметрыПредставленияСчетаФактуры(Основание, Организация, НеТребуется = Ложь) Экспорт
	
	Перем РеквизитыСчетаФактуры;
	
	СчетаФактуры = Документы.СчетФактураПолученный.СчетаФактурыПоОснованию(Основание, Организация, РеквизитыСчетаФактуры, Ложь);
	Если СчетаФактуры.Количество() > 0 Тогда
		ТекстСчетФактура = ПредставлениеСчетаФактуры(
			РеквизитыСчетаФактуры.Номер,
			РеквизитыСчетаФактуры.Дата,
			РеквизитыСчетаФактуры.Проведен);
			
		Если СчетаФактуры.Количество() > 1 Тогда
			ТекстСписок = НСтр("ru='Счета-фактуры (%Количество%)'"); 
			ТекстСписок = СтрЗаменить(ТекстСписок,"%Количество%",Строка(СчетаФактуры.Количество())); 
		Иначе
			ТекстСписок = "";
		КонецЕсли;
		
		ГиперссылкаСчетФактура = Истина;
		РазрешенВводНового = Ложь;
		РазрешеныИсправления = Истина;
		
	ИначеЕсли НеТребуется Тогда
		ТекстСчетФактура = НСтр("ru='Счет-фактура не требуется'");
		ТекстСписок = "";
		ГиперссылкаСчетФактура = Ложь;	
		РазрешенВводНового = Ложь;
		РазрешеныИсправления = Ложь;
		
	ИначеЕсли Не ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.СчетФактураПолученный) Тогда
		ТекстСчетФактура = НСтр("ru='Счет-фактура не введен'");
		ТекстСписок = "";
		ГиперссылкаСчетФактура = Ложь;
		РазрешенВводНового = Ложь;
		РазрешеныИсправления = Ложь;
		
	Иначе
		ТекстСчетФактура = НСтр("ru='Зарегистрировать счет-фактуру'"); ;
		ТекстСписок = "";
		ГиперссылкаСчетФактура = Истина;
		РазрешенВводНового = Истина;
		РазрешеныИсправления = Ложь;
		
	КонецЕсли;
	
	Возврат Новый Структура("ТекстСчетФактура, ТекстСписок, ГиперссылкаСчетФактура, РазрешенВводНового, РазрешеныИсправления",
		ТекстСчетФактура, ТекстСписок, ГиперссылкаСчетФактура, РазрешенВводНового, РазрешеныИсправления);
	
КонецФункции

// Возвращает структуру параметров представления заявления о ввозе товаров из стран ЕАЭС в документе.
//
// Параметры:
//  Основание - ДокументСсылка - Документ, на основании которого вводится заявление о ввозе товаров;
//  Организация - СправочникСсылка.Организации - Организация, на имя которой оформляется заявление о ввозе товаров;
//	НеТребуется - Булево - Истина - для документа не требуется вводить заявление о ввозе товаров.
//
// Возвращаемое значение:
//  Структура - структура с полями:
//   * ТекстЗаявлениеОВвозе              - Строка - Представление заявление о ввозе;
//   * ГиперссылкаЗаявлениеОВвозеТоваров - Булево - Признак гиперссылки представления заявления в ввозе товаров;
//   * РазрешенВводНового                - Булево - Истина - Доступен ввод заявления о ввозе товаров по команде "Создать заявление о ввозе товаров из страны-члена ЕАЭС";
//
Функция ПараметрыПредставленияЗаявленияОВвозеТоваров(Основание, Организация, НеТребуется = Ложь) Экспорт
	
	Перем РеквизитыЗаявленияВВвозе;
	
	ЗаявленияОВвозе = Документы.ЗаявлениеОВвозеТоваров.ЗаявленияОВвозеТоваровПоОснованию(Основание, Организация, РеквизитыЗаявленияВВвозе);
	Если ЗаявленияОВвозе.Количество() > 0 Тогда
		ТекстЗаявлениеОВвозеТоваров = ПредставлениеЗаявленияОВвозеТоваров(
			РеквизитыЗаявленияВВвозе.Номер,
			РеквизитыЗаявленияВВвозе.Дата,
			РеквизитыЗаявленияВВвозе.Проведен,
			РеквизитыЗаявленияВВвозе.ДатаПодтвержденияОплаты);
		Если ЗаявленияОВвозе.Количество() > 1 Тогда
			ТекстСписок = НСтр("ru='Заявления о ввозе товаров'") + " (" + Строка(ЗаявленияОВвозе.Количество()) + ")"; 
		Иначе
			ТекстСписок = "";
		КонецЕсли;
		ГиперссылкаЗаявлениеОВвозеТоваров = ПравоДоступа("Изменение", Метаданные.Документы.ЗаявлениеОВвозеТоваров);;
		РазрешенВводНового = Ложь;
		
	ИначеЕсли НеТребуется Тогда
		ТекстЗаявлениеОВвозеТоваров = НСтр("ru='Заявление о ввозе не требуется'");
		ТекстСписок = "";
		ГиперссылкаЗаявлениеОВвозеТоваров = Ложь;	
		РазрешенВводНового = Ложь;
		
	ИначеЕсли Не ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.ЗаявлениеОВвозеТоваров) Тогда
		ТекстЗаявлениеОВвозеТоваров = НСтр("ru='Заявление о ввозе товаров из стран ЕАЭС не введено'");
		ТекстСписок = "";
		ГиперссылкаЗаявлениеОВвозеТоваров = Ложь;
		РазрешенВводНового = Ложь;
		
	Иначе
		ТекстЗаявлениеОВвозеТоваров = НСтр("ru='Зарегистрировать заявление о ввозе товаров из страны ЕАЭС'"); ;
		ТекстСписок = "";
		ГиперссылкаЗаявлениеОВвозеТоваров = Истина;
		РазрешенВводНового = Истина;
		
	КонецЕсли;
	
	Возврат Новый Структура("ТекстЗаявлениеОВвозеТоваров, ТекстСписок, ГиперссылкаЗаявлениеОВвозеТоваров, РазрешенВводНового",
		ТекстЗаявлениеОВвозеТоваров, ТекстСписок, ГиперссылкаЗаявлениеОВвозеТоваров, РазрешенВводНового);
	
КонецФункции

// Возвращает данные счета-фактуры в виде структуры
//
// Параметры:
//  ДокументОснование - ДокументСсылка - Документ, на основании которого вводится счет-фактура;
//  Организация - СправочникСсылка.Организации - Организация, на имя которой оформляется счет-фактура;
//  Номер - Строка - Номер счета-фактуры;
//  ДатаСоставления - Дата - Дата счета-фактуры;
//  Валюта - СправочникСсылка.Валюты - Валюта счета-фактуры;
//  ДатаПолучения - Дата - Дата получения счета-фактуры.
//
// Возвращаемое значение:
//  Структура - поля счета-фактуры.
//
Функция ДанныеСчетаФактурыСтруктурой(ДокументОснование, Организация, Контрагент, Номер, ДатаСоставления, Валюта, ДатаПолучения) Экспорт
	
	ДанныеСчетаФактуры = Новый Структура;
	
	ДанныеСчетаФактуры.Вставить("ДокументОснование", ДокументОснование);
	ДанныеСчетаФактуры.Вставить("Организация",       Организация);
	ДанныеСчетаФактуры.Вставить("Контрагент",        Контрагент);
	ДанныеСчетаФактуры.Вставить("Номер",             Номер);
	ДанныеСчетаФактуры.Вставить("ДатаСоставления",   ДатаСоставления);
	ДанныеСчетаФактуры.Вставить("Валюта",            Валюта);
	ДанныеСчетаФактуры.Вставить("Дата",              ДатаПолучения);
	
	Возврат ДанныеСчетаФактуры;
	
КонецФункции

// Создает документ "Счет-фактура полученный".
//
// Параметры:
//  ДанныеСчетаФактуры - Структура - Данные счета-фактуры в виде структуры;
//  ПредъявленСчетФактура - Булево - Реквизит "ПредъявленСчетФактура" документа-основания;
//  НеТребуется - Булево - Истина - для документа не требуется вводить счет-фактуру.
//  НовыйСчетФактура - ДокументСсылка.СчетФактураПолученный - Возвращается ссылка на созданный счет-фактуру.
//
// Возвращаемое значение:
//  Структура - структура с полями:
//   * ТекстСчетФактура       - Строка - Представление счета-фактуры;
//   * ТекстСписок            - Строка - Представление команды открытия списка счетов-фактур;
//   * ГиперссылкаСчетФактура - Булево - Признак гиперссылки представления счета-фактуры;
//   * РазрешенВводНового     - Булево - Истина, значит доступен ввод счета-фактуры по команды "Создать счет-фактуру";
//   * РазрешеныИсправления   - Булево - Истина, значит доступен ввод исправлений счета-фактуры.
//
Функция ВвестиСчетФактуру(ДанныеСчетаФактуры, ПредъявленСчетФактура, НеТребуется = Ложь, НовыйСчетФактура = Неопределено) Экспорт
	
	СчетаФактуры = Документы.СчетФактураПолученный.СчетаФактурыПоОснованию(ДанныеСчетаФактуры.ДокументОснование);
	
	Если ПредъявленСчетФактура И (НЕ НеТребуется) И СчетаФактуры.Количество() = 0 Тогда
		
		СчетФактура = Документы.СчетФактураПолученный.СоздатьДокумент();
		СчетФактура.Заполнить(ДанныеСчетаФактуры);
		СчетФактура.Записать(РежимЗаписиДокумента.Проведение);
		
		ПредъявленСчетФактура = Ложь;
		НовыйСчетФактура = СчетФактура.Ссылка;
		
		ПараметрыПредставления = Новый Структура();
		ПараметрыПредставления.Вставить("ТекстСчетФактура", ПредставлениеСчетаФактуры(ДанныеСчетаФактуры.Номер, ДанныеСчетаФактуры.ДатаСоставления, Истина));
		ПараметрыПредставления.Вставить("ТекстСписок", "");
		ПараметрыПредставления.Вставить("ГиперссылкаСчетФактура", Истина);
		ПараметрыПредставления.Вставить("ГиперссылкаСчетФактура", Истина);
		ПараметрыПредставления.Вставить("РазрешенВводНового", Ложь);
		ПараметрыПредставления.Вставить("РазрешеныИсправления", Истина);
		
	Иначе
		
		ПараметрыПредставления = ПараметрыПредставленияСчетаФактуры(ДанныеСчетаФактуры.ДокументОснование, ДанныеСчетаФактуры.Организация, НеТребуется);
		
	КонецЕсли;
	
	Возврат ПараметрыПредставления;
	
КонецФункции

// Устанавливает режим выбора групп и элементов у склада на форме
//
// Параметры:
//  Склад - СправочникСсылка.Склады - изменяет режим выбора для складов данной группы.
//
Процедура УстановитьРежимВыбораГруппЭлементовСклада(Склад) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСкладыВТабличнойЧастиДокументовЗакупки") Тогда
		Склад.ВыборГруппИЭлементов = ГруппыИЭлементы.ГруппыИЭлементы;
	Иначе
		Склад.ВыборГруппИЭлементов = ГруппыИЭлементы.Элементы;
	КонецЕсли;
	
КонецПроцедуры

// Проверяет наличие корректировок и счетов-фактур, введенных на основании изменяемого документа.
//
// Параметры:
// Ссылка - ДокументСсылка - Изменяемый документ
// Основание - ДокументСсылка - Основание для корректировок
// ЕстьКорректировки - Булево - ВозвращаемыйПараметр. Признак наличия корректировок изменяемого документа.
// ЕстьСчетаФактуры - Булево - ВозвращаемыйПараметр. Признак наличия счетов-фактур изменяемого документа.
//
Процедура ПроверитьНаличиеКорректировокИСчетовФактур(Ссылка, Основание, ЕстьКорректировки, ЕстьСчетаФактуры) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеДокумента.Ссылка КАК Корректировка
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.ДокументОснование = &Основание
	|	И ДанныеДокумента.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.МоментВремени УБЫВ
	|");
	Запрос.УстановитьПараметр("Основание", Основание);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЕстьКорректировки = ЗначениеЗаполнено(Выборка.Корректировка) И Выборка.Корректировка <> Ссылка;
	Иначе
		ЕстьКорректировки = НЕ РезультатЗапроса.Пустой();
	КонецЕсли;	
	
	Если НЕ ЕстьКорректировки Тогда
		СчетаФактуры = Документы.СчетФактураПолученный.СчетаФактурыПоОснованию(Ссылка);
		ЕстьСчетаФактуры = СчетаФактуры.Количество() > 0;
	Иначе
		ЕстьСчетаФактуры = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Обновляет информацию о количестве документов поступлений в табличной части документа возврата
//
// Параметры:
//		СписокПоступлений - Список значений, содержащий список документов поступлений
//		ПоступлениеВШапке - Ссылка на документ поступления в шапке документа
//		НадписьЗаголовок - Надпись, содержащая информацию о количестве документов поступлений
//		ЭлементыФормы - Элементы формы документа возврата
//		КолонкаВТабличнойЧасти - Колонка табличной части, содержащая документы поступления
//		ТабличнаяЧасть - Табличная часть документа возврата
//		ИмяПоступленияВТабличнойЧасти - Название колонки с документами поступлений в табличной части
//		
Процедура ОбновитьИнформациюПоПоступлениямВФорме(СписокПоступлений,
	                                             ПоступлениеВШапке,
	                                             НадписьЗаголовок,
	                                             ЭлементыФормы,
	                                             КолонкаВТабличнойЧасти,
	                                             Знач ТабличнаяЧасть,
	                                             Знач ИмяПоступленияВТабличнойЧасти) Экспорт
	
	СписокПоступлений.Очистить();
	Для Каждого ТекСтрока Из ТабличнаяЧасть Цикл
		Если ЗначениеЗаполнено(ТекСтрока[ИмяПоступленияВТабличнойЧасти]) И СписокПоступлений.НайтиПоЗначению(ТекСтрока[ИмяПоступленияВТабличнойЧасти]) = Неопределено Тогда
			СписокПоступлений.Добавить(ТекСтрока[ИмяПоступленияВТабличнойЧасти]);
		КонецЕсли;
	КонецЦикла;
	
	Если СписокПоступлений.Количество() = 1 Тогда
		ПоступлениеВШапке = СписокПоступлений[0].Значение;
	Иначе
		ПоступлениеВШапке = Неопределено;
	КонецЕсли;
	
	Если СписокПоступлений.Количество() > 1 Тогда
		НадписьЗаголовок = СтрЗаменить(
			НСтр("ru='Всего документов: %КоличествоПоступлений%'"), "%КоличествоПоступлений%",
			СписокПоступлений.Количество());
	КонецЕсли;
	
	Если СписокПоступлений.Количество() < 1 Тогда
		ПоступлениеВШапке = Неопределено;
	КонецЕсли;
	
	Если СписокПоступлений.Количество() <= 1 Тогда
		ЭлементыФормы.СтраницыПоступления.ТекущаяСтраница = ЭлементыФормы.СтраницаПоступление;
	Иначе
		ЭлементыФормы.СтраницыПоступления.ТекущаяСтраница = ЭлементыФормы.СтраницаПоступления;
	КонецЕсли;
	
КонецПроцедуры

// Определяет хозяйственную операцию возврата по хозяйственной операции поступления
//
// Параметры:
//	ХозяйственнаяОперацияПоступление - ПеречислениеСсылка.ХозяйственныеОперации - исходная хозяйственная операция.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция возврата.
//
Функция ПолучитьХозяйственнуюОперациюВозвратаПоПоступлению(Знач ХозяйственнаяОперацияПоступление) Экспорт
	
	Если ХозяйственнаяОперацияПоступление = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика
	 Или Не ЗначениеЗаполнено(ХозяйственнаяОперацияПоступление) Тогда
		Возврат Перечисления.ХозяйственныеОперации.ВозвратТоваровПоставщику;
	ИначеЕсли ХозяйственнаяОперацияПоступление = Перечисления.ХозяйственныеОперации.ПриемНаКомиссию Тогда
		Возврат Перечисления.ХозяйственныеОперации.ВозвратТоваровКомитенту;
	ИначеЕсли ХозяйственнаяОперацияПоступление = Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи Тогда
		Возврат Перечисления.ХозяйственныеОперации.ОтгрузкаПринятыхСПравомПродажиТоваровСХранения;
	КонецЕсли;
	
КонецФункции

// Заполняет номенклатуру поставщика при изменении поставщика в документе
//
// Параметры:
//  Таблица - ТаблицаЗначений - таблица, в которой необходимо заполнить номенклатуру поставщика.
//  Партнер - СправочникСсылка.Партнеры - поставщик, которому принадлежит номенклатура.
//
Процедура ЗаполнитьНоменклатуруПоставщикаВТаблице(Таблица, Партнер) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНоменклатуруПоставщиков") Или Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТабличнаяЧасть.НомерСтроки    КАК НомерСтроки,
		|	ТабличнаяЧасть.Номенклатура   КАК Номенклатура,
		|	ТабличнаяЧасть.Характеристика КАК Характеристика,
		|	ТабличнаяЧасть.Упаковка       КАК Упаковка
		|ПОМЕСТИТЬ
		|	ТабличнаяЧасть
		|ИЗ
		|	&ТабличнаяЧасть КАК ТабличнаяЧасть
		|ГДЕ
		|	ТабличнаяЧасть.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|;
		|ВЫБРАТЬ
		|	МАКСИМУМ(НоменклатураПоставщиков.Ссылка)   КАК НоменклатураПоставщика,
		|	КОЛИЧЕСТВО(*)                              КАК КоличествоНоменклатурыПоставщика,
		|	НоменклатураПоставщиков.Номенклатура       КАК Номенклатура,
		|	НоменклатураПоставщиков.Характеристика     КАК Характеристика,
		|	НоменклатураПоставщиков.Упаковка           КАК Упаковка
		|ПОМЕСТИТЬ
		|	НоменклатураПоставщика
		|ИЗ
		|	Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
		|ГДЕ
		|	НоменклатураПоставщиков.Владелец = &Партнер
		|	И Не НоменклатураПоставщиков.ПометкаУдаления
		|	И (Номенклатура,Характеристика,Упаковка) В (
		|		ВЫБРАТЬ
		|			Товары.Номенклатура,
		|			Товары.Характеристика,
		|			Товары.Упаковка
		|		ИЗ
		|			ТабличнаяЧасть КАК Товары
		|		)
		|СГРУППИРОВАТЬ ПО
		|	НоменклатураПоставщиков.Номенклатура,
		|	НоменклатураПоставщиков.Характеристика,
		|	НоменклатураПоставщиков.Упаковка
		|;
		|ВЫБРАТЬ
		|	МАКСИМУМ(НоменклатураПоставщиков.Ссылка)   КАК НоменклатураПоставщика,
		|	НоменклатураПоставщиков.Номенклатура       КАК Номенклатура,
		|	НоменклатураПоставщиков.Характеристика     КАК Характеристика,
		|	НоменклатураПоставщиков.Упаковка           КАК Упаковка
		|ПОМЕСТИТЬ
		|	НоменклатураПоставщикаБезУпаковок
		|ИЗ
		|	Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
		|ГДЕ
		|	НоменклатураПоставщиков.Владелец = &Партнер
		|	И Не НоменклатураПоставщиков.ПометкаУдаления
		|	И (Номенклатура,Характеристика,Упаковка) В (
		|		ВЫБРАТЬ
		|			Товары.Номенклатура,
		|			Товары.Характеристика,
		|			ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|		ИЗ
		|			ТабличнаяЧасть КАК Товары
		|		)
		|СГРУППИРОВАТЬ ПО
		|	НоменклатураПоставщиков.Номенклатура,
		|	НоменклатураПоставщиков.Характеристика,
		|	НоменклатураПоставщиков.Упаковка
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(*) = 1
		|;
		|ВЫБРАТЬ
		|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА
		|			НоменклатураПоставщикаСУпаковками.КоличествоНоменклатурыПоставщика = 1
		|		ТОГДА
		|			НоменклатураПоставщикаСУпаковками.НоменклатураПоставщика
		|		КОГДА
		|			НоменклатураПоставщикаСУпаковками.НоменклатураПоставщика ЕСТЬ NULL
		|		ТОГДА
		|			НоменклатураПоставщикаБезУпаковок.НоменклатураПоставщика
		|	КОНЕЦ КАК НоменклатураПоставщика
		|ИЗ
		|	ТабличнаяЧасть КАК ТабличнаяЧасть
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	НоменклатураПоставщика КАК НоменклатураПоставщикаСУпаковками
		|ПО
		|	ТабличнаяЧасть.Номенклатура = НоменклатураПоставщикаСУпаковками.Номенклатура
		|	И ТабличнаяЧасть.Характеристика = НоменклатураПоставщикаСУпаковками.Характеристика
		|	И ТабличнаяЧасть.Упаковка = НоменклатураПоставщикаСУпаковками.Упаковка
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	НоменклатураПоставщикаБезУпаковок КАК НоменклатураПоставщикаБезУпаковок
		|ПО
		|	ТабличнаяЧасть.Номенклатура = НоменклатураПоставщикаБезУпаковок.Номенклатура
		|	И ТабличнаяЧасть.Характеристика = НоменклатураПоставщикаБезУпаковок.Характеристика
		|ГДЕ
		|	НоменклатураПоставщикаСУпаковками.НоменклатураПоставщика ЕСТЬ НЕ NULL
		|	ИЛИ НоменклатураПоставщикаБезУпаковок.НоменклатураПоставщика ЕСТЬ НЕ NULL
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки ВОЗР
		|");
		
	Запрос.УстановитьПараметр("ТабличнаяЧасть", Таблица.Выгрузить(,"НомерСтроки,Номенклатура,Характеристика,Упаковка"));
	Запрос.УстановитьПараметр("Партнер", Партнер);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если Не РезультатЗапроса[3].Пустой() Тогда
	
		Выборка = РезультатЗапроса[3].Выбрать();
		
		Для Каждого ТекСтрока Из Таблица Цикл
			
			Если Выборка.НайтиСледующий(ТекСтрока.НомерСтроки, "НомерСтроки") Тогда
				ТекСтрока.НоменклатураПоставщика = Выборка.НоменклатураПоставщика;
				Выборка.Сбросить();
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет номенклатуру, характеристику, упаковку в номенклатуре поставщика с пустой номенклатурой
//
// Параметры:
//  Товары - ТаблицаЗначений - таблица, содержащая данные для связывания номенклатуры с номенклатурой поставщика.
//  Отказ - Булево - Истина, если не удалось связать номенклатуру с номенклатурой поставщика.
//
Процедура СвязатьНоменклатуруСНоменклатуройПоставщика(Товары, Отказ) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНоменклатуруПоставщиков") Тогда
		Возврат;
	КонецЕсли;
		
		Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	Товары.НоменклатураПоставщика КАК НоменклатураПоставщика,
			|	Товары.Номенклатура           КАК Номенклатура,
			|	Товары.Характеристика         КАК Характеристика,
			|	Товары.Упаковка               КАК Упаковка
			|ПОМЕСТИТЬ
			|	Товары
			|ИЗ
			|	&Товары КАК Товары
			|ГДЕ
			|	Товары.НоменклатураПоставщика <> ЗНАЧЕНИЕ(Справочник.НоменклатураПоставщиков.ПустаяСсылка)
			|	И Товары.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
			|;
			|ВЫБРАТЬ
			|	Товары.НоменклатураПоставщика КАК НоменклатураПоставщика,
			|	Товары.Номенклатура           КАК Номенклатура,
			|	Товары.Характеристика         КАК Характеристика,
			|	Товары.Упаковка               КАК Упаковка
			|ПОМЕСТИТЬ
			|	СгруппированныеТовары
			|ИЗ
			|	Товары КАК Товары
			|ГДЕ
			|	Товары.НоменклатураПоставщика.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
			|СГРУППИРОВАТЬ ПО
			|	Товары.НоменклатураПоставщика,
			|	Товары.Номенклатура,
			|	Товары.Характеристика,
			|	Товары.Упаковка
			|;
			|ВЫБРАТЬ
			|	СгруппированныеТовары.НоменклатураПоставщика КАК НоменклатураПоставщика,
			|	СгруппированныеТовары.Номенклатура           КАК Номенклатура,
			|	СгруппированныеТовары.Характеристика         КАК Характеристика,
			|	КОЛИЧЕСТВО(*)                                КАК КоличествоУпаковок,
			|	МАКСИМУМ(СгруппированныеТовары.Упаковка)     КАК Упаковка
			|ИЗ
			|	СгруппированныеТовары КАК СгруппированныеТовары
			|СГРУППИРОВАТЬ ПО
			|	СгруппированныеТовары.НоменклатураПоставщика,
			|	СгруппированныеТовары.Номенклатура,
			|	СгруппированныеТовары.Характеристика
			|");
			
		Запрос.УстановитьПараметр("Товары", Товары.Выгрузить(,"НомерСтроки,НоменклатураПоставщика,Номенклатура,Характеристика,Упаковка"));
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		Если Не РезультатЗапроса[2].Пустой() Тогда
			
			Выборка = РезультатЗапроса[2].Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				Попытка
					
					ЗаблокироватьДанныеДляРедактирования(Выборка.НоменклатураПоставщика);
					
					НоменклатураПоставщикаОбъект = Выборка.НоменклатураПоставщика.ПолучитьОбъект();
					ЗаполнитьЗначенияСвойств(НоменклатураПоставщикаОбъект, Выборка);
					Если Выборка.КоличествоУпаковок > 1 Тогда
						НоменклатураПоставщикаОбъект.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
					КонецЕсли;
					НоменклатураПоставщикаОбъект.Записать();
					
				Исключение
					
					ТекстОшибки = НСтр("ru='Не удалось заблокировать %Элемент%. %ОписаниеОшибки%'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Элемент%",        Выборка.НоменклатураПоставщика);
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
					
					Отказ = Истина;
					ВызватьИсключение ТекстОшибки;
						
				КонецПопытки;
				
			КонецЦикла;
			
		КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру параметров для передачи в функцию ЗаполнитьТоварыПоФактическойПриемке()
//
// Возвращаемое значение:
//   Структура - структура с полями, необходимыми для заполнения по фактической приемке.
//
Функция ПараметрыЗаполненияПоФактическойПриемке() Экспорт
	
	ПараметрыЗаполнения = Новый Структура("ДокументПоступления,
	                                      |ДатаПоступления,
	                                      |СтруктураДействийСИзмененнымиСтроками,
                                          |СтруктураДействийСДобавленнымиСтроками");
	ПараметрыЗаполнения.Вставить("Соглашение", Справочники.СоглашенияСПоставщиками.ПустаяСсылка());
	ПараметрыЗаполнения.Вставить("Договор",    Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	ПараметрыЗаполнения.Вставить("Склад",                        Неопределено);
	ПараметрыЗаполнения.Вставить("МассивРаспоряжений",           Неопределено);
	ПараметрыЗаполнения.Вставить("ЗаполнениеВАктеОРасхождениях", Ложь);
	ПараметрыЗаполнения.Вставить("ЗаполнитьПоПоступлениям",      Ложь);
	ПараметрыЗаполнения.Вставить("ВариантПриемкиТоваров",        Перечисления.ВариантыПриемкиТоваров.ПустаяСсылка());
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Заполняет количество в ТЧ Товары по фактически принятым товарам.
//
// Параметры:
//	Товары                                 - Таблица - таблица товаров документа,
//	ПараметрыЗаполнения                    - Структура - см. функцию ПараметрыЗаполненияПоФактическойПриемке(),
//		ДокументПоступления                    - Ссылка - ссылка на документ поступления,
//		ДатаПоступления                        - Дата - дата поступления товаров,
//		СтруктураДействийСИзмененнымиСтроками  - Структура - действия, которые нужно произвести над измененными строками,
//		СтруктураДействийСДобавленнымиСтроками - Структура - действия, которые нужно произвести над добавленными строками,
//		Склад                                  - СправочникСсылка.Склады, Массив - склады поступления,
//		Соглашение                             - СправочникСсылка.СоглашенияСПоставщиками - соглашение поступления,
//		МассивРаспоряжений                     - Массив - распоряжения на поступление
//		ЗаполнениеВАктеОРасхождениях           - Булево - признак того, что выполняется заполнение в "Акт о расхождениях после приемки"
//	ЕстьПринятыеТовары                     - Булево - флаг, по которому можно определить что товары отгружены,
//  ДополнительныеПараметрыПоиска          - Структура - возможность задать дополнительные поля поиска.
//
// Возвращаемое значение:
//	Булево - Флаг, по которому можно определить что в результате перезаполнения ТЧ документа изменилась.
//
Функция ЗаполнитьТоварыПоФактическойПриемке(Товары, ПараметрыЗаполнения, ЕстьПринятыеТовары, ДополнительныеПараметрыПоиска = Неопределено) Экспорт
	
	ДокументПоступления                    = ПараметрыЗаполнения.ДокументПоступления;
	ДатаПоступления                        = ПараметрыЗаполнения.ДатаПоступления;
	СтруктураДействийСИзмененнымиСтроками  = ПараметрыЗаполнения.СтруктураДействийСИзмененнымиСтроками;
	СтруктураДействийСДобавленнымиСтроками = ПараметрыЗаполнения.СтруктураДействийСДобавленнымиСтроками;
	Склад                                  = ПараметрыЗаполнения.Склад;
	Соглашение                             = ПараметрыЗаполнения.Соглашение;
	Договор                                = ПараметрыЗаполнения.Договор;
	МассивРаспоряжений                     = ПараметрыЗаполнения.МассивРаспоряжений;
	ЗаполнениеВАктеОРасхождениях           = ПараметрыЗаполнения.ЗаполнениеВАктеОРасхождениях;
	ЗаполнитьПоПоступлениям                = ПараметрыЗаполнения.ЗаполнитьПоПоступлениям;
	
	ВариантПриемкиТоваров = Неопределено;
	Если ПараметрыЗаполнения.Свойство("ВариантПриемкиТоваров")
		И ЗначениеЗаполнено(ПараметрыЗаполнения.ВариантПриемкиТоваров) Тогда
		ВариантПриемкиТоваров = ПараметрыЗаполнения.ВариантПриемкиТоваров;
	Иначе
		ВариантПриемкиТоваров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументПоступления, "ВариантПриемкиТоваров");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЕстьИзменения = Ложь;
	ЕстьПринятыеТовары = Ложь;
	
	ТипПоступления = ТипЗнч(ДокументПоступления);
	ВозможнаПорча = ПолучитьФункциональнуюОпцию("ИспользоватьКачествоТоваров") И ТипПоступления = Тип("ДокументСсылка.ВозвратТоваровОтКлиента");
	
	Если МассивРаспоряжений <> Неопределено Тогда
		Запрос = Новый Запрос(ТекстЗапросаПроверкаПринимающихсяТоваровПоРаспоряжениям(ТипПоступления));
		Запрос.УстановитьПараметр("МассивРаспоряжений",  МассивРаспоряжений);
		Запрос.УстановитьПараметр("ДокументПоступления", ДокументПоступления);
		Запрос.УстановитьПараметр("Соглашение",          Соглашение);
		Запрос.УстановитьПараметр("Договор",             Договор);
		Запрос.УстановитьПараметр("Склад",               Склад);
		Запрос.УстановитьПараметр("ВариантПриемкиТоваров", ВариантПриемкиТоваров);
		Запрос.УстановитьПараметр("ЗаполнитьПоПоступлениям", ЗаполнитьПоПоступлениям);
		УстановитьПараметрОтбораРаспоряженийВРегистреТоварыКПоступлению(Запрос);
		Результат = Запрос.ВыполнитьПакет();
		Выборка = Результат[3].Выбрать();
	Иначе
		Запрос = Новый Запрос(ТекстЗапросаПроверкаПринимающихсяТоваровПоНакладной());
		Запрос.УстановитьПараметр("ДокументПоступления",   ДокументПоступления);
		Запрос.УстановитьПараметр("Соглашение",            Соглашение);
		Запрос.УстановитьПараметр("Договор",               Договор);
		Запрос.УстановитьПараметр("Склад",                 Склад);
		Запрос.УстановитьПараметр("ВариантПриемкиТоваров", ВариантПриемкиТоваров);
		УстановитьПараметрОтбораРаспоряженийВРегистреТоварыКПоступлению(Запрос);
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
	КонецЕсли;
	
	Если Выборка.Следующий() Тогда
		ЕстьПринятыеТовары = (Выборка.Количество > 0);
	КонецЕсли;
	
	УдаляемыеСтроки = Новый Соответствие;
	МассивПерепоставленныхСтрок = Новый Массив();
	
	ВыгружаемыеКолонки = "Номенклатура, Характеристика, Серия, Упаковка, Количество";
	
	Если Склад = Неопределено Тогда
		ДобавлятьКолонкуСклад = Ложь;
		ВыгружаемыеКолонки = ВыгружаемыеКолонки + ", Склад";
	Иначе
		ДобавлятьКолонкуСклад = Истина;
	КонецЕсли;
	
	ДобавлятьКолонкуЗаказПоставщику = Истина;
	
	Если МассивРаспоряжений = Неопределено Тогда
		
		ВыгружаемыеКолонки = ВыгружаемыеКолонки + ", Назначение";
		
	ИначеЕсли МассивРаспоряжений <> Неопределено
		И (ТипПоступления = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
			)
		И ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным Тогда
		
		ДобавлятьКолонкуЗаказПоставщику = Ложь;
		ВыгружаемыеКолонки = ВыгружаемыеКолонки + ", ЗаказПоставщику";
		
	КонецЕсли;
	
	ТаблицаТоваров = Товары.Выгрузить(, ВыгружаемыеКолонки);
	
	Если ДобавлятьКолонкуСклад Тогда
		ТаблицаТоваров.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
		Для Каждого ТекСтрока Из ТаблицаТоваров Цикл
			ТекСтрока.Склад = Склад;
		КонецЦикла;
	КонецЕсли;
	
	Если ДобавлятьКолонкуЗаказПоставщику Тогда
		ТаблицаТоваров.Колонки.Добавить("ЗаказПоставщику", Новый ОписаниеТипов("ДокументСсылка.ЗаказПоставщику"));
	КонецЕсли;
	
	Если МассивРаспоряжений <> Неопределено Тогда
		Запрос = Новый Запрос(ТекстЗапросаПоРаспоряжениям(ТипПоступления));
		Запрос.УстановитьПараметр("МассивРаспоряжений", МассивРаспоряжений);
	Иначе
		Запрос = Новый Запрос(ТекстЗапросаПоНакладнойИлиСоглашению());
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Дата",       ?(ЗначениеЗаполнено(ДатаПоступления), ДатаПоступления, ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("Ссылка",     ДокументПоступления);
	Запрос.УстановитьПараметр("ДокументПоступления", ДокументПоступления);
	Запрос.УстановитьПараметр("Товары",     ТаблицаТоваров);
	Запрос.УстановитьПараметр("Соглашение", Соглашение);
	Запрос.УстановитьПараметр("Договор",    Договор);
	Запрос.УстановитьПараметр("Склад",      Склад);
	Запрос.УстановитьПараметр("ВариантПриемкиТоваров", ВариантПриемкиТоваров);
	Запрос.УстановитьПараметр("ЗаполнитьПоПоступлениям", ЗаполнитьПоПоступлениям);
	УстановитьПараметрОтбораРаспоряженийВРегистреТоварыКПоступлению(Запрос);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если РезультатЗапроса[0].Пустой() Или Не ЕстьПринятыеТовары Тогда
		Возврат ЕстьИзменения;
	КонецЕсли;
	
	Если МассивРаспоряжений <> Неопределено Тогда
		ТаблицаОстатков = РезультатЗапроса[2].Выгрузить();
		ТаблицаОстатков.Индексы.Добавить("Номенклатура, Характеристика");
		
		ВыборкаУслуг             = РезультатЗапроса[4].Выбрать();
		ВыборкаУпаковок          = РезультатЗапроса[5].Выбрать();
		ВыборкаНеордерныхСкладов = РезультатЗапроса[6].Выбрать();
	Иначе
		ТаблицаОстатков = РезультатЗапроса[0].Выгрузить();
		ТаблицаОстатков.Индексы.Добавить("Номенклатура, Характеристика");
		
		ВыборкаУслуг             = РезультатЗапроса[2].Выбрать();
		ВыборкаУпаковок          = РезультатЗапроса[3].Выбрать();
		ВыборкаНеордерныхСкладов = РезультатЗапроса[4].Выбрать();
	КонецЕсли;
	
	РаспределяемыеПоСериямСтроки = Новый Массив;
	
	ИсходныеСтрокиКДозаполнениюСОстатками = Новый Массив;
	ПоляДляДозаполнения = "Строка,Номенклатура,Характеристика,Склад,Количество,КоэффициентУпаковки,СтрокаОбработана";
	
	Для Каждого ТекСтрока Из Товары Цикл
		
		Если ДополнительныеПараметрыПоиска <> Неопределено Тогда
			ЕстьНесоответствие = Ложь;
			Для Каждого ДополнительныйПараметрПоиска Из ДополнительныеПараметрыПоиска Цикл
				Если ТекСтрока[ДополнительныйПараметрПоиска.Ключ] <> ДополнительныйПараметрПоиска.Значение Тогда
					ЕстьНесоответствие = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ЕстьНесоответствие Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ВозможнаПорча И (Не ЗаполнениеВАктеОРасхождениях) И ТекСтрока.Порча Тогда
			ТекНоменклатура   = ТекСтрока.НоменклатураОприходование;
			ТекХарактеристика = ТекСтрока.ХарактеристикаОприходование;
		Иначе
			ТекНоменклатура   = ТекСтрока.Номенклатура;
			ТекХарактеристика = ТекСтрока.Характеристика;
		КонецЕсли;
		
		ВыборкаУслуг.Сбросить();
		Если ВыборкаУслуг.НайтиСледующий(ТекНоменклатура, "Номенклатура") Тогда
			Продолжить;
		КонецЕсли;
			
		Если Склад = Неопределено Или ТипЗнч(Склад) = Тип("Массив") Тогда
			СкладПоиска = ТекСтрока.Склад;
		Иначе
			СкладПоиска = Склад;
		КонецЕсли;
		
		ВыборкаНеордерныхСкладов.Сбросить();
		Если ВыборкаНеордерныхСкладов.НайтиСледующий(СкладПоиска, "Склад") Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура("Номенклатура, Характеристика, Серия, Склад",
			ТекНоменклатура, ТекХарактеристика, ТекСтрока.Серия, СкладПоиска);
		
		Если МассивРаспоряжений = Неопределено Тогда
			
			СтруктураПоиска.Вставить("Назначение", ТекСтрока.Назначение);
			
		ИначеЕсли МассивРаспоряжений <> Неопределено
			И (ТипПоступления = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
				)
			И ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным Тогда
			
			СтруктураПоиска.Вставить("Распоряжение", ТекСтрока.ЗаказПоставщику);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекСтрока.Упаковка) Тогда
			Если ВыборкаУпаковок.НайтиСледующий(ТекСтрока.Упаковка, "Упаковка") Тогда
				КоэффициентУпаковки = ВыборкаУпаковок.Коэффициент;
			Иначе
				КоэффициентУпаковки = 1;
			КонецЕсли;
			ВыборкаУпаковок.Сбросить();
		Иначе
			КоэффициентУпаковки = 1;
		КонецЕсли;
		
		ИсходноеКоличество = ТекСтрока.Количество;
		ТекущееКоличество = ТекСтрока.Количество;
		
		ИсходнаяСтрокаОбработана = Ложь;
		
		СтрокиТаблицыОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураПоиска);
		
		Если СтрокиТаблицыОстатков.Количество() > 0
			И СтрокиТаблицыОстатков[0].Количество > 0 Тогда
			
			СтрокаОстатков = СтрокиТаблицыОстатков[0];
			ЕстьПринятыеТовары = Истина;
			
			Если ТекущееКоличество > 0 Тогда
				
				ДобавляемоеКоличество = Мин(ТекущееКоличество, СтрокаОстатков.Количество);
				
				ТекущееКоличество = ТекущееКоличество - ДобавляемоеКоличество;
				СтрокаОстатков.Количество = СтрокаОстатков.Количество - ДобавляемоеКоличество;
				СтрокаОстатков.НоменклатураДобавлена = (СтрокаОстатков.Количество = 0);
				
				ДобавляемоеКоличествоУпаковок = ДобавляемоеКоличество / КоэффициентУпаковки;
				
				ТекСтрока.Серия = СтрокаОстатков.Серия;
				ТекСтрока.Количество = ДобавляемоеКоличество;
				ТекСтрока.КоличествоУпаковок = ДобавляемоеКоличествоУпаковок;
				ИсходнаяСтрокаОбработана = Истина;
				
				Если СтрокаОстатков.Количество = 0 Тогда
					ТаблицаОстатков.Удалить(СтрокаОстатков)
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ТекущееКоличество > 0
			И ТекСтрока.СтатусУказанияСерий > 0 Тогда
				
			СтрокаКДозаполнению = Новый Структура(ПоляДляДозаполнения);
			ЗаполнитьЗначенияСвойств(СтрокаКДозаполнению, СтруктураПоиска);
			СтрокаКДозаполнению.Строка = ТекСтрока;
			СтрокаКДозаполнению.Количество = ТекущееКоличество;
			СтрокаКДозаполнению.КоэффициентУпаковки = КоэффициентУпаковки;
			СтрокаКДозаполнению.СтрокаОбработана = ИсходнаяСтрокаОбработана;
			ИсходныеСтрокиКДозаполнениюСОстатками.Добавить(СтрокаКДозаполнению);
			
		ИначеЕсли ТекСтрока.Количество = 0
			Или Не ИсходнаяСтрокаОбработана Тогда
			
			УдаляемыеСтроки.Вставить(ТекСтрока);
			
		КонецЕсли;
		
		Если ТекСтрока.Количество <> ИсходноеКоличество Тогда
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекСтрока, СтруктураДействийСИзмененнымиСтроками, Неопределено);
			
		КонецЕсли;
		
		ЕстьИзменения = ЕстьИзменения 
			Или ТекСтрока.Количество <> ИсходноеКоличество;
			
	КонецЦикла;
	
	// Поиск в остатках к оформлению поступлений без учета серий
	СтруктураПоиска = Новый Структура("НоменклатураДобавлена, Номенклатура, Характеристика, Склад");
	СтруктураПоиска.НоменклатураДобавлена = Ложь;
	Для Каждого СтрокаКДозаполнению Из ИсходныеСтрокиКДозаполнениюСОстатками Цикл
		
		ТекСтрока                = СтрокаКДозаполнению.Строка;
		ТекущееКоличество        = СтрокаКДозаполнению.Количество;
		ИсходнаяСтрокаОбработана = СтрокаКДозаполнению.СтрокаОбработана;
		ИсходноеКоличество       = ТекСтрока.Количество;
		ИсходнаяСерия            = ТекСтрока.Серия;
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска,СтрокаКДозаполнению);
		
		Если МассивРаспоряжений = Неопределено Тогда
			
			СтруктураПоиска.Вставить("Назначение", ТекСтрока.Назначение);
			
		ИначеЕсли МассивРаспоряжений <> Неопределено
			И (ТипПоступления = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
				)
			И ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным Тогда
			
			СтруктураПоиска.Вставить("Распоряжение", ТекСтрока.ЗаказПоставщику);
			
		КонецЕсли;
		
		СтрокиТаблицыОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураПоиска);
		
		Для Каждого СтрокаОстатков Из СтрокиТаблицыОстатков Цикл
			
			Если СтрокаОстатков.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ЕстьПринятыеТовары = Истина;
			
			Если ТекущееКоличество = 0 Тогда
				Прервать;
			КонецЕсли;
			
			ДобавляемоеКоличество = Мин(ТекущееКоличество, СтрокаОстатков.Количество);
			
			ТекущееКоличество = ТекущееКоличество - ДобавляемоеКоличество;
			СтрокаОстатков.Количество = СтрокаОстатков.Количество - ДобавляемоеКоличество;
			СтрокаОстатков.НоменклатураДобавлена = (СтрокаОстатков.Количество = 0);
			
			ДобавляемоеКоличествоУпаковок = ДобавляемоеКоличество / КоэффициентУпаковки;
			
			Если Не ИсходнаяСтрокаОбработана Тогда
				
				ТекСтрока.Серия = СтрокаОстатков.Серия;
				ТекСтрока.Количество = ДобавляемоеКоличество;
				ТекСтрока.КоличествоУпаковок = ДобавляемоеКоличествоУпаковок;
				ИсходнаяСтрокаОбработана = Истина;
				
			Иначе
				
				РаспределяемыеПоСериямСтроки.Добавить(Новый Структура("Строка,Серия,Количество,КоличествоУпаковок",
								ТекСтрока, СтрокаОстатков.Серия, ДобавляемоеКоличество, ДобавляемоеКоличествоУпаковок));
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ТекСтрока.Количество = 0
			Или Не ИсходнаяСтрокаОбработана Тогда
			УдаляемыеСтроки.Вставить(ТекСтрока);
		ИначеЕсли ТекСтрока.Количество <> ИсходноеКоличество Тогда
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекСтрока, СтруктураДействийСИзмененнымиСтроками, Неопределено);
		КонецЕсли;
		
		ЕстьИзменения = ЕстьИзменения 
			Или ТекСтрока.Количество <> ИсходноеКоличество
			Или ИсходнаяСерия <> ТекСтрока.Серия;
		
	КонецЦикла;
	
	Для Каждого СтрокаССерией Из РаспределяемыеПоСериямСтроки Цикл
		НоваяСтрока = Товары.Вставить(Товары.Индекс(СтрокаССерией.Строка)+1);
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаССерией.Строка);
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаССерией); // Серия, Количество, КоличествоУпаковок
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействийСДобавленнымиСтроками, Неопределено);
	КонецЦикла;
	
	НераспределенныеСтроки = ТаблицаОстатков.НайтиСтроки(Новый Структура("НоменклатураДобавлена", Ложь));
	ЕстьИзменения = ЕстьИзменения
		Или РаспределяемыеПоСериямСтроки.Количество() > 0
		Или УдаляемыеСтроки.Количество() > 0
		Или НераспределенныеСтроки.Количество() > 0;
	
	Для Каждого ТекЭлемент Из УдаляемыеСтроки Цикл
		Товары.Удалить(ТекЭлемент.Ключ);
	КонецЦикла;
	
	ИсключаемыеСвойства = Новый Структура("КодСтроки, ДокументРеализации, ЦенаЗаказа", 0, Неопределено, 0);
	СтруктураПоиска = Новый Структура("Номенклатура, Характеристика, Серия");
	Если Склад = Неопределено Или ТипЗнч(Склад) = Тип("Массив") Тогда
		СтруктураПоиска.Вставить("Склад");
	КонецЕсли;
	Если МассивРаспоряжений <> Неопределено
		И (ТипПоступления = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
			)
		И ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным Тогда
		
		СтруктураПоиска.Вставить("ЗаказПоставщику");
		
	КонецЕсли;
	
	Если НераспределенныеСтроки.Количество() > 0 Тогда
		ДопустимыеОтклонения = ДопустимыеОтклоненияМерныхТоваров(МассивРаспоряжений);
	КонецЕсли;
	
	Для Каждого СтрокаКДобавлению Из НераспределенныеСтроки Цикл
		
		Если Не СтруктураПоиска.Свойство("Склад")
			И Склад <> СтрокаКДобавлению.Склад Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаКДобавлению);
		Если СтруктураПоиска.Свойство("ЗаказПоставщику") Тогда
			СтруктураПоиска.ЗаказПоставщику = СтрокаКДобавлению.Распоряжение;
		КонецЕсли;
		
		СтрокиТоваров = Товары.НайтиСтроки(СтруктураПоиска);
		
		Если СтрокиТоваров.Количество() = 0 Тогда
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКДобавлению);
			
			
			НоваяСтрока.КоличествоУпаковок = СтрокаКДобавлению.Количество;
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействийСДобавленнымиСтроками, Неопределено);
		Иначе
			СтрокаТоваров = СтрокиТоваров[0];
			
			Если ЗначениеЗаполнено(СтрокаТоваров.Упаковка) Тогда
				Если ВыборкаУпаковок.НайтиСледующий(СтрокаТоваров.Упаковка, "Упаковка") Тогда
					КоэффициентУпаковки = ВыборкаУпаковок.Коэффициент;
				Иначе
					КоэффициентУпаковки = 1;
				КонецЕсли;
				ВыборкаУпаковок.Сбросить();
			Иначе
				КоэффициентУпаковки = 1;
			КонецЕсли;
			
			СтруктураПоискаОтклонений = Новый Структура("Номенклатура, Характеристика",
											СтрокаКДобавлению.Номенклатура,
											СтрокаКДобавлению.Характеристика);
			
			Если МассивРаспоряжений <> Неопределено
				И (ТипПоступления = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
					)
				И ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным Тогда
				
				СтруктураПоискаОтклонений.Вставить("Заказ",СтрокаКДобавлению.Распоряжение);
				
			КонецЕсли;
			
			СтрокиДопустимыхОтклонений = ДопустимыеОтклонения.НайтиСтроки(СтруктураПоискаОтклонений);
			
			Если МассивРаспоряжений <> Неопределено И ЗначениеЗаполнено(СтрокаТоваров.КодСтроки) Тогда
				
				Если СтрокиДопустимыхОтклонений.Количество() > 0
					И СтрокиДопустимыхОтклонений[0].ДопустимоеОтклонение >= СтрокаКДобавлению.Количество Тогда
					СтрокаТоваров.Количество = СтрокаТоваров.Количество + СтрокаКДобавлению.Количество;
					СтрокаТоваров.КоличествоУпаковок = СтрокаТоваров.Количество / КоэффициентУпаковки;
					ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТоваров, СтруктураДействийСИзмененнымиСтроками, Неопределено);
				Иначе
					НоваяСтрока = Товары.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТоваров);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ИсключаемыеСвойства);
					НоваяСтрока.Количество = СтрокаКДобавлению.Количество;
					НоваяСтрока.КоличествоУпаковок = НоваяСтрока.Количество / КоэффициентУпаковки;
					ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействийСДобавленнымиСтроками, Неопределено);
				КонецЕсли;
				
			Иначе
				СтрокаТоваров.Количество = СтрокаТоваров.Количество + СтрокаКДобавлению.Количество;
				СтрокаТоваров.КоличествоУпаковок = СтрокаТоваров.КоличествоУпаковок + СтрокаКДобавлению.Количество / КоэффициентУпаковки;
				ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТоваров, СтруктураДействийСИзмененнымиСтроками, Неопределено);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	
	Возврат ЕстьИзменения;
	
КонецФункции

// Процедура настраивает состав списка выбора налогообложения в зависимости от операции.
//
// Параметры:
//	Поле - ПолеФормы - Поле формы для выбора налогообложения
//	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция документа
//
Процедура ЗаполнитьСписокВыбораНалогообложения(Поле, ХозяйственнаяОперация) Экспорт
	
	СписокНалогообложений = Поле.СписокВыбора;
	
	ЭтоБазовая 				    = ПолучитьФункциональнуюОпцию("БазоваяВерсия");
	ДоступноНалоговыйАгентПоНДС = 
			НЕ ЭтоБазовая 
			И (ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПриемНаКомиссию)
			И (ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
			И (ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ОказаниеАгентскихУслуг)
			И (ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию)
			И (ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию);
	ЗначениеНалоговыйАгентПоНДС = СписокНалогообложений.НайтиПоЗначению(Перечисления.ТипыНалогообложенияНДС.НалоговыйАгентПоНДС);
	
	Если ДоступноНалоговыйАгентПоНДС И ЗначениеНалоговыйАгентПоНДС = Неопределено Тогда
		СписокНалогообложений.Добавить(Перечисления.ТипыНалогообложенияНДС.НалоговыйАгентПоНДС);
	ИначеЕсли НЕ ДоступноНалоговыйАгентПоНДС И ЗначениеНалоговыйАгентПоНДС <> Неопределено Тогда
		СписокНалогообложений.Удалить(ЗначениеНалоговыйАгентПоНДС);
	КонецЕсли;
	
	ДоступноПродажаОблагаетсяНДС = ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОказаниеАгентскихУслуг
		ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаКомиссию;
		
	ЗначениПродажаОблагаетсяНДС = СписокНалогообложений.НайтиПоЗначению(Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС);
	Если ДоступноПродажаОблагаетсяНДС И ЗначениПродажаОблагаетсяНДС = Неопределено Тогда
		СписокНалогообложений.Добавить(Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС, НСтр("ru = 'Продажа облагается НДС'"));
	ИначеЕсли НЕ ДоступноПродажаОблагаетсяНДС И ЗначениПродажаОблагаетсяНДС = Неопределено Тогда
		СписокНалогообложений.Добавить(Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС, НСтр("ru = 'Закупка облагается НДС'"));
	ИначеЕсли ДоступноПродажаОблагаетсяНДС И ЗначениПродажаОблагаетсяНДС <> Неопределено Тогда
		СписокНалогообложений.Удалить(ЗначениПродажаОблагаетсяНДС);
		СписокНалогообложений.Добавить(Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС, НСтр("ru = 'Продажа облагается НДС'"));
	ИначеЕсли НЕ ДоступноПродажаОблагаетсяНДС И ЗначениПродажаОблагаетсяНДС <> Неопределено Тогда
		СписокНалогообложений.Удалить(ЗначениПродажаОблагаетсяНДС);
		СписокНалогообложений.Добавить(Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС, НСтр("ru = 'Закупка облагается НДС'"));
	КонецЕсли;
	
	ЗначениеПродажаНеОблагаетсяНДС = СписокНалогообложений.НайтиПоЗначению(Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС);
	Если ДоступноПродажаОблагаетсяНДС И ЗначениеПродажаНеОблагаетсяНДС = Неопределено Тогда
		СписокНалогообложений.Добавить(Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС, НСтр("ru = 'Продажа не облагается НДС'"));
	ИначеЕсли НЕ ДоступноПродажаОблагаетсяНДС И ЗначениеПродажаНеОблагаетсяНДС = Неопределено Тогда
		СписокНалогообложений.Добавить(Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС, НСтр("ru = 'Закупка не облагается НДС'"));
	ИначеЕсли ДоступноПродажаОблагаетсяНДС И ЗначениеПродажаНеОблагаетсяНДС <> Неопределено Тогда
		СписокНалогообложений.Удалить(ЗначениеПродажаНеОблагаетсяНДС);
		СписокНалогообложений.Добавить(Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС, НСтр("ru = 'Продажа не облагается НДС'"));
	ИначеЕсли НЕ ДоступноПродажаОблагаетсяНДС И ЗначениеПродажаНеОблагаетсяНДС <> Неопределено Тогда
		СписокНалогообложений.Удалить(ЗначениеПродажаНеОблагаетсяНДС);
		СписокНалогообложений.Добавить(Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС, НСтр("ru = 'Закупка не облагается НДС'"));
	КонецЕсли;
	
	ДоступноОблагаетсяНДСУПокупателя = ПолучитьФункциональнуюОпцию("ПокупкаТоваровОблагаемыхНДСУПокупателя") 
			И (ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПриемНаКомиссию)
			И (ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо)
			И (ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ОказаниеАгентскихУслуг)
			И (ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию);
	ЗначениеОблагаетсяНДСУПокупателя = СписокНалогообложений.НайтиПоЗначению(Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя);
	
	Если НЕ ДоступноОблагаетсяНДСУПокупателя И ЗначениеОблагаетсяНДСУПокупателя <> Неопределено Тогда
		СписокНалогообложений.Удалить(ЗначениеОблагаетсяНДСУПокупателя);
	КонецЕсли;
	
	
КонецПроцедуры

// Возвращается таблицу допустимых отклонений мерных товаров
//
// Параметры:
//  МассивРаспоряжений	 - Массив	 - массив распоряжений
// 
// Возвращаемое значение:
//  ТаблицаЗначений
//
Функция ДопустимыеОтклоненияМерныхТоваров(МассивРаспоряжений, РаспоряжениеЗаказ = Истина) Экспорт
	
	ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров = 
		Константы.ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров.Получить();
	
	Если НЕ ЗначениеЗаполнено(МассивРаспоряжений)
		ИЛИ НЕ РаспоряжениеЗаказ
		ИЛИ ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров = 0 
		ИЛИ МассивРаспоряжений.Количество() = 0 Тогда
		Таблица = Новый ТаблицаЗначений;
		Таблица.Колонки.Добавить("Заказ");
		Таблица.Колонки.Добавить("ЗаказПоставщику");
		Таблица.Колонки.Добавить("Номенклатура");
		Таблица.Колонки.Добавить("Характеристика");
		Таблица.Колонки.Добавить("Склад");
		Возврат Таблица;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("МассивРаспоряжений", МассивРаспоряжений);
	Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений",
	                           Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
	Запрос.УстановитьПараметр("ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров",
	                           Константы.ДопустимоеОтклонениеПриОкругленииКоличества.Получить());
	
	Если ТипЗнч(МассивРаспоряжений[0]) = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента") Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТоварыКПоступлению.ЗаявкаНаВозвратТоваровОтКлиента КАК Заказ,
		|	ТоварыКПоступлению.Номенклатура                    КАК Номенклатура,
		|	ТоварыКПоступлению.Характеристика                  КАК Характеристика,
		|	СУММА(ТоварыКПоступлению.КОформлениюПриход * 
		|		(&ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров / 100)) КАК ДопустимоеОтклонение
		|ИЗ
		|	РегистрНакопления.ЗаявкиНаВозвратТоваровОтКлиентов.Обороты(&НачПериод,&КонПериод,, 
		|		ЗаявкаНаВозвратТоваровОтКлиента В (&МассивРаспоряжений)
		|	) КАК ТоварыКПоступлению
		|ГДЕ
		|	ТоварыКПоступлению.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины В (&МерныеТипыЕдиницИзмерений)
		|СГРУППИРОВАТЬ ПО
		|	ТоварыКПоступлению.ЗаявкаНаВозвратТоваровОтКлиента,
		|	ТоварыКПоступлению.Номенклатура,
		|	ТоварыКПоступлению.Характеристика";
		
		ГраницыОборотов = ОбщегоНазначенияУТ.ГраницыОборотовРегистра("ЗаявкиНаВозвратТоваровОтКлиентов",
																"ЗаявкаНаВозвратТоваровОтКлиента В (&МассивРаспоряжений)",
																Запрос.Параметры);
		
		Запрос.УстановитьПараметр("НачПериод", ГраницыОборотов.МинимальнаяДата);
		Запрос.УстановитьПараметр("КонПериод", ГраницыОборотов.МаксимальнаяДата);
		
	ИначеЕсли ТипЗнч(МассивРаспоряжений[0]) = Тип("ДокументСсылка.ЗаказПоставщику")
		Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТоварыКПоступлению.ЗаказПоставщику КАК ЗаказПоставщику,
		|	ТоварыКПоступлению.ЗаказПоставщику КАК Заказ,
		|	ТоварыКПоступлению.Номенклатура    КАК Номенклатура,
		|	ТоварыКПоступлению.Характеристика  КАК Характеристика,
		|	ТоварыКПоступлению.Склад           КАК Склад,
		|	СУММА(ТоварыКПоступлению.КОформлениюПриход * 
		|		(&ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров / 100)) КАК ДопустимоеОтклонение
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Обороты(&НачПериод,&КонПериод,, 
		|		ЗаказПоставщику В (&МассивРаспоряжений)) КАК ТоварыКПоступлению
		|ГДЕ
		|	ТоварыКПоступлению.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины В (&МерныеТипыЕдиницИзмерений)
		|СГРУППИРОВАТЬ ПО
		|	ТоварыКПоступлению.ЗаказПоставщику,
		|	ТоварыКПоступлению.Номенклатура,
		|	ТоварыКПоступлению.Характеристика,
		|	ТоварыКПоступлению.Склад";
		
		ГраницыОборотов = ОбщегоНазначенияУТ.ГраницыОборотовРегистра("ЗаказыПоставщикам",
																"ЗаказПоставщику В (&МассивРаспоряжений)",
																Запрос.Параметры);
		
		Запрос.УстановитьПараметр("НачПериод", ГраницыОборотов.МинимальнаяДата);
		Запрос.УстановитьПараметр("КонПериод", ГраницыОборотов.МаксимальнаяДата);
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСВариантамиПриемки

// Заполняет список выбора вариантов приемки
//
// Параметры:
//  ОформлениеОрдера - ПолеФормы - поле формы с настройкой оформления ордера
//
Процедура ЗаполнитьСписокВыбораВариантовПриемки(ОформлениеОрдера) Экспорт
	
	ЗначениеОформленияОрдера = "";
	ЗначениеКонстанты = Константы.ВариантПриемкиТоваров.Получить();
	ЗаполнитьНастройкиВариантовПриемки(ЗначениеКонстанты, ЗначениеОформленияОрдера);
	
	СписокВыбораРаспоряжение = ОформлениеОрдера.СписокВыбора;
	
	СписокВыбораРаспоряжение.Очистить();
	
	ЗаказыИспользуются = ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыПоставщикам")
		Или ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыНаПеремещение")
		Или ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыНаСборку");
	
	Если ЗаказыИспользуются Тогда
		СписокВыбораРаспоряжение.Добавить("ПослеЗаказа", НСтр("ru='После оформления заказа'"));
		СписокВыбораРаспоряжение.Добавить("ПослеНакладной", НСтр("ru='После оформления накладной'"));
	Иначе
		СписокВыбораЗначение = ?(ЗначениеОформленияОрдера = "ПослеНакладной", ЗначениеОформленияОрдера, "ПослеЗаказа");
		СписокВыбораРаспоряжение.Добавить(ЗначениеОформленияОрдера, НСтр("ru='После оформления накладной'"));
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список выбора вариантов приемки по соглашениям
//
// Параметры:
//  ОформлениеОрдера - ПолеФормы - поле формы с настройкой оформления ордера
//  ПриемкаТоваров	 - ПолеФормы - поле формы с настройкой приемки товаров
//
Процедура ЗаполнитьСписокВыбораВариантовПриемкиПоСоглашениям(ОформлениеОрдера, ПриемкаТоваров) Экспорт
	
	ЗначениеОформленияОрдера = "";
	ЗначениеКонстанты = Константы.ВариантПриемкиТоваров.Получить();
	ЗаполнитьНастройкиВариантовПриемки(ЗначениеКонстанты, ЗначениеОформленияОрдера);
	
	ИспользоватьСоглашенияСПоставщиками = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСПоставщиками");
	СписокВыбораРаспоряжение = ОформлениеОрдера.СписокВыбора;
	
	СписокВыбораРаспоряжение.Очистить();
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыПоставщикам") Тогда
		СписокВыбораРаспоряжение.Добавить("ПослеЗаказа", НСтр("ru='После оформления заказа'"));
		СписокВыбораРаспоряжение.Добавить("ПослеНакладной", НСтр("ru='После оформления накладной'"));
	Иначе
		СписокВыбораЗначение = ?(ЗначениеОформленияОрдера = "ПослеНакладной", ЗначениеОформленияОрдера, "ПослеЗаказа");
		СписокВыбораРаспоряжение.Добавить(ЗначениеОформленияОрдера, НСтр("ru='После оформления накладной'"));
	КонецЕсли;
	
	Если ИспользоватьСоглашенияСПоставщиками Тогда
		СписокВыбораРаспоряжение.Добавить("ПоСоглашению", НСтр("ru='По текущему соглашению (в т.ч. без документов)'"));
	КонецЕсли;
	ОформлениеОрдера.Доступность = СписокВыбораРаспоряжение.Количество()>1;
	
	Если ПриемкаТоваров <> Неопределено Тогда
		СписокВыбораПриемка = ПриемкаТоваров.СписокВыбора;
		СписокВыбораПриемка.Очистить();
		СписокВыбораПриемка.Добавить("Разделена", НСтр("ru='Разделена по документам'"));
		Если ИспользоватьСоглашенияСПоставщиками Тогда
			СписокВыбораПриемка.Добавить("НеРазделена", НСтр("ru='Сгруппирована по соглашениям'"));
		КонецЕсли;
		ПриемкаТоваров.Доступность = СписокВыбораПриемка.Количество()>1;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет настройки вариантов приемки по значению перечисления
//
// Параметры:
//  ВариантПриемкиТоваров	 - ПеречислениеСсылка.ВариантыПриемкиТоваров - Вариант приемки
//  ОформлениеОрдера - Строка - настройка оформления ордера
//  ПриемкаТоваров	 - Строка - настройка деления приемки
// 
Процедура ЗаполнитьНастройкиВариантовПриемки(ВариантПриемкиТоваров, ОформлениеОрдера, ПриемкаТоваров = Неопределено) Экспорт
	
	Если ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамБезЗаказовИНакладных Тогда
		ОформлениеОрдера = "ПоДоговору";
		ПриемкаТоваров = "НеРазделена";
	ИначеЕсли ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамПослеЗаказовИлиНакладных Тогда
		ОформлениеОрдера = "ПослеЗаказа";
		ПриемкаТоваров = "НеРазделена";
	ИначеЕсли ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамПослеНакладных Тогда
		ОформлениеОрдера = "ПослеНакладной";
		ПриемкаТоваров = "НеРазделена";
	ИначеЕсли ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным Тогда
		ОформлениеОрдера = "ПослеЗаказа";
		ПриемкаТоваров = "Разделена";
	ИначеЕсли ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным Тогда
		ОформлениеОрдера = "ПослеНакладной";
		ПриемкаТоваров = "Разделена";
	КонецЕсли;
	
КонецПроцедуры

// Заполняет настройки вариантов приемки по значению перечисления
//
// Параметры:
//  ОформлениеОрдера - Строка - настройка оформления ордера
//  ПриемкаТоваров   - Строка - настройка деления приемки
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ВариантыПриемкиТоваров - Вариант приемки
//
Функция ПолучитьВариантовПриемкиПоНастройкам(ОформлениеОрдера, ПриемкаТоваров) Экспорт
	
	Если ОформлениеОрдера = "ПоДоговору" Тогда
		ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамБезЗаказовИНакладных
	ИначеЕсли ОформлениеОрдера = "ПослеЗаказа" И ПриемкаТоваров = "НеРазделена" Тогда
		ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамПослеЗаказовИлиНакладных
	ИначеЕсли ОформлениеОрдера = "ПослеНакладной" И ПриемкаТоваров = "НеРазделена" Тогда
		ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамПослеНакладных
	ИначеЕсли ОформлениеОрдера = "ПослеЗаказа" И ПриемкаТоваров = "Разделена" Тогда
		ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным;
	ИначеЕсли ОформлениеОрдера = "ПослеНакладной" И ПриемкаТоваров = "Разделена" Тогда
		ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным;
	КонецЕсли;
	
	Возврат ВариантПриемкиТоваров;
	
КонецФункции

// Возвращает признак использования накладной как распоряжения на приемку
//
// Параметры:
//	ВариантПриемкиТоваров - ПеречислениеСсылка.ВариантыПриемкиТоваров - ссылка на вариант приемки
//
// Возвращаемое значение:
//	Булево - Истина, если используется соглашение при приемке.
//
Функция РаспоряжениеНаПриемкуТовараНакладная(Знач ВариантПриемкиТоваров) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ВариантПриемкиТоваров) Тогда
		ВариантПриемкиТоваров = Константы.ВариантПриемкиТоваров.Получить();
	КонецЕсли;
	
	Результат = Ложь;
	
	Если ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным Тогда 
		
		Результат = Истина;
		
	КонецЕсли;
	
	Возврат Результат
	
КонецФункции

// Возвращает признак использования заказа/накладной без заказа как распоряжения на приемку
//
// Параметры:
//	ВариантПриемкиТоваров - ПеречислениеСсылка.ВариантыПриемкиТоваров - ссылка на вариант приемки
//
// Возвращаемое значение:
//	Булево - Истина, если используется соглашение при приемке.
//
Функция РаспоряжениеНаПриемкуТовараЗаказ(Знач ВариантПриемкиТоваров) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ВариантПриемкиТоваров) Тогда
		ВариантПриемкиТоваров = Константы.ВариантПриемкиТоваров.Получить();
	КонецЕсли;
	
	Результат = Ложь;
	
	Если ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным Тогда 
		
		Результат = Истина;
		
	КонецЕсли;
	
	Возврат Результат
	
КонецФункции

// Заполняет список выбора вариантов приемки по соглашениям
//
// Параметры:
//  ОформлениеОрдера - ПолеФормы - поле формы с настройкой оформления ордера
//  ПриемкаТоваров	 - ПолеФормы - поле формы с настройкой приемки товаров
//
Процедура ЗаполнитьСписокВыбораВариантовПриемкиПоДоговорам(ОформлениеОрдера, ПриемкаТоваров, ВариантОформленияЗакупок = Неопределено) Экспорт
	
	ЗначениеОформленияОрдера = "";
	ЗначениеКонстанты = Константы.ВариантПриемкиТоваров.Получить();
	ЗаполнитьНастройкиВариантовПриемки(ЗначениеКонстанты, ЗначениеОформленияОрдера);
	
	ИспользоватьДоговорыСПоставщиками = ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСПоставщиками");
	СписокВыбораРаспоряжение = ОформлениеОрдера.СписокВыбора;
	
	ВыбранаНеотфактурованнаяПоставка = (ВариантОформленияЗакупок = Перечисления.ВариантыОформленияЗакупок.НеотфактурованныеПоставки);
	ВыбраныТоварыВПути = (ВариантОформленияЗакупок = Перечисления.ВариантыОформленияЗакупок.ТоварыВПути);
	
	СписокВыбораРаспоряжение.Очистить();
	Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыПоставщикам") 
		И ВыбранаНеотфактурованнаяПоставка Тогда
		СписокВыбораРаспоряжение.Добавить("ПослеЗаказа", НСтр("ru='После оформления заказа'"));
	ИначеЕсли ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыПоставщикам") 
		И Не ВыбранаНеотфактурованнаяПоставка Тогда
		СписокВыбораРаспоряжение.Добавить("ПослеЗаказа", НСтр("ru='После оформления заказа'"));
		СписокВыбораРаспоряжение.Добавить("ПослеНакладной", НСтр("ru='После оформления накладной'"));
	ИначеЕсли ВыбраныТоварыВПути Тогда
		СписокВыбораРаспоряжение.Добавить("ПослеНакладной", НСтр("ru='После оформления накладной'"));
	ИначеЕсли Не ВыбранаНеотфактурованнаяПоставка Тогда
		СписокВыбораЗначение = ?(ЗначениеОформленияОрдера = "ПослеНакладной", ЗначениеОформленияОрдера, "ПослеЗаказа");
		СписокВыбораРаспоряжение.Добавить(ЗначениеОформленияОрдера, НСтр("ru='После оформления накладной'"));
	КонецЕсли;
	
	Если ИспользоватьДоговорыСПоставщиками Тогда
		СписокВыбораРаспоряжение.Добавить("ПоДоговору", НСтр("ru='По текущему договору (в т.ч. без документов)'"));
	КонецЕсли;
	
	ОформлениеОрдера.Доступность = СписокВыбораРаспоряжение.Количество() > 1;
	
	Если ПриемкаТоваров <> Неопределено Тогда
		СписокВыбораПриемка = ПриемкаТоваров.СписокВыбора;
		СписокВыбораПриемка.Очистить();
		СписокВыбораПриемка.Добавить("Разделена", НСтр("ru='Разделена по документам'"));
		Если ИспользоватьДоговорыСПоставщиками Тогда
			СписокВыбораПриемка.Добавить("НеРазделена", НСтр("ru='Сгруппирована по договорам'"));
		КонецЕсли;
		ПриемкаТоваров.Доступность = СписокВыбораПриемка.Количество() > 1;
	КонецЕсли;
	
КонецПроцедуры

// Получает вариант приемки товаров по распоряжению, по соглашению, либо по одноименной константе.
//
// Параметры:
//  Распоряжение - ДокументСсылка - документ содержащий реквизит "ВариантПриемкиТоваров"
//  Договор		 - СправочникСсылка.ДоговорыКонтрагентов - договор переопределяющий вариант приемки товаров
// 
// Возвращаемое значение:
//   - ПеречислениеСсылка.ВариантыПриемкиТоваров - найденный вариант приемки
//
Функция ПолучитьВариантПриемкиТоваров(Распоряжение = Неопределено, Договор = Неопределено) Экспорт
	
	ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(Распоряжение)
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Распоряжение, "ВариантПриемкиТоваров") Тогда
		ВариантПриемкиТоваров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Распоряжение, "ВариантПриемкиТоваров");
	КонецЕсли;
	Если ЗначениеЗаполнено(Договор) Тогда
		ВариантПриемкиТоваров = ?(ЗначениеЗаполнено(ВариантПриемкиТоваров),
								 ВариантПриемкиТоваров,
								 ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВариантПриемкиТоваров"));
	КонецЕсли;
	
	ВариантПриемкиТоваров = ?(ЗначениеЗаполнено(ВариантПриемкиТоваров),
							 ВариантПриемкиТоваров,
							 Константы.ВариантПриемкиТоваров.Получить());
	
	Возврат ВариантПриемкиТоваров;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииОбновленияЗависимыхРеквизитовВЗаказахИНакладных

// Обновляет реквизиты списания на расходы в табличной части документа, в зависимости от назначения.
// 
// Параметры:
//   СтрокиКоллекции - Массив - массив строк коллекции, в которой нужно обновить реквизиты.
//
Процедура ОчиститьРеквизитыСписанияНаРасходыПриУстановкеНазначенияВТабличнойЧасти(СтрокиКоллекции) Экспорт
	
	Для Каждого СтрокаТаблицы Из СтрокиКоллекции Цикл
	
		Если ЗначениеЗаполнено(СтрокаТаблицы.Назначение) Тогда
			
			СтрокаТаблицы.СписатьНаРасходы = Ложь;
			СтрокаТаблицы.СтатьяРасходов = ПредопределенноеЗначение("ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка");
			СтрокаТаблицы.АналитикаРасходов = Неопределено;
			
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ШаблонУсловияЗакупок()
	
	Возврат Новый Структура("
		|Соглашение,
		|Партнер,
		|Контрагент,
		|Организация,
		|Валюта,
		|ВалютаВзаиморасчетов,
		|ЦенаВключаетНДС,
		|НалогообложениеНДС,
		|Склад,
		|ФормаОплаты,
		|ПорядокОплаты,
		|ГруппаФинансовогоУчета,
		|СтатьяДвиженияДенежныхСредств,
		|СрокПоставки,
		|ХозяйственнаяОперация,
		|ВидЦеныПоставщика,
		|РегистрироватьЦеныПоставщика,
		|ГруппаФинансовогоУчета,
		|СпособРасчетаВознаграждения,
		|ПроцентВознаграждения,
		|УдержатьВознаграждение,
		|ИспользуютсяДоговорыКонтрагентов,
		|ПорядокРасчетов,
		|ВозвращатьМногооборотнуюТару,
		|СрокВозвратаМногооборотнойТары,
		|РассчитыватьДатуВозвратаТарыПоКалендарю,
		|КалендарьВозвратаТары,
		|ТребуетсяЗалогЗаТару,
		|НаправлениеДеятельности
		|");
		
КонецФункции

Функция ТекстЗапросаПроверкаПринимающихсяТоваровПоРаспоряжениям(ТипПоступления)
	
	ТекстЗапросаПоРаспоряжениям = "";
	
	Если ТипПоступления = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		
		ТекстЗапросаПоРаспоряжениям = 
		"ВЫБРАТЬ
		|	ТоварыПоступления.Номенклатура КАК Номенклатура,
		|	ТоварыПоступления.Характеристика КАК Характеристика,
		|	ТоварыПоступления.Серия КАК Серия,
		|	ТоварыПоступления.Склад КАК Склад,
		|	СУММА(ТоварыПоступления.Количество) КАК КоличествоПоступления
		|ПОМЕСТИТЬ втТоварыПоступления
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ТоварыПоступления
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &ВариантПриемкиТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным)
		|				ТОГДА ТоварыПоступления.ЗаказПоставщику В (&МассивРаспоряжений)
		|			КОГДА &ВариантПриемкиТоваров В (ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.НеРазделенаПоЗаказамИНакладным), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.МожетПроисходитьБезЗаказовИНакладных), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.НеРазделенаПоНакладным))
		|				ТОГДА ТоварыПоступления.Ссылка.Соглашение = &Соглашение
		|			КОГДА &ВариантПриемкиТоваров В (ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамБезЗаказовИНакладных), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамПослеЗаказовИлиНакладных), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамПослеНакладных))
		|				ТОГДА ТоварыПоступления.Ссылка.Договор = &Договор
		|		КОНЕЦ
		|	И ТоварыПоступления.Ссылка <> &ДокументПоступления
		|	И ТоварыПоступления.Ссылка.Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыПоступления.Номенклатура,
		|	ТоварыПоступления.Характеристика,
		|	ТоварыПоступления.Серия,
		|	ТоварыПоступления.Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
	ИначеЕсли ТипПоступления = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
		
		ТекстЗапросаПоРаспоряжениям = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА Поступление.Порча
		|			ТОГДА Поступление.НоменклатураОприходование
		|		ИНАЧЕ Поступление.Номенклатура
		|	КОНЕЦ КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА Поступление.Порча
		|			ТОГДА Поступление.ХарактеристикаОприходование
		|		ИНАЧЕ Поступление.Характеристика
		|	КОНЕЦ КАК Характеристика,
		|	Поступление.Серия КАК Серия,
		|	Поступление.Ссылка.Склад КАК Склад,
		|	СУММА(Поступление.Количество) КАК КоличествоПоступления
		|ПОМЕСТИТЬ втТоварыПоступления
		|ИЗ
		|	Документ.ВозвратТоваровОтКлиента.Товары КАК Поступление
		|ГДЕ
		|	&ВариантПриемкиТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным)
		|	И Поступление.Ссылка.ЗаявкаНаВозвратТоваровОтКлиента В(&МассивРаспоряжений)
		|	И Поступление.Ссылка <> &ДокументПоступления
		|	И Поступление.Ссылка.Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА Поступление.Порча
		|			ТОГДА Поступление.НоменклатураОприходование
		|		ИНАЧЕ Поступление.Номенклатура
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА Поступление.Порча
		|			ТОГДА Поступление.ХарактеристикаОприходование
		|		ИНАЧЕ Поступление.Характеристика
		|	КОНЕЦ,
		|	Поступление.Серия,
		|	Поступление.Ссылка.Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
	КонецЕсли;
	
	ТекстЗапросаПоРаспоряжениям = ТекстЗапросаПоРаспоряжениям + 
	"ВЫБРАТЬ
	|	ТоварыКПоступлению.Номенклатура КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика КАК Характеристика,
	|	ТоварыКПоступлению.Серия КАК Серия,
	|	ТоварыКПоступлению.Склад КАК Склад,
	|	СУММА(ТоварыКПоступлению.КОформлениюПоступленийПоОрдерам) КАК Количество
	|ПОМЕСТИТЬ втТоварыКОформлению
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению КАК ТоварыКПоступлению
	|ГДЕ
	|	НЕ &ЗаполнитьПоПоступлениям
	|	И ТоварыКПоступлению.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ТоварыКПоступлению.ДокументПоступления В (&МассивРаспоряженийОтборДляРегистра)
	|	И ТоварыКПоступлению.Склад В (&Склад)
	|	И ТоварыКПоступлению.КОформлениюПоступленийПоОрдерам > 0
	|СГРУППИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	Склад
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыКПоступлению.Номенклатура КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика КАК Характеристика,
	|	ТоварыКПоступлению.Серия КАК Серия,
	|	ТоварыКПоступлению.Склад КАК Склад,
	|	СУММА(ТоварыКПоступлению.КОформлениюПоступленийПоНакладным) КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению КАК ТоварыКПоступлению
	|ГДЕ
	|	&ЗаполнитьПоПоступлениям
	|	И ТоварыКПоступлению.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ТоварыКПоступлению.ДокументПоступления В (&МассивРаспоряженийОтборДляРегистра)
	|	И ТоварыКПоступлению.Склад В (&Склад)
	|	И ТоварыКПоступлению.КОформлениюПоступленийПоНакладным > 0
	|СГРУППИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТоварыКОформлению.Номенклатура КАК Номенклатура,
	|	втТоварыКОформлению.Характеристика КАК Характеристика,
	|	втТоварыКОформлению.Характеристика КАК Серия,
	|	втТоварыКОформлению.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА втТоварыКОформлению.Количество > ЕСТЬNULL(втТоварыПоступления.КоличествоПоступления, 0)
	|			ТОГДА втТоварыКОформлению.Количество - ЕСТЬNULL(втТоварыПоступления.КоличествоПоступления, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ втНеоформленныеТовары
	|ИЗ
	|	втТоварыКОформлению КАК втТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТоварыПоступления КАК втТоварыПоступления
	|		ПО втТоварыКОформлению.Номенклатура = втТоварыПоступления.Номенклатура
	|			И втТоварыКОформлению.Характеристика = втТоварыПоступления.Характеристика
	|			И втТоварыКОформлению.Серия = втТоварыПоступления.Серия
	|			И втТоварыКОформлению.Склад = втТоварыПоступления.Склад
	|ГДЕ
	|	втТоварыКОформлению.Количество - ЕСТЬNULL(втТоварыПоступления.КоличествоПоступления, 0) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(НеоформленныеТовары.Количество), 0) КАК Количество
	|ИЗ
	|	втНеоформленныеТовары КАК НеоформленныеТовары";
	
	Возврат ТекстЗапросаПоРаспоряжениям;
	
КонецФункции

Процедура УстановитьПараметрОтбораРаспоряженийВРегистреТоварыКПоступлению(Запрос)
	
	МассивРаспоряжений = Новый Массив;
	
	Если Запрос.Параметры.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным Тогда
		Если Запрос.Параметры.Свойство("МассивРаспоряжений") Тогда
			МассивРаспоряжений = Запрос.Параметры.МассивРаспоряжений;
		Иначе
   			МассивРаспоряжений.Добавить(Запрос.Параметры.ДокументПоступления);
		КонецЕсли;
	ИначеЕсли Запрос.Параметры.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным Тогда
   		МассивРаспоряжений.Добавить(Запрос.Параметры.ДокументПоступления);
	ИначеЕсли Запрос.Параметры.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.НеРазделенаПоЗаказамИНакладным
		Или Запрос.Параметры.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.МожетПроисходитьБезЗаказовИНакладных
 		Или Запрос.Параметры.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.НеРазделенаПоНакладным Тогда
  		МассивРаспоряжений.Добавить(Запрос.Параметры.Соглашение);
	ИначеЕсли Запрос.Параметры.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамБезЗаказовИНакладных
		Или Запрос.Параметры.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамПослеЗаказовИлиНакладных
		Или Запрос.Параметры.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамПослеНакладных Тогда
  		МассивРаспоряжений.Добавить(Запрос.Параметры.Договор);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивРаспоряженийОтборДляРегистра", МассивРаспоряжений);	
	
КонецПроцедуры

Функция ТекстЗапросаПроверкаПринимающихсяТоваровПоНакладной()
	
	ТекстЗапросаПоНакладной = 
	"ВЫБРАТЬ
	|	СУММА(ТоварыКПоступлению.КОформлениюПоступленийПоОрдерам) КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению КАК ТоварыКПоступлению
	|ГДЕ
	|	ТоварыКПоступлению.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ТоварыКПоступлению.ДокументПоступления В (&МассивРаспоряженийОтборДляРегистра)
	|	И ТоварыКПоступлению.Склад В (&Склад)
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКПоступлению.КОформлениюПоступленийПоОрдерам) > 0";
	
	Возврат ТекстЗапросаПоНакладной;
	
КонецФункции

Функция ТекстЗапросаПоРаспоряжениям(ТипПоступления)
	
	ТекстЗапросаПоРаспоряжениям = "";
	
	Если ТипПоступления = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		
		ТекстЗапросаПоРаспоряжениям = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА &ВариантПриемкиТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным)
		|			ТОГДА ТоварыПоступления.ЗаказПоставщику
		|		КОГДА &ВариантПриемкиТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным)
		|			ТОГДА ТоварыПоступления.Ссылка
		|		КОГДА &ВариантПриемкиТоваров В (ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.НеРазделенаПоНакладным), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.НеРазделенаПоЗаказамИНакладным), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.МожетПроисходитьБезЗаказовИНакладных))
		|			ТОГДА ТоварыПоступления.Ссылка.Соглашение
		|		КОГДА &ВариантПриемкиТоваров В (ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамБезЗаказовИНакладных), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамПослеЗаказовИлиНакладных), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамПослеНакладных))
		|			ТОГДА ТоварыПоступления.Ссылка.Договор
		|		КОНЕЦ КАК Распоряжение,
		|	ТоварыПоступления.Номенклатура КАК Номенклатура,
		|	ТоварыПоступления.Характеристика КАК Характеристика,
		|	ТоварыПоступления.Серия КАК Серия,
		|	ТоварыПоступления.Склад КАК Склад,
		|	СУММА(ТоварыПоступления.Количество) КАК КоличествоПоступления
		|ПОМЕСТИТЬ втТоварыПоступления
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ТоварыПоступления
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &ВариантПриемкиТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным)
		|				ТОГДА ТоварыПоступления.ЗаказПоставщику В (&МассивРаспоряжений)
		|			КОГДА &ВариантПриемкиТоваров В (ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.НеРазделенаПоНакладным), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.НеРазделенаПоЗаказамИНакладным), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.МожетПроисходитьБезЗаказовИНакладных))
		|				ТОГДА ТоварыПоступления.Ссылка.Соглашение = &Соглашение
		|			КОГДА &ВариантПриемкиТоваров В (ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамБезЗаказовИНакладных), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамПослеЗаказовИлиНакладных), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамПослеНакладных))
		|				ТОГДА ТоварыПоступления.Ссылка.Договор = &Договор
		|		КОНЕЦ
		|	И ТоварыПоступления.Ссылка <> &Ссылка
		|	И ТоварыПоступления.Ссылка.Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА &ВариантПриемкиТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным)
		|			ТОГДА ТоварыПоступления.ЗаказПоставщику
		|		КОГДА &ВариантПриемкиТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным)
		|			ТОГДА ТоварыПоступления.Ссылка
		|		КОГДА &ВариантПриемкиТоваров В (ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.НеРазделенаПоНакладным), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.НеРазделенаПоЗаказамИНакладным), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.МожетПроисходитьБезЗаказовИНакладных))
		|			ТОГДА ТоварыПоступления.Ссылка.Соглашение
		|		КОГДА &ВариантПриемкиТоваров В (ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамБезЗаказовИНакладных), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамПослеЗаказовИлиНакладных), ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПоДоговорамПослеНакладных))
		|			ТОГДА ТоварыПоступления.Ссылка.Договор
		|		КОНЕЦ,
		|	ТоварыПоступления.Номенклатура,
		|	ТоварыПоступления.Характеристика,
		|	ТоварыПоступления.Серия,
		|	ТоварыПоступления.Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
	ИначеЕсли ТипПоступления = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
		
		ТекстЗапросаПоРаспоряжениям = 
		"ВЫБРАТЬ
		|	Поступление.Ссылка КАК Распоряжение,
		|	ВЫБОР
		|		КОГДА Поступление.Порча
		|			ТОГДА Поступление.НоменклатураОприходование
		|		ИНАЧЕ Поступление.Номенклатура
		|	КОНЕЦ КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА Поступление.Порча
		|			ТОГДА Поступление.ХарактеристикаОприходование
		|		ИНАЧЕ Поступление.Характеристика
		|	КОНЕЦ КАК Характеристика,
		|	Поступление.Серия КАК Серия,
		|	Поступление.Ссылка.Склад КАК Склад,
		|	СУММА(Поступление.Количество) КАК КоличествоПоступления
		|ПОМЕСТИТЬ втТоварыПоступления
		|ИЗ
		|	Документ.ВозвратТоваровОтКлиента.Товары КАК Поступление
		|ГДЕ
		|	Поступление.Ссылка.ЗаявкаНаВозвратТоваровОтКлиента В(&МассивРаспоряжений)
		|	И Поступление.Ссылка <> &Ссылка
		|	И Поступление.Ссылка.Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	Поступление.Ссылка,
		|	ВЫБОР
		|		КОГДА Поступление.Порча
		|			ТОГДА Поступление.НоменклатураОприходование
		|		ИНАЧЕ Поступление.Номенклатура
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА Поступление.Порча
		|			ТОГДА Поступление.ХарактеристикаОприходование
		|		ИНАЧЕ Поступление.Характеристика
		|	КОНЕЦ,
		|	Поступление.Серия,
		|	Поступление.Ссылка.Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
	КонецЕсли;
	
	
	
	ТекстЗапросаПоРаспоряжениям = ТекстЗапросаПоРаспоряжениям +
	"ВЫБРАТЬ
	|	ТоварыКПоступлению.ДокументПоступления КАК ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика КАК Характеристика,
	|	ТоварыКПоступлению.Серия КАК Серия,
	|	ТоварыКПоступлению.Склад КАК Склад,
	|	СУММА(ТоварыКПоступлению.КОформлениюПоступленийПоОрдерам) КАК Количество
	|ПОМЕСТИТЬ втТоварыКОформлениюПоступления
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению КАК ТоварыКПоступлению
	|ГДЕ
	|	НЕ &ЗаполнитьПоПоступлениям
	|	И ТоварыКПоступлению.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ТоварыКПоступлению.ДокументПоступления В (&МассивРаспоряженийОтборДляРегистра)
	|	И ТоварыКПоступлению.Склад В (&Склад)
	|	И ТоварыКПоступлению.КОформлениюПоступленийПоОрдерам > 0
	|СГРУППИРОВАТЬ ПО
	|	ДокументПоступления,
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	Склад
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыКПоступлению.ДокументПоступления КАК ДокументПоступления,
	|	ТоварыКПоступлению.Номенклатура КАК Номенклатура,
	|	ТоварыКПоступлению.Характеристика КАК Характеристика,
	|	ТоварыКПоступлению.Серия КАК Серия,
	|	ТоварыКПоступлению.Склад КАК Склад,
	|	СУММА(ТоварыКПоступлению.КОформлениюПоступленийПоНакладным) КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению КАК ТоварыКПоступлению
	|ГДЕ
	|	&ЗаполнитьПоПоступлениям
	|	И ТоварыКПоступлению.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ТоварыКПоступлению.ДокументПоступления В (&МассивРаспоряженийОтборДляРегистра)
	|	И ТоварыКПоступлению.Склад В (&Склад)
	|	И ТоварыКПоступлению.КОформлениюПоступленийПоНакладным > 0
	|СГРУППИРОВАТЬ ПО
	|	ДокументПоступления,
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	Склад
	|;
	|
	|ВЫБРАТЬ
	|	втТоварыКОформлениюПоступления.ДокументПоступления КАК Распоряжение,
	|	втТоварыКОформлениюПоступления.Номенклатура КАК Номенклатура,
	|	втТоварыКОформлениюПоступления.Характеристика КАК Характеристика,
	|	втТоварыКОформлениюПоступления.Серия КАК Серия,
	|	втТоварыКОформлениюПоступления.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА втТоварыКОформлениюПоступления.Количество > ЕСТЬNULL(втТоварыПоступления.КоличествоПоступления, 0)
	|			ТОГДА втТоварыКОформлениюПоступления.Количество - ЕСТЬNULL(втТоварыПоступления.КоличествоПоступления, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Количество,
	|	ЛОЖЬ КАК НоменклатураДобавлена
	|ИЗ
	|	втТоварыКОформлениюПоступления КАК втТоварыКОформлениюПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТоварыПоступления КАК втТоварыПоступления
	|			ПО втТоварыКОформлениюПоступления.ДокументПоступления = втТоварыПоступления.Распоряжение
	|			И втТоварыКОформлениюПоступления.Номенклатура = втТоварыПоступления.Номенклатура
	|			И втТоварыКОформлениюПоступления.Характеристика = втТоварыПоступления.Характеристика
	|			И втТоварыКОформлениюПоступления.Серия = втТоварыПоступления.Серия
	|			И втТоварыКОформлениюПоступления.Склад = втТоварыПоступления.Склад
	|ГДЕ
	|	втТоварыКОформлениюПоступления.Количество  - ЕСТЬNULL(втТоварыПоступления.КоличествоПоступления, 0) > 0
	|	И втТоварыКОформлениюПоступления.Склад В(&Склад)
	|
	|;
	|
	|ВЫБРАТЬ
	|	Товары.ЗаказПоставщику КАК ЗаказПоставщику,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.Склад КАК Склад,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Номенклатура КАК Номенклатура
	|ИЗ
	|	Товары КАК Товары
	|ГДЕ
	|	НЕ ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|;
	|
	|ВЫБРАТЬ
	|	Товары.Упаковка КАК Упаковка,
	|	&ТекстЗапросаКоэффициентУпаковки КАК Коэффициент
	|ИЗ
	|	Товары КАК Товары
	|ГДЕ
	|	Товары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Склад КАК Склад
	|ИЗ
	|	Товары КАК Товары
	|ГДЕ
	|	НЕ ВЫРАЗИТЬ(Склад КАК Справочник.Склады).ИспользоватьОрдернуюСхемуПриПоступлении
	|	ИЛИ &Дата < ВЫРАЗИТЬ(Склад КАК Справочник.Склады).ДатаНачалаОрдернойСхемыПриПоступлении";
	
	ТекстЗапросаПоРаспоряжениям = СтрЗаменить(ТекстЗапросаПоРаспоряжениям, 
		"&ТекстЗапросаКоэффициентУпаковки", 
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки("Товары.Упаковка", "Товары.Номенклатура"));
	
	Возврат ТекстЗапросаПоРаспоряжениям;
	
КонецФункции

Функция ТекстЗапросаПоНакладнойИлиСоглашению()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаОстатков.Номенклатура,
		|	ТаблицаОстатков.Характеристика,
		|	ТаблицаОстатков.Серия,
		|	ТаблицаОстатков.Назначение,
		|	ТаблицаОстатков.Склад,
		|	СУММА(ТаблицаОстатков.Количество) КАК Количество,
		|	ЛОЖЬ КАК НоменклатураДобавлена
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТоварыКОформлениюПоступленияОстатки.Номенклатура КАК Номенклатура,
		|		ТоварыКОформлениюПоступленияОстатки.Характеристика КАК Характеристика,
		|		ТоварыКОформлениюПоступленияОстатки.Серия КАК Серия,
		|		ТоварыКОформлениюПоступленияОстатки.Назначение КАК Назначение,
		|		ТоварыКОформлениюПоступленияОстатки.Склад КАК Склад,
		|		ВЫБОР
		|			КОГДА &ЗаполнитьПоПоступлениям = ИСТИНА ТОГДА
		|				-ТоварыКОформлениюПоступленияОстатки.КОформлениюПоступленийПоНакладнымОстаток
		|			ИНАЧЕ
		|				ТоварыКОформлениюПоступленияОстатки.КОформлениюПоступленийПоОрдерамОстаток
		|		КОНЕЦ КАК Количество
		|	ИЗ
		|		РегистрНакопления.ТоварыКПоступлению.Остатки(
		|				,
		|				ДокументПоступления В (&МассивРаспоряженийОтборДляРегистра)) КАК ТоварыКОформлениюПоступленияОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТоварыКОформлениюПоступления.Номенклатура,
		|		ТоварыКОформлениюПоступления.Характеристика,
		|		ТоварыКОформлениюПоступления.Серия,
		|		ТоварыКОформлениюПоступления.Назначение,
		|		ТоварыКОформлениюПоступления.Склад,
		|		ВЫБОР
		|			КОГДА &ЗаполнитьПоПоступлениям = ИСТИНА ТОГДА
		|				ТоварыКОформлениюПоступления.КОформлениюПоступленийПоНакладным
		|			ИНАЧЕ
		|				ТоварыКОформлениюПоступления.КОформлениюПоступленийПоОрдерам
		|		КОНЕЦ КАК Количество
		|	ИЗ
		|		РегистрНакопления.ТоварыКПоступлению КАК ТоварыКОформлениюПоступления
		|	ГДЕ
		|		ТоварыКОформлениюПоступления.Регистратор = &Ссылка) КАК ТаблицаОстатков
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаОстатков.Номенклатура,
		|	ТаблицаОстатков.Характеристика,
		|	ТаблицаОстатков.Серия,
		|	ТаблицаОстатков.Назначение,
		|	ТаблицаОстатков.Склад
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТаблицаОстатков.Количество) > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Серия КАК Серия,
		|	Товары.Назначение КАК Назначение,
		|	Товары.Склад КАК Склад,
		|	Товары.Упаковка КАК Упаковка,
		|	Товары.Количество КАК Количество
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Номенклатура КАК Номенклатура
		|ИЗ
		|	Товары КАК Товары
		|ГДЕ
		|	НЕ ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Упаковка КАК Упаковка,
		|	ВЫРАЗИТЬ(Товары.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения).Числитель /
		|	ВЫРАЗИТЬ(Товары.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения).Знаменатель КАК Коэффициент
		|ИЗ
		|	Товары КАК Товары
		|ГДЕ
		|	Товары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Склад КАК Склад
		|ИЗ
		|	Товары КАК Товары
		|ГДЕ
		|	(НЕ ВЫРАЗИТЬ(Товары.Склад КАК Справочник.Склады).ИспользоватьОрдернуюСхемуПриПоступлении
		|			ИЛИ &Дата < ВЫРАЗИТЬ(Товары.Склад КАК Справочник.Склады).ДатаНачалаОрдернойСхемыПриПоступлении)";
		
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура СкорректироватьСписокВыбораХозяйственнойОперацииПоступления(Форма) Экспорт
	
	Если Не Форма.ПолучитьФункциональнуюОпциюФормы("ИспользоватьКомиссиюПриЗакупках") Тогда
		
		ЭлементСписка = Форма.Элементы.ХозяйственнаяОперация.СписокВыбора.НайтиПоЗначению(Перечисления.ХозяйственныеОперации.ПриемНаКомиссию);
		Если ЭлементСписка <> Неопределено Тогда
			Форма.Элементы.ХозяйственнаяОперация.СписокВыбора.Удалить(ЭлементСписка);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Форма.ПолучитьФункциональнуюОпциюФормы("ИспользоватьУправленческуюОрганизацию") Тогда
		
		ЭлементСписка = Форма.Элементы.ХозяйственнаяОперация.СписокВыбора.НайтиПоЗначению(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет);
		Если ЭлементСписка <> Неопределено Тогда
			Форма.Элементы.ХозяйственнаяОперация.СписокВыбора.Удалить(ЭлементСписка);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Форма.ПолучитьФункциональнуюОпциюФормы("ИспользоватьИмпортныеЗакупки") Тогда
		
		ЭлементСписка = Форма.Элементы.ХозяйственнаяОперация.СписокВыбора.НайтиПоЗначению(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту);
		Если ЭлементСписка <> Неопределено Тогда
			Форма.Элементы.ХозяйственнаяОперация.СписокВыбора.Удалить(ЭлементСписка);
		КонецЕсли;
		
		ЭлементСписка = Форма.Элементы.ХозяйственнаяОперация.СписокВыбора.НайтиПоЗначению(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС);
		Если ЭлементСписка <> Неопределено Тогда
			Форма.Элементы.ХозяйственнаяОперация.СписокВыбора.Удалить(ЭлементСписка);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Инициализирует и возвращает структуру дополнительных параметров отбора договоров.
// 
// Возвращаемое значение:
//  Структура - структура со свойствами:
// 	* ВалютаВзаиморасчетов — СправочникСсылка.Валюты — валюта взаиморасчетов по договору
// 	* Налогообложение — ПеречислениеСсылка.ТипыНалогообложенияНДС — тип налогообложения НДС
// 	* НаправлениеДеятельности — СправочникСсылка.НаправленияДеятельности — направление деятельности по договору
// 	* ВариантОформленияЗакупок — ПеречислениеСсылка.ВариантыОформленияЗакупок — вариант оформления закупок по договору
//
Функция ДополнительныеПараметрыОтбораДоговоров() Экспорт
	
	Возврат Новый Структура(
		"ВалютаВзаиморасчетов, 
		|Налогообложение,	
		|НаправлениеДеятельности, 
		|ВариантОформленияЗакупок");
	
КонецФункции

#Область ПроцедурыФормированияВременныхТаблиц

// Формирует запрос к шапке документа и помещает во временную таблицу
//
// Параметры:
// ТекстЗапроса     - Строка - текстовая строка, к которой необходимо добавить текст запроса
// ПараметрыЗапроса - Структура - структура, содержащая параметры запроса
// ДокументЗакупки  - ДокументОбъект, который необходимо проверить
//
Процедура СформироватьЗапросВременнаяТаблицаШапкаДокументаЗакупки(ТекстЗапроса,
	                                                              ПараметрыЗапроса,
	                                                              Знач ДокументЗакупки)

	ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ДокументЗакупки.Партнер                                 КАК Партнер,
		|	ДокументЗакупки.Контрагент                              КАК Контрагент,
		|	ДокументЗакупки.Валюта                                  КАК Валюта,
		|	ДокументЗакупки.ВалютаВзаиморасчетов                    КАК ВалютаВзаиморасчетов,
		|	ДокументЗакупки.ЦенаВключаетНДС                         КАК ЦенаВключаетНДС,
		|	ДокументЗакупки.НалогообложениеНДС                      КАК НалогообложениеНДС,
		|	ДокументЗакупки.ХозяйственнаяОперация                   КАК ХозяйственнаяОперация,
		|	ДокументЗакупки.Соглашение                              КАК Соглашение,
		|	ДокументЗакупки.Дата                                    КАК Дата,
		|	ДокументЗакупки.ЖелаемаяДатаПоступления                 КАК ЖелаемаяДатаПоступления,
		|	ДокументЗакупки.Организация                             КАК Организация,
		|	ДокументЗакупки.Склад                                   КАК Склад,
		|	ДокументЗакупки.ФормаОплаты                             КАК ФормаОплаты,
		|	ДокументЗакупки.СуммаДокумента                          КАК СуммаДокумента,
		|	ДокументЗакупки.РассчитыватьДатуВозвратаТарыПоКалендарю КАК РассчитыватьДатуВозвратаТарыПоКалендарю,
		|	ДокументЗакупки.КалендарьВозвратаТары                   КАК КалендарьВозвратаТары,
		|	ДокументЗакупки.СрокВозвратаМногооборотнойТары          КАК СрокВозвратаМногооборотнойТары
		|ПОМЕСТИТЬ
		|	ВременнаяТаблицаДокументЗакупки
		|ИЗ
		|	&ДокументЗакупки КАК ДокументЗакупки
		|;
		|";
	
	// Сформируем необходимые колонки таблицы шапки документа закупки
	
	ТипВалюта   = Новый ОписаниеТипов("СправочникСсылка.Валюты");
	ТипДата     = Новый ОписаниеТипов("Дата");
	ТипБулево   = Новый ОписаниеТипов("Булево");
	ТипЧисло    = Новый ОписаниеТипов("Число");
	
	ТаблицаДокумента = Новый ТаблицаЗначений();
	
	ТаблицаДокумента.Колонки.Добавить("Партнер",                              Новый ОписаниеТипов("СправочникСсылка.Партнеры"));
	ТаблицаДокумента.Колонки.Добавить("Контрагент",                           Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаДокумента.Колонки.Добавить("Валюта",                               ТипВалюта);
	ТаблицаДокумента.Колонки.Добавить("ВалютаВзаиморасчетов",                 ТипВалюта);
	ТаблицаДокумента.Колонки.Добавить("ЦенаВключаетНДС",                      ТипБулево);
	ТаблицаДокумента.Колонки.Добавить("НалогообложениеНДС",                   Новый ОписаниеТипов("ПеречислениеСсылка.ТипыНалогообложенияНДС"));
	ТаблицаДокумента.Колонки.Добавить("Дата",                                 ТипДата);
	ТаблицаДокумента.Колонки.Добавить("ЖелаемаяДатаПоступления",              ТипДата);
	ТаблицаДокумента.Колонки.Добавить("Организация",                          Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДокумента.Колонки.Добавить("Склад",                                Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТаблицаДокумента.Колонки.Добавить("ФормаОплаты",                          Новый ОписаниеТипов("ПеречислениеСсылка.ФормыОплаты"));
	ТаблицаДокумента.Колонки.Добавить("СуммаДокумента",                       Новый ОписаниеТипов("Число"));
	ТаблицаДокумента.Колонки.Добавить("Соглашение",                           Новый ОписаниеТипов("СправочникСсылка.СоглашенияСПоставщиками"));
	ТаблицаДокумента.Колонки.Добавить("ХозяйственнаяОперация",                Новый ОписаниеТипов("ПеречислениеСсылка.ХозяйственныеОперации"));
	ТаблицаДокумента.Колонки.Добавить("РассчитыватьДатуВозвратаТарыПоКалендарю", ТипБулево);
	ТаблицаДокумента.Колонки.Добавить("КалендарьВозвратаТары",                Новый ОписаниеТипов("СправочникСсылка.ПроизводственныеКалендари"));
	ТаблицаДокумента.Колонки.Добавить("СрокВозвратаМногооборотнойТары",       ТипЧисло);
	
	// Добавим строку и заполним значениями из шапки документа
	
	НоваяСтрока = ТаблицаДокумента.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ДокументЗакупки);
	
	ПараметрыЗапроса.Вставить("ДокументЗакупки", ТаблицаДокумента);

КонецПроцедуры

// Формирует запрос к тч Товары документа и помещает во временную таблицу
//
// Параметры:
// ТекстЗапроса           -Строка - текстовая строка, к которой необходимо добавить текст запроса
// ПараметрыЗапроса       - Структура - структура, содержащая параметры запроса
// ДокументЗакупки        - ДокументОбъект, который необходимо проверить
// ИмяТаблицы             - Строка - имя таблицы, к которой будет сформирован запрос
//
Процедура СформироватьЗапросВременнаяТаблицаТоварыДокументаЗакупки(ТекстЗапроса,
	                                                               ПараметрыЗапроса,
	                                                               ДокументЗакупки,
	                                                               ИмяТаблицы)
	
	ПредставлениеТабличнойЧасти = НСтр("ru='Товары'");
	ИмяТабличнойЧасти = "Товары";
	
	Если ИмяТаблицы = "Документ.ЗаказПоставщику" Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	&ПредставлениеТабличнойЧасти  КАК ПредставлениеТабличнойЧасти,
			|	&ИмяТабличнойЧасти            КАК ИмяТабличнойЧасти,
			|	Товары.НомерСтроки            КАК НомерСтроки,
			|	Товары.НоменклатураПоставщика КАК НоменклатураПоставщика,
			|	Товары.Номенклатура           КАК Номенклатура,
			|	Товары.Характеристика         КАК Характеристика,
			|	Товары.Цена                   КАК Цена,
			|	Товары.Сумма                  КАК Сумма,
			|	Товары.ВидЦеныПоставщика      КАК ВидЦеныПоставщика,
			|	Товары.Упаковка               КАК Упаковка,
			|	Товары.СтатьяРасходов         КАК СтатьяРасходов,
			|	Товары.СтавкаНДС              КАК СтавкаНДС,
			|	Товары.ПроцентРучнойСкидки    КАК ПроцентРучнойСкидки,
			|	Товары.Склад                  КАК Склад,
			|	Товары.Отменено               КАК Отменено,
			|	Товары.ДатаПоступления        КАК ДатаПоступления,
			|	Товары.Цена - Товары.Цена * Товары.ПроцентРучнойСкидки / 100 КАК ЦенаСоСкидкой
			|ПОМЕСТИТЬ
			|	ВременнаяТаблицаТовары
			|ИЗ
			|	&Товары КАК Товары
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура,
			|	Характеристика
			|;
			|";
			
		ИменаКолонок = "НомерСтроки,
			|Номенклатура,
			|Характеристика,
			|НоменклатураПоставщика,
			|Цена,
			|Сумма,
			|ВидЦеныПоставщика,
			|Упаковка,
			|СтатьяРасходов,
			|СтавкаНДС,
			|ПроцентРучнойСкидки,
			|Склад,
			|Отменено,
			|ДатаПоступления";
		
	ИначеЕсли ИмяТаблицы = "Документ.ПриобретениеТоваровУслуг" Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	&ПредставлениеТабличнойЧасти  КАК ПредставлениеТабличнойЧасти,
			|	&ИмяТабличнойЧасти            КАК ИмяТабличнойЧасти,
			|	Товары.НомерСтроки            КАК НомерСтроки,
			|	Товары.НоменклатураПоставщика КАК НоменклатураПоставщика,
			|	Товары.Номенклатура           КАК Номенклатура,
			|	Товары.Характеристика         КАК Характеристика,
			|	Товары.Цена                   КАК Цена,
			|	Товары.Сумма                  КАК Сумма,
			|	Товары.ВидЦеныПоставщика      КАК ВидЦеныПоставщика,
			|	Товары.Упаковка               КАК Упаковка,
			|	Товары.СтатьяРасходов         КАК СтатьяРасходов,
			|	Товары.СтавкаНДС              КАК СтавкаНДС,
			|	Товары.ПроцентРучнойСкидки    КАК ПроцентРучнойСкидки,
			|	Товары.Склад                  КАК Склад,
			|	Товары.Цена - Товары.Цена * Товары.ПроцентРучнойСкидки / 100 КАК ЦенаСоСкидкой
			|ПОМЕСТИТЬ
			|	ВременнаяТаблицаТовары
			|ИЗ
			|	&Товары КАК Товары
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура,
			|	Характеристика
			|;
			|";
			
		ИменаКолонок = "НомерСтроки,
			|Номенклатура,
			|Характеристика,
			|НоменклатураПоставщика,
			|Цена,
			|Сумма,
			|ВидЦеныПоставщика,
			|Упаковка,
			|СтатьяРасходов,
			|СтавкаНДС,
			|ПроцентРучнойСкидки,
			|Склад";
			
	ИначеЕсли ИмяТаблицы = "Документ.ПередачаТоваровМеждуОрганизациями" Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	&ПредставлениеТабличнойЧасти  КАК ПредставлениеТабличнойЧасти,
			|	&ИмяТабличнойЧасти            КАК ИмяТабличнойЧасти,
			|	Товары.НомерСтроки            КАК НомерСтроки,
			|	Товары.Номенклатура           КАК Номенклатура
			|ПОМЕСТИТЬ
			|	ВременнаяТаблицаТовары
			|ИЗ
			|	&Товары КАК Товары
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура
			|;
			|";
			
		ИменаКолонок = "НомерСтроки,
		|Номенклатура";
		
	ИначеЕсли ИмяТаблицы = "Документ.АктОРасхожденияхПослеПриемки" Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	&ПредставлениеТабличнойЧасти  КАК ПредставлениеТабличнойЧасти,
			|	&ИмяТабличнойЧасти            КАК ИмяТабличнойЧасти,
			|	Товары.НомерСтроки            КАК НомерСтроки,
			|	Товары.НоменклатураПоставщика КАК НоменклатураПоставщика,
			|	Товары.Номенклатура           КАК Номенклатура,
			|	Товары.Характеристика         КАК Характеристика,
			|	Товары.Склад                  КАК Склад
			|ПОМЕСТИТЬ
			|	ВременнаяТаблицаТовары
			|ИЗ
			|	&Товары КАК Товары
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура,
			|	Характеристика
			|;
			|";
			
		ИменаКолонок = "НомерСтроки,
			|Номенклатура,
			|Характеристика,
			|НоменклатураПоставщика,
			|Склад";
			
		
	ИначеЕсли ИмяТаблицы = "Документ.ДоверенностьВыданная" Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	&ПредставлениеТабличнойЧасти  КАК ПредставлениеТабличнойЧасти,
			|	&ИмяТабличнойЧасти            КАК ИмяТабличнойЧасти,
			|	Товары.НомерСтроки            КАК НомерСтроки,
			|	Товары.НоменклатураПоставщика КАК НоменклатураПоставщика,
			|	Товары.Номенклатура           КАК Номенклатура,
			|	Товары.Характеристика         КАК Характеристика
			|ПОМЕСТИТЬ
			|	ВременнаяТаблицаТовары
			|ИЗ
			|	&Товары КАК Товары
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура,
			|	Характеристика
			|;
			|";
			
		ИменаКолонок = "НомерСтроки,
			|Номенклатура,
			|Характеристика,
			|НоменклатураПоставщика";
			
	ИначеЕсли ИмяТаблицы = "Документ.ВыкупВозвратнойТарыУПоставщика" Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	&ПредставлениеТабличнойЧасти  КАК ПредставлениеТабличнойЧасти,
			|	&ИмяТабличнойЧасти            КАК ИмяТабличнойЧасти,
			|	Товары.НомерСтроки            КАК НомерСтроки,
			|	Товары.НоменклатураПоставщика КАК НоменклатураПоставщика,
			|	Товары.Номенклатура           КАК Номенклатура,
			|	Товары.Характеристика         КАК Характеристика
			|ПОМЕСТИТЬ
			|	ВременнаяТаблицаТовары
			|ИЗ
			|	&Товары КАК Товары
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура,
			|	Характеристика
			|;
			|";
			
		ИменаКолонок = "НомерСтроки,
			|Номенклатура,
			|Характеристика,
			|НоменклатураПоставщика";
		
	ИначеЕсли ИмяТаблицы = "Документ.КорректировкаПриобретения" Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	&ПредставлениеТабличнойЧасти  КАК ПредставлениеТабличнойЧасти,
			|	&ИмяТабличнойЧасти            КАК ИмяТабличнойЧасти,
			|	Товары.НомерСтроки            КАК НомерСтроки,
			|	Товары.НоменклатураПоставщика КАК НоменклатураПоставщика,
			|	Товары.Номенклатура           КАК Номенклатура,
			|	Товары.Характеристика         КАК Характеристика,
			|	Товары.Цена                   КАК Цена,
			|	Товары.Сумма                  КАК Сумма,
			|	Товары.Упаковка               КАК Упаковка,
			|	Товары.СтатьяРасходов         КАК СтатьяРасходов,
			|	Товары.СтавкаНДС              КАК СтавкаНДС,
			|	Товары.Склад                  КАК Склад
			|ПОМЕСТИТЬ
			|	ВременнаяТаблицаТовары
			|ИЗ
			|	&Товары КАК Товары
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура,
			|	Характеристика
			|;
			|";
			
		ИменаКолонок = "НомерСтроки,
			|Номенклатура,
			|Характеристика,
			|НоменклатураПоставщика,
			|Цена,
			|Сумма,
			|Упаковка,
			|СтатьяРасходов,
			|СтавкаНДС,
			|Склад";
		
	ИначеЕсли ИмяТаблицы = "Документ.ВозвратТоваровПоставщику" Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	&ПредставлениеТабличнойЧасти  КАК ПредставлениеТабличнойЧасти,
			|	&ИмяТабличнойЧасти            КАК ИмяТабличнойЧасти,
			|	Товары.НомерСтроки            КАК НомерСтроки,
			|	Товары.Номенклатура           КАК Номенклатура,
			|	Товары.Характеристика         КАК Характеристика,
			|	Товары.Цена                   КАК Цена,
			|	Товары.Сумма                  КАК Сумма
			|ПОМЕСТИТЬ
			|	ВременнаяТаблицаТовары
			|ИЗ
			|	&Товары КАК Товары
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура,
			|	Характеристика
			|;
			|";
			
		ИменаКолонок = "НомерСтроки,
			|Номенклатура,
			|Характеристика,
			|Цена,
			|Сумма";
		
	КонецЕсли;
	
	ПараметрыЗапроса.Вставить("Товары", ДокументЗакупки[ИмяТабличнойЧасти].Выгрузить(,ИменаКолонок));
	ПараметрыЗапроса.Вставить("ИмяТабличнойЧасти", ИмяТабличнойЧасти);
	ПараметрыЗапроса.Вставить("ПредставлениеТабличнойЧасти", ПредставлениеТабличнойЧасти);
	
КонецПроцедуры

// Формирует запрос к курсам валют на дату документа и помещает во временную таблицу
//
// Параметры:
// ТекстЗапроса - Строка - текстовая строка, к которой необходимо добавить текст запроса
//
Процедура СформироватьЗапросВременнаяТаблицаКурсыВалют(ТекстЗапроса)

	ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	КурсыВалютСрезПоследних.Валюта    КАК Валюта,
		|	КурсыВалютСрезПоследних.Курс      КАК Курс,
		|	КурсыВалютСрезПоследних.Кратность КАК Кратность
		|ПОМЕСТИТЬ
		|	ВременнаяТаблицаКурсыВалют
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(&Дата) КАК КурсыВалютСрезПоследних
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|";

КонецПроцедуры

// Формирует запрос к тч ЭтапыОплаты документа и помещает во временную таблицу
//
// Параметры:
//		ТекстЗапроса     - Строка - текстовая строка, к которой необходимо добавить текст запроса
//		ПараметрыЗапроса - Структура - структура, содержащая параметры запроса
//		ДокументЗакупки  - ДокументОбъект, ДокументСсылка , документ, к которому необходимо сформировать запрос
//		ПоСсылке         - Булево - флаг, если Истина проверка осуществляется по ссылке на объект
//		ИмяТаблицы       - Строка - имя таблицы документа, к которой необходимо сформировать запрос
//
Процедура СформироватьЗапросВременнаяТаблицаЭтапыОплаты(ТекстЗапроса, ПараметрыЗапроса, ДокументЗакупки, ПоСсылке = Ложь, ИмяТаблицы = "")

	Если ПоСсылке Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	ЭтапыГрафикаОплаты.НомерСтроки     КАК НомерСтроки,
			|	ЭтапыГрафикаОплаты.ВариантОплаты   КАК ВариантОплаты,
			|	ЭтапыГрафикаОплаты.ДатаПлатежа     КАК ДатаПлатежа,
			|	ЭтапыГрафикаОплаты.ПроцентПлатежа  КАК ПроцентПлатежа
			|ПОМЕСТИТЬ
			|	ВременнаяТаблицаЭтапыГрафикаОплаты
			|ИЗ
			|" + ИмяТаблицы + ".ЭтапыГрафикаОплаты КАК ЭтапыГрафикаОплаты
			|ГДЕ
			|	ЭтапыГрафикаОплаты.Ссылка = &ДокументЗакупки
			|ИНДЕКСИРОВАТЬ ПО
			|	НомерСтроки
			|;
			|";
			
			ПараметрыЗапроса.Вставить("ДокументЗакупки", ДокументЗакупки);
		
	Иначе
		
		ТекстЗапроса = ТекстЗапроса + "
			|ВЫБРАТЬ
			|	ЭтапыГрафикаОплаты.НомерСтроки     КАК НомерСтроки,
			|	ЭтапыГрафикаОплаты.ВариантОплаты   КАК ВариантОплаты,
			|	ЭтапыГрафикаОплаты.ДатаПлатежа     КАК ДатаПлатежа,
			|	ЭтапыГрафикаОплаты.ПроцентПлатежа  КАК ПроцентПлатежа
			|ПОМЕСТИТЬ
			|	ВременнаяТаблицаЭтапыГрафикаОплаты
			|ИЗ
			|	&ЭтапыГрафикаОплаты КАК ЭтапыГрафикаОплаты
			|;
			|";
	
		ПараметрыЗапроса.Вставить("ЭтапыГрафикаОплаты", ДокументЗакупки.ЭтапыГрафикаОплаты.Выгрузить(, "НомерСтроки,ДатаПлатежа,ВариантОплаты,ПроцентПлатежа"));
		
	КонецЕсли;
		
КонецПроцедуры

// Формирует запрос по элементам справочника "Склады" и помещает во временную таблицу
// К корректным складам относятся:
// 		- Склад, равный складу соглашения из временной таблицы по документу
// 		- Склады, входящие в иерархию склада соглашения из временной таблицы по документу
//
Процедура СформироватьЗапросВременнаяТаблицаСкладыКорректныеПоСоглашению(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + "
	|ВЫБРАТЬ
	|	Склады.Ссылка КАК Склад
	|ПОМЕСТИТЬ СкладыКорректныеПоСоглашению
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ВременнаяТаблицаДокументЗакупки КАК ВременнаяТаблицаДокумента
	|ПО
	|	ИСТИНА
	|ГДЕ
	|	ВременнаяТаблицаДокумента.Соглашение.Склад ЕСТЬ НЕ NULL
	|	И ВременнаяТаблицаДокумента.Соглашение.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|	И Склады.Ссылка В ИЕРАРХИИ
	|			(ВЫБРАТЬ
	|				ВременнаяТаблицаДокумента.Соглашение.Склад
	|			ИЗ
	|				ВременнаяТаблицаДокументЗакупки КАК ВременнаяТаблицаДокумента)
	|;
	|";
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыФормированияЗапросовПроверкиКорректностиЗаполненияДокументов

// Формирует текст запроса для проверки тч Товары на наличие дублей номенклатуры поставщика
//
// Параметры:
// ТекстЗапроса - Строка - текстовая строка, к которой необходимо добавить текст запроса
//
Процедура СформироватьЗапросКорректностьЗаполненияНоменклатурыПоставщика(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	МАКСИМУМ(ВложенныйЗапросПоДублям.НомерСтроки)         КАК НомерСтроки,
		|	ВложенныйЗапросПоДублям.НоменклатураПоставщика        КАК НоменклатураПоставщика
		|ИЗ (
		|	ВЫБРАТЬ
		|		МАКСИМУМ(ДокументТовары.НомерСтроки)              КАК НомерСтроки,
		|		ДокументТовары.НоменклатураПоставщика             КАК НоменклатураПоставщика,
		|		ДокументТовары.Номенклатура                       КАК Номенклатура,
		|		ДокументТовары.Характеристика                     КАК Характеристика
		|	ИЗ
		|		ВременнаяТаблицаТовары КАК ДокументТовары
		|	ГДЕ
		|		ДокументТовары.НоменклатураПоставщика <> ЗНАЧЕНИЕ(Справочник.НоменклатураПоставщиков.ПустаяСсылка)
		|		И ДокументТовары.НоменклатураПоставщика.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	СГРУППИРОВАТЬ ПО
		|		ДокументТовары.НоменклатураПоставщика,
		|		ДокументТовары.Номенклатура,
		|		ДокументТовары.Характеристика
		|) КАК ВложенныйЗапросПоДублям
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапросПоДублям.НоменклатураПоставщика
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(*) > 1
		|;
		|";
		
КонецПроцедуры

// Формирует текст запроса для проверки наличия услуг в документе закупки
//
// Параметры:
// ТекстЗапроса - Строка - текстовая строка, к которой необходимо добавить текст запроса
//
Процедура СформироватьЗапросНаличиеУслугВДокументеЗакупки(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ДокументТовары.НомерСтроки                  КАК НомерСтроки,
		|	ДокументТовары.Номенклатура                 КАК Номенклатура,
		|	ДокументТовары.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ДокументТовары
		|ГДЕ
		|	ДокументТовары.Номенклатура.ТипНоменклатуры НЕ В
		|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки ВОЗР
		|;
		|";
	
КонецПроцедуры

// Формирует текст запроса для проверки заполненности аналитик услуг для услуг в документе закупки
//
// Параметры:
// ТекстЗапроса - Строка - текстовая строка, к которой необходимо добавить текст запроса
//
Процедура СформироватьЗапросКорректностьЗаполненияАналитикиУслуг(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ДокументТовары.НомерСтроки  КАК НомерСтроки,
		|	ДокументТовары.Номенклатура КАК Номенклатура
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ДокументТовары
		|ГДЕ
		|	ДокументТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		|	И ДокументТовары.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки ВОЗР
		|;
		|";
	
КонецПроцедуры

// Формирует текст запроса для проверки заполненности склада
//
// Параметры:
// ТекстЗапроса - Строка - текстовая строка, к которой необходимо добавить текст запроса
//
Процедура СформироватьЗапросКорректностьЗаполненияСклада(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьОшибкиЗаполненияСклад
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ВременнаяТаблицаДокументЗакупки КАК ВременнаяТаблицаДокументЗакупки
		|ПО
		|	ИСТИНА
		|ГДЕ
		|	ВременнаяТаблицаТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|	И ВременнаяТаблицаДокументЗакупки.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|;
		|";
	
КонецПроцедуры

// Формирует текст запроса для проверки заполненности реквизита "Склад" в строках табличной части товаров
// Проверка должна выполняться только если:
// 		- в строке табличной части выбрана номенклатура типа товар;
//
Процедура СформироватьЗапросКорректностьЗаполненияСкладовВТабличнойЧастиТоварыБезСкладаВШапке(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.ПредставлениеТабличнойЧасти КАК ПредставлениеТабличнойЧасти,
		|	ВременнаяТаблицаТовары.ИмяТабличнойЧасти КАК ИмяТабличнойЧасти,
		|	ВременнаяТаблицаТовары.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|ГДЕ
		|	ВременнаяТаблицаТовары.Номенклатура.ТипНоменклатуры В
		|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|	И ВременнаяТаблицаТовары.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|;
		|";
	
КонецПроцедуры

// Формирует текст запроса для проверки заполненности реквизита "Склад" в строках табличной части товаров
// Проверка должна выполняться только если:
// 		- в строке табличной части выбрана номенклатура типа товар;
// 		- в шапке документа заполнен реквизит "Склад".
//
Процедура СформироватьЗапросКорректностьЗаполненияСкладовВТабличнойЧастиТовары(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.ПредставлениеТабличнойЧасти КАК ПредставлениеТабличнойЧасти,
		|	ВременнаяТаблицаТовары.ИмяТабличнойЧасти КАК ИмяТабличнойЧасти,
		|	ВременнаяТаблицаТовары.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ВременнаяТаблицаДокументЗакупки КАК ВременнаяТаблицаДокументЗакупки
		|ПО
		|	ИСТИНА
		|ГДЕ
		|	ВременнаяТаблицаДокументЗакупки.Склад.ЭтоГруппа
		|	И ВременнаяТаблицаДокументЗакупки.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|	И ВременнаяТаблицаТовары.Номенклатура.ТипНоменклатуры В
		|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|	И ВременнаяТаблицаТовары.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|;
		|";
	
КонецПроцедуры

// Формирует проверки для контроля завершения поступления по заказу
// и для контроля завершения расчетов по заказу
//
// Параметры:
// КонтролируемыйЗаказ - ДокументСсылка.ЗаказПоставщику - контролируемый документ.
// Отказ - Булево - параметр Отказ.
//
Процедура ВыполнитьКонтрольЗаказаПослеПроведения(КонтролируемыйЗаказ, Отказ) Экспорт
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику КАК ЗаказПоставщику
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки(, 
		|			ЗаказПоставщику = &Ссылка
		|			И ЗаказПоставщику.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Закрыт)
		|			И ЗаказПоставщику.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию)
		|			И ЗаказПоставщику.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи)
		|	) КАК ЗаказыПоставщикамОстатки
		|ГДЕ
		|	&КонтролироватьПоступление
		|	И &ИспользоватьСтатусы
		|	И (ЗаказыПоставщикамОстатки.ЗаказаноОстаток <> 0 
		|		ИЛИ ЗаказыПоставщикамОстатки.КОформлениюОстаток <> 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТоварыКПоступлениюОстатки.ДокументПоступления КАК ЗаказПоставщику
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению.Остатки(, 
		|			ДокументПоступления = &Ссылка
		|			И ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ЗаказПоставщику).Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Закрыт)
		|			И ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ЗаказПоставщику).ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию)
		|			И ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ЗаказПоставщику).ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи)
		|	) КАК ТоварыКПоступлениюОстатки
		|ГДЕ
		|	&КонтролироватьПоступление
		|	И &ИспользоватьСтатусы
		|	И (ТоварыКПоступлениюОстатки.КОформлениюОрдеровОстаток + ТоварыКПоступлениюОстатки.ПринимаетсяОстаток) <> 0
		|;
		|
		|ВЫБРАТЬ
		|	РасчетыСПоставщикамиОстатки.Валюта КАК Валюта,
		|	-1*(РасчетыСПоставщикамиОстатки.КОплатеОстаток) КАК КОплатеОстаток
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками.Остатки(, 
		|			ЗаказПоставщику = &Ссылка
		|			И ЗаказПоставщику.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Закрыт)
		|			И ЗаказПоставщику.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию)
		|			И ЗаказПоставщику.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи)
		|			И ЗаказПоставщику.ПорядокРасчетов <> ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
		|	) КАК РасчетыСПоставщикамиОстатки
		|ГДЕ
		|	&КонтролироватьРасчеты
		|	И &ИспользоватьСтатусы
		|	И РасчетыСПоставщикамиОстатки.КОплатеОстаток < 0
		|");
		
	КонтролироватьПоступление = ПолучитьФункциональнуюОпцию("НеЗакрыватьЗаказыПоставщикамБезПолногоПоступления");
	КонтролироватьРасчеты     = ПолучитьФункциональнуюОпцию("НеЗакрыватьЗаказыПоставщикамБезПолнойОплаты");
	ИспользоватьСтатусы       = ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыЗаказовПоставщикам");
	
	Запрос.УстановитьПараметр("Ссылка",                    КонтролируемыйЗаказ);
	Запрос.УстановитьПараметр("КонтролироватьПоступление", КонтролироватьПоступление);
	Запрос.УстановитьПараметр("КонтролироватьРасчеты",     КонтролироватьРасчеты);
	Запрос.УстановитьПараметр("ИспользоватьСтатусы",       ИспользоватьСтатусы);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ВыборкаПоступление = Результат[0].Выбрать();
	ВыборкаРасчеты  = Результат[1].Выбрать();
	
	Если ВыборкаПоступление.Следующий() Тогда 
		Если ТипЗнч(КонтролируемыйЗаказ) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			ТекстОшибки = НСтр("ru='Документ ""%Заказ%"" поступил не полностью.
			|Закрытие заказа возможно только с полностью поступившими/отмененными строками'");
		КонецЕсли;
		
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Заказ%", КонтролируемыйЗаказ);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			КонтролируемыйЗаказ,
			,
			,
			Отказ);
	КонецЕсли;
	
	Если ВыборкаРасчеты.Следующий() Тогда 
		Если ТипЗнч(КонтролируемыйЗаказ) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			ТекстОшибки = НСтр("ru='Расчеты по документу ""%Заказ%"" не завершены.
			|Для закрытия заказа требуется оплата %СуммаКОплате% %Валюта%.
			|Закрытие заказа возможно только с полностью оплаченными/отмененными строками'");
		КонецЕсли;
		
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Заказ%",        КонтролируемыйЗаказ);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СуммаКОплате%", Строка(ВыборкаРасчеты.КОплатеОстаток));
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Валюта%",       Строка(ВыборкаРасчеты.Валюта));
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			КонтролируемыйЗаказ,
			,
			,
			Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Формирует тескт запроса для контроля завершения поступления по заказу
// и для контроля завершения расчетов по заказу
//
//	Возвращаемое значение:
//		Строка
//
Функция ТекстЗапросаКонтрольЗаказаПослеПроведения() Экспорт
	
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КонтрольЗакрытия.ЗаказПоставщику КАК ЗаказПоставщику
		|ПОМЕСТИТЬ
		|	ВтОстаткиПоЗаказам
		|ИЗ (ВЫБРАТЬ
		|	ЗаказыПоставщикамОстатки.ЗаказПоставщику КАК ЗаказПоставщику
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.Остатки(, 
		|			ЗаказПоставщику В (&МассивДокументов)
		|			И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Закрыт)
		|			И ЗаказПоставщику.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию)
		|			И ЗаказПоставщику.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи)
		|	) КАК ЗаказыПоставщикамОстатки
		|ГДЕ
		|	&КонтролироватьПоступление
		|	И &ИспользоватьСтатусы
		|	И (ЗаказыПоставщикамОстатки.ЗаказаноОстаток > 0 
		|		ИЛИ ЗаказыПоставщикамОстатки.КОформлениюОстаток > 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТоварыКПоступлениюОстатки.ДокументПоступления КАК ЗаказПоставщику
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению.Остатки(, 
		|			ДокументПоступления В (&МассивДокументов)
		|			И ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ЗаказПоставщику).Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Закрыт)
		|			И ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ЗаказПоставщику).ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию)
		|			И ВЫРАЗИТЬ(ДокументПоступления КАК Документ.ЗаказПоставщику).ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи)
		|	) КАК ТоварыКПоступлениюОстатки
		|ГДЕ
		|	&КонтролироватьПоступление
		|	И &ИспользоватьСтатусы
		|	И (ТоварыКПоступлениюОстатки.КОформлениюОрдеровОстаток + ТоварыКПоступлениюОстатки.ПринимаетсяОстаток) > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасчетыСПоставщикамиОстатки.ЗаказПоставщику КАК ЗаказПоставщику
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками.Остатки(, 
		|			ЗаказПоставщику В (&МассивДокументов)
		|			И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Закрыт)
		|			И ЗаказПоставщику.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию)
		|			И ЗаказПоставщику.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи)
		|			И ЗаказПоставщику.ПорядокРасчетов <> ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
		|	) КАК РасчетыСПоставщикамиОстатки
		|ГДЕ
		|	&КонтролироватьРасчеты
		|	И &ИспользоватьСтатусы
		|	И РасчетыСПоставщикамиОстатки.КОплатеОстаток < 0) КАК КонтрольЗакрытия
		|;"
	
КонецФункции

#КонецОбласти

#Область ПроцедурыФормированияЗапросовПроверкиСоответствияДокументовУсловиямЗакупки

// Формирует текст запроса для проверки шапки документа закупки
//
// Параметры:
// ТекстЗапроса - Строка - текстовая строка, к которой необходимо добавить текст запроса
//
Процедура СформироватьЗапросШапкаДокументаЗакупки(ТекстЗапроса, ИмяТаблицы)
	
	ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА СоглашениеСПоставщиком.Партнер ЕСТЬ НЕ NULL И
		|			ДокументЗакупки.Партнер <> СоглашениеСПоставщиком.Партнер
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияПартнер,
		|	ВЫБОР
		|		КОГДА СоглашениеСПоставщиком.Статус ЕСТЬ НЕ NULL И
		|			СоглашениеСПоставщиком.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийСПоставщиками.НеСогласовано)
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиСтатусСоглашения,
		|	ВЫБОР
		|		КОГДА СоглашениеСПоставщиком.Контрагент ЕСТЬ НЕ NULL И
		|			СоглашениеСПоставщиком.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) И
		|			ДокументЗакупки.Контрагент <> СоглашениеСПоставщиком.Контрагент
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияКонтрагент,
		|	ВЫБОР
		|		КОГДА СоглашениеСПоставщиком.Организация ЕСТЬ НЕ NULL И
		|			СоглашениеСПоставщиком.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) И
		|			ДокументЗакупки.Организация <> СоглашениеСПоставщиком.Организация
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияОрганизация,
		|	ВЫБОР
		|		КОГДА СоглашениеСПоставщиком.ФормаОплаты ЕСТЬ НЕ NULL И
		|			СоглашениеСПоставщиком.ФормаОплаты <> ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.ПустаяСсылка) И
		|			ДокументЗакупки.ФормаОплаты <> СоглашениеСПоставщиком.ФормаОплаты
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияФормаОплаты,
		|	ВЫБОР
		|		КОГДА СоглашениеСПоставщиком.Валюта ЕСТЬ НЕ NULL И
		|			ДокументЗакупки.Валюта <> СоглашениеСПоставщиком.Валюта
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияВалюта,
		|	ВЫБОР
		|		КОГДА СоглашениеСПоставщиком.ВозвращатьМногооборотнуюТару <> &ВозвратМногооборотнойТары
		|		ТОГДА 
		|			ИСТИНА
		|		ИНАЧЕ 
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияВозвращатьМногооборотнуюТару,
		|" + ?(ИмяТаблицы <> "Документ.ПриобретениеТоваровУслуг","
		|	ЛОЖЬ КАК ЕстьОшибкиЗаполненияВалютаВзаиморасчетов,
		|	ЛОЖЬ КАК ЕстьОшибкиДатаВозвратаМногооборотнойТары,
		|	ВЫБОР
		|		КОГДА ДокументЗакупки.СрокВозвратаМногооборотнойТары <> СоглашениеСПоставщиком.СрокВозвратаМногооборотнойТары
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияСрокВозвратаМногооборотнойТары,
		|","
		|	ВЫБОР
		|		КОГДА СоглашениеСПоставщиком.Валюта ЕСТЬ НЕ NULL И
		|			ДокументЗакупки.ВалютаВзаиморасчетов <> СоглашениеСПоставщиком.Валюта
		|			И ДокументЗакупки.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаКомиссию)
		|			И ДокументЗакупки.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи)
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияВалютаВзаиморасчетов,
		|	ИСТИНА КАК ЕстьОшибкиДатаВозвратаМногооборотнойТары,
		|	ЛОЖЬ КАК ЕстьОшибкиЗаполненияСрокВозвратаМногооборотнойТары,
		|") + "
		|	ВЫБОР
		|		КОГДА СоглашениеСПоставщиком.ЦенаВключаетНДС ЕСТЬ НЕ NULL И
		|			ДокументЗакупки.ЦенаВключаетНДС <> СоглашениеСПоставщиком.ЦенаВключаетНДС
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияЦенаВключаетНДС,
		|	ВЫБОР
		|		КОГДА СоглашениеСПоставщиком.НалогообложениеНДС ЕСТЬ НЕ NULL И
		|			ДокументЗакупки.НалогообложениеНДС <> СоглашениеСПоставщиком.НалогообложениеНДС
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияНалогообложениеНДС,
		|	ВЫБОР
		|		КОГДА НЕ СоглашениеСПоставщиком.ХозяйственнаяОперация ЕСТЬ NULL
		|			И НЕ ДокументЗакупки.ХозяйственнаяОперация ЕСТЬ NULL
		|			И ДокументЗакупки.ХозяйственнаяОперация <> СоглашениеСПоставщиком.ХозяйственнаяОперация
		|			И ДокументЗакупки.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
		|		ТОГДА
		|			ИСТИНА
		|		КОГДА
		|			ДокументЗакупки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
		|			И СоглашениеСПоставщиком.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
		|			И НЕ СоглашениеСПоставщиком.ХозяйственнаяОперация ЕСТЬ NULL
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияХозяйственнаяОперация,
		|	ЕСТЬNULL(СоглашениеСПоставщиком.Склад.ЭтоГруппа, ЛОЖЬ) КАК СкладСоглашениеЕстьГруппа,
		|	ВЫБОР
		|		КОГДА СоглашениеСПоставщиком.Склад ЕСТЬ НЕ NULL
		|			И СоглашениеСПоставщиком.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|			И СкладыКорректныеПоСоглашению.Склад ЕСТЬ NULL
		|		ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияСклад,
		|" + ?(ИмяТаблицы = "Справочник.СоглашенияСПоставщиками","
		|	ИСТИНА КАК ЭтоСоглашение,
		|","
		|	ЛОЖЬ КАК ЭтоСоглашение,
		|") + "
		|	&ВозвратМногооборотнойТары                      КАК ВозвратМногооборотнойТары,
		|	ДокументЗакупки.Партнер                         КАК Партнер,
		|	ДокументЗакупки.Контрагент                      КАК Контрагент,
		|	ДокументЗакупки.Организация                     КАК Организация,
		|	ДокументЗакупки.ФормаОплаты                     КАК ФормаОплаты,
		|	ДокументЗакупки.Соглашение                      КАК Соглашение,
		|	ДокументЗакупки.Валюта                          КАК Валюта,
		|	ДокументЗакупки.Склад                           КАК Склад,
		|	ДокументЗакупки.ЦенаВключаетНДС                 КАК ЦенаВключаетНДС,
		|	ДокументЗакупки.НалогообложениеНДС              КАК НалогообложениеНДС,
		|	СоглашениеСПоставщиком.Партнер                  КАК ПартнерСоглашение,
		|	СоглашениеСПоставщиком.Контрагент               КАК КонтрагентСоглашение,
		|	СоглашениеСПоставщиком.Организация              КАК ОрганизацияСоглашение,
		|	СоглашениеСПоставщиком.ФормаОплаты              КАК ФормаОплатыСоглашение,
		|	СоглашениеСПоставщиком.Валюта                   КАК ВалютаСоглашение,
		|	СоглашениеСПоставщиком.ДатаНачалаДействия       КАК ДатаНачалаДействияСоглашение,
		|	СоглашениеСПоставщиком.ДатаОкончанияДействия    КАК ДатаОкончанияДействияСоглашение,
		|	СоглашениеСПоставщиком.Склад                    КАК СкладСоглашение,
		|	СоглашениеСПоставщиком.ЦенаВключаетНДС          КАК ЦенаВключаетНДССоглашение,
		|	СоглашениеСПоставщиком.НалогообложениеНДС       КАК НалогообложениеНДССоглашение,
		|	СоглашениеСПоставщиком.ХозяйственнаяОперация    КАК ХозяйственнаяОперацияСоглашение,
		|	СоглашениеСПоставщиком.СрокВозвратаМногооборотнойТары          КАК СрокВозвратаМногооборотнойТары,
		|	СоглашениеСПоставщиком.РассчитыватьДатуВозвратаТарыПоКалендарю КАК РассчитыватьДатуВозвратаТарыПоКалендарю,
		|	СоглашениеСПоставщиком.КалендарьВозвратаТары    КАК КалендарьВозвратаТары
		|ИЗ
		|	ВременнаяТаблицаДокументЗакупки                 КАК ДокументЗакупки
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.СоглашенияСПоставщиками КАК СоглашениеСПоставщиком
		|ПО 
		|	ДокументЗакупки.Соглашение = СоглашениеСПоставщиком.Ссылка
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	СкладыКорректныеПоСоглашению КАК СкладыКорректныеПоСоглашению
		|ПО 
		|	ДокументЗакупки.Склад = СкладыКорректныеПоСоглашению.Склад
		|;
		|";
	
КонецПроцедуры

// Формирует текст запроса для проверки тч Товары документа закупки
//
// Параметры:
// ТекстЗапроса - Строка - текстовая строка, к которой необходимо добавить текст запроса
//
Процедура СформироватьЗапросТоварыДокументаЗакупки(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.НомерСтроки                    КАК НомерСтроки,
		|	ВложенныйЗапрос.Номенклатура                   КАК Номенклатура,
		|	ВложенныйЗапрос.Характеристика                 КАК Характеристика,
		|	ВложенныйЗапрос.ВалютаЦены                     КАК ВалютаЦены,
		|	ВложенныйЗапрос.ЕстьОшибкиЗаполненияСтавкаНДС  КАК ЕстьОшибкиЗаполненияСтавкаНДС,
		|	ВложенныйЗапрос.ЕстьОшибкиОбратноеОбложениеНДС КАК ЕстьОшибкиОбратноеОбложениеНДС,
		|	ВложенныйЗапрос.СтавкаНДССоглашение            КАК СтавкаНДССоглашение,
		|	ВложенныйЗапрос.МаксимальноДопустимаяЦена      КАК МаксимальноДопустимаяЦена,
		|	ВЫБОР
		|		КОГДА
		|			ВложенныйЗапрос.КонтролироватьЦеныЗакупки
		|			И ВложенныйЗапрос.Цена > ВложенныйЗапрос.МаксимальноДопустимаяЦена
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиМаксимальноДопустимаяЦена
		|ИЗ (
		|ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.НомерСтроки            КАК НомерСтроки,
		|	ВременнаяТаблицаДокументЗакупки.Валюта        КАК ВалютаЦены,
		|	ВременнаяТаблицаТовары.Номенклатура           КАК Номенклатура,
		|	ВременнаяТаблицаТовары.Характеристика         КАК Характеристика,
		|	ВременнаяТаблицаТовары.Цена                   КАК Цена,
		|	ВременнаяТаблицаТовары.СтавкаНДС              КАК СтавкаНДС,
		|	ВЫБОР
		|		КОГДА 
		|			ВременнаяТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) 
		|			И &ВозвратМногооборотнойТары
		|		ТОГДА
		|			ЛОЖЬ
		|		КОГДА
		|			ВременнаяТаблицаДокументЗакупки.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
		|			И ВременнаяТаблицаТовары.СтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|		ТОГДА
		|			ИСТИНА
		|		КОГДА
		|			ВременнаяТаблицаДокументЗакупки.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
		|			И ВременнаяТаблицаТовары.СтавкаНДС <> ВременнаяТаблицаТовары.Номенклатура.СтавкаНДС
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияСтавкаНДС,
		|	ВЫБОР
		|		КОГДА
		|			ВременнаяТаблицаДокументЗакупки.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя)
		|			И НЕ ВременнаяТаблицаТовары.Номенклатура.ОблагаетсяНДСУПокупателя
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиОбратноеОбложениеНДС,
		|	ВЫБОР
		|		КОГДА
		|			ВременнаяТаблицаДокументЗакупки.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
		|		ТОГДА
		|			ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|		КОГДА
		|			ВременнаяТаблицаДокументЗакупки.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
		|		ТОГДА
		|			ВременнаяТаблицаТовары.Номенклатура.СтавкаНДС
		|	КОНЕЦ КАК СтавкаНДССоглашение,
		|	ВЫБОР
		|		КОГДА
		|			ЦеныНоменклатурыПоставщиковСрезПоследних.Цена ЕСТЬ НЕ NULL
		|			И СоглашениеШапка.КонтролироватьЦеныЗакупки
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК КонтролироватьЦеныЗакупки,
		|	ВЫБОР
		|		КОГДА
		|			ЦеныНоменклатурыПоставщиковСрезПоследних.Цена ЕСТЬ NULL
		|		ТОГДА
		|			0
		|		ИНАЧЕ
		|			ВЫРАЗИТЬ(
		|			ВЫБОР
		|				КОГДА
		|					ВременнаяТаблицаТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|				ТОГДА
		|					&ТекстЗапросаКоэффициентУпаковки1
		|				ИНАЧЕ
		|					1
		|			КОНЕЦ
		|			* ЦеныНоменклатурыПоставщиковСрезПоследних.Цена/ЕстьNULL(&ТекстЗапросаКоэффициентУпаковки2,1)
		|			* ВЫБОР
		|				КОГДА ВременнаяТаблицаДокументЗакупки.Валюта <> ЦеныНоменклатурыПоставщиковСрезПоследних.Валюта
		|					ТОГДА ВЫБОР
		|							КОГДА ЕСТЬNULL(КурсыВалютыЦены.Кратность, 0) > 0
		|								И ЕСТЬNULL(КурсыВалютыЦены.Курс, 0) > 0
		|								И ЕСТЬNULL(КурсыВалюты.Кратность, 0) > 0
		|								И ЕСТЬNULL(КурсыВалюты.Курс, 0) > 0
		|							ТОГДА
		|								(КурсыВалютыЦены.Курс * КурсыВалюты.Кратность)
		|								/ (КурсыВалюты.Курс * КурсыВалютыЦены.Кратность)
		|							ИНАЧЕ 0
		|						КОНЕЦ
		|				ИНАЧЕ
		|					1
		|			КОНЕЦ КАК ЧИСЛО(15, 2))
		|	КОНЕЦ КАК МаксимальноДопустимаяЦена
		|ИЗ
		| ВременнаяТаблицаТовары
		|		
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ВременнаяТаблицаДокументЗакупки КАК ВременнаяТаблицаДокументЗакупки
		|ПО
		|	ИСТИНА
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.СоглашенияСПоставщиками КАК СоглашениеШапка
		|ПО
		|	ВременнаяТаблицаДокументЗакупки.Соглашение = СоглашениеШапка.Ссылка
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.ЦеныНоменклатурыПоставщиков.СрезПоследних(КОНЕЦПЕРИОДА(&Дата, ДЕНЬ),
		|				ВидЦеныПоставщика В
		|				(ВЫБРАТЬ
		|					ВременнаяТаблицаДокументЗакупки.Соглашение.ВидЦеныПоставщика
		|				ИЗ
		|					ВременнаяТаблицаДокументЗакупки КАК ВременнаяТаблицаДокументЗакупки) И
		|				(Номенклатура, Характеристика) В
		|				(ВЫБРАТЬ
		|					ВременнаяТаблицаТовары.Номенклатура,
		|					ВременнаяТаблицаТовары.Характеристика
		|				ИЗ
		|					ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары)
		|) КАК ЦеныНоменклатурыПоставщиковСрезПоследних
		|ПО
		|	ВременнаяТаблицаТовары.Номенклатура = ЦеныНоменклатурыПоставщиковСрезПоследних.Номенклатура
		|	И ВременнаяТаблицаТовары.Характеристика = ЦеныНоменклатурыПоставщиковСрезПоследних.Характеристика
		|	И ВременнаяТаблицаТовары.ВидЦеныПоставщика = ЦеныНоменклатурыПоставщиковСрезПоследних.ВидЦеныПоставщика
		|	И СоглашениеШапка.КонтролироватьЦеныЗакупки
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.КурсыВалют.СрезПоследних(&Дата) КАК КурсыВалютыЦены
		|ПО 
		|	ЦеныНоменклатурыПоставщиковСрезПоследних.Валюта = КурсыВалютыЦены.Валюта
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата,
		|				Валюта В
		|				(ВЫБРАТЬ
		|					ВременнаяТаблицаДокументЗакупки.Валюта
		|				ИЗ
		|					ВременнаяТаблицаДокументЗакупки КАК ВременнаяТаблицаДокументЗакупки)
		|) КАК КурсыВалюты
		|	По Истина
		|) КАК ВложенныйЗапрос
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки ВОЗР
		|;
		|";
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ВременнаяТаблицаТовары.Упаковка",
		"ВременнаяТаблицаТовары.Номенклатура"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ЦеныНоменклатурыПоставщиковСрезПоследних.Упаковка",
		"ЦеныНоменклатурыПоставщиковСрезПоследних.Номенклатура"));
		
КонецПроцедуры

// Формирует текст запроса для проверки вхождения цен документа закупки в допустимый диапазон
//
// Параметры:
// ТекстЗапроса     - Строка - текстовая строка, к которой необходимо добавить текст запроса
// ПараметрыЗапроса - Структура - структура, содержащая параметры запроса
// ДокументЗакупки  - ДокументОбъект, который необходимо проверить
//
Процедура СформироватьЗапросВхождениеЦенВДопустимыйДиапазон(ТекстЗапроса)
		
	ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.НомерСтроки                 КАК НомерСтроки,
		|	ВложенныйЗапрос.Номенклатура                КАК Номенклатура,
		|	ВложенныйЗапрос.Характеристика              КАК Характеристика,
		|	ВложенныйЗапрос.ВалютаЦены                  КАК ВалютаЦены,
		|	ВложенныйЗапрос.Цена                        КАК Цена,
		|	ВложенныйЗапрос.МаксимальноДопустимаяЦена   КАК МаксимальноДопустимаяЦена,
		|	ВЫБОР
		|		КОГДА
		|			НЕ МаксимальнаяЦенаОтсутствует
		|			И ВложенныйЗапрос.Цена > ВложенныйЗапрос.МаксимальноДопустимаяЦена
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ                                       КАК ЕстьОшибкиМаксимальноДопустимаяЦена
		|ИЗ (
		|ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.НомерСтроки          КАК НомерСтроки,
		|	ВременнаяТаблицаТовары.Номенклатура         КАК Номенклатура,
		|	ВременнаяТаблицаТовары.Характеристика       КАК Характеристика,
		|	ВременнаяТаблицаДокументЗакупки.Валюта      КАК ВалютаЦены,
		|	ВременнаяТаблицаТовары.ЦенаСоСкидкой        КАК Цена,
		|	ВЫБОР
		|		КОГДА
		|			МаксимальныеЦеныНоменклатурыСрезПоследних.Цена ЕСТЬ NULL
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК МаксимальнаяЦенаОтсутствует,
		|	ВЫРАЗИТЬ(ЕСТЬNULL(МаксимальныеЦеныНоменклатурыСрезПоследних.Цена,0)
		|		/ ЕстьNULL(&ТекстЗапросаКоэффициентУпаковки1,1)
		|			* ВЫБОР
		|				КОГДА
		|					ВременнаяТаблицаТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|				ТОГДА
		|					&ТекстЗапросаКоэффициентУпаковки2
		|				ИНАЧЕ 
		|					1
		|			КОНЕЦ
		|			* ВЫБОР
		|				КОГДА
		|					МаксимальныеЦеныНоменклатурыСрезПоследних.Валюта <> ВременнаяТаблицаДокументЗакупки.Валюта
		|				ТОГДА
		|					ВЫБОР
		|						КОГДА ЕСТЬNULL(КурсыВалютыЦены.Кратность, 0) > 0
		|							И ЕСТЬNULL(КурсыВалютыЦены.Курс, 0) > 0
		|							И ЕСТЬNULL(КурсыВалютыМаксимальнойЦены.Кратность, 0) > 0
		|							И ЕСТЬNULL(КурсыВалютыМаксимальнойЦены.Курс, 0) > 0
		|						ТОГДА
		|							(КурсыВалютыМаксимальнойЦены.Курс * КурсыВалютыЦены.Кратность)
		|							/ (КурсыВалютыЦены.Курс * КурсыВалютыМаксимальнойЦены.Кратность)
		|						ИНАЧЕ
		|							0
		|					КОНЕЦ
		|				ИНАЧЕ 
		|					1
		|			КОНЕЦ КАК ЧИСЛО(15, 2))
		|	КАК МаксимальноДопустимаяЦена
		|
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			ВременнаяТаблицаДокументЗакупки КАК ВременнаяТаблицаДокументЗакупки
		|		ПО
		|			ИСТИНА
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			ВременнаяТаблицаКурсыВалют КАК КурсыВалютыЦены
		|		ПО
		|			ВременнаяТаблицаДокументЗакупки.Валюта = КурсыВалютыЦены.Валюта
		|
		|/////////////////////////////////////////////////////////////////////////////
		|// СОЕДИНЕНИЯ С МАКСИМАЛЬНО ДОПУСТИМЫМИ ЦЕНАМИ
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|				КОНЕЦПЕРИОДА(&Дата, ДЕНЬ),
		|				ВидЦены В (
		|					ВЫБРАТЬ
		|						Константы.ВидМаксимальноДопустимыхЦенЗакупки КАК ВидЦены
		|					ИЗ
		|						Константы КАК Константы
		|					)
		|				И (Номенклатура,Характеристика) В (
		|					ВЫБРАТЬ
		|						Товары.Номенклатура,
		|						Товары.Характеристика
		|					ИЗ
		|						ВременнаяТаблицаТовары КАК Товары
		|					)
		|			) КАК МаксимальныеЦеныНоменклатурыСрезПоследних
		|		ПО
		|			ВременнаяТаблицаТовары.Номенклатура = МаксимальныеЦеныНоменклатурыСрезПоследних.Номенклатура
		|			И ВременнаяТаблицаТовары.Характеристика = МаксимальныеЦеныНоменклатурыСрезПоследних.Характеристика
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|			ВременнаяТаблицаКурсыВалют КАК КурсыВалютыМаксимальнойЦены
		|		ПО
		|			МаксимальныеЦеныНоменклатурыСрезПоследних.Валюта = КурсыВалютыМаксимальнойЦены.Валюта
		|
		|ГДЕ
		|	ВременнаяТаблицаТовары.Цена > 0
		|) КАК ВложенныйЗапрос
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки ВОЗР
		|;
		|";
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"МаксимальныеЦеныНоменклатурыСрезПоследних.Упаковка",
		"МаксимальныеЦеныНоменклатурыСрезПоследних.Номенклатура"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ВременнаяТаблицаТовары.Упаковка",
		"ВременнаяТаблицаТовары.Номенклатура"));
		
КонецПроцедуры

// Формирует текст запроса для проверки допустимости ручных скидок и наценок
//
// Параметры:
// ТекстЗапроса - Строка - текстовая строка, к которой необходимо добавить текст запроса
//
Процедура СформироватьЗапросДопустимостьРучныхСкидокНаценок(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.НомерСтроки               КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА
		|			ВременнаяТаблицаТовары.ПроцентРучнойСкидки < 0
		|			И -ВременнаяТаблицаТовары.ПроцентРучнойСкидки > СоглашениеСПоставщиком.ПроцентРучнойНаценки
		|			И СоглашениеСПоставщиком.Ссылка.КонтролироватьЦеныЗакупки
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ                                            КАК ЕстьОшибкиЗаполненияРучнойНаценки,
		|	СоглашениеСПоставщиком.ПроцентРучнойСкидки       КАК МаксимальнаяСуммаРучнойСкидки,
		|	СоглашениеСПоставщиком.ПроцентРучнойНаценки      КАК МаксимальнаяСуммаРучнойНаценки
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ВременнаяТаблицаДокументЗакупки
		|ПО
		|	ИСТИНА
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.СоглашенияСПоставщиками КАК СоглашениеСПоставщиком
		|ПО
		|	ВременнаяТаблицаДокументЗакупки.Соглашение = СоглашениеСПоставщиком.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаТовары.ПроцентРучнойСкидки <> 0
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки ВОЗР
		|;
		|";
	
КонецПроцедуры

// Формирует запрос для проверки корректности заполнения этапов графика оплаты
//
// Параметры:
// ТекстЗапроса - Строка - текстовая строка, к которой необходимо добавить текст запроса
//
Процедура СформироватьЗапросКорректностьЭтаповГрафикаОплаты(ТекстЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА
		|			ВременнаяТаблицаДокументЗакупки.СуммаДокумента = 0
		|			ИЛИ ВременнаяТаблицаДокументЗакупки.Соглашение = ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)
		|		ТОГДА
		|			ЛОЖЬ
		|		ИНАЧЕ
		|			ИСТИНА
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияЭтапыГрафикаОплаты,
		|	ВЫБОР
		|		КОГДА
		|			ЭтапыШаблона.НомерСтроки ЕСТЬ NULL
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ                               КАК ЭтапШаблонаОтсутствует,
		|	ЭтапыДокумента.НомерСтроки          КАК НомерСтроки,
		|	ЭтапыДокумента.ВариантОплаты        КАК ВариантОплаты,
		|	ЭтапыДокумента.ДатаПлатежа          КАК ДатаПлатежа,
		|	ЭтапыДокумента.ПроцентПлатежа       КАК ПроцентПлатежа,
		|	ЭтапыШаблона.НомерСтроки            КАК НомерСтрокиШаблона,
		|	ЭтапыШаблона.ВариантОплаты          КАК ВариантОплатыШаблона,
		|	ЭтапыШаблона.ПроцентПлатежа         КАК ПроцентПлатежаШаблона,
		|	ЭтапыШаблона.Сдвиг                  КАК Сдвиг,
		|	ЭтапыШаблона.Ссылка.Календарь       КАК Календарь,
		|	ВременнаяТаблицаДокументЗакупки.Дата                 КАК Дата,
		|	ВременнаяТаблицаДокументЗакупки.ЖелаемаяДатаПоступления КАК ЖелаемаяДатаПоступления,
		|	ВЫБОР
		|		КОГДА
		|			ЭтапыШаблона.ВариантОплаты = ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыПоставщику.КредитПослеПоступления)
		|		ТОГДА
		|			ВЫБОР
		|				КОГДА
		|					ВременнаяТаблицаДокументЗакупки.ЖелаемаяДатаПоступления = ДАТАВРЕМЯ(1,1,1)
		|				ТОГДА
		|					НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВременнаяТаблицаДокументЗакупки.Дата,День,ЭтапыШаблона.Сдвиг),День)
		|				ИНАЧЕ
		|					НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВременнаяТаблицаДокументЗакупки.ЖелаемаяДатаПоступления,День,ЭтапыШаблона.Сдвиг),День)
		|			КОНЕЦ
		|		ИНАЧЕ
		|			НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВременнаяТаблицаДокументЗакупки.Дата,День,ЭтапыШаблона.Сдвиг),День)
		|	КОНЕЦ КАК ДатаПлатежаШаблона,
		|	ВЫБОР
		|		КОГДА
		|			ЭтапыДокумента.ВариантОплаты <> ЭтапыШаблона.ВариантОплаты
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияВариантаОплаты,
		|	ВЫБОР
		|		КОГДА
		|			ЭтапыДокумента.ПроцентПлатежа > ЭтапыШаблона.ПроцентПлатежа
		|			И ЭтапыШаблона.ПроцентПлатежа ЕСТЬ НЕ NULL
		|		ТОГДА
		|			ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияПроцентаПлатежа
		|ИЗ
		|	ВременнаяТаблицаЭтапыГрафикаОплаты КАК ЭтапыДокумента
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ВременнаяТаблицаДокументЗакупки КАК ВременнаяТаблицаДокументЗакупки
		|ПО
		|	ИСТИНА
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.СоглашенияСПоставщиками.ЭтапыГрафикаОплаты КАК ЭтапыШаблона
		|
		|ПО
		|	ВременнаяТаблицаДокументЗакупки.Соглашение = ЭтапыШаблона.Ссылка
		|	И ЭтапыДокумента.НомерСтроки = ЭтапыШаблона.НомерСтроки
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки ВОЗР
		|;
		|";

КонецПроцедуры

// Формирует запрос для проверки корректности количества авансовых этапов оплаты
//
// Параметры:
// ТекстЗапроса - Строка - текстовая строка, к которой необходимо добавить текст запроса
//
Процедура СформироватьЗапросКорректностьКоличестваЭтаповГрафикаОплаты(ТекстЗапроса)

	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СУММА(ВложенныйЗапрос.КоличествоЭтаповДокумента) <> СУММА(ВложенныйЗапрос.КоличествоЭтаповШаблона)
	|				И МАКСИМУМ(ВложенныйЗапрос.ЕстьОшибкиЗаполненияЭтапыГрафикаОплаты) = ИСТИНА
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьОшибкиЗаполненияЭтапыГрафикаОплаты,
	|	СУММА(ВложенныйЗапрос.КоличествоЭтаповДокумента) КАК КоличествоЭтаповДокумента,
	|	СУММА(ВложенныйЗапрос.КоличествоЭтаповШаблона) КАК КоличествоЭтаповШаблона
	|ИЗ
	|	(ВЫБРАТЬ
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЭтапыДокумента.НомерСтроки) КАК КоличествоЭтаповДокумента,
	|		NULL КАК КоличествоЭтаповШаблона,
	|		NULL КАК ЕстьОшибкиЗаполненияЭтапыГрафикаОплаты
	|	ИЗ
	|		ВременнаяТаблицаЭтапыГрафикаОплаты КАК ЭтапыДокумента
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		NULL,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ГрафикиОплатыЭтапы.НомерСтроки),
	|		NULL
	|	ИЗ
	|		Справочник.СоглашенияСПоставщиками.ЭтапыГрафикаОплаты КАК ГрафикиОплатыЭтапы
	|	ГДЕ
	|		ГрафикиОплатыЭтапы.Ссылка В
	|				(ВЫБРАТЬ
	|					ВременнаяТаблицаДокументЗакупки.Соглашение КАК Соглашение
	|				ИЗ
	|					ВременнаяТаблицаДокументЗакупки КАК ВременнаяТаблицаДокументЗакупки)
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		NULL,
	|		NULL,
	|		ВЫБОР
	|			КОГДА ВременнаяТаблицаДокументЗакупки.СуммаДокумента = 0
	|					ИЛИ ВременнаяТаблицаДокументЗакупки.Соглашение = ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	ИЗ
	|		ВременнаяТаблицаДокументЗакупки КАК ВременнаяТаблицаДокументЗакупки) КАК ВложенныйЗапрос
	|;";
		
КонецПроцедуры


// Формирует текст запроса для проверки заполнения цен в документе, позволяя нулевые цены в отмененных строках
//
// Параметры:
// ТекстЗапроса - Строка - текстовая строка, к которой необходимо добавить текст запроса
//
Процедура СформироватьЗапросКонтрольЗаполненияЦенСУчетомОтмененных(ТекстЗапроса)
	
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА
		|			ВременнаяТаблицаТовары.Сумма = 0
		|		ТОГДА
		|			ВЫБОР
		|				КОГДА (&ВозвратМногооборотнойТары И НЕ &ТребуетсяЗалогЗаТару
		|					И ВременнаяТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|					ИЛИ ВременнаяТаблицаТовары.Отменено ТОГДА
		|					ЛОЖЬ
		|				ИНАЧЕ
		|					ИСТИНА
		|			КОНЕЦ
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияСумм,
		|	ВЫБОР
		|		КОГДА
		|			ВременнаяТаблицаТовары.Цена = 0
		|		ТОГДА
		|			ВЫБОР
		|				КОГДА (&ВозвратМногооборотнойТары И НЕ &ТребуетсяЗалогЗаТару
		|					И ВременнаяТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|					ИЛИ ВременнаяТаблицаТовары.Отменено ТОГДА
		|					ЛОЖЬ
		|				ИНАЧЕ
		|					ИСТИНА
		|			КОНЕЦ
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияЦен
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки ВОЗР
		|;
		|";
		
КонецПроцедуры

// Формирует текст запроса для проверки заполнения цен в документе
//
// Параметры:
// ТекстЗапроса - Строка - текстовая строка, к которой необходимо добавить текст запроса
//
Процедура СформироватьЗапросКонтрольЗаполненияЦен(ТекстЗапроса)
	
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА
		|			ВременнаяТаблицаТовары.Сумма = 0
		|		ТОГДА
		|			ВЫБОР
		|				КОГДА (&ВозвратМногооборотнойТары И НЕ &ТребуетсяЗалогЗаТару
		|					И ВременнаяТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)) ТОГДА
		|					ЛОЖЬ
		|				ИНАЧЕ
		|					ИСТИНА
		|			КОНЕЦ
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияСумм,
		|	ВЫБОР
		|		КОГДА
		|			ВременнаяТаблицаТовары.Цена = 0
		|		ТОГДА
		|			ВЫБОР
		|				КОГДА (&ВозвратМногооборотнойТары И НЕ &ТребуетсяЗалогЗаТару
		|					И ВременнаяТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)) ТОГДА
		|					ЛОЖЬ
		|				ИНАЧЕ
		|					ИСТИНА
		|			КОНЕЦ
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ КАК ЕстьОшибкиЗаполненияЦен
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки ВОЗР
		|;
		|";
		
КонецПроцедуры

// Формирует текст запроса для проверки организации распоряжений
//
// Параметры:
// ТекстЗапроса - Строка - текстовая строка, к которой необходимо добавить текст запроса
//
Процедура СформироватьЗапросСоответствиеОрганизацииРаспоряжений(ТекстЗапроса)

	ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.ПредставлениеТабличнойЧасти КАК ПредставлениеТабличнойЧасти,
		|	ВременнаяТаблицаТовары.ИмяТабличнойЧасти КАК ИмяТабличнойЧасти,
		|	ВременнаяТаблицаТовары.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	ВременнаяТаблицаДокументЗакупки КАК ВременнаяТаблицаДокументЗакупки
		|ПО
		|	ИСТИНА
		|ГДЕ
		|	ВременнаяТаблицаДокументЗакупки.Организация <> ВременнаяТаблицаТовары.Распоряжение.Организация
		|;
		|";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииПроверкиКорректностиДокументов

// Выводит сообщения об ошибках в шапке документа закупки
//
// Параметры:
// Выборка         - Выборка из результата запроса
// ДокументЗакупки - ДокументОбъект, для которого необходимо вывести сообщения об ошибках
// Отказ           - Булево - Флаг отказа от проведения документа
// МассивОтклонений - Массив - Массив отклонений условий продаж по шапке документа
//
Процедура СообщитьОбОшибкахШапкаДокументаЗакупки(Выборка,
	                                             ДокументЗакупки,
	                                             Отказ,
	                                             МассивОтклонений = Неопределено)
	
	ВыводитьВОтчет = (МассивОтклонений <> Неопределено);
	
	Если Выборка.Следующий() Тогда
		
		Если Выборка.ЕстьОшибкиЗаполненияПартнер Тогда
			
			Если НЕ ВыводитьВОтчет Тогда
				ТекстОшибки = НСтр("ru='Партнер не должен отличаться от значения в соглашении ""%ПартнерСоглашение%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ПартнерСоглашение%", Выборка.ПартнерСоглашение);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					"Партнер",
					,
					Отказ);
			Иначе
				
				ТекстОшибки = НСтр("ru='""%ПартнерДокумент%"" отличается от партнера в соглашении ""%ПартнерСоглашение%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ПартнерДокумент%", ДокументЗакупки.Партнер);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ПартнерСоглашение%", Выборка.ПартнерСоглашение);
				
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Партнер'"), ТекстОшибки, МассивОтклонений);
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.ЕстьОшибкиСтатусСоглашения Тогда
			Если НЕ ВыводитьВОтчет Тогда
				ТекстОшибки = НСтр("ru='Соглашение находится в статус ""Не согласовано"". Проведение документа запрещено.'");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					"Соглашение",
					,
					Отказ);
			Иначе
				ТекстОшибки = НСтр("ru='Соглашение находится в статус ""Не согласовано"".'");
				
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Соглашение'"), ТекстОшибки, МассивОтклонений);
				
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.ЕстьОшибкиЗаполненияКонтрагент Тогда
			
			Если НЕ ВыводитьВОтчет Тогда
				ТекстОшибки = НСтр("ru='Контрагент не должен отличаться от значения в соглашении ""%КонтрагентСоглашение%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%КонтрагентСоглашение%", Выборка.КонтрагентСоглашение);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					"Контрагент",
					,
					Отказ);
			Иначе 
				ТекстОшибки = НСтр("ru='""%Контрагент%"" отличается от контрагента в соглашении ""%КонтрагентСоглашение%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%КонтрагентСоглашение%", Выборка.КонтрагентСоглашение);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Контрагент%", ДокументЗакупки.Контрагент);
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Контрагент'"), ТекстОшибки, МассивОтклонений);
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.ЕстьОшибкиЗаполненияОрганизация Тогда
			
			Если НЕ ВыводитьВОтчет Тогда
				ТекстОшибки = НСтр("ru='Организация не должна отличаться от значения в соглашении ""%ОрганизацияСоглашение%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОрганизацияСоглашение%", Выборка.ОрганизацияСоглашение);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					"Организация",
					,
					Отказ);
			Иначе
				ТекстОшибки = НСтр("ru='""%Организация%"" отличается от организации в соглашении ""%ОрганизацияСоглашение%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Организация%", ДокументЗакупки.Организация);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОрганизацияСоглашение%", Выборка.ОрганизацияСоглашение);
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Организация'"), ТекстОшибки, МассивОтклонений);
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.ЕстьОшибкиЗаполненияФормаОплаты Тогда
			Если НЕ ВыводитьВОтчет Тогда 
				ТекстОшибки = НСтр("ru='Форма оплаты не должна отличаться от значения в соглашении ""%ФормаОплатыСоглашение%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ФормаОплатыСоглашение%", Выборка.ФормаОплатыСоглашение);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					"ФормаОплаты",
					,
					Отказ);
			Иначе
				ТекстОшибки = НСтр("ru='""%ФормаОплаты%"" отличается от формы оплаты в соглашении ""%ФормаОплатыГрафик%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ФормаОплатыГрафик%", Выборка.ФормаОплатыСоглашение);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ФормаОплаты%", ?(ЗначениеЗаполнено(ДокументЗакупки.ФормаОплаты),ДокументЗакупки.ФормаОплаты, НСтр("ru='Любая'")));
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Форма оплаты'"), ТекстОшибки, МассивОтклонений);
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.ЕстьОшибкиЗаполненияВалюта Тогда
			Если НЕ ВыводитьВОтчет Тогда
				ТекстОшибки = НСтр("ru='Валюта не должна отличаться от значения в соглашении ""%ВалютаСоглашение%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ВалютаСоглашение%", Выборка.ВалютаСоглашение);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					"Валюта",
					,
					Отказ);
			Иначе 
				ТекстОшибки = НСтр("ru='""%Валюта%"" отличается от валюты в соглашении ""%ВалютаСоглашение%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Валюта%", ДокументЗакупки.Валюта);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ВалютаСоглашение%", Выборка.ВалютаСоглашение);
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Валюта'"), ТекстОшибки, МассивОтклонений);
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.ЕстьОшибкиЗаполненияВалютаВзаиморасчетов Тогда
			Если НЕ ВыводитьВОтчет Тогда
				ТекстОшибки = НСтр("ru='Валюта взаиморасчетов не должна отличаться от значения в соглашении ""%ВалютаСоглашение%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ВалютаСоглашение%", Выборка.ВалютаСоглашение);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					"ВалютаВзаиморасчетов",
					,
					Отказ);
			Иначе
				ТекстОшибки = НСтр("ru='""%Валюта%"" отличается от валюты взаиморасчетов в соглашении ""%ВалютаСоглашение%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ВалютаСоглашение%", Выборка.ВалютаСоглашение);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Валюта%", ДокументЗакупки.ВалютаВзаиморасчетов);
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Валюта взаиморасчетов'"), ТекстОшибки, МассивОтклонений);
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.ЕстьОшибкиЗаполненияНалогообложениеНДС Тогда
			Если НЕ ВыводитьВОтчет Тогда
				ТекстОшибки = НСтр("ru='Тип налогообложения НДС не должен отличаться от значения в соглашении ""%НалогообложениеНДС%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НалогообложениеНДС%",Выборка.НалогообложениеНДССоглашение);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					"НалогообложениеНДС",
					,
					Отказ);
			Иначе 
				ТекстОшибки = НСтр("ru='""%НалогообложениеНДС%"" отличается от налогообложения в соглашении ""%НалогообложениеНДССоглашение%""'");
					
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НалогообложениеНДС%", ДокументЗакупки.НалогообложениеНДС); 
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НалогообложениеНДССоглашение%", Выборка.НалогообложениеНДССоглашение);
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Налогообложение НДС'"), ТекстОшибки, МассивОтклонений);
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.ЕстьОшибкиЗаполненияХозяйственнаяОперация Тогда
			Если НЕ ВыводитьВОтчет Тогда
				ТекстОшибки = НСтр("ru='Операция не должна отличаться от значения в соглашении ""%Операция%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Операция%", Выборка.ХозяйственнаяОперацияСоглашение); 
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					"ХозяйственнаяОперация",
					,
					Отказ);
			Иначе 
				ТекстОшибки = НСтр("ru='""%Операция%"" отличается от операции в соглашении ""%ОперацияСоглашения%""'");
				
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Операция%", ДокументЗакупки.ХозяйственнаяОперация); 
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОперацияСоглашения%", Выборка.ХозяйственнаяОперацияСоглашение); 
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Хозяйственная операция'"), ТекстОшибки, МассивОтклонений);
			КонецЕсли; 
		КонецЕсли;

		Если Выборка.ЕстьОшибкиЗаполненияЦенаВключаетНДС Тогда
			
			ТекстОшибки = НСтр("ru='Признак ""Цена включает НДС"" не должен отличаться от значения в соглашении ""%ЦенаВключаетНДС%""'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ЦенаВключаетНДС%", ?(Выборка.ЦенаВключаетНДССоглашение, НСтр("ru='Цена включает НДС'"), НСтр("ru='Цена не включает НДС'"))); 
			
			Если НЕ ВыводитьВОтчет Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					"ЦенаВключаетНДС",
					,
					Отказ);
			Иначе 
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Цена включает НДС'"), ТекстОшибки, МассивОтклонений);
			КонецЕсли;

		КонецЕсли;
		
		Если Выборка.ЕстьОшибкиЗаполненияСклад Тогда
			Если НЕ ВыводитьВОтчет Тогда
				Если Выборка.СкладСоглашениеЕстьГруппа Тогда
					ТекстОшибки = НСтр("ru='Склад должен входить в группу складов ""%СкладСоглашение%""'");
				Иначе
					ТекстОшибки = НСтр("ru='Склад не должен отличаться от значения в соглашении ""%СкладСоглашение%""'");
				КонецЕсли;
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СкладСоглашение%", Выборка.СкладСоглашение);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					"Склад",
					,
					Отказ);
			Иначе 
				Если Выборка.СкладСоглашениеЕстьГруппа Тогда
					ТекстОшибки = НСтр("ru='""%Склад%"" должен входить в группу складов соглашения ""%СкладСоглашение%""'");
				Иначе
					ТекстОшибки = НСтр("ru='""%Склад%"" отличается от склада в соглашении ""%СкладСоглашение%""'");
				КонецЕсли;
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СкладСоглашение%", Выборка.СкладСоглашение);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Склад%", ДокументЗакупки.Склад);
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Склад'"), ТекстОшибки, МассивОтклонений);
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.ЕстьОшибкиЗаполненияВозвращатьМногооборотнуюТару Тогда
			
			Если Выборка.ЭтоСоглашение Тогда
				ТекстОшибки = НСтр("ru='Признак возврата многооборотной тары не должен отличаться от значения в типовом соглашении'");
			Иначе
				ТекстОшибки = НСтр("ru='Признак возврата многооборотной тары не должен отличаться от значения в соглашении'");
			КонецЕсли;
			
			Если НЕ ВыводитьВОтчет Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					?(Выборка.ЭтоСоглашение,"ВозвращатьМногооборотнуюТару","ВернутьМногооборотнуюТару"),
					,
					Отказ);
			Иначе 
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Возврат тары'"), ТекстОшибки, МассивОтклонений);
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.ЕстьОшибкиЗаполненияСрокВозвратаМногооборотнойТары И Выборка.ВозвратМногооборотнойТары Тогда
			
			Если НЕ ВыводитьВОтчет Тогда
				ТекстОшибки = НСтр("ru='Срок возврата многооборотной тары не должен отличаться от значения в соглашении (%СрокВозврата%)'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СрокВозврата%", Выборка.СрокВозвратаМногооборотнойТары);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					"СрокВозвратаМногооборотнойТары",
					,
					Отказ);
			Иначе 
					
				ТекстОшибки = НСтр("ru='(%СрокВозвратаТиповое%) отличается от срока в соглашении (%СрокВозврата%)'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СрокВозврата%", Выборка.СрокВозвратаМногооборотнойТары);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%СрокВозвратаТиповое%", ДокументЗакупки.СрокВозвратаМногооборотнойТары);
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Срок возврата тары'"), ТекстОшибки, МассивОтклонений);
			КонецЕсли;

		КонецЕсли;
		
		Если Выборка.ЕстьОшибкиДатаВозвратаМногооборотнойТары И Выборка.ВозвратМногооборотнойТары Тогда
			
			ДатаВозвратаМногооборотнойТары = МногооборотнаяТараСервер.РассчитатьДатуВозвратаМногооборотнойТары(
				ДокументЗакупки,
				Выборка.СрокВозвратаМногооборотнойТары,
				Выборка.РассчитыватьДатуВозвратаТарыПоКалендарю,
				Выборка.КалендарьВозвратаТары);
			
			Если ДатаВозвратаМногооборотнойТары <> ДокументЗакупки.ДатаВозвратаМногооборотнойТары Тогда
				ТекстОшибки = НСтр("ru='Дата возврата многооборотной тары не должна отличаться от срока возврата в соглашении'");
				
				Если НЕ ВыводитьВОтчет Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							ТекстОшибки,
							ДокументЗакупки,
							"ДатаВозвратаМногооборотнойТары",
							,
							Отказ);
				Иначе 
					Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Дата возврата тары'"), ТекстОшибки, МассивОтклонений);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Выводит сообщения об ошибках в тч Товары документа закупки
//
// Параметры:
// Выборка         - Выборка из результата запроса
// ДокументЗакупки - ДокументОбъект, для которого необходимо вывести сообщения об ошибках
// Отказ           - Булево- Флаг отказа от проведения документа
// ДеревоОтклонений  - ДеревоЗначений - Дерево с отклонениями табличной части от условий закупок
//
Процедура СообщитьОбОшибкахТоварыДокументаЗакупки(Выборка,
	                                              ДокументЗакупки,
	                                              Отказ,
	                                              ДеревоОтклонений = Неопределено)
	
	ВыводитьВОтчет = (ДеревоОтклонений <> Неопределено);
	
	Пока Выборка.Следующий() Цикл
		НомерСтроки = Выборка.НомерСтроки;
		АдресОшибки = " " + НСтр("ru='в строке %НомерСтроки% списка ""Товары""'");
		АдресОшибки = СтрЗаменить(АдресОшибки, "%НомерСтроки%", НомерСтроки);
		
		Если Выборка.ЕстьОшибкиМаксимальноДопустимаяЦена Тогда
			
			Если ЗначениеЗаполнено(Выборка.Характеристика) Тогда
				
				ТекстОшибки = НСтр("ru='Цена на номенклатуру ""%Номенклатура%"" с характеристикой ""%Характеристика%"" не должна быть выше цены по соглашению ""%МаксимальноДопустимаяЦена% %ВалютаЦены%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки,"%Номенклатура%",              Выборка.Номенклатура);
				ТекстОшибки = СтрЗаменить(ТекстОшибки,"%Характеристика%",            Выборка.Характеристика);
				ТекстОшибки = СтрЗаменить(ТекстОшибки,"%МаксимальноДопустимаяЦена%", Выборка.МаксимальноДопустимаяЦена);
				ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ВалютаЦены%",                Выборка.ВалютаЦены);
				
			Иначе
				
				ТекстОшибки = НСтр("ru='Цена на номенклатуру ""%Номенклатура%"" не должна быть выше цены по соглашению ""%МаксимальноДопустимаяЦена% %ВалютаЦены%""'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Номенклатура%",              Выборка.Номенклатура);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%МаксимальноДопустимаяЦена%", Выборка.МаксимальноДопустимаяЦена);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ВалютаЦены%",                Выборка.ВалютаЦены);
				
			КонецЕсли;
			
			Если ВыводитьВОтчет Тогда
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеСтрокиВДеревоОтклонений(НомерСтроки, "Цена", ТекстОшибки, ДеревоОтклонений);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", НомерСтроки, "Цена"),
					,
					Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.ЕстьОшибкиЗаполненияСтавкаНДС И НЕ ВыводитьВОтчет Тогда
			
			ТекстОшибки = НСтр("ru='Ставка НДС не должна отличаться от ставки НДС в соглашении ""%СтавкаНДССоглашение%""'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки,"%СтавкаНДССоглашение%", Выборка.СтавкаНДССоглашение);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки + АдресОшибки,
				ДокументЗакупки,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", НомерСтроки, "СтавкаНДС"),
				,
				Отказ);
			
		КонецЕсли;
		
		Если Выборка.ЕстьОшибкиОбратноеОбложениеНДС Тогда
			
			ТекстОшибки = НСтр("ru='Товар не соответствует режиму налогообложения ""Облагается НДС у покупателя""'");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки + АдресОшибки,
				ДокументЗакупки,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", НомерСтроки, "СтавкаНДС"),
				,
				Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Выводит сообщения об ошибках наличия дублей в тч Товары документа закупки
//
// Параметры:
// Выборка         - Выборка из результата запроса
// ДокументЗакупки - ДокументОбъект, для которого необходимо вывести сообщения об ошибках
// Отказ           - Булево - Флаг отказа от проведения документа
//
Процедура СообщитьОбОшибкахКорректностьЗаполненияНоменклатурыПоставщика(Выборка,
	                                                                    ДокументЗакупки,
	                                                                    Отказ)
	
	Пока Выборка.Следующий() Цикл
		
		ТекстОшибки = НСтр("ru='В строке не может быть выбрана ""%НоменклатураПоставщика%"", т.к. в предыдущих строках она соответствует другой номенклатуре'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НоменклатураПоставщика%", Выборка.НоменклатураПоставщика);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ДокументЗакупки,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки, "НоменклатураПоставщика"),
			,
			Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Выводит сообщения об ошибках вхождения цен документа закупки в допустимый диапазон
//
// Параметры:
// Выборка         - Выборка из результата запроса
// ДокументЗакупки - ДокументОбъект, для которого необходимо вывести сообщения об ошибках
// Отказ           - Булево - Флаг отказа от проведения документа
// ДеревоОтклонений  - ДеревоЗначений - Дерево с отклонениями табличной части от условий закупок
//
Процедура СообщитьОбОшибкахВхождениеЦенВДопустимыйДиапазон(Выборка,
	                                                       ДокументЗакупки,
	                                                       Отказ,
	                                                       ИспользоватьСкидкиНаценки,
	                                                       ДеревоОтклонений = Неопределено)
	
	ВыводитьВОтчет = (ДеревоОтклонений <> Неопределено);
	
	Пока Выборка.Следующий() Цикл
		НомерСтроки = Выборка.НомерСтроки;
		
		Если Выборка.ЕстьОшибкиМаксимальноДопустимаяЦена Тогда
			
			Если ЗначениеЗаполнено(Выборка.Характеристика) Тогда
				
				Если ИспользоватьСкидкиНаценки Тогда
					ТекстОшибки = НСтр("ru='Цена с учетом скидки (наценки) на номенклатуру ""%Номенклатура%"" с характеристикой ""%Характеристика%"" не должна быть выше максимально допустимой цены ""%МаксимальноДопустимаяЦена% %ВалютаЦены%""'");
				Иначе
					ТекстОшибки = НСтр("ru='Цена на номенклатуру ""%Номенклатура%"" с характеристикой ""%Характеристика%"" не должна быть выше максимально допустимой цены ""%МаксимальноДопустимаяЦена% %ВалютаЦены%""'");
				КонецЕсли;
					
				ТекстОшибки = СтрЗаменить(ТекстОшибки,"%Номенклатура%",              Выборка.Номенклатура);
				ТекстОшибки = СтрЗаменить(ТекстОшибки,"%Характеристика%",            Выборка.Характеристика);
				ТекстОшибки = СтрЗаменить(ТекстОшибки,"%МаксимальноДопустимаяЦена%", Выборка.МаксимальноДопустимаяЦена);
				ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ВалютаЦены%",                Выборка.ВалютаЦены);
				
			Иначе
				
				Если ИспользоватьСкидкиНаценки Тогда
					ТекстОшибки = НСтр("ru='Цена с учетом скидки (наценки) на номенклатуру ""%Номенклатура%"" не должна быть выше максимально допустимой цены ""%МаксимальноДопустимаяЦена% %ВалютаЦены%""'");
				Иначе
					ТекстОшибки = НСтр("ru='Цена на номенклатуру ""%Номенклатура%"" не должна быть выше максимально допустимой цены ""%МаксимальноДопустимаяЦена% %ВалютаЦены%""'");
				КонецЕсли;
					
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Номенклатура%",              Выборка.Номенклатура);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%МаксимальноДопустимаяЦена%", Выборка.МаксимальноДопустимаяЦена);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ВалютаЦены%",                Выборка.ВалютаЦены);
				
			КонецЕсли;
			
			Если ВыводитьВОтчет Тогда
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеСтрокиВДеревоОтклонений(НомерСтроки, "Цена", ТекстОшибки, ДеревоОтклонений);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", НомерСтроки, "Цена"),
					,
					Отказ);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Выводит сообщения об ошибках заполненности аналитик услуг для услуг в документе закупки
//
// Параметры:
// Выборка         - Выборка из результата запроса
// ДокументЗакупки - ДокументОбъект, для которого необходимо вывести сообщения об ошибках
// Отказ           - Булево - Флаг отказа от проведения документа
//
Процедура СообщитьОбОшибкахКорректностьЗаполненияАналитикиУслуг(Выборка, ДокументЗакупки, Отказ)
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов") Тогда
	
		Пока Выборка.Следующий() Цикл
			
			ТекстОшибки = НСтр("ru='Необходимо заполнить статью расходов для услуги ""%Номенклатура%"" в строке %НомерСтроки% списка ""Товары""'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Номенклатура%", Выборка.Номенклатура);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НомерСтроки%",  Выборка.НомерСтроки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				ДокументЗакупки,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки,"СтатьяРасходов"),
				,
				Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Выводит сообщения об ошибках наличия услуг в документе закупки
//
// Параметры:
// Выборка         - Выборка из результата запроса
// ДокументЗакупки - ДокументОбъект, для которого необходимо вывести сообщения об ошибках
// Отказ           - Булево - Флаг отказа от проведения документа
//
Процедура СообщитьОбОшибкахНаличиеУслугВДокументеЗакупки(Выборка, ДокументЗакупки, Отказ)
	
	Пока Выборка.Следующий() Цикл
		
		ТекстОшибки = НСтр("ru='Нельзя указывать номенклатуру ""%Номенклатура%"" с типом ""%ТипНоменклатуры%"" (строка %НомерСтроки% списка ""Товары""), для документа с типом хоз. операции ""%ХозяйственнаяОперация%""'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Номенклатура%",          Выборка.Номенклатура);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ТипНоменклатуры%",       Выборка.ТипНоменклатуры);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НомерСтроки%",           Выборка.НомерСтроки);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ХозяйственнаяОперация%", ДокументЗакупки.ХозяйственнаяОперация);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ДокументЗакупки,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки,"Номенклатура"),
			,
			Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Выводит сообщения об ошибках отсутствия соглашения в документе закупки
//
// Параметры:
// ЕстьОшибкиСоглашениеНеУказано - Булево - признак наличия ошибки незаполненности соглашения 
// ДокументЗакупки               - ДокументОбъект, для которого необходимо вывести сообщения об ошибках
// Отказ                         - Булево - Флаг отказа от проведения документа
//
Процедура СообщитьОбОшибкахСоглашениеНеУказано(ЕстьОшибкиСоглашениеНеУказано, ДокументЗакупки, Отказ)
	
	Если ЕстьОшибкиСоглашениеНеУказано Тогда
	
		ТекстОшибки = НСтр("ru='Соглашение не указано. Документ не может быть проведен без согласования'");
			
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ДокументЗакупки,
			"Соглашение",
			,
			Отказ);
			
	КонецЕсли;
		
КонецПроцедуры

// Выводит сообщения об ошибках наличия услуг в документе закупки
//
// Параметры:
// Выборка         - Выборка из результата запроса
// ДокументЗакупки - ДокументОбъект, для которого необходимо вывести сообщения об ошибках
// Отказ           - Булево - Флаг отказа от проведения документа
// ДеревоОтклонений  - ДеревоЗначений - Дерево с отклонениями табличной части от условий закупок
//
Процедура СообщитьОбОшибкахДопустимостьРучныхСкидокНаценок(Знач Выборка,
	                                                       Знач ДокументЗакупки,
	                                                       Отказ,
	                                                       ДеревоОтклонений = Неопределено)
	
	ВыводитьВОтчет = (ДеревоОтклонений <> Неопределено);
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ЕстьОшибкиЗаполненияРучнойНаценки Тогда
			НомерСтроки = Выборка.НомерСтроки;
			ТекстОшибки = НСтр("ru='Ручная наценка превышает максимально допустимую наценку ""%МаксимальнаяСуммаРучнойНаценки% %"" в строке %НомерСтроки% списка ""Товары""'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%МаксимальнаяСуммаРучнойНаценки%", Выборка.МаксимальнаяСуммаРучнойНаценки);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НомерСтроки%",                    НомерСтроки);
			
			Если ВыводитьВОтчет Тогда
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеСтрокиВДеревоОтклонений(НомерСтроки, "ПроцентРучнойСкидки", ТекстОшибки, ДеревоОтклонений);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ДокументЗакупки,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки,"ПроцентРучнойСкидки"),
					,
					Отказ);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Выводит сообщения об ошибках заполнения склада в документе закупки
//
// Параметры:
// Выборка         - Выборка из результата запроса
// ДокументЗакупки - ДокументОбъект, для которого необходимо вывести сообщения об ошибках
// Отказ           - Булево - Флаг отказа от проведения документа
//
Процедура СообщитьОбОшибкахКорректностьЗаполненияСклада(Знач Выборка,
	                                                    Знач ДокументЗакупки,
	                                                    Отказ)
	
	Если Выборка.Следующий() Тогда
		
		ТекстОшибки = НСтр("ru='Поле ""Склад"" не заполнено'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ДокументЗакупки,
			"Склад",
			,
			Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

// Выводит сообщения об ошибках заполнения склада в табличной части "Товары" документа закупки
//
Процедура СообщитьОбОшибкахКорректностьЗаполненияСкладаВТабличнойЧастиТовары(Знач Выборка,
	                                                    Знач ДокументЗакупки,
	                                                    Отказ)
	
	Пока Выборка.Следующий() Цикл
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Не заполнена колонка ""Склад"" в строке %1 списка ""%2""'"),
			Выборка.НомерСтроки,
			Выборка.ПредставлениеТабличнойЧасти);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ДокументЗакупки,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(Выборка.ИмяТабличнойЧасти, Выборка.НомерСтроки,"Склад"),
			,
			Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Выводит сообщения об ошибках корректности заполнения этапов графика оплаты
//
// Параметры:
// Выборка          - Выборка из результата запроса
// ДокументЗакупки  - ДокументОбъект, для которого необходимо вывести сообщения об ошибках
// Отказ            - Булево - Флаг отказа от проведения документа
// ДеревоОтклонений  - ДеревоЗначений - Дерево с отклонениями табличной части от условий закупок
//
Процедура СообщитьОбОшибкахКорректностьЭтаповГрафикаОплаты(Знач Выборка, Знач ДокументЗакупки, Отказ, ДеревоОтклонений = Неопределено)
	
	МаксДатаАванса = Дата(1,1,1);
	ОдинДень       = 86400;
	СуммаПроцентовПлатежа = 0;
	СуммаПроцентовПлатежаШаблона = 0;
	ВыводитьВОтчет = (ДеревоОтклонений <> Неопределено);
	
	Пока Выборка.Следующий() Цикл
		
		Если Не Выборка.ЕстьОшибкиЗаполненияЭтапыГрафикаОплаты Тогда
			Возврат;
		КонецЕсли;
		
		Если Выборка.ЭтапШаблонаОтсутствует Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтроки = Выборка.НомерСтроки;
		АдресОшибки = " " + НСтр("ru='в строке %НомерСтроки% списка ""Этапы оплаты""'");
		АдресОшибки = СтрЗаменить(АдресОшибки, "%НомерСтроки%", НомерСтроки);
		
		Если Выборка.ЕстьОшибкиЗаполненияВариантаОплаты Тогда
			
			ТекстОшибки = НСтр("ru='Вариант оплаты отличается от значения в графике оплаты ""%ВариантОплатыШаблона%""'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ВариантОплатыШаблона%", Выборка.ВариантОплатыШаблона); 
			
			Если ВыводитьВОтчет Тогда
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеСтрокиВДеревоОтклонений(НомерСтроки, "ВариантОплаты", ТекстОшибки, ДеревоОтклонений);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки + АдресОшибки,
					,
					"НадписьЭтапыОплаты",
					,
					Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Календарь = Выборка.Календарь;
		
		Если ЗначениеЗаполнено(Календарь) Тогда
			
			Если Выборка.ВариантОплатыШаблона = Перечисления.ВариантыОплатыПоставщику.КредитПослеПоступления Тогда
				Если ЗначениеЗаполнено(Выборка.ЖелаемаяДатаПоступления) Тогда
					ДатаПлатежаШаблона = КалендарныеГрафики.ДатаПоКалендарю(Календарь, Выборка.ЖелаемаяДатаПоступления, Выборка.Сдвиг );
				ИначеЕсли ЗначениеЗаполнено(МаксДатаАванса) Тогда
					ДатаПлатежаШаблона = КалендарныеГрафики.ДатаПоКалендарю(Календарь, МаксДатаАванса, Выборка.Сдвиг);
				Иначе
					ДатаПлатежаШаблона = КалендарныеГрафики.ДатаПоКалендарю(Календарь, Выборка.Дата, Выборка.Сдвиг);
				КонецЕсли;
			Иначе
				ДатаПлатежаШаблона = КалендарныеГрафики.ДатаПоКалендарю(Календарь, Выборка.Дата, Выборка.Сдвиг);
			КонецЕсли;
			
		Иначе
			
			Если ЗначениеЗаполнено(Выборка.ЖелаемаяДатаПоступления) Тогда
				ДатаПлатежаШаблона = Выборка.ДатаПлатежаШаблона;
			ИначеЕсли ЗначениеЗаполнено(МаксДатаАванса) Тогда
				ДатаПлатежаШаблона = МаксДатаАванса + Выборка.Сдвиг * ОдинДень;
			Иначе
				ДатаПлатежаШаблона = Выборка.ДатаПлатежаШаблона;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Выборка.ВариантОплатыШаблона = Перечисления.ВариантыОплатыПоставщику.АвансДоПодтверждения Или
			Выборка.ВариантОплатыШаблона = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления Тогда
			
			МаксДатаАванса = ДатаПлатежаШаблона;
			
		КонецЕсли;
		
		Если ДатаПлатежаШаблона > Выборка.ДатаПлатежа Тогда
			
			ТекстОшибки = НСтр("ru='Дата платежа должна быть не меньше, чем в графике оплаты ""%ДатаПлатежаШаблона%""'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ДатаПлатежаШаблона%", Формат(ДатаПлатежаШаблона, "ДЛФ=DD")); 
			
			Если ВыводитьВОтчет Тогда
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеСтрокиВДеревоОтклонений(НомерСтроки, "ДатаПлатежа", ТекстОшибки, ДеревоОтклонений);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки + АдресОшибки,
					,
					"НадписьЭтапыОплаты",
					,
					Отказ);
			КонецЕсли;
		КонецЕсли;
		
		СуммаПроцентовПлатежаШаблона = СуммаПроцентовПлатежаШаблона + Выборка.ПроцентПлатежаШаблона;
		СуммаПроцентовПлатежа = СуммаПроцентовПлатежа + Выборка.ПроцентПлатежа;
		
		Если Выборка.ЕстьОшибкиЗаполненияПроцентаПлатежа И СуммаПроцентовПлатежа > СуммаПроцентовПлатежаШаблона Тогда
			
			ТекстОшибки = НСтр("ru='Проценты до следующего платежа по графику (""%ПроцентПлатежа%%"") не должны превышать проценты по графику соглашения (""%ПроцентПлатежаШаблона%%"")'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ПроцентПлатежа%", СуммаПроцентовПлатежа); 
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ПроцентПлатежаШаблона%", СуммаПроцентовПлатежаШаблона); 
			
			Если ВыводитьВОтчет Тогда
				Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеСтрокиВДеревоОтклонений(НомерСтроки, "ПроцентПлатежа", ТекстОшибки, ДеревоОтклонений);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки + АдресОшибки,
					,
					"НадписьЭтапыОплаты",
					,
					Отказ);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

// Выводит сообщения об ошибках корректности количества этапов графика оплаты
//
// Параметры:
// Выборка          - Выборка из результата запроса
// ДокументЗакупки  - ДокументОбъект, для которого необходимо вывести сообщения об ошибках
// Отказ            - Булево - Флаг отказа от проведения документа
// МассивОтклонений - Массив - Массив отклонений
//
Процедура СообщитьОбОшибкахКорректностьКоличестваЭтаповГрафикаОплаты(Знач Выборка, Знач ДокументЗакупки, Отказ, МассивОтклонений = Неопределено)
	
	ВыводитьВОтчет = (МассивОтклонений <> Неопределено);
	Выборка.Следующий();
	
	Если Выборка.КоличествоЭтаповШаблона <> 0 И Выборка.ЕстьОшибкиЗаполненияЭтапыГрафикаОплаты Тогда
			
		ТекстОшибки = НСтр("ru='Количество этапов оплаты в документе (%КоличествоЭтаповДокумента%) должно совпадать с количеством этапов в графике (%КоличествоЭтаповГрафика%)'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%КоличествоЭтаповДокумента%", Выборка.КоличествоЭтаповДокумента);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%КоличествоЭтаповГрафика%",   Выборка.КоличествоЭтаповШаблона);
		
		Если ВыводитьВОтчет Тогда
			Отчеты.ОтклоненияОтУсловийЗакупок.ДобавитьОтклонениеШапкиВМассивОтклонений(НСтр("ru='Этапы оплаты'"), ТекстОшибки, МассивОтклонений);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				,
				"НадписьЭтапыОплаты",
				,
				Отказ);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Выводит сообщения об ошибках корректности заполнения возвращаемых товаров
//
// Параметры:
// ТаблицаОшибок          - Выгрузка результата запроса
Процедура СообщитьОбОшибкахКорректностиВозвращаемыхТоваров(Знач ТаблицаОшибок)
	
	Для каждого СтрокаОшибки Из ТаблицаОшибок Цикл
		СообщениеОбОшибке = НСтр("ru='Возврат по номенклатуре %Номенклатура% \ %Характеристика% превышает количество закупленных товаров по документу поступления %НомерРеализации% на %Количество% %ЕдиницаИзмерения%'");
		Если СтрокаОшибки.Количество < 0 Тогда
			СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, "%Номенклатура%",         СтрокаОшибки.Номенклатура);
			СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, "%Характеристика%",       СтрокаОшибки.Характеристика);
			СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, "%Количество%",          -СтрокаОшибки.Количество);
			СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, "%ЕдиницаИзмерения%",     СтрокаОшибки.ЕдиницаИзмерения);
			СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, "%НомерРеализации%",      СтрокаОшибки.НомерПоступления);
			Если НЕ ЗначениеЗаполнено(СтрокаОшибки.Характеристика) Тогда
				СообщениеОбОшибке = СтрЗаменить(СообщениеОбОшибке, " \ ",      "");
			КонецЕсли;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке);
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры


// Выводит сообщения об ошибках заполнения цен в табличной части "Товары" документа закупки
//
Процедура СообщитьОбОшибкахКонтрольЗаполненияЦен(Знач Выборка,
	                                             Знач ДокументЗакупки,
	                                             Отказ)
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЕстьОшибкиЗаполненияЦен Тогда
			
			ТекстОшибки = НСтр("ru='Не заполнена колонка ""Цена"" в строке %НомерСтроки% списка ""Товары""'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НомерСтроки%",  Выборка.НомерСтроки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				ДокументЗакупки,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки,"Цена"),
				,
				Отказ);
			
		КонецЕсли;
			
		Если Выборка.ЕстьОшибкиЗаполненияСумм Тогда
			
			ТекстОшибки = НСтр("ru='Не заполнена колонка ""Сумма"" в строке %НомерСтроки% списка ""Товары""'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НомерСтроки%",  Выборка.НомерСтроки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				ДокументЗакупки,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки,"Сумма"),
				,
				Отказ);
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Выводит сообщение, если организация распоряжения отличается от организации документа
//
Процедура СообщитьОбОшибкахСоответствиеОрганизацииРаспоряжений(Знач Выборка, Знач ДокументЗакупки, Отказ)
	
	Пока Выборка.Следующий() Цикл
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Организация распоряжения указанного в строке %1 списка ""%2"" должна совпадать с организацией документа'"),
			Выборка.НомерСтроки,
			Выборка.ПредставлениеТабличнойЧасти);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ДокументЗакупки,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(Выборка.ИмяТабличнойЧасти, Выборка.НомерСтроки, "Распоряжение"),
			,
			Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает текст запроса временной таблицы товаров
//
// Параметры:
// 		ИмяТаблицы - Строка - Строка имени для временной таблицы
//
// Возвращаемое значение:
// 		Строка - Текст запроса временной таблицы
//
Функция ПолучитьТекстЗапросаВременнойТаблицыТоваров(ИмяТаблицы)
	
	Возврат "
	|ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Упаковка КАК Упаковка
	|ПОМЕСТИТЬ " + ИмяТаблицы + "
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|";
	
КонецФункции

// Возвращает текст запроса временной таблицы цен товаров по номерам строк
//
// Параметры:
// 		ИмяТаблицы - Строка - Строка имени для временной таблицы
// 		ИмяВременнойТаблицыТоваров - Строка - Строка имени временной таблицы товаров
// 		ВидЦенКакПараметр - Булево - Истина, если в запросе "ВидЦен" должен использоваться как параметр; Ложь - если как поле временной таблицы товаров
//
// Возвращаемое значение:
// 		Строка - Текст запроса временной таблицы
//
Функция ПолучитьТекстЗапросаВременнойТаблицыЦен(ИмяТаблицы, ИмяВременнойТаблицыТоваров)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВременнаяТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	&ВидЦеныПоставщика КАК ВидЦеныПоставщика,
	|	ВЫБОР
	|		КОГДА
	|			ВЫРАЗИТЬ (&Соглашение КАК Справочник.СоглашенияСПоставщиками).ВозвращатьМногооборотнуюТару И НЕ &ЭтоВыкупТары
	|			И ВременнаяТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|		КОГДА
	|			ВЫРАЗИТЬ (&Соглашение КАК Справочник.СоглашенияСПоставщиками).НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		ТОГДА
	|			ВременнаяТаблицаТовары.Номенклатура.СтавкаНДС
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	КОНЕЦ КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА
	|			ВременнаяТаблицаТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|		ТОГДА
	|			&ТекстЗапросаКоэффициентУпаковки1
	|		ИНАЧЕ
	|			1
	|	КОНЕЦ
	|	* ЦеныНоменклатурыПоставщиковСрезПоследних.Цена/ЕстьNULL(&ТекстЗапросаКоэффициентУпаковки2,1)
	|	* ВЫБОР
	|		КОГДА &Валюта <> ЦеныНоменклатурыПоставщиковСрезПоследних.Валюта
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(КурсыВалютыЦены.Кратность, 0) > 0
	|						И ЕСТЬNULL(КурсыВалютыЦены.Курс, 0) > 0
	|						И ЕСТЬNULL(КурсыВалюты.Кратность, 0) > 0
	|						И ЕСТЬNULL(КурсыВалюты.Курс, 0) > 0
	|					ТОГДА 
	|						(КурсыВалютыЦены.Курс * КурсыВалюты.Кратность)
	|						/ (КурсыВалюты.Курс * КурсыВалютыЦены.Кратность)
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Цена
	|ПОМЕСТИТЬ " + ИмяТаблицы + "
	|ИЗ
	| " + ИмяВременнойТаблицыТоваров + " КАК ВременнаяТаблицаТовары
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.ЦеныНоменклатурыПоставщиков.СрезПоследних(КОНЕЦПЕРИОДА(&Дата, ДЕНЬ),
	|				ВидЦеныПоставщика = &ВидЦеныПоставщика И
	|				(Номенклатура, Характеристика) В
	|				(ВЫБРАТЬ
	|					ВременнаяТаблицаТовары.Номенклатура,
	|					ВременнаяТаблицаТовары.Характеристика
	|				ИЗ
	|					" + ИмяВременнойТаблицыТоваров + " КАК ВременнаяТаблицаТовары)
	|) КАК ЦеныНоменклатурыПоставщиковСрезПоследних
	|ПО
	|	ВременнаяТаблицаТовары.Номенклатура = ЦеныНоменклатурыПоставщиковСрезПоследних.Номенклатура
	|	И ВременнаяТаблицаТовары.Характеристика = ЦеныНоменклатурыПоставщиковСрезПоследних.Характеристика
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалютыЦены
	|ПО 
	|	ЦеныНоменклатурыПоставщиковСрезПоследних.Валюта = КурсыВалютыЦены.Валюта
	|	
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, Валюта = &Валюта) КАК КурсыВалюты
	|	По Истина
	|;
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ВременнаяТаблицаТовары.Упаковка",
		"ВременнаяТаблицаТовары.Номенклатура"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ЦеныНоменклатурыПоставщиковСрезПоследних.Упаковка",
		"ЦеныНоменклатурыПоставщиковСрезПоследних.Номенклатура"));
		
	Возврат ТекстЗапроса
	
КонецФункции

// Функция формирует представление счет-фактуры.
//
// Параметры:
//  Номер - Строка - Номер счета-фактуры;
//  Дата - Дата - Дата счета-фактуры;
//  Проведен - Булево - Состояние проведения счета-фактуры.
//  Уточнение - Строка - например "полученный", 
//		добавляется для отличения счетов-фактур разного вида в одном документе
//
// Возвращаемое значение:
//	Строка - Представление счета-фактуры.
//
Функция ПредставлениеСчетаФактуры(Номер, Дата, Проведен, Уточнение = "") Экспорт
	
	Возврат  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Счет-фактура %1'"), Уточнение)
		+ " " +"№ " + СокрЛП(Номер) + " " + НСтр("ru='от'") + " " + Формат(Дата, "ДЛФ=DD")
		+ ?(НЕ Проведен,  " " + НСтр("ru='(не проведен)'"),"");
	
КонецФункции

// Возвращает форматированный текст представления счета-фактуры в документе.
//
// Параметры:
//	Основание - ДокументСсылка - Документ, на основании которого вводится счет-фактура;
//	Организация - СправочникСсылка.Организации - Организация, на имя которой оформляется счет-фактура;
//	НеТребуется - Булево - Истина - для документа не требуется вводить счет-фактуру.
//
// Возвращаемое значение:
//	Строка - Представление счета-фактуры.
//
Функция ПредставлениеСчетаФактурыВДокументеЗакупки(Основание, Организация, НеТребуется = Ложь) Экспорт
	
	Перем РеквизитыСчетаФактуры;
	
	ПараметрыОтбора = Новый Структура("Организация", Организация);
	СчетаФактуры = Документы.СчетФактураПолученный.СчетаФактурыПоОснованию(Основание, Организация, РеквизитыСчетаФактуры, Ложь);
	МассивСтрок = Новый Массив;
	
	ВозможноПробитиеЧека = Ложь;
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
		ВозможноПробитиеЧека = Истина;
	КонецЕсли;
	Если ВозможноПробитиеЧека Тогда
		ФискальнаяОперацияДанныеЖурнала = ПодключаемоеОборудованиеУТВызовСервера.ДанныеЖурналаФискальныхОпераций(Основание);
	КонецЕсли;
	
	Уточнение = ?(ТипЗнч(Основание) = Тип("ДокументСсылка.КорректировкаРеализации")
					ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями"), 
				НСтр("ru='полученный'") + " ",
				"");
	
	Если СчетаФактуры.Количество() > 0 Тогда
		
		Если СчетаФактуры.Количество() > 1 Тогда
		
			ТекстСчетФактура = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Счета-фактуры (%1)'"), Строка(СчетаФактуры.Количество()));
				
			СтрокаГиперссылки = Новый ФорматированнаяСтрока(
				ТекстСчетФактура, , 
				ЦветаСтиля.ЦветГиперссылки, ,
				"ОткрытьСписокСчетовФактурПолученных");
			
		Иначе
			
			СтрокаГиперссылки = Новый ФорматированнаяСтрока(
				ПредставлениеСчетаФактуры(РеквизитыСчетаФактуры.Номер, РеквизитыСчетаФактуры.ДатаСоставления, РеквизитыСчетаФактуры.Проведен, Уточнение), ,
				ЦветаСтиля.ЦветГиперссылки, ,
				ПолучитьНавигационнуюСсылку(РеквизитыСчетаФактуры.Ссылка));
			
		КонецЕсли;
		
		МассивСтрок.Добавить(СтрокаГиперссылки);
				
	ИначеЕсли НеТребуется Тогда
		
		ТекстСчетФактура = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Счет-фактура %1не требуется'"), Уточнение);
		
		МассивСтрок.Добавить(ТекстСчетФактура);
		
	ИначеЕсли Не ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.СчетФактураПолученный) Тогда
		
		Если Не ВозможноПробитиеЧека
			Или (ВозможноПробитиеЧека И ФискальнаяОперацияДанныеЖурнала = Неопределено) Тогда
			
			ТекстСчетФактура = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Счет-фактура %1не зарегистрирован'"), Уточнение);
			
			МассивСтрок.Добавить(ТекстСчетФактура);
			
		КонецЕсли;
		
		Если ВозможноПробитиеЧека И ФискальнаяОперацияДанныеЖурнала = Неопределено Тогда
			
			МассивСтрок.Добавить(".");
			МассивСтрок.Добавить(" ");
			МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(
					НСтр("ru = 'Пробить чек'"),,
					ЦветаСтиля.ЦветГиперссылки,,
					"ПробитьЧек"));
			
		КонецЕсли;
		
		Если ВозможноПробитиеЧека И ФискальнаяОперацияДанныеЖурнала <> Неопределено Тогда
			
			МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(
					СтрШаблон(
						НСтр("ru = 'Пробит чек №%1'"),
						ФискальнаяОперацияДанныеЖурнала.НомерЧекаККМ),,
					ЦветаСтиля.ЦветГиперссылки,,
					"ОткрытьЗаписьЖурналаФискальныхОпераций"));
			
		КонецЕсли;
		
	Иначе
		
		Если Не ВозможноПробитиеЧека
			Или (ВозможноПробитиеЧека И ФискальнаяОперацияДанныеЖурнала = Неопределено) Тогда
			
			ТекстСчетФактура = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Зарегистрировать счет-фактуру %1'"), Уточнение);
				
			СтрокаГиперссылки = Новый ФорматированнаяСтрока(
				ТекстСчетФактура, ,
				ЦветаСтиля.ЦветГиперссылки, ,
				"ВвестиНовыйСчетФактуру");
				
			МассивСтрок.Добавить(СтрокаГиперссылки);
			
		КонецЕсли;
		
		Если ВозможноПробитиеЧека И ФискальнаяОперацияДанныеЖурнала = Неопределено Тогда
			
			МассивСтрок.Добавить(" ");
			МассивСтрок.Добавить(НСтр("ru = 'или'"));
			МассивСтрок.Добавить(" ");
			МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(
					НСтр("ru = 'пробить чек'"),,
					ЦветаСтиля.ЦветГиперссылки,,
					"ПробитьЧек"));
			
		КонецЕсли;
		
		Если ВозможноПробитиеЧека И ФискальнаяОперацияДанныеЖурнала <> Неопределено Тогда
			
			МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(
					СтрШаблон(
						НСтр("ru = 'Пробит чек №%1'"),
						ФискальнаяОперацияДанныеЖурнала.НомерЧекаККМ),,
					ЦветаСтиля.ЦветГиперссылки,,
					"ОткрытьЗаписьЖурналаФискальныхОпераций"));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецФункции

// Функция формирует представление заявления о ввозе товаров из стран ЕАЭС.
//
// Параметры:
//  Номер - Строка - Номер заявления о ввозе;
//  Дата - Дата - Дата заявления о ввозе;
//  Проведен - Булево - Состояние проведения заявления о ввозе.
//  ДатаПодтвержденияОплаты - Дата - Дата подтверждения оплаты по заявлению от ФНС.
//
// Возвращаемое значение:
//	Строка - Представление заявления о ввозе.
//
Функция ПредставлениеЗаявленияОВвозеТоваров(Номер, Дата, Проведен, ДатаПодтвержденияОплаты)
	
	СтрокаЕстьОплата = "";
	Если ЗначениеЗаполнено(ДатаПодтвержденияОплаты) Тогда
		СтрокаЕстьОплата = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					", " + НСтр("ru='подтверждено %1 г.'"),
					Формат(ДатаПодтвержденияОплаты, "ДЛФ=D"));
	КонецЕсли;
	
	Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Завление о ввозе товаров № %1 от %2 г.%3%4'"),
				СокрЛП(Номер),
				Формат(Дата, "ДЛФ=D"),
				?(НЕ Проведен, " " + НСтр("ru='(не проведен)'"), ""),
				СтрокаЕстьОплата);
	
	Возврат Результат;
	
КонецФункции

//Вызывает исключение, если партнер не является поставщиком
//
// Параметры:
//	Партнер       - СправочникСсылка.Партнеры-Проверяемый партнер
//
Процедура ПроверитьВозможностьВводаНаОснованииПартнераПоставщикаКонкурента(Партнер) Экспорт
	
	Если Не ЗначениеЗаполнено(Партнер) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыПартнера = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Партнер, "Поставщик,Конкурент");
	
	Если Не РеквизитыПартнера.Поставщик И Не РеквизитыПартнера.Конкурент Тогда
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьКонкурентнуюРазведку") Тогда
			ТекстОшибки = НСтр("ru='Партнер не является поставщиком или конкурентом. Ввод на основании доступен только для поставщика или конкурента.'");
		Иначе
			ТекстОшибки = НСтр("ru='Партнер не является поставщиком. Ввод на основании доступен только для поставщика.'");
		КонецЕсли;
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УсловноеОформление

// Устанавливает условное для реквизитов связанных с отнесением расходов в табличной части "Товары".
//
// Параметры:
//	Форма - УправляемаяФорма - форма, для которой настраивается условное оформление
//
Процедура УстановитьУсловноеОформлениеПоРасходам(Форма) Экспорт
	
	// оформление статей и аналитики расходов

	// отключение видимости полей, необходимых для услуг и работ, для определенных операций

	Элемент = Форма.УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыСписатьНаРасходы.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыСтатьяРасходов.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыАналитикаРасходов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ХозяйственнаяОперация");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ОтборЭлемента.ПравоеЗначение = НоменклатураСервер.СписокХозяйственныхОперацийИсключающихУслуги();

	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	// только просмотр, если товар

	Элемент = Форма.УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыСписатьНаРасходы.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыСтатьяРасходов.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыАналитикаРасходов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ТипНоменклатуры");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(Перечисления.ТипыНоменклатуры.Товар);
	СписокЗначений.Добавить(Перечисления.ТипыНоменклатуры.МногооборотнаяТара);
	ОтборЭлемента.ПравоеЗначение = СписокЗначений;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	// только просмотр, если работа под назначение

	Элемент = Форма.УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыСписатьНаРасходы.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыСтатьяРасходов.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыАналитикаРасходов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ТипНоменклатуры");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыНоменклатуры.Работа;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.Назначение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	// только просмотр, если услуга

	Элемент = Форма.УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыСписатьНаРасходы.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ТипНоменклатуры");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыНоменклатуры.Услуга;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	// поясняющая надпись, если товар

	Элемент = Форма.УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыСтатьяРасходов.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыАналитикаРасходов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ТипНоменклатуры");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(Перечисления.ТипыНоменклатуры.Товар);
	СписокЗначений.Добавить(Перечисления.ТипыНоменклатуры.МногооборотнаяТара);
	ОтборЭлемента.ПравоеЗначение = СписокЗначений;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<для работ/услуг без назначения>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);

	// поясняющая надпись, если работа под назначение

	Элемент = Форма.УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыСтатьяРасходов.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыАналитикаРасходов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ТипНоменклатуры");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыНоменклатуры.Работа;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.Назначение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<для работ/услуг без назначения>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);

	// поясняющая надпись, если работа и на расходы не списываем

	Элемент = Форма.УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыАналитикаРасходов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ТипНоменклатуры");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыНоменклатуры.Работа;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.СписатьНаРасходы");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.Назначение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<при списании на расходы>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);

	// отметка незаполненного статьи расходов, если списываем на расходы

	Элемент = Форма.УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыСтатьяРасходов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.СписатьНаРасходы");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.СтатьяРасходов");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	// текст аналитики расходов, если статья не выбрана
	
	Элемент = Форма.УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыАналитикаРасходов.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.СтатьяРасходов");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.АналитикаРасходов");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.СписатьНаРасходы");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<выберите статью расходов>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//Аналитика расходов "Только просмотр", если не заполнена статья расходов
	
	Элемент = Форма.УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.ТоварыАналитикаРасходов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.СтатьяРасходов");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
