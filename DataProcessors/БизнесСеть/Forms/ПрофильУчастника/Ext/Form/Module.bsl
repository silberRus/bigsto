
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ИНН", ПрофильИНН);
	Параметры.Свойство("КПП", ПрофильКПП);
	Параметры.Свойство("Ссылка", Ссылка);
	
	ПерезаполнитьДанные = Истина;
	Если Параметры.Свойство("Реквизиты") Тогда
		ЗаполнитьРеквизитыПрофиля(Параметры.Реквизиты);
		ПерезаполнитьДанные = Ложь;
	Иначе
		ПолучитьДанныеСервиса();
	КонецЕсли;
	
	Если ПрофильКПП = "0" Тогда
		ПрофильКПП = "";
	КонецЕсли;

	Если ЗначениеЗаполнено(ПрофильИНН) И НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Ссылка = ОбменСКонтрагентамиПереопределяемый.СсылкаНаОбъектПоИННКПП("Контрагенты", ПрофильИНН, ПрофильКПП);
	КонецЕсли;

	УстановитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоискТорговыхПредложенийНажатие(Элемент)
	
	// Команда вызова метода доступна только при встраивании подсистемы "Торговые предложения".
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Поставщик",             Ссылка);
	ПараметрыОткрытия.Вставить("ИНН",                   ПрофильИНН);
	ПараметрыОткрытия.Вставить("КПП",                   ПрофильКПП);
	ПараметрыОткрытия.Вставить("ПоставщикНаименование", ПрофильНаименование);
	
	// Установка отбора по поставщику, если форма уже открыта.
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ОтборПоставщик", ПараметрыОткрытия);
	Оповестить("ТорговыеПредложения_Обновить", ПараметрыОповещения, ЭтотОбъект);
	
	ИмяФормыПоиска = "Обработка.ТорговыеПредложения.Форма.ПоискПредложенийПоСтроке";
	ОткрытьФорму(ИмяФормыПоиска, ПараметрыОткрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьДанныеСервиса()
	
	Отказ = Ложь;
	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("Ссылка", Ссылка);
	ПараметрыКоманды.Вставить("ИНН", ПрофильИНН);
	ПараметрыКоманды.Вставить("КПП", ПрофильКПП);
	Результат = БизнесСетьКлиентСервер.ОписаниеРезультатаКомандыСервиса();
	БизнесСетьВызовСервера.ПолучитьРеквизитыУчастника(ПараметрыКоманды, Результат, Отказ);
	Если Не Отказ И Результат.КодСостояния = 200 Тогда
		ЗаполнитьРеквизитыПрофиля(Результат.Данные);
	ИначеЕсли Не Отказ И Результат.КодСостояния = 404 Тогда
		ВызватьИсключение НСтр("ru = 'Участник 1С:Бизнес-сеть не найден.'");
	Иначе
		ВызватьИсключение НСтр("ru = 'Ошибка получения профиля участника 1С:Бизнес-сеть.'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыПрофиля(Источник)
	
	Если Источник.Свойство("ДатаРегистрации") Тогда
		ПрофильДатаРегистрации = БизнесСетьКлиентСервер.ДатаИзUnixTime(Источник.ДатаРегистрации);
	КонецЕсли;
	
	Источник.Свойство("Наименование",     ПрофильНаименование);
	Источник.Свойство("ИНН",              ПрофильИНН);
	Источник.Свойство("КПП",              ПрофильКПП);
	Источник.Свойство("НаименованиеЕГРН", ПрофильНаименованиеЕГРН);
	Источник.Свойство("Адрес",            ПрофильАдрес);
	Источник.Свойство("ОГРН",             ПрофильОГРН);
	Источник.Свойство("Сайт",             ПрофильСайт);
	Источник.Свойство("Телефон",          ПрофильТелефон);
	Источник.Свойство("ЭлектроннаяПочта", ПрофильЭлектроннаяПочта);
	Источник.Свойство("КоличествоТорговыхПредложений", ПрофильКоличествоТорговыхПредложений);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	Элементы.Ссылка.Видимость                   = ЗначениеЗаполнено(Ссылка);
	Элементы.ПрофильКПП.Видимость               = ЗначениеЗаполнено(ПрофильКПП);
	Элементы.ПрофильОГРН.Видимость              = ЗначениеЗаполнено(ПрофильОГРН);
	Элементы.ПрофильАдрес.Видимость             = ЗначениеЗаполнено(ПрофильАдрес);
	Элементы.ПрофильТелефон.Видимость           = ЗначениеЗаполнено(ПрофильТелефон);
	Элементы.ПрофильЭлектроннаяПочта.Видимость  = ЗначениеЗаполнено(ПрофильЭлектроннаяПочта);
	Элементы.ПрофильСайт.Видимость              = ЗначениеЗаполнено(ПрофильСайт);
	
	Заголовок = НСтр("ru = '%1 (профиль участника 1С:Бизнес-сеть)'");
	Заголовок = СтрШаблон(Заголовок, ПрофильНаименование);
	
	// ЭлектронноеВзаимодействие.ТорговыеПредложения
	ЕстьТорговыеПредложения = ОбщегоНазначения.ПодсистемаСуществует("ЭлектронноеВзаимодействие.ТорговыеПредложения");
	
	Элементы.ПоискТорговыхПредложений.Видимость = 
		ЗначениеЗаполнено(ПрофильКоличествоТорговыхПредложений) И ЕстьТорговыеПредложения;
	
	Если ПрофильКоличествоТорговыхПредложений <> 0 Тогда
		Элементы.ПоискТорговыхПредложений.Заголовок =
			СтрШаблон(НСтр("ru = 'Открыть торговые предложения (%1)'"), ПрофильКоличествоТорговыхПредложений);
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ТорговыеПредложения
	
КонецПроцедуры

#КонецОбласти
