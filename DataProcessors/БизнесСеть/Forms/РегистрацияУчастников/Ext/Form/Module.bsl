
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не Пользователи.РолиДоступны("АдминистрированиеАбонентаБизнесСеть") Тогда
		ТекстОшибки = НСтр("ru = 'Недостаточно прав для выполнения операции в сервисе 1С:Бизнес-сеть.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
		Возврат;
	КонецЕсли;
		
	ОбновитьДанныеАутентификации();
	
	ОдиночнаяРегистрация = ЗначениеЗаполнено(Параметры.Ссылка);
	ИспользуетсяНесколькоОрганизаций = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизацийЭД");
	
	Если Не ОдиночнаяРегистрация И Не ИспользуетсяНесколькоОрганизаций Тогда
		ОдиночнаяРегистрация = Истина;
		Параметры.Ссылка = ЭлектронноеВзаимодействиеСлужебный.ОрганизацияПоУмолчанию();
		Если Не ЗначениеЗаполнено(Параметры.Ссылка) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось определить организацию.'"),,,, Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ОдиночнаяРегистрация Тогда
		ЗаполнитьРеквизитыПоСсылке(Параметры.Ссылка);
	КонецЕсли;
	
	ИнициализацияДинамическогоСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьВидимостьДоступностьЭлементов();

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИнтернетПоддержкаОтключена" Тогда
		Логин = "";
		ОтобразитьСостояниеПодключенияИПП(ЭтотОбъект, Логин);
	ИначеЕсли ИмяСобытия = "ИнтернетПоддержкаПодключена" Тогда
		ОбновитьДанныеАутентификации();
	ИначеЕсли ИмяСобытия = "БизнесСеть_РегистрацияОрганизаций" И Источник <> ЭтотОбъект Тогда
		Если ОдиночнаяРегистрация Тогда
			ЗаполнитьРеквизитыПоСсылке(Параметры.Ссылка);
			ОбновитьВидимостьДоступностьЭлементов();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗарегистрироватьОрганизации(Команда)
	
	ОчиститьСообщения();
	ЕстьПодключениеКСервису = Неопределено;
	ОбновитьДанныеАутентификации(ЕстьПодключениеКСервису);

	Если Не ЕстьПодключениеКСервису Тогда
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(
			Новый ОписаниеОповещения("ЗарегистрироватьОрганизацииПродолжение", ЭтотОбъект), ЭтотОбъект);
	Иначе
		ЗарегистрироватьОрганизацииПродолжение(Неопределено, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьОрганизации(Команда)
	
	Если ОдиночнаяРегистрация Тогда
		ТекстВопроса =
			НСтр("ru = 'Будет произведено отключение организации от сервиса 1С:БизнесСеть в программе (информационной базе).
			|При этом, для других участников сервиса организация будет отображаться как зарегистрированные.'");
	Иначе
		ТекстВопроса =
			НСтр("ru = 'Организации будут отключены от сервиса 1С:БизнесСеть в программе (информационной базе).
			|При этом, для других участников сервиса организации будут отображаться как зарегистрированные.'");
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ОтключитьОрганизацииЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьОрганизацииВСервисе(Команда)
	
	Если ОдиночнаяРегистрация Тогда
		ТекстВопроса = НСтр("ru = 'Организация будет удалена в сервисе 1С:БизнесСеть.
		|Обмен электронными документами и другие функции сервиса будут недоступны.'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Выбранные организации будут удалены в сервисе 1С:БизнесСеть.
		|Обмен электронными документами и другие функции сервиса будут недоступны.'");
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("УдалитьОрганизацииВСервисЗавершение", ЭтотОбъект),
		ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройки(Команда)
	
	ОчиститьСообщения();
	ОткрытьФорму("Обработка.БизнесСеть.Форма.НастройкаПодключения");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьДанныеАутентификации(ЕстьПодключениеКСервису = Неопределено)
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Элементы.ДекорацияЛогинИПП.Видимость = Ложь;
		ЕстьПодключениеКСервису = Истина;
	Иначе
		УстановитьПривилегированныйРежим(Истина);
		ПараметрыАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если ТипЗнч(ПараметрыАутентификации) = Тип("Структура") Тогда
			Логин = ПараметрыАутентификации.Логин;
			ЕстьПодключениеКСервису = Истина;
		Иначе
			Логин = "";
			ЕстьПодключениеКСервису = Ложь;
		КонецЕсли;
		ОтобразитьСостояниеПодключенияИПП(ЭтотОбъект, Логин);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтобразитьСостояниеПодключенияИПП(Форма, Логин)
	
	Если ЗначениеЗаполнено(Логин) Тогда
		МассивСтрок = Новый Массив;
		ШаблонЛогина    = НСтр("ru = 'Интернет-поддержка: подключена для пользователя <b>%1</b>'");
		ШаблонПодсказки = НСтр("ru = 'Регистрация организаций производится с присоединением к учетной записи Интернет-поддержки <b>%1</b>.'");
		ШаблонЛогина = СтрШаблон(ШаблонЛогина, Логин);
		ШаблонПодсказки = СтрШаблон(ШаблонПодсказки, Логин);
	Иначе
		ШаблонЛогина    = НСтр("ru = 'Интернет-поддержка: требуется подключение.'");
		ШаблонПодсказки = НСтр("ru = 'Требуется подключение Интернет-поддержки в программе.
									 |Регистрация организаций производится с присоединением к учетной записи Интернет-поддержки.'");
	КонецЕсли;
	Форма.Элементы.ДекорацияЛогинИПП.Заголовок = СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(ШаблонЛогина);
	Форма.Элементы.ДекорацияЛогинИПП.РасширеннаяПодсказка.Заголовок = СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(ШаблонПодсказки);

КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьОрганизацииПродолжение(Знач ПараметрыАутентификации, Контекст) Экспорт
	
	ПараметрыКоманды = Неопределено;
	Если ТипЗнч(ПараметрыАутентификации)= Тип("Структура") Тогда
		Логин = ПараметрыАутентификации.Логин;
	ИначеЕсли ПараметрыАутентификации <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ТребуетсяОбновитьИнтерфейс = Ложь;
	
	ЗарегистрироватьОрганизацию(ТребуетсяОбновитьИнтерфейс, Отказ);
	
	Если ТребуетсяОбновитьИнтерфейс Тогда
		#Если НЕ ВебКлиент Тогда
		ОбновитьИнтерфейс();
		#КонецЕсли
		ОбновитьПовторноИспользуемыеЗначения();
		Оповестить("Запись_НаборКонстант",, "ИспользоватьОбменБизнесСеть");
	КонецЕсли;
	
	Если Не Отказ Тогда
		Если ОдиночнаяРегистрация Тогда
			СсылкаОповещения = ПолучитьНавигационнуюСсылку(Параметры.Ссылка);
			ОповеститьОбИзменении(Параметры.Ссылка);
		Иначе
			СсылкаОповещения = Неопределено;
		КонецЕсли;
		ТекстОповещения = НСтр("ru = 'Регистрация организации успешно выполнена.'");
		ПоказатьОповещениеПользователя(НСтр("ru = '1С:Бизнес-сеть'"), СсылкаОповещения, ТекстОповещения, 
			БиблиотекаКартинок.БизнесСеть);
			
		Оповестить("БизнесСеть_РегистрацияОрганизаций",, ЭтотОбъект);
	КонецЕсли;
	
	Если Не Отказ И (ОдиночнаяРегистрация
		ИЛИ ЭтотОбъект.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца) Тогда
		// Успешная регистрация.
		Закрыть(Истина);
	Иначе
		Элементы.Список.Обновить();
	КонецЕсли;
	
	ОбновитьВидимостьДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьСсылкиРегистрации(МассивСсылок, Отказ)
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка,
	|	Организации.ИНН КАК ИНН
	|ИЗ
	|	&Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Организации1СБизнесСеть КАК Организации1СБизнесСеть
	|		ПО (Организации1СБизнесСеть.Организация = Организации.Ссылка)
	|ГДЕ
	|	Организации.Ссылка В(&МассивСсылок)
	|	И Организации1СБизнесСеть.Организация ЕСТЬ NULL";
	
	ИмяСправочникаОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяПрикладногоСправочника("Организации");
	ИННОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Организации.ИНН", "Организации." + ИННОрганизации);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Организации", "Справочник." + ИмяСправочникаОрганизации);
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ТекстОшибки = НСтр("ru = 'Команда не может быть выполнена для выделенных строк.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	МассивСсылок.Очистить();
	Пока Выборка.Следующий() Цикл
		Если ПустаяСтрока(Выборка.ИНН) Тогда
			ТекстОшибки = НСтр("ru = 'Не заполнено свойство ""ИНН"" %1 ""%2"".'");
			ТекстОшибки = СтрШаблон(ТекстОшибки,
				НРег(ИмяСправочникаОрганизации), Выборка.Ссылка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.Ссылка);
		Иначе
			МассивСсылок.Добавить(Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивСсылок.Количество() = 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьОрганизацииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если ОдиночнаяРегистрация Тогда
		ВыполнитьОтключениеОрганизацииОдиночное(Ложь);
	Иначе
		ВыполнитьОтключениеОрганизаций(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьОрганизацииВСервисЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если ОдиночнаяРегистрация Тогда
		ВыполнитьОтключениеОрганизацииОдиночное(Истина);
	Иначе
		ВыполнитьОтключениеОрганизаций(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыПоСсылке(Ссылка)
	
	СокращенноеНаименованиеОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("СокращенноеНаименованиеОрганизации");
	ИННОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
	КППОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППОрганизации");
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Наименование КАК Наименование,
	|	Организации.ИНН КАК ИНН,
	|	Организации.КПП КАК КПП,
	|	ВЫБОР
	|		КОГДА Организации1СБизнесСеть.Организация ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК УчастникЗарегистрирован
	|ИЗ
	|	&Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Организации1СБизнесСеть КАК Организации1СБизнесСеть
	|		ПО Организации.Ссылка = Организации1СБизнесСеть.Организация
	|ГДЕ
	|	Организации.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Параметры.Ссылка);
	ИмяСправочникаОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяПрикладногоСправочника("Организации");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Организации", "Справочник." + ИмяСправочникаОрганизации);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Организации.Наименование", "Организации." + СокращенноеНаименованиеОрганизации);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Организации.ИНН", "Организации." + ИННОрганизации);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Организации.КПП", "Организации." + КППОрганизации);
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	УчастникЗарегистрирован = Ложь;
	
	УчастникНаименование = "";
	УчастникИНН = "";
	УчастникКПП = "";
	
	Если Выборка.Следующий() Тогда
		УчастникНаименование = Выборка.Наименование;
		УчастникИНН = Выборка.ИНН;
		УчастникКПП = Выборка.КПП;
		УчастникЗарегистрирован = Выборка.УчастникЗарегистрирован;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьДоступностьЭлементов()
	
	Элементы.ГруппаОдиночное.Видимость             = ОдиночнаяРегистрация;
	Элементы.ГруппаСписок.Видимость                = Не ОдиночнаяРегистрация;
	Элементы.УчастникЗарегистрирован.Видимость     = УчастникЗарегистрирован;
	Элементы.Подключить.Доступность                = Не УчастникЗарегистрирован;
	Элементы.ГруппаКПП.Видимость                   = ЗначениеЗаполнено(УчастникКПП);
	
КонецПроцедуры

&НаСервере
Процедура ИнициализацияДинамическогоСписка()
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА Организации1СБизнесСеть.Организация ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Регистрация,
	|	Организации.ИНН КАК ИНН,
	|	Организации.КПП КАК КПП
	|ИЗ
	|	&Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Организации1СБизнесСеть КАК Организации1СБизнесСеть
	|		ПО Организации.Ссылка = Организации1СБизнесСеть.Организация";
	
	ИмяСправочникаОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяПрикладногоСправочника("Организации");
	ИННОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
	КППОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППОрганизации");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Организации", "Справочник." + ИмяСправочникаОрганизации);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ".ИНН", "." + ИННОрганизации);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ".КПП", "." + КППОрганизации);
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ОсновнаяТаблица = "Справочник." + ИмяСправочникаОрганизации;
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);	
	
КонецПроцедуры
 
&НаСервере
Процедура ЗарегистрироватьОрганизацию(ТребуетсяОбновитьИнтерфейс, Отказ)
	
	ОтобразитьСостояниеПодключенияИПП(ЭтотОбъект, Логин);
	
	Если ОдиночнаяРегистрация Тогда
		МассивСсылок = Новый Массив;
		МассивСсылок.Добавить(Параметры.Ссылка);
	Иначе
		МассивСсылок = Элементы.Список.ВыделенныеСтроки;
		ПроверитьСсылкиРегистрации(МассивСсылок, Отказ);
	КонецЕсли;
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	БизнесСеть.ЗарегистрироватьОрганизации(МассивСсылок, Отказ, ТребуетсяОбновитьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОтключениеОрганизаций(РежимУдаленияВСервисе)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	МассивСсылок = Элементы.Список.ВыделенныеСтроки;
	
	ТребуетсяОбновитьИнтерфейс = Ложь;
	БизнесСетьВызовСервера.ОтключитьОрганизации(МассивСсылок, РежимУдаленияВСервисе, Отказ, ТребуетсяОбновитьИнтерфейс);

	Если ТребуетсяОбновитьИнтерфейс Тогда
		#Если НЕ ВебКлиент Тогда
		ОбновитьИнтерфейс();
		#КонецЕсли
	 	ОбновитьПовторноИспользуемыеЗначения();
		Оповестить("Запись_НаборКонстант",, "ИспользоватьОбменБизнесСеть");
	КонецЕсли;
	
	Если Не Отказ Тогда
		Если РежимУдаленияВСервисе Тогда
			ТекстОповещения = НСтр("ru = 'Удаление организаций в сервисе успешно выполнено.'");
		Иначе
			ТекстОповещения = НСтр("ru = 'Отключение организаций в программе успешно выполнено.'");
		КонецЕсли;
		ПоказатьОповещениеПользователя(НСтр("ru = '1С:Бизнес-сеть'"),, ТекстОповещения, БиблиотекаКартинок.БизнесСеть);
		Оповестить("БизнесСеть_РегистрацияОрганизаций",, ЭтотОбъект);
	КонецЕсли;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОтключениеОрганизацииОдиночное(РежимУдаленияВСервисе)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	Массив = Новый Массив;
	Массив.Добавить(Параметры.Ссылка);
	ТребуетсяОбновитьИнтерфейс = Ложь;
	БизнесСетьВызовСервера.ОтключитьОрганизации(Массив, РежимУдаленияВСервисе, Отказ, ТребуетсяОбновитьИнтерфейс);

	Если ТребуетсяОбновитьИнтерфейс Тогда
		#Если НЕ ВебКлиент Тогда
		ОбновитьИнтерфейс();
		#КонецЕсли
	 	ОбновитьПовторноИспользуемыеЗначения();
		Оповестить("Запись_НаборКонстант",, "ИспользоватьОбменБизнесСеть");
	КонецЕсли;
	
	Если Не Отказ Тогда
		Если РежимУдаленияВСервисе Тогда
			ТекстОповещения = НСтр("ru = 'Удаление организации в сервисе успешно выполнено.'");
		Иначе
			ТекстОповещения = НСтр("ru = 'Отключение организации в программе успешно выполнено.'");
		КонецЕсли;
		ПоказатьОповещениеПользователя(НСтр("ru = '1С:Бизнес-сеть'"),, ТекстОповещения, БиблиотекаКартинок.БизнесСеть);
		УчастникЗарегистрирован = Истина;
		
		Оповестить("БизнесСеть_РегистрацияОрганизаций",, ЭтотОбъект);
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
