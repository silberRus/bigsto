#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Выгрузка

// Заполняет табл. часть "Банковские счета" настройками счетов для выгрузки платежей
//
Процедура ЗаполнитьТаблицуСчетовВыгрузки() Экспорт
	
	ТаблицаСчетов = Обработки.КлиентБанк.ТаблицаСчетов();
	
	ДополнитьТаблицуСчетовПоддержкойПрямогоОбмена(ТаблицаСчетов);
	
	БанковскиеСчета.Загрузить(ТаблицаСчетов);
	
КонецПроцедуры

// Выполняет выгрузку платежей в файл по всем выбранным счетам
//
// Параметры:
//    ИдФормы - УникальныйИдентификатор - идентификатор вызывающей метод формы
//
Процедура ВыгрузитьПлатежи(ИдФормы = "") Экспорт
	
	Отбор = Новый Структура("Пометка", Истина);
	ВыгружаемыеБанковскиеСчета = БанковскиеСчета.Выгрузить(БанковскиеСчета.НайтиСтроки(Отбор));
	ИнформацияОбОбъединенииФайлов = ИнформацияОбОбъединенииФайлов(ВыгружаемыеБанковскиеСчета);
	
	Если ИнформацияОбОбъединенииФайлов.СчетаБезПравил.Количество() <> 0 Тогда
		// Типовой алгоритм выгрузки по счетам
		Для каждого Счет из ИнформацияОбОбъединенииФайлов.СчетаБезПравил Цикл
			Если Не Счет.Пометка Или Не ЗначениеЗаполнено(Счет.НастройкаОбмена) Или Счет.Выгружается Или Счет.ПрямойОбмен Тогда
				Продолжить;
			КонецЕсли;
			
			ВыгрузитьПлатежиПоСчету(Счет, ИдФормы);
		КонецЦикла;
	КонецЕсли;
	
	Если ИнформацияОбОбъединенииФайлов.КоличествоСчетовВПравиле.Количество() <> 0 Тогда
		// Выгрузка порциями, с объединением по правилам
		ВыгрузитьПлатежиПорциямиПоПравилам(ИнформацияОбОбъединенииФайлов, ИдФормы);
	КонецЕсли;
	
	ОбновитьИнформациюОВыгруженныхФайлах(ИнформацияОбОбъединенииФайлов);
	
КонецПроцедуры

// Выполняет выгрузку платежей в файл порциями по правилам
//
// Параметры:
//    ИнформацияОбОбъединенииФайлов - Структура - информация для выгрузки платежек потоками
//    ИдФормы - УникальныйИдентификатор - идентификатор вызывающей метод формы
//
Процедура ВыгрузитьПлатежиПорциямиПоПравилам(ИнформацияОбОбъединенииФайлов, ИдФормы = "") Экспорт
	
	ПотокиВыгрузки = ИнформацияОбОбъединенииФайлов.ПотокиВыгрузки;
	КоличествоСчетовВПравиле = ИнформацияОбОбъединенииФайлов.КоличествоСчетовВПравиле;
	
	Для Каждого ПотокВыгрузки Из ПотокиВыгрузки Цикл
		СтрокиКВыгрузке = ДокументыКВыгрузкеПоСчетам(ПотокВыгрузки.Счета);
		Если СтрокиКВыгрузке.Количество() Тогда
			ПараметрыВыгрузки = Новый Структура;
			ПараметрыВыгрузки.Вставить("БанковскийСчет",     ПотокВыгрузки.Счета);
			ПараметрыВыгрузки.Вставить("НастройкаОбмена",    ПотокВыгрузки.Правило);
			ПараметрыВыгрузки.Вставить("Кодировка",          ПотокВыгрузки.Счета[0].Кодировка);
			
			ТаблицаДокументов = ДокументыКВыгрузке.Выгрузить(СтрокиКВыгрузке);
			ПараметрыВыгрузки.Вставить("ТаблицаДокументов", ТаблицаДокументов);
			
			ИзменитьРеквизитыСчетовПотока(ПотокВыгрузки.Счета,
				Новый Структура("Выгружен, АдресХранилищаДокументов", Ложь, ПоместитьВоВременноеХранилище(ТаблицаДокументов, ИдФормы)));
				
			Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
				ИдФормы,
				"Обработки.КлиентБанк.Выгрузить",
				ПараметрыВыгрузки,
				НСтр("ru='Выгрузка платежей в банк'")
			);
			
			ИзменитьРеквизитыСчетовПотока(ПотокВыгрузки.Счета, 
				Новый Структура("АдресХранилищаФайла", Результат.АдресХранилища));
			
			Если Результат.ЗаданиеВыполнено Тогда
				ИзменитьРеквизитыСчетовПотока(ПотокВыгрузки.Счета, 
					Новый Структура("СохранитьФайл", Истина));
			Иначе
				ИзменитьРеквизитыСчетовПотока(ПотокВыгрузки.Счета, 
					Новый Структура("Выгружается, ИдентификаторВыгрузки", Истина, Результат.ИдентификаторЗадания));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Выполняет выгрузку платежей в файл по указанному счету
//
// Параметры:
//    Счет - Строка табличной части - Строка списка банковских счетов
//    ИдФормы - УникальныйИдентификатор - идентификатор вызывающей метод формы
//
Процедура ВыгрузитьПлатежиПоСчету(Счет, ИдФормы = "") Экспорт
	
	Счет.Выгружен = Ложь;
	
	СтрокиКВыгрузке = ДокументыКВыгрузкеПоСчету(Счет);
	Если СтрокиКВыгрузке.Количество() Тогда
		ПараметрыВыгрузки = Новый Структура;
		ПараметрыВыгрузки.Вставить("БанковскийСчет",     Счет.Ссылка);
		ПараметрыВыгрузки.Вставить("НастройкаОбмена",    Счет.НастройкаОбмена);
		ПараметрыВыгрузки.Вставить("Кодировка",          Счет.Кодировка);
		
		ТаблицаДокументов = ДокументыКВыгрузке.Выгрузить(СтрокиКВыгрузке);
		ПараметрыВыгрузки.Вставить("ТаблицаДокументов", ТаблицаДокументов);
		
		Счет.АдресХранилищаДокументов = ПоместитьВоВременноеХранилище(ТаблицаДокументов, ИдФормы);
		
		Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			ИдФормы,
			"Обработки.КлиентБанк.Выгрузить",
			ПараметрыВыгрузки,
			НСтр("ru='Выгрузка платежей в банк'")
		);
		
		Счет.АдресХранилищаФайла = Результат.АдресХранилища;
		
		Если Результат.ЗаданиеВыполнено Тогда
			Счет.СохранитьФайл = Истина;
		Иначе
			Счет.Выгружается = Истина;
			Счет.ИдентификаторВыгрузки = Результат.ИдентификаторЗадания;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Формирует Табличный документ с отчетом о выгруженных платежах
//
// Возвращаемое значение:
//    ТабличныйДокумент - отчет о выгрузке
//
Функция ПечатьОтчетаОВыгрузке(ВыделенныеСтроки) Экспорт
	
	ПолеОтчета = Неопределено;
	
	Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		
		Если ПолеОтчета = Неопределено Тогда
			ПолеОтчета = Новый ТабличныйДокумент;
			ПолеОтчета.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		КонецЕсли;
		
		Счет = БанковскиеСчета.Получить(ВыделеннаяСтрока);
		
		Если Счет.Выгружен Тогда
			Если ЭтоАдресВременногоХранилища(Счет.АдресХранилищаДокументов) Тогда
				ВыгруженныеДокументы = ПолучитьИзВременногоХранилища(Счет.АдресХранилищаДокументов);
				Если ВыгруженныеДокументы <> Неопределено Тогда
					ОтборПоСчету = Новый Структура("БанковскийСчет", Счет.Ссылка);
					ДокументыПоСчету = ВыгруженныеДокументы.Скопировать(ОтборПоСчету);
					ВывестиСекциюОтчетаОВыгрузке(ПолеОтчета, ДокументыПоСчету, Счет.Ссылка, Счет.Выгружен);
				КонецЕсли;
			КонецЕсли;
		Иначе
			Отбор = Новый Структура;
			Отбор.Вставить("Выгружать", Истина);
			Отбор.Вставить("БанковскийСчет", Счет.Ссылка);
			ДокументыКВыгрузкеПоСчету = ДокументыКВыгрузке.Выгрузить(Отбор);
			Если ДокументыКВыгрузкеПоСчету.Количество() Тогда
				ВывестиСекциюОтчетаОВыгрузке(ПолеОтчета, ДокументыКВыгрузкеПоСчету, Счет.Ссылка, Счет.Выгружен);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПолеОтчета;
	
КонецФункции

// Записывает дату выгрузки в выгруженные документы, сведения о последней выгрузке по банковскому счету
//
Процедура ЗаписатьДатуВыгрузкиПлатежей() Экспорт
	
	Для каждого Счет из БанковскиеСчета Цикл
		
		Если Счет.Выгружен Тогда
			Если ЭтоАдресВременногоХранилища(Счет.АдресХранилищаДокументов) Тогда
				ВыгруженныеДокументы = ПолучитьИзВременногоХранилища(Счет.АдресХранилищаДокументов);
				Если ТипЗнч(ВыгруженныеДокументы) = Тип("ТаблицаЗначений") Тогда
			
					Для каждого ВыгруженныйДокумент Из ВыгруженныеДокументы Цикл
						ДокументОбъект = ВыгруженныйДокумент.Ссылка.ПолучитьОбъект();
						ДокументОбъект.ДатаВыгрузки = ТекущаяДатаСеанса();
						ДокументОбъект.Записать();
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
			РегистрыСведений.ПоследнийОбменСБанками.СоздатьЗапись(Счет.Ссылка, Новый Структура("ДатаВыгрузки", ТекущаяДатаСеанса()));
			Счет.ДатаПоследнейВыгрузки = ТекущаяДатаСеанса();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Заполняет табл. часть "Документы к выгрузке" платежными поручениями и требованиями
//
// Параметры:
//     ДокументОтбор - ТаблицаЗначений - Таблица документов для выгрузки
//
Процедура ЗаполнитьТаблицуПлатежей(ДокументыОтбор = Неопределено) Экспорт
	
	ДокументыКВыгрузке.Загрузить(
		Обработки.КлиентБанк.ТаблицаДокументовКВыгрузке(СписокСчетов, ДатаНачалаВыгрузки, ДатаКонцаВыгрузки, ТолькоНеВыгруженные));
	ДокументыКВыгрузке.Сортировать("Дата");
	
	Если ДокументыОтбор <> Неопределено Тогда
		Для Каждого ДокументКВыгрузке Из ДокументыКВыгрузке Цикл
		
			Отбор = Новый Структура("Ссылка", ДокументКВыгрузке.Ссылка);
			НайденныеСтроки = ДокументыОтбор.НайтиСтроки(Отбор);
			
			Если НайденныеСтроки.Количество() = 0 Тогда
				ДокументКВыгрузке.Выгружать = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ПроверитьДокументыКВыгрузке();
	
КонецПроцедуры

#КонецОбласти

#Область Загрузка

// Заполняет табл. часть "Банковские счета" настройками счетов для загрузки платежей
//
Процедура ЗаполнитьТаблицуСчетовЗагрузки() Экспорт
	
	ТаблицаСчетов = Обработки.КлиентБанк.ТаблицаСчетов(Истина);
	
	Для каждого Счет из ТаблицаСчетов Цикл
		Счет.ПоследняяВыписка = Формат(Счет.ДатаНачалаПоследнейЗагрузки, "ДЛФ=Д")
			+ " - "
			+ Формат(Счет.ДатаКонцаПоследнейЗагрузки, "ДЛФ=Д");
			
		Если Счет.ПрямойОбмен Тогда
			Если ЗначениеЗаполнено(Счет.ДатаКонцаПоследнейЗагрузки) Тогда
				Счет.ДатаНачалаЗагрузки = Счет.ДатаКонцаПоследнейЗагрузки;
			Иначе
				Счет.ДатаНачалаЗагрузки = ТекущаяДатаСеанса();
			КонецЕсли;
			Счет.ДатаКонцаЗагрузки = ТекущаяДатаСеанса();
		КонецЕсли;
	КонецЦикла;
	
	ДополнитьТаблицуСчетовПоддержкойПрямогоОбмена(ТаблицаСчетов);
	
	БанковскиеСчета.Загрузить(ТаблицаСчетов);
	
КонецПроцедуры

// Выполняет загрузку платежей из файлов по всем выбранным счетам
//
// Параметры:
//    ИдФормы - УникальныйИдентификатор - идентификатор вызывающей метод формы
//
Процедура ЗагрузитьПлатежи(ИдФормы = "") Экспорт
	
	Для каждого Счет из БанковскиеСчета Цикл
		
		Если Не Счет.Пометка Или Не ЗначениеЗаполнено(Счет.НастройкаОбмена) Или Счет.Загружается Или Счет.ПрямойОбмен
			Или Не ЭтоАдресВременногоХранилища(Счет.АдресХранилищаФайла) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗагрузитьПлатежиПоСчету(Счет, ИдФормы);
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет загрузку платежей из файлов по указанному счету
//
// Параметры:
//    Счет - Строка табличной части - Строка списка банковских счетов
//    ИдФормы - УникальныйИдентификатор - идентификатор вызывающей метод формы
//
Процедура ЗагрузитьПлатежиПоСчету(Счет, ИдФормы = "") Экспорт
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("БанковскийСчет",        Счет.Ссылка);
	ПараметрыЗагрузки.Вставить("ПрямойОбмен",           Счет.ПрямойОбмен);
	ПараметрыЗагрузки.Вставить("ДатаНачалаЗагрузки",    Счет.ДатаНачалаЗагрузки);
	ПараметрыЗагрузки.Вставить("ДатаКонцаЗагрузки",     Счет.ДатаКонцаЗагрузки);

	ПараметрыЗагрузки.Вставить("СтрокиВыписки",         ПолучитьИзВременногоХранилища(Счет.АдресХранилищаФайла));
	ПараметрыЗагрузки.Вставить("ДокументыКЗагрузке",    ДокументыКЗагрузке.ВыгрузитьКолонки());
	
	ПараметрыЗагрузки.Вставить("СоздаватьКонтрагентов", СоздаватьКонтрагентов);
	ПараметрыЗагрузки.Вставить("ПроводитьДокументы",    ПроводитьДокументы);
	
	Счет.Загружен = Ложь;
	
	Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
		ИдФормы,
		"Обработки.КлиентБанк.Загрузить",
		ПараметрыЗагрузки,
		НСтр("ru='Загрузка выписки банка'")
		);
		
	Счет.АдресХранилищаДокументов = Результат.АдресХранилища;
	
	Если Результат.ЗаданиеВыполнено Тогда
		Счет.СохранитьФайл = Истина;
		Счет.Пометка = Ложь;
	Иначе
		Счет.Загружается = Истина;
		Счет.ИдентификаторЗагрузки = Результат.ИдентификаторЗадания;
	КонецЕсли;
	
КонецПроцедуры

// Формирует Табличный документ с отчетом о загруженных платежах
//
// Возвращаемое значение:
//    ТабличныйДокумент - отчет о загрузке
//
Функция ПечатьОтчетаОЗагрузке(ВыделенныеСтроки) Экспорт
	
	ПолеОтчета = Неопределено;
	
	Для каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		
		Если ПолеОтчета = Неопределено Тогда
			ПолеОтчета = Новый ТабличныйДокумент;
			ПолеОтчета.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		КонецЕсли;
		
		Счет = БанковскиеСчета.Получить(ВыделеннаяСтрока);
		
		Если ЭтоАдресВременногоХранилища(Счет.АдресХранилищаДокументов) Тогда
			ЗагруженныеДокументы = ПолучитьИзВременногоХранилища(Счет.АдресХранилищаДокументов);
			Если ЗагруженныеДокументы <> Неопределено
				И ТипЗнч(ЗагруженныеДокументы) = Тип("ТаблицаЗначений")
				И ЗагруженныеДокументы.Количество() Тогда
				
				ВывестиСекциюОтчетаОЗагрузке(ПолеОтчета, ЗагруженныеДокументы, Счет.Ссылка, Счет.Загружен);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПолеОтчета;
	
КонецФункции

// Записывает период последней загруженной выписки
//
Процедура ЗаписатьПериодПоследнейВыписки() Экспорт
	
	Для каждого Счет из БанковскиеСчета Цикл
		Если Счет.Загружен Тогда
			
			РегистрыСведений.ПоследнийОбменСБанками.СоздатьЗапись(
				Счет.Ссылка,
				Новый Структура("ДатаНачалаЗагрузки, ДатаКонцаЗагрузки", Счет.ДатаНачалаЗагрузки, Счет.ДатаКонцаЗагрузки));
			
			Счет.ПоследняяВыписка = Формат(Счет.ДатаНачалаЗагрузки, "ДЛФ=Д")
				+ " - "
				+ Формат(Счет.ДатаКонцаЗагрузки, "ДЛФ=Д");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Записывает данные об остатках на банковских счетах по данным загруженных выписок
//
Процедура ЗаписатьОстаткиНаСчетахПоДаннымВыписки() Экспорт
	
	Для каждого Счет из БанковскиеСчета Цикл
		Если Счет.Загружен И ЭтоАдресВременногоХранилища(Счет.АдресХранилищаФайла) Тогда
			
			СтрокиВыписки = ПолучитьИзВременногоХранилища(Счет.АдресХранилищаФайла);
			
			Если Счет.ПрямойОбмен Тогда
				ДанныеВыписки = Обработки.КлиентБанк.РазобратьДеревоВыписки(СтрокиВыписки);
			Иначе
				ДанныеВыписки = ДенежныеСредстваКлиентСервер.РазобратьФайлВыписки(СтрокиВыписки, Ложь);
			КонецЕсли;
			
			Если ДанныеВыписки <> Неопределено Тогда
				РасчетныеСчета = ДанныеВыписки.РасчетныеСчета;
				Для каждого РасчетныйСчет из РасчетныеСчета Цикл
					Если Счет.НомерСчета = РасчетныйСчет.РасчСчет Тогда
						
						ПараметрыЗаписи = Новый Структура;
						
						НачальныйОстаток = ДенежныеСредстваКлиентСервер.ПреобразоватьВЧисло(РасчетныйСчет.НачальныйОстаток);
						КонечныйОстаток = ДенежныеСредстваКлиентСервер.ПреобразоватьВЧисло(РасчетныйСчет.КонечныйОстаток);
						Поступление = ДенежныеСредстваКлиентСервер.ПреобразоватьВЧисло(РасчетныйСчет.ВсегоПоступило);
						Списание = ДенежныеСредстваКлиентСервер.ПреобразоватьВЧисло(РасчетныйСчет.ВсегоСписано);
						
						ДатаНачала = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(РасчетныйСчет.ДатаНачала);
						ДатаКонца = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(РасчетныйСчет.ДатаКонца);
						
						Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаКонца)
							И НачалоДня(ДатаНачала) = НачалоДня(ДатаКонца) Тогда
							
							ПараметрыЗаписи.Вставить("НачальныйОстаток", НачальныйОстаток);
							ПараметрыЗаписи.Вставить("Поступление", Поступление);
							ПараметрыЗаписи.Вставить("Списание", Списание);
							ПараметрыЗаписи.Вставить("КонечныйОстаток", КонечныйОстаток);
							
							РегистрыСведений.ОстаткиНаБанковскихСчетахПоДаннымВыписок.СоздатьЗапись(
								ДатаНачала,
								Счет.Ссылка,
								ПараметрыЗаписи);
							
						Иначе
							Если ЗначениеЗаполнено(ДатаНачала) Тогда
								
								ПараметрыЗаписи.Вставить("НачальныйОстаток", НачальныйОстаток);
								
								РегистрыСведений.ОстаткиНаБанковскихСчетахПоДаннымВыписок.СоздатьЗапись(
									ДатаНачала,
									Счет.Ссылка,
									ПараметрыЗаписи);
								
							КонецЕсли;
							
							Если ЗначениеЗаполнено(ДатаКонца) Тогда
								
								ПараметрыЗаписи.Вставить("КонечныйОстаток", КонечныйОстаток);
								
								РегистрыСведений.ОстаткиНаБанковскихСчетахПоДаннымВыписок.СоздатьЗапись(
									ДатаКонца,
									Счет.Ссылка,
									ПараметрыЗаписи);
								
							КонецЕсли;
						КонецЕсли;
						
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#Область ВыгрузкаПорциямиПоПравиламОбъединения

Функция ИнформацияОбОбъединенииФайлов(ВыгружаемыеБанковскиеСчета)
	
	НовыйПоток = Новый Структура("Правило, Счета");
	
	КоличествоСчетовВПравиле = Новый Соответствие;
	ПотокиВыгрузки = Новый Массив;
	СчетаБезПравил = Новый Массив;
	
	ИнформацияОбОбъединенииФайлов = Новый Структура("
		|ТекущееПравило,
		|АдресТекущегоПотокаВыгрузки, 
		|ВсегоСчетовВТекущемПотоке, 
		|ВыгруженоСчетовВТекущийПоток,
		|КоличествоСчетовВПравиле,
		|СчетаБезПравил,
		|ПотокиВыгрузки");
		
	ИнформацияОбОбъединенииФайлов.Вставить("ТекущееПравило", "");
	ИнформацияОбОбъединенииФайлов.Вставить("ВсегоСчетовВТекущемПотоке", 0);
	ИнформацияОбОбъединенииФайлов.Вставить("ВсегоСчетовВТекущемПотоке", 0);
	ИнформацияОбОбъединенииФайлов.Вставить("КоличествоСчетовВПравиле", КоличествоСчетовВПравиле);
	ИнформацияОбОбъединенииФайлов.Вставить("СчетаБезПравил", СчетаБезПравил);
	ИнформацияОбОбъединенииФайлов.Вставить("ПотокиВыгрузки", ПотокиВыгрузки);
		
	Для Каждого Счет Из ВыгружаемыеБанковскиеСчета Цикл
		Если ПустаяСтрока(Счет.ПравилоФайловогоОбменаСБанками) Тогда
			СчетаБезПравил.Добавить(Счет);
		Иначе
			Количество = КоличествоСчетовВПравиле.Получить(Счет.ПравилоФайловогоОбменаСБанками);
			Если Количество = Неопределено Тогда
				ИнформацияОбОбъединенииФайлов["КоличествоСчетовВПравиле"].Вставить(
					Счет.ПравилоФайловогоОбменаСБанками, 1);
			Иначе
				ИнформацияОбОбъединенииФайлов["КоличествоСчетовВПравиле"].Вставить(
					Счет.ПравилоФайловогоОбменаСБанками, Количество + 1);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Правило Из КоличествоСчетовВПравиле Цикл 
		ПравилоПотока = Правило.Ключ;
		
		ТекущийПоток = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(НовыйПоток);
		ТекущийПоток.Вставить("Правило", Правило.Ключ);
		ТекущийПоток.Вставить("Счета", ВыгружаемыеБанковскиеСчета.Скопировать(
			ВыгружаемыеБанковскиеСчета.НайтиСтроки(Новый Структура("ПравилоФайловогоОбменаСБанками", Правило.Ключ))));
		
		ИнформацияОбОбъединенииФайлов["ПотокиВыгрузки"].Добавить(ТекущийПоток);
	КонецЦикла;
	
	Возврат ИнформацияОбОбъединенииФайлов;
	
КонецФункции

Процедура ИзменитьРеквизитыСчетовПотока(Счета, ИзменяемыеПараметры)
	
	Для Каждого Счет Из Счета Цикл
		ЗаполнитьЗначенияСвойств(Счет, ИзменяемыеПараметры);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьИнформациюОВыгруженныхФайлах(Знач ИнформацияОбОбъединенииФайлов)
	
	Для Каждого ПотокВыгрузки Из ИнформацияОбОбъединенииФайлов.ПотокиВыгрузки Цикл 
		Для Каждого ВыгруженныйСчет Из ПотокВыгрузки.Счета Цикл 
			Отбор = Новый Структура("Ссылка", ВыгруженныйСчет.Ссылка);
			НайденныеСтроки = БанковскиеСчета.НайтиСтроки(Отбор);
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
				ЗаполнитьЗначенияСвойств(НайденнаяСтрока, ВыгруженныйСчет);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого ВыгруженныйСчет Из ИнформацияОбОбъединенииФайлов.СчетаБезПравил Цикл 
		Отбор = Новый Структура("Ссылка", ВыгруженныйСчет.Ссылка);
		НайденныеСтроки = БанковскиеСчета.НайтиСтроки(Отбор);
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
			ЗаполнитьЗначенияСвойств(НайденнаяСтрока, ВыгруженныйСчет);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

Процедура ДополнитьТаблицуСчетовПоддержкойПрямогоОбмена(ТаблицаСчетов)
	
	Для каждого Счет из ТаблицаСчетов Цикл
		Если ОбменСБанками.ВозможенПрямойОбменСБанком(Счет.БИКБанка, Неопределено) Тогда
			Счет.ЕстьВозможностьПрямогоОбмена = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьДокументыКВыгрузке()
	
	// Основные реквизиты
	Проверки = Новый Соответствие;
	Проверки.Вставить("Номер",   НСтр("ru='Номер документа'"));
	Проверки.Вставить("Дата",    НСтр("ru='Дата документа'"));
	Проверки.Вставить("Сумма",   НСтр("ru='Сумма документа'"));
	
	// Реквизиты организации
	ПроверкиОрганизацииПР = Новый Соответствие;
	ПроверкиОрганизацииПР.Вставить("ОрганизацияРасчСчет",          НСтр("ru='Банковский счет организации'"));
	ПроверкиОрганизацииПР.Вставить("ОрганизацияНаим",              НСтр("ru='Наименование организации'"));
	ПроверкиОрганизацииПР.Вставить("ОрганизацияИНН",               НСтр("ru='ИНН организации'"));
	
	// Реквизиты организации непрямые расчеты
	ПроверкиОрганизацииНПР = Новый Соответствие;
	ПроверкиОрганизацииНПР.Вставить("ОрганизацияРасчСчет",         НСтр("ru='Банковский счет организации'"));
	ПроверкиОрганизацииНПР.Вставить("ОрганизацияБанкДляРасчетов",  НСтр("ru='Банк для расчетов организации'"));
	ПроверкиОрганизацииНПР.Вставить("ОрганизацияГородБанка",       НСтр("ru='Город банка организации'"));
	ПроверкиОрганизацииНПР.Вставить("ОрганизацияБИКРЦБанка",       НСтр("ru='БИК РЦ банка организации'"));
	
	// Реквизиты контрагента
	ПроверкиКонтрагентаПР = Новый Соответствие;
	ПроверкиКонтрагентаПР.Вставить("КонтрагентРасчСчет",           НСтр("ru='Банковский счет контрагента'"));
	ПроверкиКонтрагентаПР.Вставить("КонтрагентНаим",               НСтр("ru='Контрагент'"));
	ПроверкиКонтрагентаПР.Вставить("КонтрагентИНН",                НСтр("ru='ИНН контрагента'"));
	
	// Реквизиты контрагента непрямые расчеты
	ПроверкиКонтрагентаНПР = Новый Соответствие;
	ПроверкиКонтрагентаНПР.Вставить("КонтрагентРасчСчет",          НСтр("ru='Банковский счет контрагента'"));
	ПроверкиКонтрагентаНПР.Вставить("КонтрагентБанкДляРасчетов",   НСтр("ru='Банк для расчетов контрагента'"));
	ПроверкиКонтрагентаНПР.Вставить("КонтрагентГородБанка",        НСтр("ru='Город банка контрагента'"));
	ПроверкиКонтрагентаНПР.Вставить("КонтрагентБИКРЦБанка",        НСтр("ru='БИК РЦ банка контрагента'"));
	
	Для каждого ДокументКВыгрузке из ДокументыКВыгрузке Цикл
		
		СтрокаОшибки = "";
		Для каждого Проверка из Проверки Цикл
			Если Не ЗначениеЗаполнено(ДокументКВыгрузке[Проверка.Ключ]) Тогда
				СтрокаОшибки = СтрокаОшибки + " " + Проверка.Значение + ",";
			КонецЕсли;
		КонецЦикла;
		
		Если Не ЗначениеЗаполнено(ДокументКВыгрузке.ОрганизацияБанкДляРасчетов) Тогда
			ПроверкаСчетаОрганизации = ПроверкиОрганизацииПР;
		Иначе
			ПроверкаСчетаОрганизации = ПроверкиОрганизацииНПР;
		КонецЕсли;
		Для каждого Проверка из ПроверкаСчетаОрганизации Цикл
			Если Не ЗначениеЗаполнено(ДокументКВыгрузке[Проверка.Ключ]) Тогда
				СтрокаОшибки = СтрокаОшибки + " " + Проверка.Значение + ",";
			КонецЕсли;
		КонецЦикла;
		
		Если Не ЗначениеЗаполнено(ДокументКВыгрузке.КонтрагентБанкДляРасчетов) Тогда
			ПроверкаСчетаКонтрагента = ПроверкиКонтрагентаПР;
		Иначе
			ПроверкаСчетаКонтрагента = ПроверкиКонтрагентаНПР;
		КонецЕсли;
		Для каждого Проверка из ПроверкаСчетаКонтрагента Цикл
			Если ДокументКВыгрузке.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент
				И Проверка.Ключ = "КонтрагентИНН" Тогда
				Продолжить;
			КонецЕсли;
				
			Если Не ЗначениеЗаполнено(ДокументКВыгрузке[Проверка.Ключ]) Тогда
				СтрокаОшибки = СтрокаОшибки + " " + Проверка.Значение + ",";
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(СтрокаОшибки) Тогда
			СтрокаОшибки = Лев(СтрокаОшибки, СтрДлина(СтрокаОшибки) - 1);
			ДокументКВыгрузке.ЕстьОшибка = Истина;
			ДокументКВыгрузке.Выгружать = Ложь;
			ДокументКВыгрузке.ОписаниеОшибок = НСтр("ru='Не заполнены реквизиты платежа:'") + СтрокаОшибки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ДокументыКВыгрузкеПоСчетам(Счета)
	
	ДокументыКВыгрузкеПоСчетам = Новый Массив;
	
	Для Каждого Счет Из Счета Цикл 
		Отбор = Новый Структура;
		Отбор.Вставить("Выгружать", Истина);
		Отбор.Вставить("БанковскийСчет", Счет.Ссылка);
		
		НайденыеДокументы = ДокументыКВыгрузке.НайтиСтроки(Отбор);
		
		Для Каждого НайденныйДокумент Из НайденыеДокументы Цикл 
			ДокументыКВыгрузкеПоСчетам.Добавить(НайденныйДокумент);
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДокументыКВыгрузкеПоСчетам;
	
КонецФункции

Функция ДокументыКВыгрузкеПоСчету(Счет)
	
	Отбор = Новый Структура;
	Отбор.Вставить("Выгружать", Истина);
	Отбор.Вставить("БанковскийСчет", Счет.Ссылка);
	
	Возврат ДокументыКВыгрузке.НайтиСтроки(Отбор);
	
КонецФункции

Процедура ВывестиСекциюОтчетаОВыгрузке(ПолеОтчета, Документы, БанковскийСчет, Выгружен)
	
	МакетОтчета = ПолучитьМакет("ОтчетОВыгрузке");
	
	Шапка    = МакетОтчета.ПолучитьОбласть("Шапка");
	Строка   = МакетОтчета.ПолучитьОбласть("Строка");
	Подвал   = МакетОтчета.ПолучитьОбласть("Подвал");
	НазвОрг  = МакетОтчета.ПолучитьОбласть("НазваниеОрганизации");
	
	РеквизитыСчета = Справочники.БанковскиеСчетаОрганизаций.ПолучитьРеквизитыБанковскогоСчетаОрганизации(БанковскийСчет);
	
	НазвОрг.Параметры.НазваниеОрганизации = Строка(РеквизитыСчета.Организация);
	ПолеОтчета.Вывести(НазвОрг);
	
	Если Выгружен Тогда
		Шапка.Параметры.ИмяОтчета = НСтр("ru='Отчет о выгруженных документах'");
	Иначе
		Шапка.Параметры.ИмяОтчета = НСтр("ru='Документы к выгрузке'");
	КонецЕсли;
	Шапка.Параметры.ИмяСуммыПоступления = "Поступление";
	Шапка.Параметры.ИмяСуммыСписания    = "Списание";
	
	ОбрБанковскийСчет = "";
	Индекс = 0; ИтогоСуммаП = 0; ИтогоСуммаР = 0;
	
	Документы.Сортировать("Дата");
	НачПериода = Документы[0].Дата;
	КонПериода = Документы[Документы.Количество() - 1].Дата;
	
	Валюта = ?(ЗначениеЗаполнено(РеквизитыСчета.Валюта), СокрЛП(Строка(РеквизитыСчета.Валюта)), НСтр("ru='валюта не указана'"));
	НачалоОтчетногоПериода = Формат(НачПериода, "ДЛФ=D");
	КонецОтчетногоПериода = Формат(КонПериода, "ДЛФ=D");
	
	Шапка.Параметры.ОписаниеПериода =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='по счету %1 (%2) с %3 по %4'"),
			РеквизитыСчета.НомерСчета,
			Валюта,
			НачалоОтчетногоПериода,
			КонецОтчетногоПериода);
	ПолеОтчета.Вывести(Шапка);
	
	Для Каждого СтрокаИсточника Из Документы Цикл
		
		Валюта = ?(ЗначениеЗаполнено(СтрокаИсточника.Валюта), СокрЛП(Строка(СтрокаИсточника.Валюта)), НСтр("ru='валюта не указана'"));
		
		Если СтрокаИсточника.ВидДокумента = Перечисления.ТипыПлатежныхДокументов.ПлатежноеТребование Тогда
			
			Строка.Параметры.Получатель = СтрокаИсточника.ОрганизацияНаим;
			Строка.Параметры.ПолучательСчет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"),
				СтрокаИсточника.ОрганизацияРасчСчет,
				Валюта);
			
			Строка.Параметры.Плательщик = СтрокаИсточника.Контрагент;
			Строка.Параметры.ПлательщикСчет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"),
				СтрокаИсточника.КонтрагентРасчСчет,
				Валюта);
				
			Строка.Параметры.СуммаПоступление  = СтрокаИсточника.Сумма;
			Строка.Параметры.СуммаСписание = "";
			ИтогоСуммаП = ИтогоСуммаП + СтрокаИсточника.Сумма;
		Иначе
			
			Строка.Параметры.Плательщик = СтрокаИсточника.ОрганизацияНаим;
			Строка.Параметры.ПлательщикСчет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"),
				СтрокаИсточника.ОрганизацияРасчСчет,
				Валюта);
			
			Строка.Параметры.Получатель = СтрокаИсточника.Контрагент;
			Строка.Параметры.ПолучательСчет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"),
				СтрокаИсточника.КонтрагентРасчСчет,
				Валюта);
				
			Строка.Параметры.СуммаПоступление = "";
			Строка.Параметры.СуммаСписание = СтрокаИсточника.Сумма;
			ИтогоСуммаР = ИтогоСуммаР + СтрокаИсточника.Сумма;
		КонецЕсли;
		
		Строка.Параметры.Документ = СтрокаИсточника.Ссылка;
		Индекс = Индекс + 1;
		Строка.Параметры.Индекс = Индекс;
		
		ПолеОтчета.Вывести(Строка);
		
	КонецЦикла;
	
	Подвал.Параметры.ИтогоСуммаП = ИтогоСуммаП;
	Подвал.Параметры.ИтогоСуммаР = ИтогоСуммаР;
	
	ПолеОтчета.Вывести(Подвал);
	
	ПолеОтчета.ОтображатьГруппировки = Ложь;
	ПолеОтчета.ОтображатьЗаголовки   = Ложь;
	ПолеОтчета.ОтображатьСетку       = Ложь;
	ПолеОтчета.ТолькоПросмотр        = Истина;
	
КонецПроцедуры

Процедура ВывестиСекциюОтчетаОЗагрузке(ПолеОтчета, Документы, БанковскийСчет, Загружен)
	
	МакетОтчета = ПолучитьМакет("ОтчетОЗагрузке");
	
	ИмеетсяСекцияРасчСчет = Ложь;
	
	Шапка   = МакетОтчета.ПолучитьОбласть("Шапка");
	Строка  = МакетОтчета.ПолучитьОбласть("Строка");
	Подвал  = МакетОтчета.ПолучитьОбласть("Подвал");
	Остатки = МакетОтчета.ПолучитьОбласть("Остатки");
	НазвОрг = МакетОтчета.ПолучитьОбласть("НазваниеОрганизации");
	
	РеквизитыСчета = Справочники.БанковскиеСчетаОрганизаций.ПолучитьРеквизитыБанковскогоСчетаОрганизации(БанковскийСчет);
	
	НазвОрг.Параметры.НазваниеОрганизации = Строка(РеквизитыСчета.Организация);
	ПолеОтчета.Вывести(НазвОрг);
	
	Если Загружен Тогда
		Шапка.Параметры.ИмяОтчета = НСтр("ru='Отчет о загруженных документах'");
	Иначе
		Шапка.Параметры.ИмяОтчета = НСтр("ru='Документы выписки'");
	КонецЕсли;
	
	ОбрБанковскийСчет = "";
	Индекс = 0; ИтогоСуммаП = 0; ИтогоСуммаР = 0;
	
	Документы.Сортировать("Дата");
	НачПериода = Документы[0].ДатаДок;
	КонПериода = Документы[Документы.Количество() - 1].ДатаДок;
	
	Валюта = ?(ЗначениеЗаполнено(РеквизитыСчета.Валюта), СокрЛП(Строка(РеквизитыСчета.Валюта)), НСтр("ru='валюта не указана'"));
	НачалоОтчетногоПериода = Формат(НачПериода, "ДЛФ=D");
	КонецОтчетногоПериода = Формат(КонПериода, "ДЛФ=D");
	
	Шапка.Параметры.ОписаниеПериода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='по счету %1 (%2) с %3 по %4'"),
		РеквизитыСчета.НомерСчета,
		Валюта,
		НачалоОтчетногоПериода,
		КонецОтчетногоПериода);
	ПолеОтчета.Вывести(Шапка);
	
	Для Каждого СтрокаИсточника Из Документы Цикл
		
		Валюта = ?(ЗначениеЗаполнено(СтрокаИсточника.Валюта), СокрЛП(Строка(СтрокаИсточника.Валюта)), НСтр("ru='валюта не указана'"));
		
		Если СтрокаИсточника.СуммаПоступило > 0 Тогда
			
			Строка.Параметры.Контрагент =
				?(ПустаяСтрока(СтрокаИсточника.Плательщик1), СтрокаИсточника.Плательщик, СтрокаИсточника.Плательщик1);
			Строка.Параметры.Счет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"),
				СтрокаИсточника.ПлательщикСчет,
				Валюта);
				
			Строка.Параметры.СуммаПоступление = СтрокаИсточника.СуммаПоступило;
			Строка.Параметры.СуммаСписание    = "";
			Строка.Параметры.Дата             = Формат(СтрокаИсточника.ДатаДок, "ДЛФ=Д");
			ИтогоСуммаП                       = ИтогоСуммаП + СтрокаИсточника.СуммаПоступило;
			
		ИначеЕсли СтрокаИсточника.СуммаСписано > 0 Тогда

			Строка.Параметры.Контрагент =
				?(ПустаяСтрока(СтрокаИсточника.Получатель1), СтрокаИсточника.Получатель, СтрокаИсточника.Получатель1);
			Строка.Параметры.Счет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (%2)'"),
				СтрокаИсточника.ПолучательСчет,
				Валюта);
				
			Строка.Параметры.СуммаСписание    = СтрокаИсточника.СуммаСписано;
			Строка.Параметры.СуммаПоступление = "";
			Строка.Параметры.Дата             =
				?(ПустаяСтрока(СтрокаИсточника.Дата), Формат(СтрокаИсточника.ДатаДок, "ДЛФ=Д"), СтрокаИсточника.Дата);
			ИтогоСуммаР                       = ИтогоСуммаР + СтрокаИсточника.СуммаСписано;

		Иначе
			Продолжить;
		КонецЕсли;
		
		Если Загружен Тогда
			Док = СтрокаИсточника.Документ;
			Если ЗначениеЗаполнено(Док) Тогда
				Строка.Параметры.Документ     = Док;
				Строка.Области.Строка.ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
			Иначе
				Строка.Параметры.Документ     = НСтр("ru='НЕ ЗАГРУЖЕН'");
				Если ЗначениеЗаполнено(СтрокаИсточника.ОшибкиЗагрузки) Тогда
					Строка.Параметры.Документ = Строка.Параметры.Документ + ": " + СтрокаИсточника.ОшибкиЗагрузки;
				КонецЕсли;
				Строка.Области.Строка.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
			КонецЕсли;
		КонецЕсли;
		
		Строка.Параметры.Номер             = СтрокаИсточника.Номер;
		Строка.Параметры.НазначениеПлатежа = СтрокаИсточника.НазначениеПлатежа;
		
		Индекс = Индекс + 1;
		Строка.Параметры.Индекс = Индекс;
		
		ПолеОтчета.Вывести(Строка);
		
	КонецЦикла;
	
	Подвал.Параметры.ИтогоСуммаП = ИтогоСуммаП;
	Подвал.Параметры.ИтогоСуммаР = ИтогоСуммаР;
	
	ПолеОтчета.Вывести(Подвал);
	ПолеОтчета.ОтображатьГруппировки = Ложь;
	ПолеОтчета.ОтображатьЗаголовки   = Ложь;
	ПолеОтчета.ОтображатьСетку       = Ложь;
	ПолеОтчета.ТолькоПросмотр        = Истина;
	
КонецПроцедуры


#КонецОбласти

#КонецЕсли
