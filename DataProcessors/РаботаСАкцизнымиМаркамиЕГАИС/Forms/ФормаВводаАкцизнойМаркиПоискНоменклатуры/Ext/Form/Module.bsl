
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Заголовок = НСтр("ru = 'Сканирование штрихкода'");
	
	АкцизнаяМарка           = Параметры.АкцизнаяМарка;
	АлкогольнаяПродукция    = Параметры.АлкогольнаяПродукция;
	КодАлкогольнойПродукции = Параметры.КодАлкогольнойПродукции;
	
	СформироватьПоясняющийТекст();
	
	СобытияФормЕГАИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПоддерживаемыеТипыПодключаемогоОборудования = "СканерШтрихкода";
	
	ОповещениеПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(
		ОповещениеПриПодключении,
		ЭтотОбъект,
		ПоддерживаемыеТипыПодключаемогоОборудования);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ОповещениеПриОтключении = Новый ОписаниеОповещения("ОтключитьОборудованиеЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(
		ОповещениеПриОтключении, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	СобытияФормЕГАИСКлиентПереопределяемый.ОбработкаОповещенияОбработаныНеизвестныеШтрихкоды(
		Новый ОписаниеОповещения("Подключаемый_ОбработаныНеизвестныеШтрихкоды", ЭтотОбъект),
		ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	СобытияФормЕГАИСКлиентПереопределяемый.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если Не ВводДоступен() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АлкогольнаяПродукция) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеСобытия = Новый Структура;
	ОписаниеСобытия.Вставить("Источник", Источник);
	ОписаниеСобытия.Вставить("Событие" , Событие);
	ОписаниеСобытия.Вставить("Данные"  , Данные);
	
	Результат = МенеджерОборудованияКлиент.ПолучитьСобытиеОтУстройства(ОписаниеСобытия);
	
	Если Результат <> Неопределено
		И Результат.Источник = "ПодключаемоеОборудование"
		И Результат.ИмяСобытия = "ScanData"
		И Найти(ПоддерживаемыеТипыПодключаемогоОборудования, "СканерШтрихкода") > 0 Тогда
		
		ОбработатьДанныеСканераШтрихкодов(МенеджерОборудованияКлиент.ПреобразоватьДанныеСоСканераВСтруктуру(Результат.Параметр));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПоясняющийТекстОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "КомандаОткрытьАлкогольнуюПродукциюЕГАИС" Тогда
		
		ПоказатьЗначение(, АлкогольнаяПродукция);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "КомандаЗапроситьАлкогольнуюПродукциюЕГАИС" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Операция",          ПредопределенноеЗначение("Перечисление.ВидыДокументовЕГАИС.ЗапросАлкогольнойПродукции"));
		ПараметрыФормы.Вставить("ИмяПараметра",      "КОД");
		ПараметрыФормы.Вставить("ЗначениеПараметра", КодАлкогольнойПродукции);
		
		ОткрытьФорму(
			"ОбщаяФорма.ФормированиеИсходящегоЗапросаЕГАИС",
			ПараметрыФормы,
			ЭтотОбъект,,,,
			Новый ОписаниеОповещения("ПослеЗакрытияФормыФормированиеИсходящегоЗапроса", ЭтотОбъект),
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	Если Не ЗначениеЗаполнено(Штрихкод) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не заполнено поле ""Штрихкод""'"),,"Штрихкод");
		Возврат;
	КонецЕсли;
	
	ОбработатьДанныеСканераШтрихкодов(Новый Структура("Штрихкод, Количество", Штрихкод, 1));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Штрихкоды

&НаКлиенте
Процедура ОбработатьДанныеСканераШтрихкодов(ДанныеШтрихкода) Экспорт
	
	Штрихкод = ДанныеШтрихкода.Штрихкод;
	
	СопоставленнаяНоменклатура = ПолучитьДанныеПоШтрихкоду(Штрихкод);
	Если ЗначениеЗаполнено(СопоставленнаяНоменклатура) Тогда
		
		ПодключитьОбработчикОжидания("ЗакрытьФормуПриСканировании", 0.1, Истина);
		
	Иначе
		
		Если ПравоРегистрацииШтрихкодовНоменклатурыДоступно Тогда
			
			ДанныеШтрихкодаИАлкогольнойПродукции = Новый Структура;
			ДанныеШтрихкодаИАлкогольнойПродукции.Вставить("АлкогольнаяПродукция", АлкогольнаяПродукция);
			ДанныеШтрихкодаИАлкогольнойПродукции.Вставить("Штрихкод",             ДанныеШтрихкода.Штрихкод);
			ДанныеШтрихкодаИАлкогольнойПродукции.Вставить("Количество",           ДанныеШтрихкода.Количество);
			
			ДанныеШтрихкодов = Новый Массив;
			ДанныеШтрихкодов.Добавить(ДанныеШтрихкодаИАлкогольнойПродукции);
			
			АдресДанныхШтрихкодов = ПоместитьВоВременноеХранилище(ДанныеШтрихкодов, УникальныйИдентификатор);
			
			ПодключитьОбработчикОжидания("Подключаемый_ОткрытьФормуРегистрацииШтрихкодовНоменклатуры", 0.1, Истина);
			
		Иначе
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(НСтр("ru = 'Номенклатура со штрихкодом %1 не найдена'"), Штрихкод));
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработаныНеизвестныеШтрихкоды(ДанныеШтрихкодов, ДополнительныеПараметры) Экспорт
	
	Для Каждого ДанныеШтрихкода Из ДанныеШтрихкодов Цикл
		ОбработатьДанныеСканераШтрихкодов(ДанныеШтрихкода);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьФормуРегистрацииШтрихкодовНоменклатуры() Экспорт
	
	ДанныеШтрихкодов = ПолучитьИзВременногоХранилища(АдресДанныхШтрихкодов);
	
	ШтрихкодированиеНоменклатурыЕГАИСКлиентПереопределяемый.ОткрытьФормуРегистрацииШтрихкодовНоменклатуры(ЭтотОбъект, ДанныеШтрихкодов);
	
КонецПроцедуры

#КонецОбласти

#Область Оборудование

&НаКлиенте
Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При отключении оборудования произошла ошибка: ""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура ЗакрытьФормуПриСканировании()
	
	Результат = АкцизныеМаркиКлиентСервер.СтруктураДанныхШтрихкода(Штрихкод);
	ЗаполнитьЗначенияСвойств(Результат, СопоставленнаяНоменклатура);
	
	Результат.Количество = 1;
	Результат.АлкогольнаяПродукция = АлкогольнаяПродукция;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеПоШтрихкоду(Штрихкод)
	
	Возврат ШтрихкодированиеНоменклатурыЕГАИСПереопределяемый.НайтиПоШтрихкоду(Штрихкод);
	
КонецФункции

&НаКлиенте
Функция ПослеЗакрытияФормыФормированиеИсходящегоЗапроса(Результат, ДополнительныеПараметры) Экспорт
	
	СформироватьПоясняющийТекст();
	
КонецФункции

&НаСервере
Процедура СформироватьПоясняющийТекст()

	Если Не ЗначениеЗаполнено(КодАлкогольнойПродукции)
		И ЗначениеЗаполнено(АкцизнаяМарка) Тогда
		КодАлкогольнойПродукции = АкцизныеМаркиВызовСервера.КодКлассификатораНоменклатурыЕГАИС(
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(АкцизнаяМарка, "ЗначениеШтрихкода"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КодАлкогольнойПродукции)
		И Не ЗначениеЗаполнено(АлкогольнаяПродукция) Тогда
		АлкогольнаяПродукция = Справочники.КлассификаторАлкогольнойПродукцииЕГАИС.НайтиПоКоду(КодАлкогольнойПродукции);
	КонецЕсли;
	
	ИспользоватьАкцизныеМарки = Истина;
	ПравоРегистрацииШтрихкодовНоменклатурыДоступно = ШтрихкодированиеНоменклатурыЕГАИСПереопределяемый.ЕстьПравоРегистрацииШтрихкодовНоменклатуры();
	
	Элементы.ЗавершитьРедактирование.Видимость = ЗначениеЗаполнено(АлкогольнаяПродукция);
	Элементы.ДекорацияКартинка.Видимость       = ЗначениеЗаполнено(АлкогольнаяПродукция);
	Элементы.Штрихкод.Видимость                = ЗначениеЗаполнено(АлкогольнаяПродукция);
	
	МассивСтрок = Новый Массив;
	Если ЗначениеЗаполнено(АлкогольнаяПродукция) Тогда
		
		НаименованиеАлкогольнойПродукции = Строка(АлкогольнаяПродукция);
		Если СтрДлина(НаименованиеАлкогольнойПродукции) > 40 Тогда
			НаименованиеАлкогольнойПродукции = Лев(НаименованиеАлкогольнойПродукции, 40) + "...";
		Иначе
			НаименованиеАлкогольнойПродукции = Строка(АлкогольнаяПродукция);
		КонецЕсли;
		
		МассивСтрок.Добавить(
			Новый ФорматированнаяСтрока(
				НСтр("ru = 'Алкогольная продукция'"),,
				ЦветаСтиля.ПоясняющийТекст));
		МассивСтрок.Добавить(" ");
		МассивСтрок.Добавить(
			Новый ФорматированнаяСтрока(
				НаименованиеАлкогольнойПродукции,
				Новый Шрифт(,,,,Истина),
				ЦветаСтиля.ЦветГиперссылкиГИСМ,,
				"КомандаОткрытьАлкогольнуюПродукциюЕГАИС"));
		МассивСтрок.Добавить(" ");
		МассивСтрок.Добавить(
			Новый ФорматированнаяСтрока(
				НСтр("ru = 'не сопоставлена с номенклатурой предприятия.
				           |Отсканируйте штрихкод товара EAN-13.'"),,
				ЦветаСтиля.ПоясняющийТекст));
	Иначе
		
		Если ЗначениеЗаполнено(КодАлкогольнойПродукции) Тогда
			МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(
					НСтр("ru = 'Алкогольная продукция c кодом'"),,
					ЦветаСтиля.ПоясняющийТекст));
			МассивСтрок.Добавить(" ");
			МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(
					Строка(КодАлкогольнойПродукции),
					Новый Шрифт(,,,,Истина),
					ЦветаСтиля.ЦветГиперссылкиГИСМ,,
					"КомандаЗапроситьАлкогольнуюПродукциюЕГАИС"));
			МассивСтрок.Добавить(" ");
			МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(
					НСтр("ru = 'не найдена в информационной базе.'"),,
					ЦветаСтиля.ПоясняющийТекст));
			МассивСтрок.Добавить(" ");
			МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(
					НСтр("ru = 'Запросить из ЕГАИС'"),
					Новый Шрифт(,,,,Истина),
					ЦветаСтиля.ЦветГиперссылкиГИСМ,,
					"КомандаЗапроситьАлкогольнуюПродукциюЕГАИС"));
		Иначе
			МассивСтрок.Добавить(
				Новый ФорматированнаяСтрока(
					НСтр("ru = 'Алкогольная продукция не найдена в информационной базе.
						       |Отложите ее в сторону.'"),,
					ЦветаСтиля.ПоясняющийТекст));
		КонецЕсли;
		
	КонецЕсли;
	
	ПоясняющийТекст = Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
