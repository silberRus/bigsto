
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("МассивВладельцев") Тогда
		Для Каждого Владелец Из Параметры.МассивВладельцев Цикл
			Строка = Объект.ВыбранныеОрганизации.Добавить();
			ЗаполнитьЗначенияСвойств(Строка, Владелец);
			Строка.Картинка = КартинкаВладельца(ЭтоПоклажедатель(Владелец));
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда 
		
		Для Каждого Организация Из ВыбранноеЗначение Цикл
			ОтборСтрок = Новый Структура;
			ОтборСтрок.Вставить("Организация", Организация);
			ОтборСтрок.Вставить("Договор", ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка"));
			
			МассивСтрок = Объект.ВыбранныеОрганизации.НайтиСтроки(ОтборСтрок);
			
			Если МассивСтрок.Количество() = 0 Тогда
				НоваяСтрока = Объект.ВыбранныеОрганизации.Добавить();
				НоваяСтрока.Организация = Организация;
				НоваяСтрока.Картинка = КартинкаВладельца(Ложь);
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		
		МассивСтрок = Объект.ВыбранныеОрганизации.НайтиСтроки(ВыбранноеЗначение);
		Если МассивСтрок.Количество() = 0 Тогда
			НоваяСтрока = Объект.ВыбранныеОрганизации.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыбранноеЗначение);
			НоваяСтрока.Картинка = КартинкаВладельца(Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыбранныеОрганизации

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КнопкаОК(Команда)
	
	МассивВладельцев = Новый Массив;
	Для Каждого Строка Из Объект.ВыбранныеОрганизации Цикл
		СтруктураВладельцы = Новый Структура("Организация, Партнер, Договор");
		ЗаполнитьЗначенияСвойств(СтруктураВладельцы, Строка);
		МассивВладельцев.Добавить(СтруктураВладельцы); 
	КонецЦикла;
	Закрыть(МассивВладельцев);
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаОтмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборОрганизации(Команда)

	ОткрытьФорму("Справочник.Организации.ФормаВыбора",Новый Структура("РежимВыбора,МножественныйВыбор,ЗакрыватьПриВыборе",Истина,Истина,Ложь), ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборПоклажедателя(Команда)
		
	ОткрытьФорму("Обработка.ПомощникОформленияСкладскихАктов.Форма.ПодборПоклажедателя", Новый Структура("ЗакрыватьПриВыборе",Ложь), ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВыбранныеОрганизацииОрганизация.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВыбранныеОрганизации.Партнер");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КартинкаВладельца(ЭтоПоклажедатель)
	
	Если ЭтоПоклажедатель Тогда
		Возврат БиблиотекаКартинок.Партнер;
	Иначе
		Возврат БиблиотекаКартинок.Организация;
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПоклажедатель(Владелец)
	
	ЭтоПоклажедатель = Ложь;
	
	Если ЗначениеЗаполнено(Владелец.Партнер) Тогда
		ЭтоПоклажедатель = Истина;
	КонецЕсли;
	
	Возврат ЭтоПоклажедатель;
	
КонецФункции

&НаКлиенте
Процедура ВыбранныеОрганизацииПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ОткрытьФорму("Справочник.Организации.ФормаВыбора",Новый Структура("РежимВыбора,МножественныйВыбор,ЗакрыватьПриВыборе",Истина,Истина,Ложь), ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

