#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	СобытияФормЕГАИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НайденныеАкцизныеМаркиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеСтроки = НайденныеАкцизныеМарки.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	ЗакрытьФорму(ДанныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура НайденныеАкцизныеМаркиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ДанныеСтроки = НайденныеАкцизныеМарки.НайтиПоИдентификатору(Значение);
	
	ЗакрытьФорму(ДанныеСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура НайденныеАкцизныеМаркиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НайтиАкцизныеМарки(Команда)
	
	ОчиститьСообщения();
	
	Если СтрДлина(СтрокаПоиска) < 6 Тогда
		ТекстСообщения = НСтр("ru = 'Необходимо указать по крайней мере 6 символов.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "СтрокаПоиска",,);
		Возврат;
	КонецЕсли;
	
	НайденныеАкцизныеМарки.Очистить();
	
	СоотвествиеШтрихКодов = ВладелецФормы.СоответствиеШтрихкодовСтрокДереваКлиент();
	
	Для Каждого ЭлементСоответствия Из СоотвествиеШтрихКодов Цикл
		
		ШтриховойКод = ЭлементСоответствия.Ключ;
		
		Если СтрДлина(ШтриховойКод) <> 68 Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ПодстрокаВКоторойИщем = Сред(ШтриховойКод, 20, 18);
		
		Если СтрНайти(ПодстрокаВКоторойИщем, СтрокаПоиска) > 0 Тогда
			
			НайденнаяСтрокаДерева = ВладелецФормы.ДеревоМаркированнойПродукции.НайтиПоИдентификатору(ЭлементСоответствия.Значение);
			Если НайденнаяСтрокаДерева <> Неопределено Тогда
				
				НоваяСтрока = НайденныеАкцизныеМарки.Добавить();
				НоваяСтрока.ЗначениеШтрихкода    = НайденнаяСтрокаДерева.ЗначениеШтрихкода;
				НоваяСтрока.АлкогольнаяПродукция = НайденнаяСтрокаДерева.АлкогольнаяПродукция;
				НоваяСтрока.НомерЗаявки          = Сред(ШтриховойКод, 20, 12);
				НоваяСтрока.НомерМарки           = Сред(ШтриховойКод, 32, 6);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗакрытьФорму(ДанныеСтроки)
	
	Если ДанныеСтроки <> Неопределено Тогда
		
		Закрыть(ДанныеСтроки.ЗначениеШтрихкода);
		
	Иначе
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти
