&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ФормаДлительнойОперации;

&НаКлиенте
Перем ТабличныйДокумент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
	КонецЕсли;
	Если Параметры.Свойство("ПериодРегистрации") Тогда
		Период.ДатаНачала = НачалоМесяца(Параметры.ПериодРегистрации);
		Период.ДатаОкончания = КонецМесяца(Параметры.ПериодРегистрации);
		Период.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
	КонецЕсли;
	
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
	ИспользоватьНесколькоОрганизаций = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	Элементы.ГруппаОтборБезОрганизации.Видимость = Не ИспользоватьНесколькоОрганизаций;
	Элементы.ГруппаОтбор.Видимость               = ИспользоватьНесколькоОрганизаций;
	Если Не ИспользоватьНесколькоОрганизаций Тогда
		ЭтаФорма.АвтоЗаголовок = Ложь;
		ЭтаФорма.Заголовок = НСтр("ru = 'Помощник исправления отрицательных остатков товаров организации'");
		Элементы.ГруппаОтбор.Заголовок = НСтр("ru = 'Выберите период, по которым будет выполнен анализ остатков товаров организации'");
		Элементы.ГруппаСостояниеОтрицательныхОстатковОстатков.Заголовок = НСтр("ru = 'Наличие отрицательных остатков товаров организации'");
		Элементы.НадписьОтрицательныеОстатки.Заголовок = НСтр("ru = 'Отрицательные остатки товаров организации могут возникнуть при несоответствии приходов и расходов товаров в разрезе организации'");
		Элементы.ГруппаОформлениеПоступленийТоваров.Заголовок = НСтр("ru = 'Поступления товаров в организацию'");
	КонецЕсли;
	
	Элементы.КартинкаЕстьПредупреждения.Видимость = Ложь;
	Элементы.ПерейтиВЖурналРегистрацииСобытийФормированияИсправительныхСФ.Видимость = Ложь;
	Элементы.ПерейтиВЖурналРегистрацииСобытийФормированияЗаданий.Видимость = Ложь;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями") Тогда
		ТекстНадписи = НСтр("ru = 'Для исправления отрицательных остатков необходимо оформить недостающие поступления товаров или оформить передачи товаров между организациями'");
	Иначе
		ТекстНадписи = НСтр("ru = 'Для исправления отрицательных остатков необходимо оформить недостающие поступления товаров'");
	КонецЕсли;
	Элементы.НадписьОтрицательныеОстаткиИсправление.Заголовок = ТекстНадписи;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеВидимостьюКоманд();
	ПолучитьСостояниеОстатковТоваровОрганизаций();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	Оповестить("ЗакрытаФорма_ПомощникИсправленияОтрицательныхОстатковОрганизаций");
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПередачаТоваровМеждуОрганизациями" Тогда
		Элементы.ТоварыКПередачеМеждуОрганизациями.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если Параметры.Свойство("Организация") Тогда
		Настройки.Удалить("Организация");
	КонецЕсли;
	
	Если Параметры.Свойство("ПериодРегистрации") Тогда
		Настройки.Удалить("Период.Вариант");
		Настройки.Удалить("Период.ДатаНачала");
		Настройки.Удалить("Период.ДатаОкончания");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Не Параметры.Свойство("Организация") Тогда
		Организация = Настройки.Получить("Организация");
	КонецЕсли;
	
	Если Не Параметры.Свойство("ПериодРегистрации") Тогда
		Вариант = Настройки.Получить("Период.Вариант");
		ДатаНачала = Настройки.Получить("Период.ДатаНачала");
		ДатаОкончания = Настройки.Получить("Период.ДатаОкончания");
	КонецЕсли;
	
	Если Вариант <> Неопределено Тогда
		Период.Вариант = Вариант;
	КонецЕсли;
	Если ДатаНачала <> Неопределено И Период.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод Тогда
		Период.ДатаНачала = ДатаНачала;
	КонецЕсли;
	Если ДатаОкончания <> Неопределено И Период.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод Тогда
		Период.ДатаОкончания = ДатаОкончания;
	КонецЕсли;
	
	Если Период.ДатаНачала > Период.ДатаОкончания Тогда
		Период.ДатаОкончания = Дата(1,1,1);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПолучитьСостояниеОстатковТоваровОрганизаций();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ПолучитьСостояниеОстатковТоваровОрганизаций();
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеНаличиеОтрицательныхОстатковНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура("Отбор, ФиксированныеНастройки, КлючНазначенияИспользования, КлючВарианта, СформироватьПриОткрытии");
	ПараметрыФормы.СформироватьПриОткрытии = Истина;
	ПараметрыФормы.КлючНазначенияИспользования = "ОтрицательныеОстатки";
	ПараметрыФормы.КлючВарианта = "ОтрицательныеОстатки";
	
	ПараметрыФормы.Отбор = Новый Структура("Период, ЕстьОтрицательныеОстатки, ТолькоОтрицательныеОстатки", Период, Истина, Истина);
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыФормы.Отбор.Вставить("Организация", Организация);
	КонецЕсли;
	
	ОткрытьФорму("Отчет.АнализОстатковТоваровОрганизаций.Форма", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеНаличиеРазвернутогоСальдоНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура("Отбор, ФиксированныеНастройки, КлючНазначенияИспользования, КлючВарианта, СформироватьПриОткрытии");
	ПараметрыФормы.СформироватьПриОткрытии = Истина;
	ПараметрыФормы.КлючНазначенияИспользования = "НаличиеРазвернутогоСальдо";
	ПараметрыФормы.КлючВарианта = "НаличиеРазвернутогоСальдо";
	
	ПараметрыФормы.Отбор = Новый Структура("Период, ЕстьРазвернутоеСальдо", Период, Истина);
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыФормы.Отбор.Вставить("Организация", Организация);
	КонецЕсли;
	
	ОткрытьФорму("Отчет.АнализОстатковТоваровОрганизаций.Форма", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеНедостаточноТоваровОрганизацийНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура("Отбор, ФиксированныеНастройки, КлючНазначенияИспользования, КлючВарианта, СформироватьПриОткрытии");
	ПараметрыФормы.СформироватьПриОткрытии = Истина;
	ПараметрыФормы.КлючНазначенияИспользования = "НедостаточноТоваров";
	ПараметрыФормы.КлючВарианта = "НедостаточноТоваров";
	
	ПараметрыФормы.Отбор = Новый Структура("Период, ЕстьОтрицательныеОстатки", Период, Истина);
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыФормы.Отбор.Вставить("Организация", Организация);
	КонецЕсли;
	
	ОткрытьФорму("Отчет.АнализОстатковТоваровОрганизаций.Форма", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеПередачаТоваровМеждуОрганизациямиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура("Отбор, ФиксированныеНастройки, КлючНазначенияИспользования, КлючВарианта, СформироватьПриОткрытии");
	ПараметрыФормы.СформироватьПриОткрытии = Истина;
	ПараметрыФормы.КлючНазначенияИспользования = "ТребуетсяПередачаТоваров";
	ПараметрыФормы.КлючВарианта = "ТребуетсяПередачаТоваров";
	
	ПараметрыФормы.Отбор = Новый Структура("Период, ЕстьОтрицательныеОстатки, ТолькоОтрицательныеОстатки", Период, Истина, Ложь);
	
	ОткрытьФорму("Отчет.АнализОстатковТоваровОрганизаций.Форма", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСостояние(Команда)
	
	ПолучитьСостояниеОстатковТоваровОрганизаций();
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьПередачуТоваров(Команда)
	
	СтрокаТаблицы = Элементы.ТоварыКПередачеМеждуОрганизациями.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		
		Если КонецМесяца(СтрокаТаблицы.Период) > КонецДня(ТекущаяДата()) Тогда
			КонецПериода = КонецДня(ТекущаяДата());
		Иначе
			КонецПериода = КонецМесяца(СтрокаТаблицы.Период);
		КонецЕсли;
		
		СтруктураОснование = Новый Структура("Дата, Организация, ОрганизацияПолучатель",
			КонецПериода,
			СтрокаТаблицы.Отправитель,
			СтрокаТаблицы.Получатель);
		СтруктураОснование.Вставить("Склад", СтрокаТаблицы.Склад);
		СтруктураОснование.Вставить("ЗаполнятьПоОтрицательнымОстаткам", Истина);
		
		СтруктураПараметры = Новый Структура("Основание", СтруктураОснование);
		ОткрытьФорму("Документ.ПередачаТоваровМеждуОрганизациями.ФормаОбъекта", СтруктураПараметры, Элементы.ТоварыКПередачеМеждуОрганизациями);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВЖурналРегистрацииСобытийПерепроведенияДокументов(Команда)
	
	ПараметрыФормы = Новый Структура;
	Если ТаблицаИзмененныхДокументов.Количество() > 0 Тогда
		ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", НСтр("ru = 'Изменены номера ГТД'"));
	Иначе
		ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", НСтр("ru = 'Перепроведение документов'"));
	КонецЕсли;
	ПараметрыФормы.Вставить("ДатаНачала", ДатаНачалаПерепроведенияДокументов);
	ПараметрыФормы.Вставить("ДатаОкончания", ДатаОкончанияПерепроведенияДокументов);
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы, ЭтаФорма,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВЖурналРегистрацииСобытийОшибки(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", НСтр("ru = 'Ошибка при перепроведении документов'"));
	ПараметрыФормы.Вставить("ДатаНачала", ДатаНачалаПерепроведенияДокументов);
	ПараметрыФормы.Вставить("ДатаОкончания", ДатаОкончанияПерепроведенияДокументов);
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы, ЭтаФорма,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВЖурналРегистрацииСобытийФормированияИсправительныхСФ(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", НСтр("ru = 'Сформирована исправительная счет-фактура'"));
	ПараметрыФормы.Вставить("ДатаНачала", НачалоДня(ТекущаяДата()));
	ПараметрыФормы.Вставить("ДатаОкончания", КонецДня(ТекущаяДата()));
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы, ЭтаФорма,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВЖурналРегистрацииСобытийФормированияЗаданий(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", НСтр("ru = 'Сформировано задание на печать счет-фактуры'"));
	ПараметрыФормы.Вставить("ДатаНачала", НачалоДня(ТекущаяДата()));
	ПараметрыФормы.Вставить("ДатаОкончания", КонецДня(ТекущаяДата()));
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы, ЭтаФорма,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьСпискаИзмененныхДокументов(Команда)
	
	ТабличныйДокумент = ПечатьСпискаИзмененныхДокументовСервер();
		
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	ТабличныйДокумент.Показать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьИзмененныхСчетовФактур(Команда)
	
	МассивДокументов = МассивИзмененныхДокументов();
		
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Обработка.ПечатьОбщихФорм",
		"СчетФактура",
		МассивДокументов,
		ЭтаФорма, // ВладелецФормы
		Новый Структура("ПечатьВВалюте", Ложь)); // ПараметрыПечати
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьИсправительныеСчетаФактуры(Команда)
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Будет сформировано %1 (исправительные счета-фактуры). Продолжить?'"),
		ЧислоДокументовПрописью(ТаблицаИзмененныхДокументов.Количество()));
    КодОтвета = Неопределено;

    ПоказатьВопрос(Новый ОписаниеОповещения("СформироватьИсправительныеСчетаФактурыЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьИсправительныеСчетаФактурыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    КодОтвета = РезультатВопроса;
    Если КодОтвета = КодВозвратаДиалога.Да Тогда
        
        СформироватьИсправительныеСчетаФактурыСервер();
        
        ТекстСообщения = НСтр("ru = 'Сформированы исправительные счета-фактуры'");
        ТекстЗаголовка = НСтр("ru = 'Сформированы документы'");
        ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Информация32);
        
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СформироватьЗаданияДляМенеджеров(Команда)
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Будет сформировано %1 для менеджеров, указанных в измененных документах. Продолжить?'"),
		ЧислоЗаданийПрописью(ТаблицаИзмененныхДокументов.Количество()));
    КодОтвета = Неопределено;

    ПоказатьВопрос(Новый ОписаниеОповещения("СформироватьЗаданияДляМенеджеровЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьЗаданияДляМенеджеровЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    КодОтвета = РезультатВопроса;
    Если КодОтвета = КодВозвратаДиалога.Да Тогда
        
        СформироватьЗаданияДляМенеджеровСервер();
        
        ТекстСообщения = НСтр("ru = 'Сформированы задания для менеджеров, указанных в измененных документах'");
        ТекстЗаголовка = НСтр("ru = 'Сформированы задания'");
        ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Информация32);
        
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОформлениеДокументов Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСостояние;
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПерепроведение Тогда
		
		Если ЕстьОтрицательныеОстатки Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОформлениеДокументов;
		Иначе
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСостояние;
		КонецЕсли;
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРезультатПерепроведения Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПерепроведение;
		
	КонецЕсли;
	
	УправлениеВидимостьюКоманд();
	
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	
	Перем ПерепроведениеВыполнено;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСостояние Тогда
		
		Если ЕстьОтрицательныеОстатки Тогда
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОформлениеДокументов;
		Иначе
			Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПерепроведение;
		КонецЕсли;
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОформлениеДокументов Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПерепроведение;
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПерепроведение Тогда
		ПерепровестиДокументы();
		
	КонецЕсли;
	
	УправлениеВидимостьюКоманд();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область УправлениеЭлементамиФормы

&НаКлиенте
Процедура УправлениеВидимостьюКоманд()
	
	Элементы.Назад.Видимость = (Элементы.Страницы.ТекущаяСтраница <> Элементы.СтраницаСостояние);
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОформлениеДокументов Тогда
		Элементы.Далее.Видимость = Истина;
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРезультатПерепроведения Тогда
		Элементы.Далее.Видимость = Ложь;
		
	Иначе
		Элементы.Далее.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеНаличиеОтрицательныхОстатков(РезультатЗапроса)
	
	Если РезультатЗапроса.Пустой() Тогда
		ЕстьОтрицательныеОстатки = Ложь;
		СостояниеНаличиеОтрицательныхОстатков = НСтр("ru = 'Отрицательных остатков не обнаружено'");
		Элементы.СостояниеНаличиеОтрицательныхОстатков.ЦветТекста = ЦветаСтиля.ПоясняющийТекст; 
		Элементы.СостояниеНаличиеОтрицательныхОстатков.Гиперссылка = Ложь;
		Элементы.НадписьОтрицательныеОстатки.Видимость = Ложь;
		Элементы.НадписьОтрицательныеОстаткиИсправление.Видимость = Ложь;
		Элементы.КартинкаОтрицательныеОстаткиИсправление.Видимость = Ложь;
		
	Иначе
		ЕстьОтрицательныеОстатки = Истина;
		СостояниеНаличиеОтрицательныхОстатков = НСтр("ru = 'Обнаружены отрицательные остатки на конец месяца:'") + " ";
		Элементы.СостояниеНаличиеОтрицательныхОстатков.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст; 
		Элементы.СостояниеНаличиеОтрицательныхОстатков.Гиперссылка = Истина;
		Элементы.НадписьОтрицательныеОстатки.Видимость = Истина;
		Элементы.НадписьОтрицательныеОстаткиИсправление.Видимость = Истина;
		Элементы.КартинкаОтрицательныеОстаткиИсправление.Видимость = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			СостояниеНаличиеОтрицательныхОстатков = СостояниеНаличиеОтрицательныхОстатков
				+ Формат(Выборка.Период, "ДФ=""ММММ гггг""")
				+ ", ";
		КонецЦикла;
		Если Прав(СостояниеНаличиеОтрицательныхОстатков, 2) = ", " Тогда
			СостояниеНаличиеОтрицательныхОстатков = Лев(СостояниеНаличиеОтрицательныхОстатков, СтрДлина(СостояниеНаличиеОтрицательныхОстатков) - 2);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеНаличиеРазвернутогоСальдо(РезультатЗапроса)
	
	Если РезультатЗапроса.Пустой() Тогда
		
		ЕстьРазвернутоеСальдо = Ложь;
		СостояниеНаличиеРазвернутогоСальдо = НСтр("ru = 'Развернутого сальдо по видам запасов и номерам ГТД не обнаружено'");
		СостояниеПерепроведениеДокументов = НСтр("ru = 'Перепроведения документов не требуется'");
		
		Элементы.СостояниеНаличиеРазвернутогоСальдо.ЦветТекста = ЦветаСтиля.ПоясняющийТекст; 
		Элементы.СостояниеНаличиеРазвернутогоСальдо.Гиперссылка = Ложь;
		Элементы.СостояниеПерепроведениеДокументов.ЦветТекста = ЦветаСтиля.ПоясняющийТекст; 
		
		Элементы.НадписьРазвернутоеСальдо.Видимость = Ложь;
		Элементы.НадписьРазвернутоеСальдоИсправление.Видимость = Ложь;
		Элементы.КартинкаРазвернутоеСальдоИсправление.Видимость = Ложь;
		
	Иначе
		
		ЕстьРазвернутоеСальдо = Истина;
		СостояниеНаличиеРазвернутогоСальдо = НСтр("ru = 'Обнаружено развернутое сальдо на конец месяца:'") + " ";
		
		Элементы.СостояниеНаличиеРазвернутогоСальдо.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст; 
		Элементы.СостояниеНаличиеРазвернутогоСальдо.Гиперссылка = Истина;
		Элементы.СостояниеПерепроведениеДокументов.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст; 
		
		Элементы.НадписьРазвернутоеСальдо.Видимость = Истина;
		Элементы.НадписьРазвернутоеСальдоИсправление.Видимость = Истина;
		Элементы.КартинкаРазвернутоеСальдоИсправление.Видимость = Истина;
		
		ПервыйПериод = Неопределено;
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			СостояниеНаличиеРазвернутогоСальдо = СостояниеНаличиеРазвернутогоСальдо
				+ Формат(Выборка.Период, "ДФ=""ММММ гггг""")
				+ ", ";
			Если ПервыйПериод = Неопределено Тогда
				ПервыйПериод = Выборка.Период;
			КонецЕсли;
		КонецЦикла;
		Если Прав(СостояниеНаличиеРазвернутогоСальдо, 2) = ", " Тогда
			СостояниеНаличиеРазвернутогоСальдо = Лев(СостояниеНаличиеРазвернутогоСальдо, СтрДлина(СостояниеНаличиеРазвернутогоСальдо) - 2);
		КонецЕсли;
		
		СостояниеПерепроведениеДокументов = НСтр("ru = 'Требуется перепроведение документов начиная с периода:'")
			+ " " + Формат(ПервыйПериод, "ДФ=""ММММ гггг""") + " " + НСтр("ru = 'г.'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеНедостаточноТоваровОрганизаций(РезультатЗапроса)
	
	Если РезультатЗапроса.Пустой() Тогда
		СостояниеНедостаточноТоваровОрганизаций = НСтр("ru = 'Оформление поступлений товаров не требуется'");
		Элементы.СостояниеНедостаточноТоваровОрганизаций.ЦветТекста = ЦветаСтиля.ПоясняющийТекст; 
		Элементы.СостояниеНедостаточноТоваровОрганизаций.Гиперссылка = Ложь;
		
	Иначе
		СостояниеНедостаточноТоваровОрганизаций = НСтр("ru = 'Требуется оформить поступления товаров в периодах:'") + " ";
		Элементы.СостояниеНедостаточноТоваровОрганизаций.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст; 
		Элементы.СостояниеНедостаточноТоваровОрганизаций.Гиперссылка = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			СостояниеНедостаточноТоваровОрганизаций = СостояниеНедостаточноТоваровОрганизаций
				+ Формат(Выборка.Период, "ДФ=""ММММ гггг""")
				+ ", ";
		КонецЦикла;
		Если Прав(СостояниеНедостаточноТоваровОрганизаций, 2) = ", " Тогда
			СостояниеНедостаточноТоваровОрганизаций = Лев(СостояниеНедостаточноТоваровОрганизаций, СтрДлина(СостояниеНедостаточноТоваровОрганизаций) - 2);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеПередачаТоваровМеждуОрганизациями(РезультатЗапроса)
	
	Если РезультатЗапроса.Пустой() Тогда
		СостояниеПередачаТоваровМеждуОрганизациями = НСтр("ru = 'Оформление передач товаров между организациями не требуется'");
		Элементы.СостояниеПередачаТоваровМеждуОрганизациями.ЦветТекста = ЦветаСтиля.ПоясняющийТекст; 
		Элементы.СостояниеПередачаТоваровМеждуОрганизациями.Гиперссылка = Ложь;
		Элементы.ТоварыКПередачеМеждуОрганизациями.Видимость = Ложь;
		
	Иначе
		СостояниеПередачаТоваровМеждуОрганизациями = НСтр("ru = 'Требуется оформить передачи товаров между организациями в периодах:'") + " ";
		Элементы.СостояниеПередачаТоваровМеждуОрганизациями.ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст; 
		Элементы.СостояниеПередачаТоваровМеждуОрганизациями.Гиперссылка = Истина;
		Элементы.ТоварыКПередачеМеждуОрганизациями.Видимость = Истина;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			СостояниеПередачаТоваровМеждуОрганизациями = СостояниеПередачаТоваровМеждуОрганизациями
				+ Формат(Выборка.Период, "ДФ=""ММММ гггг""")
				+ ", ";
		КонецЦикла;
		Если Прав(СостояниеПередачаТоваровМеждуОрганизациями, 2) = ", " Тогда
			СостояниеПередачаТоваровМеждуОрганизациями = Лев(СостояниеПередачаТоваровМеждуОрганизациями, СтрДлина(СостояниеПередачаТоваровМеждуОрганизациями) - 2);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПерепроведениеДокументов

&НаСервере
Процедура УстановитьАктивностьДвиженийПоРегистрам(МассивДокументов, Активность)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|   ТоварыОрганизаций.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|ГДЕ
	|	(ТоварыОрганизаций.Регистратор В (&МассивДокументов) ИЛИ &Активность)
	|	И ТоварыОрганизаций.Активность <> &Активность
	|");
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.УстановитьПараметр("Активность", Активность);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыНакопления.ТоварыОрганизаций.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		НаборЗаписей.Прочитать();
		НаборЗаписей.УстановитьАктивность(Активность);
		НаборЗаписей.Записывать = Истина;
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры // УстановитьАктивностьДвиженийПоРегистрам()

&НаКлиентеНаСервереБезКонтекста
Функция ЧислоДокументовПрописью(КоличествоДокументов)
	
	КоличествоПрописью = ЧислоПрописью(
		КоличествоДокументов,
		"Л = ru_RU; НП = Истина; НД = Ложь; ДП = Ложь;",
		НСтр("ru = 'документ,документа,документов,м,,,,,0'"));
	Поз = СтрНайти(КоличествоПрописью, "документ");
	Если Поз <> 0 Тогда
		КоличествоПрописью = Сред(КоличествоПрописью, Поз);
	КонецЕсли;
	КоличествоПрописью = Строка(КоличествоДокументов) + " " + НРег(КоличествоПрописью);
	
	Возврат КоличествоПрописью;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЧислоЗаданийПрописью(КоличествоЗаданий)
	
	КоличествоПрописью = ЧислоПрописью(
		КоличествоЗаданий,
		"Л = ru_RU; НП = Истина; НД = Ложь; ДП = Ложь;",
		НСтр("ru = 'задание,задания,заданий,с,,,,,0'"));
	Поз = СтрНайти(КоличествоПрописью, "задани");
	Если Поз <> 0 Тогда
		КоличествоПрописью = Сред(КоличествоПрописью, Поз);
	КонецЕсли;
	КоличествоПрописью = Строка(КоличествоЗаданий) + " " + НРег(КоличествоПрописью);
	
	Возврат КоличествоПрописью;
	
КонецФункции

&НаСервере
Функция МассивДокументовДляПолногоПерепроведения()
	
	МассивОрганизаций = МассивДоступныхОрганизаций();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварыОрганизаций.Регистратор КАК Регистратор,
	|	Данные.Дата КАК Дата
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор,
	|		Организация В (&МассивОрганизаций)
	|	) КАК ТоварыОрганизаций
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК Данные
	|	ПО ТоварыОрганизаций.Регистратор = Данные.Документ
	|ГДЕ
	|	Не ТоварыОрганизаций.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|	И ТоварыОрганизаций.КоличествоРасход <> 0
	|    
	|УПОРЯДОЧИТЬ ПО
	|	Данные.Дата Возр,
	|	ТоварыОрганизаций.Регистратор 
	|");
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	
	ТаблицаДокументов = Запрос.Выполнить().Выгрузить();
	МассивДокументов = ТаблицаДокументов.ВыгрузитьКолонку("Регистратор");
	
	Возврат МассивДокументов;
	
КонецФункции

&НаСервере
Функция МассивДокументовДляПерепроведения()
	
	МассивОрганизаций = МассивДоступныхОрганизаций();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(ЗакрытиеМесяцаСервер.ЗапросОтрицательныеОстаткиТоваровОрганизаций() + "
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|" +
	"ВЫБРАТЬ
	|	Остатки.Период КАК Период,
	|	Остатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Остатки.Организация КАК Организация,
	|	Остатки.ВидЗапасов КАК ВидЗапасов,
	|	Остатки.НомерГТД КАК НомерГТД,
	|	СУММА(Остатки.Количество) КАК Количество,
	|	СУММА(Остатки.Оборот) КАК Оборот
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыОрганизацийОстаткиИОбороты.Период КАК Период,
	|		ТоварыОрганизацийОстаткиИОбороты.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТоварыОрганизацийОстаткиИОбороты.Организация КАК Организация,
	|		ТоварыОрганизацийОстаткиИОбороты.ВидЗапасов КАК ВидЗапасов,
	|		ТоварыОрганизацийОстаткиИОбороты.НомерГТД КАК НомерГТД,
	|		ТоварыОрганизацийОстаткиИОбороты.КоличествоКонечныйОстаток КАК Количество,
	|		ТоварыОрганизацийОстаткиИОбороты.КоличествоОборот КАК Оборот
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций.ОстаткиИОбороты(
	|				&ГраницаКонецПредыдущегоПериода,
	|				&ГраницаКонецПериода,
	|				Месяц,
	|				,
	|				АналитикаУчетаНоменклатуры В
	|						(ВЫБРАТЬ
	|							ОтрицательныеОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|						ИЗ
	|							ОтрицательныеОстатки КАК ОтрицательныеОстатки)
	|					И Организация В (&МассивОрганизаций)
	|				) КАК ТоварыОрганизацийОстаткиИОбороты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Резервы.Период,
	|		Резервы.АналитикаУчетаНоменклатуры,
	|		Резервы.Организация,
	|		Резервы.ВидЗапасов,
	|		Резервы.НомерГТД,
	|		Резервы.КоличествоКонечныйОстаток,
	|		Резервы.КоличествоОборот
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций.ОстаткиИОбороты(
	|				&ГраницаКонецПредыдущегоПериода,
	|				&ГраницаКонецПериода,
	|				Месяц,
	|				,
	|				АналитикаУчетаНоменклатуры В
	|						(ВЫБРАТЬ
	|							ОтрицательныеОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|						ИЗ
	|							ОтрицательныеОстатки КАК ОтрицательныеОстатки)
	|					И Организация В (&МассивОрганизаций)
	|				) КАК Резервы) КАК Остатки
	|
	|СГРУППИРОВАТЬ ПО
	|	Остатки.НомерГТД,
	|	Остатки.ВидЗапасов,
	|	Остатки.Организация,
	|	Остатки.АналитикаУчетаНоменклатуры,
	|	Остатки.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОстатков.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаОстатков.Период КАК Период
	|ПОМЕСТИТЬ ВтОтсутствующиеТовары
	|ИЗ
	|	ТаблицаОстатков КАК ТаблицаОстатков
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОстатков.АналитикаУчетаНоменклатуры,
	|	ТаблицаОстатков.Период
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаОстатков.Количество) < 0 И
	|	СУММА(ТаблицаОстатков.Оборот) < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	Период
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтрицательныеОстатки.Организация КАК Организация,
	|	ОтрицательныеОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ОтрицательныеОстатки.ВидЗапасов КАК ВидЗапасов,
	|	ОтрицательныеОстатки.НомерГТД КАК НомерГТД,
	|	ОтрицательныеОстатки.Период КАК Период
	|	
	|ПОМЕСТИТЬ ВтРазвернутоеСальдо
	|ИЗ
	|	ОтрицательныеОстатки КАК ОтрицательныеОстатки	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ВтОтсутствующиеТовары КАК ОтсутствующиеТовары
	|	ПО
	|		ОтрицательныеОстатки.АналитикаУчетаНоменклатуры = ОтсутствующиеТовары.АналитикаУчетаНоменклатуры
	|		И ОтрицательныеОстатки.Период = ОтсутствующиеТовары.Период
	|ГДЕ
	|	ОтсутствующиеТовары.Период ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов,
	|	НомерГТД,
	|	Период
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РазвернутоеСальдо.Организация,
	|	РазвернутоеСальдо.АналитикаУчетаНоменклатуры,
	|	РазвернутоеСальдо.Период
	|	
	|ПОМЕСТИТЬ ВтТаблицаОтборНоменклатурыПоПериодам
	|ИЗ
	|	ВтРазвернутоеСальдо КАК РазвернутоеСальдо
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.ПериодСекунда,
	|	ТоварыОрганизаций.Регистратор,
	|	СУММА(ТоварыОрганизаций.КоличествоРасход) КАК КоличествоРасход
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.ОстаткиИОбороты(&ГраницаКонецПредыдущегоПериода, &ГраницаКонецПериода, Авто, Движения, 
	|		(Организация, АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ
	|				РазвернутоеСальдо.Организация,
	|				РазвернутоеСальдо.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ВтРазвернутоеСальдо КАК РазвернутоеСальдо
	|			)
	|	) КАК ТоварыОрганизаций
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтТаблицаОтборНоменклатурыПоПериодам КАК ТаблицаОтборНоменклатурыПоПериодам
	|	ПО
	|		ТоварыОрганизаций.Организация = ТаблицаОтборНоменклатурыПоПериодам.Организация
	|		И ТоварыОрганизаций.АналитикаУчетаНоменклатуры = ТаблицаОтборНоменклатурыПоПериодам.АналитикаУчетаНоменклатуры
	|		И ТоварыОрганизаций.ПериодМесяц = ТаблицаОтборНоменклатурыПоПериодам.Период
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтРазвернутоеСальдо КАК РазвернутоеСальдо
	|	ПО
	|		ТоварыОрганизаций.Организация = РазвернутоеСальдо.Организация
	|		И ТоварыОрганизаций.АналитикаУчетаНоменклатуры = РазвернутоеСальдо.АналитикаУчетаНоменклатуры
	|		И ТоварыОрганизаций.ВидЗапасов = РазвернутоеСальдо.ВидЗапасов
	|		И ТоварыОрганизаций.НомерГТД = РазвернутоеСальдо.НомерГТД
	|		И ТоварыОрганизаций.ПериодМесяц = РазвернутоеСальдо.Период
	|ГДЕ
	|	Не ТоварыОрганизаций.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|	И ТоварыОрганизаций.КоличествоРасход <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыОрганизаций.ПериодСекунда,
	|	ТоварыОрганизаций.Регистратор
	|		
	|УПОРЯДОЧИТЬ ПО
	|	КоличествоРасход ВОЗР,
	|	ТоварыОрганизаций.ПериодСекунда,
	|	ТоварыОрганизаций.Регистратор
	|");
	
	Запрос.УстановитьПараметр("ГраницаКонецПредыдущегоПериода", 	Период.ДатаНачала);
	Запрос.УстановитьПараметр("ГраницаКонецПериода",				Период.ДатаОкончания);
	Запрос.УстановитьПараметр("МассивОрганизаций",					МассивОрганизаций);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;; // Используется для вызова отладочной процедуры ЗапасыСервер.ПоказатьВременнуюТаблицу
	
	ТаблицаДокументов = Запрос.Выполнить().Выгрузить();
	МассивДокументов = ТаблицаДокументов.ВыгрузитьКолонку("Регистратор");
	
	Возврат МассивДокументов;
	
КонецФункции

&НаСервере
Процедура ПерепроведениеДокументовПоТоварамОрганизаций(МассивДокументов, Знач КоличествоДокументовЗаСеанс, Счетчик, ТекущийПериод = Неопределено, МассивНепроведенныхДокументов = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИсходноеПроводитьБезКонтроляОстатковТоваровОрганизаций = ПараметрыСеанса.ПроводитьБезКонтроляОстатковТоваровОрганизаций;
	ПараметрыСеанса.ПроводитьБезКонтроляОстатковТоваровОрганизаций = Истина;
	
	МассивОбработанныхДокументов = Новый Массив;
	КодЯзыка = Метаданные.ОсновнойЯзык.КодЯзыка;
	
	Для Каждого Регистратор Из МассивДокументов Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			ДокументОбъект = Регистратор.ПолучитьОбъект();
			ДокументОбъект.ДополнительныеСвойства.Вставить("ПроверкаДатыЗапретаИзменения", Ложь);
			ДокументОбъект.ДополнительныеСвойства.Вставить("ВычитатьДвиженияПоТоварамОрганизаций", Ложь);
			ДокументОбъект.ДополнительныеСвойства.Вставить("ОкончаниеПериодаПомощникаИсправленияОстатков", Период.ДатаОкончания);
			
			ДокументОбъект.ДополнительныеСвойства.Вставить(ЗапасыСервер.ИмяДопСвойстваДляПерезаполненияВидовЗапасов(), Истина);
		
			Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
			 И ДокументОбъект.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию Тогда
			 
				СчетФактура = ПродажиВызовСервера.СчетФактураДокумента(Регистратор, ДокументОбъект.Организация);
				Если ЗначениеЗаполнено(СчетФактура) Тогда
					ДокументОбъект.ДополнительныеСвойства.Вставить("КонтролироватьИзменениеНомеровГТД", Истина);
				КонецЕсли;
				
			ИначеЕсли ТипЗнч(Регистратор) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
				
				СчетФактура = ПродажиВызовСервера.СчетФактураДокумента(Регистратор, ДокументОбъект.Организация);
				Если ЗначениеЗаполнено(СчетФактура) Тогда
					ДокументОбъект.ДополнительныеСвойства.Вставить("КонтролироватьИзменениеНомеровГТД", Истина);
				КонецЕсли;
				
		КонецЕсли;
		
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			ДокументПроведенУспешно = Истина;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Ошибка при проведении документа'", КодЯзыка), 
				УровеньЖурналаРегистрации.Ошибка,
				,
				, 
				ПодробноеПредставлениеОшибки);
			ДокументПроведенУспешно = Ложь;
		КонецПопытки;
		
		Если ДокументПроведенУспешно Тогда
			
			Если ДокументОбъект.ДополнительныеСвойства.Свойство("НедостаточноОстатковТоваровОрганизаций") Тогда
				СтруктураОтбора = Новый Структура("Документ", Регистратор);
				Если ТаблицаНеПроведенныхДокументов.НайтиСтроки(СтруктураОтбора).Количество() = 0 Тогда
					ЗаписьЖурналаРегистрации(
						НСтр("ru = 'Ошибка при перепроведении документов'", КодЯзыка), 
						УровеньЖурналаРегистрации.Ошибка,
						ДокументОбъект.ДополнительныеСвойства.ДляПроведения.МетаданныеДокумента,
						ДокументОбъект.Ссылка,
						НСтр("ru = 'Для документа недостаточно остатков товаров организаций. Документ не перепроведен'", КодЯзыка),
						РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
					НоваяСтрока = ТаблицаНеПроведенныхДокументов.Добавить();
					НоваяСтрока.Документ = Регистратор;
					НоваяСтрока.Тип = ТипЗнч(Регистратор);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ДокументОбъект, "Дата, Номер, Организация");
				КонецЕсли;
			Иначе
				ЗаписьЖурналаРегистрации(
					НСтр("ru = 'Перепроведение документов'", КодЯзыка),
					УровеньЖурналаРегистрации.Информация,
					ДокументОбъект.ДополнительныеСвойства.ДляПроведения.МетаданныеДокумента,
					ДокументОбъект.Ссылка,
					НСтр("ru = 'Выполнено перезаполнение видов запасов документа. Документ перепроведен'", КодЯзыка),
					РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			КонецЕсли;
			
			Если ДокументОбъект.ДополнительныеСвойства.Свойство("ИзменилисьНомераГТД") Тогда
				
				НоваяСтрока = ТаблицаИзмененныхДокументов.Добавить();
				НоваяСтрока.Документ = Регистратор;
				НоваяСтрока.Ответственный = ДокументОбъект.Менеджер;
				НоваяСтрока.Тип = ТипЗнч(Регистратор);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДокументОбъект, "Дата, Номер, Организация, Склад, Подразделение");
				
				ЗаписьЖурналаРегистрации(
					НСтр("ru = 'Изменены номера ГТД'", КодЯзыка),
					УровеньЖурналаРегистрации.Предупреждение,
					ДокументОбъект.ДополнительныеСвойства.ДляПроведения.МетаданныеДокумента,
					ДокументОбъект.Ссылка,
					НСтр("ru = 'В документе изменены данные о номерах ГТД товаров'", КодЯзыка),
					РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ТекущийПериод = Дата(1,1,1) Тогда
			ТекущийПериод = НачалоМесяца(ДокументОбъект.Дата);
		КонецЕсли;
		
		Если ДокументОбъект.ДополнительныеСвойства.Свойство("НедостаточноОстатковТоваровОрганизаций")
		 И МассивНепроведенныхДокументов <> Неопределено Тогда
			МассивНепроведенныхДокументов.Добавить(Регистратор);
		КонецЕсли;
		
		МассивОбработанныхДокументов.Добавить(Регистратор);
		Счетчик = Счетчик + 1;
		КоличествоДокументовЗаСеанс = КоличествоДокументовЗаСеанс - 1;
		
		Если МассивНепроведенныхДокументов <> Неопределено
		 И МассивНепроведенныхДокументов.Количество() > 0
		 И ТекущийПериод <> НачалоМесяца(ДокументОбъект.Дата) Тогда
		 
			ПерепроведениеДокументовПоТоварамОрганизаций(МассивНепроведенныхДокументов, КоличествоДокументовЗаСеанс, Счетчик);
			ТекущийПериод = НачалоМесяца(ДокументОбъект.Дата);
			
		КонецЕсли;
		
		Если КоличествоДокументовЗаСеанс = 0 Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Регистратор Из МассивОбработанныхДокументов Цикл
		Индекс = МассивДокументов.Найти(Регистратор);
		Если Индекс <> Неопределено Тогда
			МассивДокументов.Удалить(Индекс);
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыСеанса.ПроводитьБезКонтроляОстатковТоваровОрганизаций = ИсходноеПроводитьБезКонтроляОстатковТоваровОрганизаций;
	
КонецПроцедуры

&НаСервере
Функция МассивИзмененныхДокументов()
	
	ТаблицаДокументов = ТаблицаИзмененныхДокументов.Выгрузить(, "Документ");
	ТаблицаДокументов.Свернуть("Документ", "");
	МассивДокументов = ТаблицаДокументов.ВыгрузитьКолонку("Документ");
	
	Возврат МассивДокументов;
	
КонецФункции

&НаСервере
Функция ПечатьСпискаИзмененныхДокументовСервер()
	
	Перем ТабличныйДокументСервер;
	
	МассивДокументов = МассивИзмененныхДокументов();
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	СхемаКомпоновкиДанных = ОбработкаОбъект.ПолучитьМакет("СхемаКомпоновкиДанныхСписокДокументов");
	ИсточникДоступныхНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных);
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.Инициализировать(ИсточникДоступныхНастроек);
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	КомпоновщикНастроек.Восстановить();
	НастройкиКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();
	
	ВнешниеНаборыДанных = Новый Структура("ТаблицаДокументов", ТаблицаИзмененныхДокументов.Выгрузить());
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		СхемаКомпоновкиДанных, 
		НастройкиКомпоновкиДанных,
		, // ДанныеРасшифровки,
		, // МакетОформления
		Тип("ГенераторМакетаКомпоновкиДанных"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(
		МакетКомпоновки,
		ВнешниеНаборыДанных,
		,); // ДанныеРасшифровки
	
	ТабличныйДокументСервер = Новый ТабличныйДокумент;		
	ТабличныйДокументСервер.Очистить();
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент();
	ПроцессорВывода.УстановитьДокумент(ТабличныйДокументСервер);
	ТабличныйДокументСервер = ПроцессорВывода.Вывести(
		ПроцессорКомпоновки,
		Истина); // Воможность прерывания пользователем
	
	ТабличныйДокументСервер.ОтображатьЗаголовки = Ложь;
	ТабличныйДокументСервер.ОтображатьСетку = Ложь;
	ТабличныйДокументСервер.ТолькоПросмотр = Истина;
	
	Возврат ТабличныйДокументСервер
	
КонецФункции

&НаСервере
Процедура СформироватьИсправительныеСчетаФактурыСервер()
	
	КодЯзыка = Метаданные.ОсновнойЯзык.КодЯзыка;
	
	Для Каждого СтрокаТаблицы Из ТаблицаИзмененныхДокументов Цикл
		
		СтруктураОснование = Новый Структура;
		СтруктураОснование.Вставить("ДокументОснование", СтрокаТаблицы.Документ);
		СтруктураОснование.Вставить("Организация", СтрокаТаблицы.Организация);
		СтруктураОснование.Вставить("Дата", СтрокаТаблицы.Дата);
		СтруктураОснование.Вставить("Исправление", Истина);
		
		НовыйДокумент = Документы.СчетФактураВыданный.СоздатьДокумент();
		НовыйДокумент.Заполнить(СтруктураОснование);
		Попытка
			НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
			ДокументСозданУспешно = Истина;
		Исключение
			ДокументСозданУспешно = Ложь;
		КонецПопытки;
		
		Если ДокументСозданУспешно Тогда
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Сформирована исправительная счет-фактура'", КодЯзыка),
				УровеньЖурналаРегистрации.Информация,
				НовыйДокумент.Метаданные(),
				НовыйДокумент.Ссылка,
				НСтр("ru = 'Сформирована исправительная счет-фактура для документа реализации товаров'", КодЯзыка),
				РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаголовокКнопкиПерейти = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Сформировано %1'", КодЯзыка),
		ЧислоДокументовПрописью(ТаблицаИзмененныхДокументов.Количество()));
	Элементы.ПерейтиВЖурналРегистрацииСобытийФормированияИсправительныхСФ.Заголовок = ЗаголовокКнопкиПерейти;
	Элементы.ПерейтиВЖурналРегистрацииСобытийФормированияИсправительныхСФ.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьЗаданияДляМенеджеровСервер()
	
	КодЯзыка = Метаданные.ОсновнойЯзык.КодЯзыка;
	
	Для Каждого СтрокаТаблицы Из ТаблицаИзмененныхДокументов Цикл
		
		НовоеЗадание = БизнесПроцессы.Задание.СоздатьБизнесПроцесс();
		НовоеЗадание.Дата = ТекущаяДатаСеанса();
		НовоеЗадание.Предмет = СтрокаТаблицы.Документ;
		НовоеЗадание.Исполнитель = СтрокаТаблицы.Ответственный;
		НовоеЗадание.Наименование = НСтр("ru = 'Перепечатать счет-фактуру для документа:'", КодЯзыка) + " " + Строка(СтрокаТаблицы.Документ);
		НовоеЗадание.Содержание = НСтр("ru = 'В документе изменены номера ГТД. Требуется перепечатать счет-фактуру.'", КодЯзыка);
		НовоеЗадание.СрокИсполнения = ТекущаяДатаСеанса();
		НовоеЗадание.Автор = Пользователи.ТекущийПользователь();
		НовоеЗадание.Важность = Перечисления.ВариантыВажностиЗадачи.Обычная;
		
		НовоеЗадание.Записать();
		НовоеЗадание.Старт();
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Сформировано задание на печать счет-фактуры'", КодЯзыка),
			УровеньЖурналаРегистрации.Информация,
			НовоеЗадание.Метаданные(),
			НовоеЗадание.Ссылка,
			НСтр("ru = 'Сформировано задание для менеджера документа реализации товаров'", КодЯзыка),
			РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		
	КонецЦикла;
	
	ЗаголовокКнопкиПерейти = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Сформировано %1'", КодЯзыка),
		ЧислоЗаданийПрописью(ТаблицаИзмененныхДокументов.Количество()));
	Элементы.ПерейтиВЖурналРегистрацииСобытийФормированияЗаданий.Заголовок = ЗаголовокКнопкиПерейти;
	Элементы.ПерейтиВЖурналРегистрацииСобытийФормированияЗаданий.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерепровестиДокументы()
	
	Если СпособПерезаполненияВидовЗапасовВДокументах = 1 Тогда
		МассивДокументов = МассивДокументовДляПолногоПерепроведения();
	Иначе
		МассивДокументов = МассивДокументовДляПерепроведения();
	КонецЕсли;
	МассивНепроведенныхДокументов = Новый Массив;
	
	КоличествоДокументов = МассивДокументов.Количество();
	КоличествоДокументовЗаСеанс = Окр(КоличествоДокументов / 10);
	
	ДополнительныеПараметры = Новый Структура("МассивДокументов", МассивДокументов);
	Если КоличествоДокументов > 1 Тогда
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Будет перепроведено %1, что может занять длительное время. Продолжить?'"),
			ЧислоДокументовПрописью(КоличествоДокументов));
		ПоказатьВопрос(Новый ОписаниеОповещения("ПерепровестиДокументыЗавершение", ЭтотОбъект, ДополнительныеПараметры),
			ТекстВопроса,
			РежимДиалогаВопрос.ДаНет);
	Иначе
		КодОтвета = КодВозвратаДиалога.Да;
		ПерепровестиДокументыЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПерепровестиДокументыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	МассивДокументов = ДополнительныеПараметры.МассивДокументов;
	МассивНепроведенныхДокументов = Новый Массив;
	
	КоличествоДокументов = МассивДокументов.Количество();
	КоличествоДокументовЗаСеанс = Окр(КоличествоДокументов / 10);
	
	Если СпособПерезаполненияВидовЗапасовВДокументах = 1 Тогда
		УстановитьАктивностьДвиженийПоРегистрам(
		МассивДокументов,
		Ложь); // Активность
	КонецЕсли;

	ДатаНачалаПерепроведенияДокументов = ТекущаяДата();
	ТаблицаИзмененныхДокументов.Очистить();
	ТаблицаНеПроведенныхДокументов.Очистить();
	
	ТекущийПериод = Дата(1,1,1);
	Счетчик = 0;
	Пока Счетчик < КоличествоДокументов Цикл
		
		Состояние(НСтр("ru='Перепроведение документов'"), Счетчик / КоличествоДокументов * 100);
		
		ПерепроведениеДокументовПоТоварамОрганизаций(
			МассивДокументов,
			КоличествоДокументовЗаСеанс,
			Счетчик,
			ТекущийПериод,
			МассивНепроведенныхДокументов);
		
		Состояние(НСтр("ru='Перепроведение документов'"), Счетчик / КоличествоДокументов * 100);
		
	КонецЦикла;
	
	Если МассивНепроведенныхДокументов.Количество() > 0 Тогда
		ПерепроведениеДокументовПоТоварамОрганизаций(
			МассивНепроведенныхДокументов,
			КоличествоДокументовЗаСеанс,
			Счетчик,
			Неопределено,
			МассивНепроведенныхДокументов);
	КонецЕсли;
	
	ДатаОкончанияПерепроведенияДокументов = ТекущаяДата();
	
	Если КоличествоДокументов = 1 Тогда
		ЗаголовокКнопкиПерейти = НСтр("ru = 'Перепроведен 1 документ'");
	Иначе
		ЗаголовокКнопкиПерейти = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Перепроведено %1'"),
			ЧислоДокументовПрописью(КоличествоДокументов));
	КонецЕсли;
	КоличествоИзмененныхДокументов = ТаблицаИзмененныхДокументов.Количество();
	Если КоличествоИзмененныхДокументов > 0 Тогда
		ЗаголовокКнопкиПерейти = ЗаголовокКнопкиПерейти + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = ', изменены данные о номерах ГТД в %1'"),
			ЧислоДокументовПрописью(КоличествоИзмененныхДокументов));
	КонецЕсли;
	Элементы.ПерейтиВЖурналРегистрацииСобытийПерепроведенияДокументов.Заголовок = ЗаголовокКнопкиПерейти;
	ЕстьИзмененныеДокументы = (ТаблицаИзмененныхДокументов.Количество() > 0);
	
	КоличествоНеПроведенныхДокументов = ТаблицаНеПроведенныхДокументов.Количество();
	Если КоличествоНеПроведенныхДокументов = 1 Тогда
		Элементы.ПерейтиВЖурналРегистрацииСобытийОшибки.Заголовок = НСтр("ru = 'В т.ч. перепроведен с ошибками 1 документ'")
	ИначеЕсли КоличествоНеПроведенныхДокументов > 1 Тогда
		Элементы.ПерейтиВЖурналРегистрацииСобытийОшибки.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В т.ч. перепроведено с ошибками %1'"),
			ЧислоДокументовПрописью(ТаблицаНеПроведенныхДокументов.Количество()));
	КонецЕсли;
	
	Элементы.КартинкаЕстьПредупреждения.Видимость = ЕстьИзмененныеДокументы;
	Элементы.ПерейтиВЖурналРегистрацииСобытийОшибки.Видимость = (КоличествоНеПроведенныхДокументов > 0);
	
	Элементы.ПечатьСпискаИзмененныхДокументов.Доступность = ЕстьИзмененныеДокументы;
	Элементы.СформироватьИсправительныеСчетаФактуры.Доступность = ЕстьИзмененныеДокументы;
	Элементы.ПечатьИзмененныхСчетовФактур.Доступность = ЕстьИзмененныеДокументы;
	Элементы.СформироватьЗаданияДляМенеджеров.Доступность = ЕстьИзмененныеДокументы;
	
	ТекстСообщения = НСтр("ru = 'Выполнено перепроведение документов, приводящих к отрицательным остаткам'");
	ТекстЗаголовка = НСтр("ru = 'Выполнено перепроведение документов'");
	ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Информация32);
	
	ПолучитьСостояниеОстатковТоваровОрганизаций();
	
	// Восстанавливаем активность движений по регистрам.
	Если СпособПерезаполненияВидовЗапасовВДокументах = 1 Тогда
		УстановитьАктивностьДвиженийПоРегистрам(
			МассивДокументов,
			Истина); // Активность
	КонецЕсли;
		
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРезультатПерепроведения;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура УстановитьПараметрыДинамическихСписков()
	
	ТоварыКПередачеМеждуОрганизациями.Параметры.УстановитьЗначениеПараметра("ДатаНачала", Период.ДатаНачала);
	ТоварыКПередачеМеждуОрганизациями.Параметры.УстановитьЗначениеПараметра("ДатаОкончания", Период.ДатаОкончания);
	ТоварыКПередачеМеждуОрганизациями.Параметры.УстановитьЗначениеПараметра("МассивОрганизаций", МассивДоступныхОрганизаций());
	
КонецПроцедуры

&НаСервере
Функция МассивДоступныхОрганизаций()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеСправочника.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК ДанныеСправочника
	|ГДЕ
	|	ДанныеСправочника.Ссылка = &Организация
	|	ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|");
	Запрос.УстановитьПараметр("Организация", Организация);
	МассивОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
	
	Возврат МассивОрганизаций;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьСостояниеОстатковТоваровОрганизаций()
	
	Если ЗначениеЗаполнено(Период.ДатаНачала)
	 И Период.ДатаНачала > Период.ДатаОкончания Тогда
	 
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Дата начала периода не должна быть больше окончания периода %1'"),
			Формат(Период.ДатаОкончания, "ДЛФ=DD"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			,
			"Период",
			, // ПутьКДанным
			);
		Возврат;
	КонецЕсли;
	
	Результат = СостояниеОстатковТоваровОрганизацийНаСервере();
	Если НЕ Результат.ЗаданиеВыполнено Тогда
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища       = Результат.АдресХранилища;
		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала = 1.2; // Уменьшим шаг увеличения времени опроса выполнения задания
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	Иначе
		ПолучитьСостояниеОстатковНаСервере();
    КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

// Унифицированная процедура проверки выполнения фонового задания.
//
&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
 
	Попытка
		Если ФормаДлительнойОперации.Открыта() 
			И ФормаДлительнойОперации.ИдентификаторЗадания = ИдентификаторЗадания Тогда
			Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда 
				ПолучитьСостояниеОстатковНаСервере();
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
			КонецЕсли;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция СостояниеОстатковТоваровОрганизацийНаСервере()
	
	МассивОрганизаций = МассивДоступныхОрганизаций();
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("МассивОрганизаций", МассивОрганизаций);
	ПараметрыЗадания.Вставить("ДатаНачала", Период.ДатаНачала);
	ПараметрыЗадания.Вставить("ДатаОкончания", Период.ДатаОкончания);
	
	НаименованиеЗадания = НСтр("ru = 'Определение состояния остатков товаров организаций'");
	Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
		УникальныйИдентификатор,
		"Обработки.ПомощникИсправленияОстатковТоваровОрганизаций.ПолучитьСостояниеОстатковТоваровОрганизаций",
		ПараметрыЗадания,
		НаименованиеЗадания);
	АдресХранилища = Результат.АдресХранилища;
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПолучитьСостояниеОстатковНаСервере()
	
	МассивРезультатов = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	ИндексПоследнегоРезультата = МассивРезультатов.ВГраница();
	РезультатЗапросаОтрицательныеОстатки 	= МассивРезультатов[ИндексПоследнегоРезультата - 3];
	РезультатЗапросаРазвернутоеСальдо 		= МассивРезультатов[ИндексПоследнегоРезультата - 2];
	РезультатЗапросаНедостаточноТоваров 	= МассивРезультатов[ИндексПоследнегоРезультата - 1];
	РезультатЗапросаПередачиТоваров 		= МассивРезультатов[ИндексПоследнегоРезультата];
	
	УстановитьСостояниеНаличиеОтрицательныхОстатков(РезультатЗапросаОтрицательныеОстатки);
	УстановитьСостояниеНаличиеРазвернутогоСальдо(РезультатЗапросаРазвернутоеСальдо);
	УстановитьСостояниеНедостаточноТоваровОрганизаций(РезультатЗапросаНедостаточноТоваров);
	УстановитьСостояниеПередачаТоваровМеждуОрганизациями(РезультатЗапросаПередачиТоваров);
		
	Элементы.Далее.Доступность = ЕстьОтрицательныеОстатки ИЛИ ЕстьРазвернутоеСальдо;
	
	УстановитьПараметрыДинамическихСписков();
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти
