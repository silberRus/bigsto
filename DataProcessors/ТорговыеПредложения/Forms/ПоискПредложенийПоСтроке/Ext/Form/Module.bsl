
#Область ОписаниеПеременных

&НаКлиенте
Перем ВыполняетсяЗакрытие; // Признак выполнения обработки закрытия формы.

&НаКлиенте
Перем ПараметрыОбработчиков; // Структура параметров обработчиков ожидания.

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПроверитьРегистрациюОрганизаций();
	
	Параметры.Свойство("ИдентификаторКатегории",         ТекущаяКатегория);
	Параметры.Свойство("СтрокаПоиска",                   СтрокаПоиска);
	Параметры.Свойство("УникальныйИдентификаторКорзины", УникальныйИдентификаторКорзины);
	Параметры.Свойство("ОтборАртикул",                   ОтборАртикул);
	Параметры.Свойство("ОтборНаименование",              ОтборНаименование);
	Параметры.Свойство("Поставщик",                      Поставщик);
	Параметры.Свойство("ПоставщикНаименование",          ПоставщикНаименование);
	Параметры.Свойство("ПоставщикИдентификатор",         ПоставщикИдентификатор);
	Параметры.Свойство("ПоставщикИНН",                   ПоставщикИНН);
	Параметры.Свойство("ПоставщикКПП",                   ПоставщикКПП);
	Параметры.Свойство("Валюта",                         Валюта);

	Если Параметры.Свойство("ИНН") Тогда
		ПоставщикИдентификатор = Параметры.ИНН
			+ ?(Параметры.Свойство("КПП") И Параметры.КПП <> "0" И Не ПустаяСтрока(Параметры.КПП),
				"/" + Параметры.КПП, "");
	КонецЕсли;
	Если ПустаяСтрока(ПоставщикНаименование) И ЗначениеЗаполнено(Поставщик) Тогда
		ПоставщикНаименование = Строка(Поставщик);
	КонецЕсли;
	
	Если Параметры.Свойство("ОтборШтрихКоды") И ЗначениеЗаполнено(Параметры.ОтборШтрихКоды) Тогда
		ОтборШтрихКоды.ЗагрузитьЗначения(Параметры.ОтборШтрихКоды);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборНаименование) Тогда
		СтрокаПоиска = ОтборНаименование;
	ИначеЕсли ЗначениеЗаполнено(ОтборАртикул) Тогда
		СтрокаПоиска = ОтборАртикул;
	ИначеЕсли ЗначениеЗаполнено(ОтборШтрихКоды) Тогда
		СтрокаПоиска = ОтборШтрихКоды;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(УникальныйИдентификаторКорзины) Тогда
		УникальныйИдентификаторКорзины = УникальныйИдентификатор;
	Иначе
		АдресТоваровВХранилище = "";
		Если Параметры.Свойство("АдресТоваровВХранилище", АдресТоваровВХранилище) Тогда
			ТоварыОбъект = ПолучитьИзВременногоХранилища(АдресТоваровВХранилище);
			Товары.Загрузить(ТоварыОбъект);
		КонецЕсли;
	КонецЕсли;
	
	КлючНастроекФормы = "Обработка.ТорговыеПредложения.Форма.ПоискПредложенийПоСтроке/ТекущиеДанные";
	Настройки = ХранилищеСистемныхНастроек.Загрузить(КлючНастроекФормы);
	СохраненнаяВалюта = Неопределено;
	Если ТипЗнч(Настройки) = Тип("Соответствие") Тогда
		СохраненнаяВалюта = Настройки.Получить("Валюта");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		Валюта = СохраненнаяВалюта;
	ИначеЕсли Валюта <> СохраненнаяВалюта И ТипЗнч(Настройки) = Тип("Соответствие") Тогда
		Настройки.Вставить("Валюта", Валюта);
		ХранилищеСистемныхНастроек.Сохранить(КлючНастроекФормы,, Настройки);
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		Валюта = ТорговыеПредложенияПереопределяемый.ВалютаРегламентированногоУчета();
	КонецЕсли;
	
	Элементы.ИзменитьВалютуПоиска.Заголовок = СтрШаблон(НСтр("ru = 'Валюта: %1'"), Валюта);
	
	ЗаполнитьНавигациюКатегорий(ТекущаяКатегория);
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьНадписьИтогов();

	Если ПустаяСтрока(РежимСортировки) Тогда
		РежимСортировки = "СортировкаЦенаВозрастание";
	КонецЕсли;
	УстановитьРежимСортировки(Элементы, РежимСортировки);
	УстановитСнятьФлагКомандыЗапрашиватьКоличество();
	
	ОбновитьЗаголовокРегиона(ЭтотОбъект);
	
	// Проверка открытия из другой формы.
	РазрешитьИзменениеПоставщика = ЭтотОбъект.ВладелецФормы = Неопределено;
	Элементы.ПоставщикНаименование.Видимость = РазрешитьИзменениеПоставщика;
	Элементы.ОтборПоПоставщику.Видимость = РазрешитьИзменениеПоставщика;
	
	ОтборыУстановлены = ЦенаОт <> 0 ИЛИ ЦенаДо <> 0
		ИЛИ РазрешитьИзменениеПоставщика И (ЗначениеЗаполнено(Поставщик)
			ИЛИ ЗначениеЗаполнено(ПоставщикНаименование));
	ОбновитьПризнакУстановкиОтборов();
	
	ЗагрузитьТорговыеПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ТорговыеПредложения_ИзменитьКоличествоВКорзине"
		И Параметр.УникальныйИдентификаторКорзины = УникальныйИдентификаторКорзины
		И Источник <> ЭтотОбъект Тогда
		
		Количество = ?(Параметр.Свойство("Количество"), Параметр.Количество, Неопределено);
		
		ДобавитьТоварыПоИдентификатору(Параметр.ПредложениеИдентификатор, Количество);
		СтрокаТовары = Товары.НайтиСтроки(Новый Структура("ПредложениеИдентификатор", Параметр.ПредложениеИдентификатор));
		
		СтрокаСписка = Неопределено;
		Если Элементы.Список.ТекущиеДанные <> Неопределено
			И Элементы.Список.ТекущиеДанные.ПредложениеИдентификатор = Параметр.ПредложениеИдентификатор Тогда
			СтрокаСписка = Элементы.Список.ТекущиеДанные;
		Иначе
			СтрокаСписка = Список.НайтиСтроки(Новый Структура("ПредложениеИдентификатор", Параметр.ПредложениеИдентификатор));
			Если СтрокаСписка.Количество() Тогда
				СтрокаСписка = СтрокаСписка[0];
			Иначе
				СтрокаСписка = Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаСписка <> Неопределено Тогда
			Если Количество = 0 Тогда 
				СтрокаСписка.ГиперссылкаДобавить = НСтр("ru = 'Добавить'");
			ИначеЕсли СтрокаТовары.Количество() Тогда
				СтрокаСписка.ГиперссылкаДобавить = СтрокаТовары[0].Количество;
			КонецЕсли;
		КонецЕсли;
		
		ОбновитьНадписьИтогов();
		
	ИначеЕсли ИмяСобытия = "ТорговыеПредложения_УдалитьИзКорзины"
		И Параметр.УникальныйИдентификаторКорзины = УникальныйИдентификаторКорзины Тогда
		
		Для Каждого ПредложениеИдентификатор Из Параметр.ИдентификаторыПредложений Цикл
			
			СтрокаТовары = Товары.НайтиСтроки(Новый Структура("ПредложениеИдентификатор", ПредложениеИдентификатор));
			Если СтрокаТовары.Количество() Тогда
				Товары.Удалить(Товары.Индекс(СтрокаТовары[0]));
			КонецЕсли;
			
			СтрокаСписка = Список.НайтиСтроки(Новый Структура("ПредложениеИдентификатор", ПредложениеИдентификатор));
			Если СтрокаСписка.Количество() Тогда 
				СтрокаСписка[0].ГиперссылкаДобавить = НСтр("ru = 'Добавить'");
			КонецЕсли;
			
		КонецЦикла;
		ОбновитьНадписьИтогов();
		
	ИначеЕсли ИмяСобытия = "ТорговыеПредложения_Обновить" Тогда
		
		ПараметрыИзменены = Ложь;
		Если Параметр.Свойство("ИдентификаторКатегории") И ТекущаяКатегория <> Параметр.ИдентификаторКатегории Тогда
			ПараметрыИзменены = Истина;
			ТекущаяКатегория = Параметр.ИдентификаторКатегории;
		КонецЕсли;
			
		Если Параметр.Свойство("ОтборНаименование") Тогда
			ПараметрыИзменены = Истина;
			ОтборНаименование = Параметр.ОтборНаименование;
			СтрокаПоиска = ОтборНаименование;
		КонецЕсли;
		Если Параметр.Свойство("ОтборАртикул") Тогда
			ПараметрыИзменены = Истина;
			ОтборАртикул = Параметр.ОтборАртикул;
			Если ПустаяСтрока(СтрокаПоиска) Тогда
				СтрокаПоиска = ОтборАртикул;
			КонецЕсли;
		КонецЕсли;
		Если Параметр.Свойство("ОтборШтрихКоды") И ТипЗнч(Параметр.ОтборШтрихКоды) = Тип("Массив")
			И Параметр.ОтборШтрихКоды.Количество() Тогда
			ПараметрыИзменены = Истина;
			ОтборШтрихКоды.ЗагрузитьЗначения(Параметр.ОтборШтрихКоды);
			Если ПустаяСтрока(СтрокаПоиска) Тогда
				СтрокаПоиска = Параметр.ОтборШтрихКоды[0];
			КонецЕсли;
		КонецЕсли;
		Если Параметр.Свойство("ОтборПоставщик") Тогда
			ПараметрыИзменены = Истина;
			ОтборПоставщик = Параметр.ОтборПоставщик;
			ПоставщикИдентификатор = ОтборПоставщик.ИНН
			+ ?(ОтборПоставщик.Свойство("КПП") И ОтборПоставщик.КПП <> "0" И Не ПустаяСтрока(ОтборПоставщик.КПП),
				"/" + ОтборПоставщик.КПП, "");
			ОтборПоставщик.Свойство("ПоставщикНаименование", ПоставщикНаименование);
			ОтборПоставщик.Свойство("Поставщик",             Поставщик);
			Если ПустаяСтрока(ПоставщикНаименование) И ЗначениеЗаполнено(Поставщик) Тогда
				ПоставщикНаименование = Строка(Поставщик);
			КонецЕсли;
		КонецЕсли;
		
		Если ПараметрыИзменены Тогда
			ПараметрыОбработчиков = Новый Структура("ТекущаяКатегория, ОчиститьСтруктуру", ТекущаяКатегория, Истина);
			ЗаполнитьНавигациюКатегорийИЗагрузитьТорговыеПредложения();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ТорговыеПредложения_СформированыЗаказы" И Параметр.Свойство("УникальныйИдентификаторКорзины")
		И Параметр.УникальныйИдентификаторКорзины = УникальныйИдентификаторКорзины Тогда
		
		// Очистка товаров.
		Для Каждого ПозицияТовары Из Товары Цикл
			СтрокаСписка = Список.НайтиСтроки(Новый Структура("ПредложениеИдентификатор", ПозицияТовары.ПредложениеИдентификатор));
			Если СтрокаСписка.Количество() Тогда
				СтрокаСписка[0].ГиперссылкаДобавить = НСтр("ru = 'Добавить'");
			КонецЕсли;
		КонецЦикла;
		Товары.Очистить();
		ОбновитьНадписьИтогов();
		
	ИначеЕсли ИмяСобытия = "БизнесСеть_РегистрацияОрганизаций" Тогда
		
		ПроверитьРегистрациюОрганизаций();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы И Товары.Количество() И Не ВыполняетсяЗакрытие Тогда
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект),
			НСтр("ru = 'Список подобранных товаров будет очищен. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтрокаПоискаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если СтрДлина(Текст) >= 3 Тогда
		
		Элемент.СписокВыбора.Очистить();
		ДанныеВыбора = КатегорииПоНаименованию(Текст);
		Если ДанныеВыбора <> Неопределено Тогда
			Для Каждого ЭлементСписка Из ДанныеВыбора Цикл
				Элемент.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
			КонецЦикла;
			ДанныеВыбора.Вставить(0, Текст, Текст + "...");
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	ОтборНаименование = "";
	ОтборАртикул = "";
	ОтборШтрихКоды.Очистить();
	
	// Если не из выбора, значит введено вручную.
	ЭлементВыбора = Элемент.СписокВыбора.НайтиПоЗначению(СтрокаПоиска);
	
	Если ЭлементВыбора = Неопределено Тогда
		ЗаполнитьТорговыеПредложения();
	Иначе
		// Изменение текущей категории.
		ТекущаяКатегория = СтрокаПоиска;
		СтрокаПоиска = "";
		ОтборыПоКатегорииАктуальны = Ложь;
		ПараметрыОбработчиков = Новый Структура("ТекущаяКатегория, ОчиститьСтруктуру", ТекущаяКатегория, Истина);
		ЗаполнитьНавигациюКатегорийИЗагрузитьТорговыеПредложения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаОчистка(Элемент, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура КатегорияПослеВыбора(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Результат.Свойство("АдресКэшаКатегорийРубрик", АдресКэшаКатегорийРубрик);
	ТекущаяКатегория = Результат.Идентификатор;
	
	СтрокаПоиска = "";
	ОтборыПоКатегорииАктуальны = Ложь;
	ПараметрыОбработчиков = Новый Структура("ТекущаяКатегория, ОчиститьСтруктуру", ТекущаяКатегория, Истина);
	ЗаполнитьНавигациюКатегорийИЗагрузитьТорговыеПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктураКатегорийОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Ключ = НавигационнаяСсылкаФорматированнойСтроки;
	ИндексРазделителя = СтрНайти(Ключ, ":");
	
	НовыйИдентификатор = Сред(Ключ, 3, ИндексРазделителя - 3); 
	
	Если ТекущаяКатегория = НовыйИдентификатор Тогда
		Возврат;
	Иначе
		ПараметрыОбработчиков = Новый Структура("ТекущаяКатегория, ОчиститьСтруктуру", НовыйИдентификатор, Ложь);
		ЗапроситьЗаполнениеНавигацииКатегорий();
	КонецЕсли;
	
	ОтборыПоКатегорииАктуальны = Ложь;
	ТекущаяКатегория = НовыйИдентификатор;
	ЗаполнитьТорговыеПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТребуетсяОбновлениеКэшаКатегории = Ложь;
	Если ПустаяСтрока(АдресКэшаКатегорийРубрик) Тогда
		АдресКэшаКатегорийРубрик = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		ТребуетсяОбновлениеКэшаКатегории = Истина;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("КатегорияПослеВыбора", ЭтотОбъект);
	ПараметрыФормыВыбора = Новый Структура;
	ПараметрыФормыВыбора.Вставить("РежимВыбора", Истина);
	ПараметрыФормыВыбора.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормыВыбора.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ПараметрыФормыВыбора.Вставить("Отбор", Новый Структура("ЭтоГруппа", Истина));
	ПараметрыФормыВыбора.Вставить("АдресКэшаКатегорийРубрик", АдресКэшаКатегорийРубрик);
	ПараметрыФормыВыбора.Вставить("ТребуетсяОбновлениеКэшаКатегории", ТребуетсяОбновлениеКэшаКатегории);
	ПараметрыФормыВыбора.Вставить("Идентификатор", ТекущаяКатегория);
	ОткрытьФорму("Обработка.ТорговыеПредложения.Форма.ДеревоКатегорий", ПараметрыФормыВыбора,,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКореньКатегорийНажатие(Элемент)
	
	ТекущаяКатегория = Неопределено;
	ОтборыПоКатегорииАктуальны = Ложь;
	ПараметрыОбработчиков = Новый Структура("ТекущаяКатегория, ОчиститьСтруктуру", Неопределено, Ложь);
	ЗаполнитьНавигациюКатегорийИЗагрузитьТорговыеПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктураПодкатегорийОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Ключ = НавигационнаяСсылкаФорматированнойСтроки;
	ИндексРазделителя = СтрНайти(Ключ, ":");
	
	НовыйИдентификатор = Сред(Ключ, 3, ИндексРазделителя - 3); 
	
	Если ТекущаяКатегория = НовыйИдентификатор Тогда
		Возврат;
	Иначе
		ПараметрыОбработчиков = Новый Структура("ТекущаяКатегория, ОчиститьСтруктуру", НовыйИдентификатор, Ложь);
		ОтборыПоКатегорииАктуальны = Ложь;
		ТекущаяКатегория = НовыйИдентификатор;
		ЗаполнитьНавигациюКатегорийИЗагрузитьТорговыеПредложения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПодобраноТоваровНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОформитьЗаказыПокупателя(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НажатиеГиперссылки(Элемент)
	
	Если Не СтрНайти(Элемент.Имя, "ЗаголовокОтбора_") Тогда
		Возврат;
	КонецЕсли;
	
	СтрелкаВверх = "↑";
	СтрелкаВниз = "↓";
	
	ИмяГруппыОтбора = СтрЗаменить(Элемент.Имя, "ЗаголовокОтбора_", "ГруппаОтбора_");
	ГруппаОтбора = Элементы[ИмяГруппыОтбора];
	Если ГруппаОтбора.Видимость Тогда
		ГруппаОтбора.Видимость = Ложь;
		Элемент.Заголовок = СтрЗаменить(Элемент.Заголовок, СтрелкаВверх, СтрелкаВниз);
	ИначеЕсли ГруппаОтбора.ПодчиненныеЭлементы.Количество() Тогда
		ГруппаОтбора.Видимость = Истина;
		Элемент.Заголовок = СтрЗаменить(Элемент.Заголовок, СтрелкаВниз, СтрелкаВверх);
	Иначе
		Идентификатор = СтрЗаменить(Элемент.Имя, "ЗаголовокОтбора_", "");
		СформироватьЭлементыХарактеристики(Идентификатор, Элемент.Подсказка);
		ГруппаОтбора.Видимость = Истина;
		Элемент.Заголовок = СтрЗаменить(Элемент.Заголовок, СтрелкаВниз, СтрелкаВверх);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ИзменениеОтбора(Элемент)
	
	ЗаполнитьТорговыеПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Нажатие(Элемент)
	
	Если Найти(Элемент.Имя, "ОчиститьОтбор_") Тогда
		
		// Очистка отбора.
		ИмяРеквизита = Сред(Элемент.Имя, СтрНайти(Элемент.Имя, "_") + 1);
		
		Если ИмяРеквизита = "Цена" Тогда
			ЦенаОт = 0; ЦенаДо = 0;
		ИначеЕсли ИмяРеквизита = "ПоставщикНаименование" Тогда
			ПоставщикНаименование = "";
			Поставщик = Неопределено;
			ПоставщикИдентификатор = Неопределено;
		Иначе
			ЗначениеРеквизита = ЭтотОбъект[ИмяРеквизита];
			Если ТипЗнч(ЗначениеРеквизита) = Тип("Булево") Тогда
				ЭтотОбъект[ИмяРеквизита] = Ложь;
			ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("Число") Тогда
				ЭтотОбъект[ИмяРеквизита] = 0;
			ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("Строка") Тогда
				ЭтотОбъект[ИмяРеквизита] = "";
			ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("СписокЗначений") Тогда 
				Для Каждого ЭлементСписка Из ЭтотОбъект[ИмяРеквизита] Цикл
					ЭлементСписка.Пометка = Ложь;
				КонецЦикла;
			Иначе
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		ЗаполнитьТорговыеПредложения();
		
	ИначеЕсли Найти(Элемент.Имя, "ЗаголовокОтбора_") Тогда
		
		ИмяРеквизита = Сред(Элемент.Имя, СтрНайти(Элемент.Имя, "_") + 1);
		Если ИмяРеквизита = "Цена" Тогда
			Если Найти(Элемент.Заголовок, НСтр("ru = 'от'")) Тогда
				ИмяРеквизита = "ЦенаОт";
			Иначе
				ИмяРеквизита = "ЦенаДо";
			КонецЕсли;
		КонецЕсли;
		
		Если Не Элементы.ГруппаПанельОтборов.Видимость Тогда
			Элементы.КомандаПоказыватьОтборы.Пометка = Истина;
			Элементы.ГруппаПанельОтборов.Видимость = Истина;
		КонецЕсли;
		
		ТекущийЭлемент = Элементы[ИмяРеквизита];
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоставщикНаименованиеПриИзменении(Элемент)
	
	Поставщик = Неопределено;
	ПоставщикИдентификатор = Неопределено;
	ЗаполнитьТорговыеПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоставщикНаименованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыОткрытия = Новый Структура("ТекущаяСтрока", Поставщик);
	ОбработкаОповещения = Новый ОписаниеОповещения("ВыборКонтрагентаПродолжение", ЭтотОбъект);
	ИмяФормыВыбора = ИмяФормыВыбораКонтрагентов();
	ОткрытьФорму(ИмяФормыВыбора, ПараметрыОткрытия, ЭтотОбъект,,,, ОбработкаОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура РегионЗаголовокНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РегионРежимОтбора",   РегионРежимОтбора);
	ПараметрыОткрытия.Вставить("РегионПредставление", РегионПредставление);
	ПараметрыОткрытия.Вставить("РегионЗначенияПолей", РегионЗначенияПолей);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборРегионаЗавершение", ЭтотОбъект);
	ОткрытьФорму("Обработка.ТорговыеПредложения.Форма.ВыборРегионаПоиска", ПараметрыОткрытия, ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ТребуетсяРегистрацияОрганизации Тогда
		Оповещение = Новый ОписаниеОповещения("ВопросРегистрацииОрганизацииПродолжение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение,
			НСтр("ru = 'Для просмотра информации необходимо зарегистрироваться в сервисе 1С:Бизнес-сеть. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;
	
	ОткрытьКарточкуТорговогоПредложения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НайтиПредложения(Команда)
	
	ЗаполнитьТорговыеПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОчиститьТовары(Команда)
	
	Если Товары.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Отсутствуют товары для заказа'"));
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОчиститьТоварыПродолжение", ЭтотОбъект);
	
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Список товаров для заказа будет очищен. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьЗаказыПокупателя(Команда)
	
	Если Товары.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Отсутствуют товары для заказа.'"));
		Возврат;
	КонецЕсли;
	
	ОбработкаЗавершения = Новый ОписаниеОповещения("ОтправкаПредложенийПродолжение", ЭтотОбъект);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("УникальныйИдентификаторКорзины", УникальныйИдентификаторКорзины);
	ПараметрыОткрытия.Вставить("АдресТоваровВХранилище", АдресХранилищаТоваров());
	ПараметрыОткрытия.Вставить("Валюта", Валюта);
	ОткрытьФорму("Обработка.ТорговыеПредложения.Форма.ФормированиеЗаказов", ПараметрыОткрытия,,,,, ОбработкаЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРежимСортировки(Команда)
	
	УстановитьРежимСортировки(Элементы, Команда.Имя);	
	ЗаполнитьТорговыеПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИсходящиеДокументы(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимИсходящихДокументов", Истина);
	ОткрытьФорму("Обработка.БизнесСеть.Форма.ДокументыОбмена", ПараметрыОткрытия);

КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьОтборы(Команда)
	
	Элементы.КомандаПоказыватьОтборы.Пометка = Не Элементы.КомандаПоказыватьОтборы.Пометка;
	Элементы.ГруппаПанельОтборов.Видимость = Элементы.КомандаПоказыватьОтборы.Пометка;
	
	Если Элементы.КомандаПоказыватьОтборы.Пометка Тогда
		Если ЗначениеЗаполнено(ТекущаяКатегория) И ОтборыПоКатегорииАктуальны = Ложь Тогда
			СформироватьОтборыПоКатегории(ТекущаяКатегория);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВалютуПоиска(Команда)
	
	Если Товары.Количество() Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ИзменитьВалютуПоискаПродолжение", ЭтотОбъект),
			НСтр("ru = 'При изменении валюты, список товаров для заказа будет очищен.'"),
			РежимДиалогаВопрос.ОКОтмена);
	Иначе
		ИзменитьВалютуПоискаПродолжение(КодВозвратаДиалога.ОК, Неопределено)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапрашиватьКоличество(Команда)
	
	ЗапрашиватьКоличество = Не ЗапрашиватьКоличество;
	УстановитСнятьФлагКомандыЗапрашиватьКоличество();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоПоставщику(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоставщикНаименование = ТекущиеДанные.ПоставщикНаименование;
	ПоставщикИдентификатор = ТекущиеДанные.ПоставщикИдентификатор;
	
	ЗаполнитьТорговыеПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтборы(Команда)
	
	ОчиститьОтборИОбновитьСписок();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВводКоличестваЗавершение(Число, ДополнительныеПараметры) Экспорт
	
	Если Число = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если Число <> 0 Тогда
		ДополнительныеПараметры.ПараметрыОповещения.Вставить("Количество", Число);
		Оповестить("ТорговыеПредложения_ИзменитьКоличествоВКорзине", ДополнительныеПараметры.ПараметрыОповещения);
	Иначе
		ДополнительныеПараметры.ПараметрыОповещения.Вставить("ИдентификаторыПредложений", 
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДополнительныеПараметры.ПараметрыОповещения.ПредложениеИдентификатор));
		Оповестить("ТорговыеПредложения_УдалитьИзКорзины", ДополнительныеПараметры.ПараметрыОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьТоварыПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОчиститьТовары();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПризнакУстановкиОтборов(СтрокаОтборов = "")
	
	Элементы.КомандаПоказыватьОтборы.Заголовок = НСтр("ru = 'Отборы'") + ?(ОтборыУстановлены, " *", "");
	Элементы.ГруппаБыстрыхОтборов.Видимость = ОтборыУстановлены;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНадписьИтогов()
	
	КоличествоТоваров     = Товары.Количество();
	ЭтотОбъект.СуммаВсего = Товары.Итог("СуммаСНДС");
	
	Если Товары.Количество() = 0 Тогда
		НадписьПодобраноТоваров = НСтр("ru = 'Отсутствуют товары для заказа'");
	Иначе
		НадписьПодобраноТоваров = НСтр("ru = 'Товаров %1 поз. на сумму %2 %3'");
		НадписьПодобраноТоваров = СтрШаблон(НадписьПодобраноТоваров, КоличествоТоваров, ЭтотОбъект.СуммаВсего, Валюта);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправкаПредложенийПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ОчиститьТовары();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьЗаполнениеНавигацииКатегорий()
	ОбработчикЗаполнитьНавигациюКатегорий();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТорговыеПредложения()
	
	ОчиститьСообщения();
	ОбработчикЗагрузитьТорговыеПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикЗагрузитьТорговыеПредложения()
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина,
		"Обработка.ТорговыеПредложения.Форма.ПоискПредложенийПоСтроке.НайтиПредложения");
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	// Получение торговых предложений в сервисе.
	ЗагрузитьТорговыеПредложения();
	
	// Позиционирование на текущей строке.
	Если ТекущиеДанные <> Неопределено Тогда
		СтрокиПоиска = Список.НайтиСтроки(Новый Структура("ПредложениеИдентификатор", ТекущиеДанные.ПредложениеИдентификатор));
		Если СтрокиПоиска.Количество() Тогда
			Элементы.Список.ТекущаяСтрока = СтрокиПоиска[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТорговыеПредложения()
	
	ИнтернетПоддержкаПодключена = Ложь;
	ДлительнаяОперация = ЗагрузитьТорговыеПредложенияВФоне(ИнтернетПоддержкаПодключена);
	Если ИнтернетПоддержкаПодключена = Ложь Тогда
		Оповещение = Новый ОписаниеОповещения("ЗагрузитьТорговыеПредложенияПродолжение", ЭтотОбъект);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(Оповещение, ЭтотОбъект);
		Возврат;
	Иначе
		ЗагрузитьТорговыеПредложенияПродолжение(Истина, ДлительнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТорговыеПредложенияПродолжение(Результат, ДлительнаяОперация) Экспорт
	
	Если Результат = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствует подключение к Интернет-поддержке пользователей.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	ИначеЕсли ТипЗнч(Результат) = Тип("Структура")
		И Результат.Свойство("Логин") Тогда
		// Повторный вызов метода после подключения к Интернет-поддержке.
		ИнтернетПоддержкаПодключена = Ложь;
		ДлительнаяОперация = ЗагрузитьТорговыеПредложенияВФоне(ИнтернетПоддержкаПодключена);
	КонецЕсли;
	
	ИдентификаторЗадания = ДлительнаяОперация.ИдентификаторЗадания;
	Если Не ДлительнаяОперация = Неопределено
		И ДлительнаяОперация.Статус = "Выполняется" Тогда
		Элементы.НайтиПредложения.Картинка = БиблиотекаКартинок.СинхронизацияДанныхДлительнаяОперация;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Поиск торговых предложений.'");
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Ложь;
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Ложь;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.Вставить("ИдентификаторЗадания", ИдентификаторЗадания);
	ДлительнаяОперацияЗавершение = Новый ОписаниеОповещения(
		"ЗагрузитьТорговыеПредложенияВФонеЗавершение", ЭтотОбъект, ПараметрыОжидания);
		
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация,
		ДлительнаяОперацияЗавершение,
		ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьТорговыеПредложенияВФоне(ИнтернетПоддержкаПодключена)
	
	// Проверка подключения Интернет-поддержки пользователей.
	ИнтернетПоддержкаПодключена = ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	Если Не ИнтернетПоддержкаПодключена Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыПоиска = СформироватьПараметрыПоиска();
	
	ОтборыУстановлены = (ПараметрыПоиска.Свойство("Атрибуты") И ПараметрыПоиска.Атрибуты.Количество())
		ИЛИ (РазрешитьИзменениеПоставщика И (ЗначениеЗаполнено(Поставщик) ИЛИ ЗначениеЗаполнено(ПоставщикНаименование)))
		ИЛИ ЦенаОт <> 0 ИЛИ ЦенаДо <> 0
		ИЛИ ВозможенСамовывоз ИЛИ ВозможнаДоставка;
		
	ОбновитьПризнакУстановкиОтборов(ПараметрыПоиска);
	Элементы.СписокНаименованиеКонтрагента.Видимость =
		Не ЗначениеЗаполнено(Поставщик) И Не ЗначениеЗаполнено(ПоставщикИдентификатор);
		
	Если ИдентификаторЗадания <> Неопределено Тогда
		ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	КонецЕсли;
	
	Задание = Новый Структура("ИмяПроцедуры, Наименование, ПараметрыПроцедуры");
	Задание.Наименование = НСтр("ru = '1С:Бизнес-сеть. Поиск торговых предложений по строке.'");
	Задание.ИмяПроцедуры = "ТорговыеПредложения.НайтиТорговыеПредложенияПоОтбору";
	Задание.ПараметрыПроцедуры = ПараметрыПоиска;	
	
	ДозаполнитьПараметрыПроцедуры(Задание.ПараметрыПроцедуры);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = Задание.Наименование;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(Задание.ИмяПроцедуры, 
		Задание.ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьТорговыеПредложенияВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Отказ = Ложь;
	ТекстСообщения = "";
	
	Если Результат = Неопределено Тогда // Отменено пользователем.
		Отказ = Истина;
		ТекстСообщения = НСтр("ru = 'Фоновое задание отменено пользователем'");
		Возврат;
	КонецЕсли;
	
	Если Не Отказ И Результат.Статус = "Выполнено" Тогда
		
		Если ЗначениеЗаполнено(Результат.АдресРезультата)
			И ЭтоАдресВременногоХранилища(Результат.АдресРезультата)
			И ИдентификаторЗадания = ДополнительныеПараметры.ИдентификаторЗадания Тогда
			
			ЗагрузитьРезультатПоиска(Результат.АдресРезультата);
			
		КонецЕсли;
		ИдентификаторЗадания = Неопределено;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		Отказ = Истина;
		ТекстСообщения = Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	Если Отказ Тогда
		Если Не ПустаяСтрока(ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 %2'"),
					ОбщегоНазначенияКлиент.ДатаСеанса(), ТекстСообщения));
		КонецЕсли;
	КонецЕсли;
	
	КоличествоСтрок = Список.Количество();
	Если КоличествоСтрок = 0 Тогда
		ЗаголовокДекорации = НСтр("ru = 'Торговые предложения не найдены'");
	ИначеЕсли КоличествоСтрок >= 1000 Тогда
		ЗаголовокДекорации = НСтр("ru = 'Отображаются первые 1000 позиций'");
	Иначе
		ЗаголовокДекорации = СтрШаблон(НСтр("ru = 'Найдено %1 позиций'"), КоличествоСтрок);
	КонецЕсли;
	Элементы.ДекорацияОграничения.Заголовок = ЗаголовокДекорации;
	
КонецПроцедуры
	
&НаКлиенте
Процедура ЗаполнитьНавигациюКатегорийИЗагрузитьТорговыеПредложения()
	
	ОбработчикЗаполнитьНавигациюКатегорийИЗагрузитьТорговыеПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикЗаполнитьНавигациюКатегорий()
	
	ЗаполнитьНавигациюКатегорий(
		ПараметрыОбработчиков.ТекущаяКатегория,
		ПараметрыОбработчиков.ОчиститьСтруктуру);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикЗаполнитьНавигациюКатегорийИЗагрузитьТорговыеПредложения()
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина,
		"Обработка.ТорговыеПредложения.Форма.ПоискПредложенийПоСтроке.ЗаполнитьНавигацию");
		
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		
	ЗагрузитьТорговыеПредложения();
	ЗаполнитьНавигациюКатегорий(
		ПараметрыОбработчиков.ТекущаяКатегория,
		ПараметрыОбработчиков.ОчиститьСтруктуру);
	
	// Позиционирование на текущей строке.
	Если ТекущиеДанные <> Неопределено Тогда
		СтрокиПоиска = Список.НайтиСтроки(Новый Структура("ПредложениеИдентификатор", ТекущиеДанные.ПредложениеИдентификатор));
		Если СтрокиПоиска.Количество() Тогда
			Элементы.Список.ТекущаяСтрока = СтрокиПоиска[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНавигациюКатегорий(Идентификатор, ОчиститьСтруктуру = Ложь)
	
	// Заполнение структуры категорий.
	СтруктураКатегорийДанные = РеквизитФормыВЗначение("СтруктураКатегорий");
	
	Если ОчиститьСтруктуру Тогда
		СтруктураКатегорийДанные.Строки.Очистить();
	КонецЕсли;
	
	Если ПустаяСтрока(Идентификатор) Тогда
		СтрокаДобавления = СтруктураКатегорийДанные;
	Иначе
		НайденныеСтроки = СтруктураКатегорийДанные.Строки.НайтиСтроки(Новый Структура("Идентификатор", Идентификатор), Истина);
		Если НайденныеСтроки.Количество() = 0 Тогда
			НоваяСтрока = СтруктураКатегорийДанные;
			СтрокаДобавления = НоваяСтрока;
		Иначе
			СтрокаДобавления = НайденныеСтроки[0];
		КонецЕсли;
	КонецЕсли;
	
	ЭтоКорневаяКатегория = ТипЗнч(СтрокаДобавления) = Тип("ДеревоЗначений") И Не ЗначениеЗаполнено(Идентификатор);
	КореньСОднойГруппой = СтрокаДобавления.Строки.Количество() = 1 И ЭтоКорневаяКатегория;
	ОтличиеПодчиненных = СтрокаДобавления.Строки.Количество() = 0
		ИЛИ Не ТипЗнч(СтрокаДобавления) = Тип("ДеревоЗначений") И СтрокаДобавления.Строки.Количество() <> СтрокаДобавления.КоличествоПодчиненных;
	
	Если КореньСОднойГруппой ИЛИ ОтличиеПодчиненных Тогда
		
		Отказ = Ложь;
		ПараметрыКоманды = Новый Структура;
		Если Не ЗначениеЗаполнено(Идентификатор) Тогда
			
			// Инициализация первого уровня.
			Результат = БизнесСетьКлиентСервер.ОписаниеРезультатаКомандыСервиса();
			ТорговыеПредложения.ВыполнитьКомандуРубрикатора("ПолучитьСписокКатегорийПервогоУровня", Неопределено, Результат, Отказ);
			Элементы.ГруппаДекорацийПодкатегорий.Видимость = Истина;
			
			Если Отказ ИЛИ Результат.КодСостояния <> 200 ИЛИ Результат.Данные.Количество() = 0 Тогда
				Возврат;
			КонецЕсли;
			
			СтрокаДобавления.Строки.Очистить();
			
			Для Каждого Категория Из Результат.Данные Цикл
				НоваяСтрока = СтрокаДобавления.Строки.Добавить();
				НоваяСтрока.Идентификатор = Формат(Категория.id, "ЧГ=");
				НоваяСтрока.Представление = Категория.title;
				НоваяСтрока.КоличествоПодчиненных = Категория.childrenCount;
			КонецЦикла;
			
		ИначеЕсли ЗначениеЗаполнено(Идентификатор) 
			И (СтруктураКатегорийДанные.Строки.Количество() = 0 ИЛИ ЭтоКорневаяКатегория) Тогда
			
			// Открытие из другой формы с передачей идентификатора.
			ПараметрыКоманды.Вставить("ИдентификаторыКатегории", "[" + Идентификатор  +"]");
			Результат = БизнесСетьКлиентСервер.ОписаниеРезультатаКомандыСервиса();
			ТорговыеПредложения.ВыполнитьКомандуРубрикатора("ПолучитьЦепочкуКатегорийДоКорневой", ПараметрыКоманды, Результат, Отказ);
			
			Если Отказ ИЛИ Результат.КодСостояния <> 200 ИЛИ Результат.Данные.Количество() = 0 Тогда
				Возврат;
			КонецЕсли;
			
			МассивКатегорий = Результат.Данные[0];
			Для Индекс = -МассивКатегорий.Количество()+1 По 0 Цикл
				СтрокаДобавления = СтрокаДобавления.Строки.Добавить();
				Категория = МассивКатегорий[-Индекс];
				СтрокаДобавления.Идентификатор = Формат(Категория.id, "ЧГ=");
				СтрокаДобавления.Представление = Категория.title;
				СтрокаДобавления.КоличествоПодчиненных = Категория.childrenCount;
			КонецЦикла;
			
		Иначе
			
			// Переход к нижестоящей группе товаров и получение подчиненных групп.
			ПараметрыКоманды.Вставить("ИдентификаторКатегории", Идентификатор);
			Результат = БизнесСетьКлиентСервер.ОписаниеРезультатаКомандыСервиса();
			ТорговыеПредложения.ВыполнитьКомандуРубрикатора("ПолучитьСписокДочернихКатегорий", ПараметрыКоманды, Результат, Отказ);
			
			Если Не Отказ И Результат.КодСостояния = 200 И Результат.Данные.Количество() Тогда
				СтрокаДобавления.Строки.Очистить();
				Для Каждого Категория Из Результат.Данные Цикл
					НоваяСтрока = СтрокаДобавления.Строки.Добавить();
					НоваяСтрока.Идентификатор = Формат(Категория.id, "ЧГ=");
					НоваяСтрока.Представление = Категория.title;
					НоваяСтрока.КоличествоПодчиненных = Категория.childrenCount;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(СтруктураКатегорийДанные, "СтруктураКатегорий");
	
	// Формирование навигации категорий.
	Если ЭтоКорневаяКатегория Тогда
		Элементы.СтруктураКатегорий.Заголовок = Новый ФорматированнаяСтрока(НСтр("ru = 'Все товары'"),,,, "id:Все товары");
		Заголовок = НСтр("ru = 'Торговые предложения 1С:Бизнес-сеть'");
		СформироватьОтборыПоКатегории(Неопределено);
		
	ИначеЕсли ТипЗнч(СтрокаДобавления) <> Тип("СтрокаДереваЗначений") Тогда
		
		Заголовок = СтрШаблон(НСтр("ru = 'Торговые предложения'"));
		Элементы.СтруктураКатегорий.Заголовок = "";
		
	Иначе
		
		РодительСтроки = СтрокаДобавления;
		МассивСтроки = Новый Массив;
		
		Пока РодительСтроки <> Неопределено Цикл
			Если МассивСтроки.Количество() Тогда
				МассивСтроки.Вставить(0, " • ");
			КонецЕсли;
			МассивСтроки.Вставить(0,
				Новый ФорматированнаяСтрока(РодительСтроки.Представление,,,, "id"
					+ Формат(РодительСтроки.Идентификатор, "ЧГ=") 
					+ ":" + РодительСтроки.Представление));
			РодительСтроки = РодительСтроки.Родитель;
		КонецЦикла; 
		
		// Заголовок формы.
		Заголовок = СтрШаблон(НСтр("ru = 'Торговые предложения - %1'"), СтрокаДобавления.Представление);
		
		// Ссылки навигационной панели.
		Элементы.СтруктураКатегорий.Заголовок = Новый ФорматированнаяСтрока(МассивСтроки);
	КонецЕсли;
	
	// Формирование навигации подкатегорий.
	МассивСтроки = Новый Массив;
	Для Каждого Категория Из СтрокаДобавления.Строки Цикл
		Если МассивСтроки.Количество() Тогда
			МассивСтроки.Вставить(0, "     ");
		КонецЕсли;
		МассивСтроки.Вставить(0, Новый ФорматированнаяСтрока(Категория.Представление,,,,
			"id" + Формат(Категория.Идентификатор, "ЧГ=") + ":" + Категория.Представление));
	КонецЦикла;
	
	Элементы.ДекорацияПодкатегорий.Видимость = МассивСтроки.Количество();
	Элементы.ДекорацияПодкатегорий.Заголовок = Новый ФорматированнаяСтрока(МассивСтроки);
	
	Если Элементы.КомандаПоказыватьОтборы.Пометка И Не ОтборыПоКатегорииАктуальны Тогда
		СформироватьОтборыПоКатегории(ТекущаяКатегория);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьПараметрыПоиска()
	
	ПараметрыПоиска = Новый Структура;
	
	ПараметрыПоиска.Вставить("Валюта", Валюта);
	
	Если ЗначениеЗаполнено(ТекущаяКатегория) Тогда
		ПараметрыПоиска.Вставить("ИдентификаторКатегории", ТекущаяКатегория);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборНаименование) Тогда
		ПараметрыПоиска.Вставить("НазваниеТовара", ОтборНаименование);
	ИначеЕсли ЗначениеЗаполнено(СтрокаПоиска) Тогда
		ПараметрыПоиска.Вставить("НазваниеТовара", СтрокаПоиска);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборАртикул) Тогда
		ПараметрыПоиска.Вставить("Артикул", ОтборАртикул);
	ИначеЕсли ПараметрыПоиска.Свойство("НазваниеТовара") Тогда
		ПараметрыПоиска.Вставить("Артикул", ПараметрыПоиска.НазваниеТовара);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборШтрихКоды) Тогда
		ПараметрыПоиска.Вставить("ШтрихКоды", ОтборШтрихКоды.ВыгрузитьЗначения());
	ИначеЕсли ПараметрыПоиска.Свойство("НазваниеТовара") Тогда
		ТипЧисло = Новый ОписаниеТипов("Число");
		Если ТипЧисло.ПривестиЗначение(ПараметрыПоиска.НазваниеТовара) <> 0 Тогда
			МассивПоискаШтрихКодов = Новый Массив;
			МассивПоискаШтрихКодов.Добавить(ПараметрыПоиска.НазваниеТовара);
			ПараметрыПоиска.Вставить("ШтрихКоды", МассивПоискаШтрихКодов);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыПоиска.Вставить("РежимСортировки", РежимСортировки);
	
	// Регионы.
	Если РегионРежимОтбора <> 0 Тогда
		ПараметрыПоиска.Вставить("РегионРежимОтбора", РегионРежимОтбора);
		ПараметрыПоиска.Вставить("РегионЗначенияПолей", РегионЗначенияПолей);
	КонецЕсли;
	
	// Отборы.
	ПараметрыПоиска.Вставить("Поставщик", Поставщик);
	ПараметрыПоиска.Вставить("ПоставщикНаименование",  ПоставщикНаименование);
	ПараметрыПоиска.Вставить("ПоставщикИдентификатор", ПоставщикИдентификатор);
	ПараметрыПоиска.Вставить("ЦенаОт", ЦенаОт * 100); // цена в сервисе учитывается в копейках.
	ПараметрыПоиска.Вставить("ЦенаДо", ЦенаДо * 100);
	
	// Быстрые отборы.
	
	БыстрыеОтборы.Очистить();
	Если Не ПустаяСтрока(ПоставщикНаименование) И РазрешитьИзменениеПоставщика Тогда
		БыстрыеОтборы.Добавить("ПоставщикНаименование", СтрШаблон(НСтр("ru = 'Поставщик: %1'"), ПоставщикНаименование));
	КонецЕсли;
	Если ЦенаОт <> 0 ИЛИ ЦенаДо <> 0 Тогда
		БыстрыеОтборы.Добавить("Цена", СтрШаблон(НСтр("ru = 'Цена, %1: %2'"), Валюта,
			?(ЦенаОт = 0, "", СтрШаблон(НСтр("ru = 'от %1'"), ЦенаОт))
			+ ?(ЦенаОт <> 0 И ЦенаДо <> 0, " ", "")
			+ ?(ЦенаДо = 0, "", СтрШаблон(НСтр("ru = 'до %1'"), ЦенаДо))));
	КонецЕсли;
	
	Если ВозможнаДоставка Тогда
		ПараметрыПоиска.Вставить("ВозможнаДоставка", Истина);
		БыстрыеОтборы.Добавить("ВозможнаДоставка", СтрШаблон(НСтр("ru = 'Возможна доставка'")));
	КонецЕсли;
	
	Если ВозможенСамовывоз Тогда
		ПараметрыПоиска.Вставить("ВозможенСамовывоз", Истина);
		БыстрыеОтборы.Добавить("ВозможенСамовывоз", СтрШаблон(НСтр("ru = 'Возможен самовывоз'")));
	КонецЕсли;
	
	Если ОтборыПоКатегорииАктуальны Тогда
		
		МассивАтрибутов = Новый Массив;
		Для Каждого ИмяРеквизита Из СписокДобавленныхРеквизитов Цикл
			ЗначениеРеквизита = ЭтотОбъект[ИмяРеквизита.Значение];
			ИдентификаторРеквизита = СтрРазделить(ИмяРеквизита.Значение, "_")[1];
			Если ТипЗнч(ЗначениеРеквизита) = Тип("Булево") Тогда
				Если ЗначениеРеквизита <> Ложь Тогда
					МассивАтрибутов.Добавить(
						Новый Структура("ИдентификаторАтрибута, Значение",
							ИдентификаторРеквизита, ЗначениеРеквизита));
					БыстрыеОтборы.Добавить(ИмяРеквизита.Значение, ИмяРеквизита.Представление + ": " + ЗначениеРеквизита);
				КонецЕсли;
			ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("СписокЗначений") Тогда 
				СтрокаЗначенийОтборов = "";
				Для Каждого ЭлементСписка Из ЗначениеРеквизита Цикл
					Если ЭлементСписка.Пометка Тогда
						МассивАтрибутов.Добавить(
							Новый Структура("ИдентификаторАтрибута, ИдентификаторЗначения",
								ИдентификаторРеквизита, ЭлементСписка.Значение));
						СтрокаЗначенийОтборов = СтрокаЗначенийОтборов + ?(ПустаяСтрока(СтрокаЗначенийОтборов), "", ", ")
							+ ЭлементСписка.Представление;
					КонецЕсли;
				КонецЦикла;
				Если Не ПустаяСтрока(СтрокаЗначенийОтборов) Тогда
					БыстрыеОтборы.Добавить(ИмяРеквизита.Значение, ИмяРеквизита.Представление + ": " + СтрокаЗначенийОтборов);
				КонецЕсли;
			ИначеЕсли ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
				МассивАтрибутов.Добавить(
					Новый Структура("ИдентификаторАтрибута, Значение",
						ИдентификаторРеквизита, ЗначениеРеквизита));
				БыстрыеОтборы.Добавить(ИмяРеквизита.Значение, ИмяРеквизита.Представление + ": " + ЗначениеРеквизита);
			КонецЕсли;
		КонецЦикла;
		
		Если МассивАтрибутов.Количество() Тогда
			ПараметрыПоиска.Вставить("Атрибуты", МассивАтрибутов);
		КонецЕсли;
		
	КонецЕсли;
	
	// Удаление старых элементов быстрых отборов.
	МассивЭлементовУдаления = Новый Массив;
	КоличествоЭлементов = Элементы.ГруппаБыстрыхОтборов.ПодчиненныеЭлементы.Количество();
	Для ОбратныйИндекс = 1 По КоличествоЭлементов Цикл
		ЭлементОтбора = Элементы.ГруппаБыстрыхОтборов.ПодчиненныеЭлементы[КоличествоЭлементов - ОбратныйИндекс];
		Если ЭлементОтбора.Видимость Тогда
			Элементы.Удалить(ЭлементОтбора);
		КонецЕсли;
	КонецЦикла;
	
	// Создание новых элементов быстрых отборов.
	Для Каждого ЭлементОтбора Из БыстрыеОтборы Цикл
		
		// Добавление пустой группы.
		НоваяГруппа = Элементы.Добавить("ГруппаБыстрогоОтбора_" + ЭлементОтбора.Значение, Тип("ГруппаФормы"), Элементы.ГруппаБыстрыхОтборов);
		НоваяГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		НоваяГруппа.Отображение = ОтображениеОбычнойГруппы.Нет;
		НоваяГруппа.ОтображатьЗаголовок = Ложь;
		НоваяГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		НоваяГруппа.Видимость = Истина;
		НоваяГруппа.ОтображатьОтступСлева = Ложь;
		НоваяГруппа.ЦветФона = WebЦвета.СветлоЖелтый;
		НоваяГруппа.ГоризонтальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
		
		НовыйЭлемент = Элементы.Добавить("ЗаголовокОтбора_" + ЭлементОтбора.Значение, Тип("ДекорацияФормы"), НоваяГруппа);
		
		// Разбитие на форматированную строку.
		МассивСтрок = Новый Массив;
		МассивСтрок.Добавить(Лев(ЭлементОтбора.Представление, СтрНайти(ЭлементОтбора.Представление, ":")));
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
			Сред(ЭлементОтбора.Представление, СтрНайти(ЭлементОтбора.Представление, ":")+1),, ЦветаСтиля.ЦветТекстаФормы));
		НовыйЭлемент.Заголовок = Новый ФорматированнаяСтрока(МассивСтрок);
		
		НовыйЭлемент.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
		НовыйЭлемент.Гиперссылка = Истина;
		НовыйЭлемент.УстановитьДействие("Нажатие", "Подключаемый_Нажатие");
		
		НовыйЭлемент = Элементы.Добавить("ОчиститьОтбор_" + ЭлементОтбора.Значение, Тип("ДекорацияФормы"), НоваяГруппа);
		НовыйЭлемент.Вид = ВидДекорацииФормы.Картинка;
		НовыйЭлемент.Картинка = БиблиотекаКартинок.Очистить;
		НовыйЭлемент.Гиперссылка = Истина;
		НовыйЭлемент.УстановитьДействие("Нажатие", "Подключаемый_Нажатие");
		
	КонецЦикла;
	
	Возврат ПараметрыПоиска;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ДозаполнитьПараметрыПроцедуры(ПараметрыПоиска)
	
	ПараметрыПоиска.Вставить("КодВалюты", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыПоиска.Валюта, "Код"));
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатПоиска(АдресРезультата)
	
	Список.Очистить();
	
	Если ЭтоАдресВременногоХранилища(АдресРезультата) Тогда 
		РезультатПоиска = ПолучитьИзВременногоХранилища(АдресРезультата);
		Если РезультатПоиска = Неопределено Тогда
			ТекстСообщения = НСтр("ru='Ошибка поиска торговых предложений.'")
				+ Символы.ПС + НСтр("ru ='Подробности см. в журнале регистрации.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Иначе
			Список.Загрузить(РезультатПоиска);
		КонецЕсли;
	КонецЕсли;
	
	// Поиск товаров для заказа.
	Для Каждого СтрокаТовары Из Товары Цикл
		СтрокиСписка = Список.НайтиСтроки(Новый Структура("ПредложениеИдентификатор", СтрокаТовары.ПредложениеИдентификатор));
		Если СтрокиСписка.Количество() Тогда
			СтрокиСписка[0].ГиперссылкаДобавить = ?(СтрокаТовары.Количество = 0,
				НСтр("ru = 'Добавить'"), СтрокаТовары.Количество);
		КонецЕсли;
	КонецЦикла;
	
	Если Элементы.КомандаПоказыватьОтборы.Пометка И Не ОтборыПоКатегорииАктуальны Тогда
		СформироватьОтборыПоКатегории(ТекущаяКатегория);
	КонецЕсли;
	
	Элементы.НайтиПредложения.Картинка = БиблиотекаКартинок.Найти;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьВыполнениеЗадания(ИдентификаторЗадания)
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
		ИдентификаторЗадания = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция АдресХранилищаТоваров()
	
	ТоварыОбъект = РеквизитФормыВЗначение("Товары");
	
	АдресТоваровВХранилище = ПоместитьВоВременноеХранилище(ТоварыОбъект, УникальныйИдентификатор);
	
	Возврат АдресТоваровВХранилище;
	
КонецФункции

&НаКлиенте
Процедура ОчиститьТовары()
	
	Для Каждого ПозицияТовары Из Товары Цикл
		СтрокаСписка = Список.НайтиСтроки(Новый Структура("ПредложениеИдентификатор", ПозицияТовары.ПредложениеИдентификатор));
		Если СтрокаСписка.Количество() Тогда
			СтрокаСписка[0].ГиперссылкаДобавить = НСтр("ru = 'Добавить'");
		КонецЕсли;
	КонецЦикла;
	Товары.Очистить();
	ОбновитьНадписьИтогов();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтборыПоКатегории(ИдентификаторКатегории)
	
	// Удаление данных.
	МассивУдаления = Новый Массив;
	Для каждого ПодчиненныйЭлемент Из Элементы.ГруппаОтборов0.ПодчиненныеЭлементы Цикл
		МассивУдаления.Добавить(ПодчиненныйЭлемент);
	КонецЦикла;
	Для каждого ПодчиненныйЭлемент Из Элементы.ГруппаОтборов1.ПодчиненныеЭлементы Цикл
		МассивУдаления.Добавить(ПодчиненныйЭлемент);
	КонецЦикла;
	Для каждого ЭлементУдаления Из МассивУдаления Цикл
		Элементы.Удалить(ЭлементУдаления);
	КонецЦикла;
	ИзменитьРеквизиты(, СписокДобавленныхРеквизитов.ВыгрузитьЗначения());
	СписокДобавленныхРеквизитов.Очистить();
	
	Если Не ЗначениеЗаполнено(ИдентификаторКатегории) Тогда
		Возврат;
	КонецЕсли;
	
	// Чтение характеристик в сервисе.
	Отказ = Ложь;
	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("ИдентификаторКатегории", ИдентификаторКатегории);
	Результат = БизнесСетьКлиентСервер.ОписаниеРезультатаКомандыСервиса();
	ТорговыеПредложения.ВыполнитьКомандуРубрикатора("ПолучитьСписокХарактеристикКатегории", ПараметрыКоманды, Результат, Отказ);
	
	Если Отказ Или Результат.КодСостояния <> 200 Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоВКолонке = Результат.Данные.Количество();
	Если КоличествоВКолонке > 20 Тогда
		КоличествоВКолонке = Окр(Результат.Данные.Количество() / 2, 0, РежимОкругления.Окр15как10) + 1;
	КонецЕсли;
	
	// Формирование элементов формы.
	СчетчикХарактеристики = 0;
	Для Каждого Характеристика Из Результат.Данные Цикл
		
		Представление = Характеристика.title + ?(ПустаяСтрока(Характеристика.unitCode),"", ", " + Характеристика.unitCode);
		
		Если СтрНайти(Характеристика.sourceId, "-") ИЛИ СтрНайти(Характеристика.sourceId, "shipping")
			ИЛИ Характеристика.title = "Минимальная цена" ИЛИ Характеристика.title = "Максимальная цена" Тогда
			Продолжить;
		КонецЕсли;
		
		НомерГруппы = Цел(СчетчикХарактеристики / КоличествоВКолонке);
		ГруппаОтборов = Элементы["ГруппаОтборов" + НомерГруппы];
		СчетчикХарактеристики = СчетчикХарактеристики + 1;
		
		ИдентификаторХарактеристики = Формат(Характеристика.id,"ЧРГ=; ЧГ=");
		
		Если ВРег(Характеристика.type) = "ENUMERATION" Тогда
			
			СтрелкаВниз = "↓";
			
			НовыйЭлемент = Элементы.Добавить("ЗаголовокОтбора_" + ИдентификаторХарактеристики, Тип("ДекорацияФормы"), ГруппаОтборов);
			НовыйЭлемент.Заголовок = Представление + " " + СтрелкаВниз;
			НовыйЭлемент.Гиперссылка = Истина;
			НовыйЭлемент.Подсказка = Представление;
			НовыйЭлемент.УстановитьДействие("Нажатие", "Подключаемый_НажатиеГиперссылки");
			
			// Добавление пустой группы.
			НоваяГруппа = Элементы.Добавить("ГруппаОтбора_" + ИдентификаторХарактеристики, Тип("ГруппаФормы"), ГруппаОтборов);
			НоваяГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			НоваяГруппа.Отображение = ОтображениеОбычнойГруппы.Нет;
			НоваяГруппа.ОтображатьЗаголовок = Ложь;
			НоваяГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
			НоваяГруппа.Видимость = Ложь;
			НоваяГруппа.ОтображатьОтступСлева = Ложь;
			
		ИначеЕсли ВРег(Характеристика.type) = "BOOLEAN" Тогда
			
			МассивРеквизитов = Новый Массив;
			Реквизит = Новый РеквизитФормы("Отбор_" + ИдентификаторХарактеристики, Новый ОписаниеТипов("Булево"));
			МассивРеквизитов.Добавить(Реквизит);
			ИзменитьРеквизиты(МассивРеквизитов);
			СписокДобавленныхРеквизитов.Добавить(Реквизит.Имя, Представление);
			
			НовыйЭлемент = Элементы.Добавить("Отбор_" + ИдентификаторХарактеристики, Тип("ПолеФормы"), ГруппаОтборов);
			НовыйЭлемент.Заголовок = Представление;
			НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
			НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
			НовыйЭлемент.ПутьКДанным = Реквизит.Имя;
			НовыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_ИзменениеОтбора");
			
		ИначеЕсли ВРег(Характеристика.type) = "NUMBER" Тогда 
			
			МассивРеквизитов = Новый Массив;
			Реквизит = Новый РеквизитФормы("Отбор_" + ИдентификаторХарактеристики, Новый ОписаниеТипов("Число"));
			МассивРеквизитов.Добавить(Реквизит);
			ИзменитьРеквизиты(МассивРеквизитов);
			СписокДобавленныхРеквизитов.Добавить(Реквизит.Имя, Представление);
			
			НовыйЭлемент = Элементы.Добавить("Отбор_" + ИдентификаторХарактеристики, Тип("ПолеФормы"), ГруппаОтборов);
			НовыйЭлемент.Заголовок = Представление;
			НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
			НовыйЭлемент.Ширина = 0;
			НовыйЭлемент.ПутьКДанным = Реквизит.Имя;
			НовыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_ИзменениеОтбора");
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОтборыПоКатегорииАктуальны = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьРежимСортировки(Элементы, НовыйРежим)

	// Установка пометки.
	Для Каждого Элемент Из Элементы.ПодменюСортировка.ПодчиненныеЭлементы Цикл
		Элемент.Пометка = (Элемент.Имя = НовыйРежим);
	КонецЦикла;
	
	РежимСортировки = НовыйРежим;
	
	СтрелкаВверх = "↑";
	СтрелкаВниз = "↓";
	Стрелка = ?(СтрНайти(НовыйРежим, "Возрастание"), СтрелкаВверх, СтрелкаВниз);
	
	Элементы.СписокНаименование.Заголовок = НСтр("ru = 'Наименование'");
	Элементы.СписокЦена.Заголовок = СтрШаблон(НСтр("ru = 'Цена, %1'"), Валюта);
	
	Если СтрНайти(НовыйРежим, "Наименование") Тогда
		Элементы.СписокНаименование.Заголовок = Элементы.СписокНаименование.Заголовок + " " + Стрелка;
	Иначе
		Элементы.СписокЦена.Заголовок = Элементы.СписокЦена.Заголовок + " " + Стрелка;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СформироватьЭлементыХарактеристики(Идентификатор, Представление)
	
	Отказ = Ложь;
	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("ИдентификаторКатегории", ТекущаяКатегория);
	ПараметрыКоманды.Вставить("ИдентификаторХарактеристики", Идентификатор);
	Результат = БизнесСетьКлиентСервер.ОписаниеРезультатаКомандыСервиса();
	ТорговыеПредложения.ВыполнитьКомандуРубрикатора("ПолучитьЗначенияХарактеристики", ПараметрыКоманды, Результат, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
		
	ЗначенияХарактеристик = Результат.Данные;
	
	МассивРеквизитов = Новый Массив;
	Реквизит = Новый РеквизитФормы("Отбор_" + Идентификатор, Новый ОписаниеТипов("СписокЗначений"));
	МассивРеквизитов.Добавить(Реквизит);
	ИзменитьРеквизиты(МассивРеквизитов);
	СписокДобавленныхРеквизитов.Добавить(Реквизит.Имя, Представление);
	СписокДобавленныхРеквизитов.СортироватьПоЗначению();
	
	Если ТипЗнч(ЗначенияХарактеристик) = Тип("Массив") Тогда
		Если ЗначенияХарактеристик.Количество() > 1 Тогда
			Для Каждого Значение Из ЗначенияХарактеристик Цикл
				ЭтотОбъект[Реквизит.Имя].Добавить(Значение.id, Значение.value);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// Сортировка элементов.
	ЭтотОбъект[Реквизит.Имя].СортироватьПоПредставлению();
	
	ГруппаОтборов = Элементы["ГруппаОтбора_" + Идентификатор];
	
	НовыйЭлемент = Элементы.Добавить("Отбор_" + Идентификатор, Тип("ТаблицаФормы"), ГруппаОтборов);
	НовыйЭлемент.Отображение = ОтображениеТаблицы.Список;
	НовыйЭлемент.АвтоМаксимальнаяВысота = Истина;
	Если ЗначенияХарактеристик.Количество() <= 5 Тогда
		НовыйЭлемент.Высота = ЗначенияХарактеристик.Количество() + 1;
	Иначе
		НовыйЭлемент.Высота = 6;
		НовыйЭлемент.ВертикальнаяПолосаПрокрутки = ИспользованиеПолосыПрокрутки.ИспользоватьВсегда;
	КонецЕсли;
	НовыйЭлемент.ГоризонтальнаяПолосаПрокрутки = ИспользованиеПолосыПрокрутки.НеИспользовать;
	НовыйЭлемент.РастягиватьПоВертикали = Ложь;
	НовыйЭлемент.ПутьКДанным = Реквизит.Имя;
	НовыйЭлемент.КоманднаяПанель.Видимость = Ложь;
	НовыйЭлемент.ЧередованиеЦветовСтрок = Истина;
	НовыйЭлемент.ЦветРамки = ЦветаСтиля.ЦветФонаФормы;
	НовыйЭлемент.Шрифт = Новый Шрифт(, 8);
	НовыйЭлемент.ИзменятьПорядокСтрок = Ложь;
	НовыйЭлемент.ИзменятьСоставСтрок = Ложь;
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_ИзменениеОтбора");
	
	ЭлементКолонки = Элементы.Добавить(НовыйЭлемент.Имя + "_Пометка", Тип("ПолеФормы"), НовыйЭлемент);
	ЭлементКолонки.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементКолонки.ШиринаЭлемента = 1;
	ЭлементКолонки.ПутьКДанным = Реквизит.Имя + ".Пометка";
	ЭлементКолонки = Элементы.Добавить(НовыйЭлемент.Имя + "_Значение", Тип("ПолеФормы"), НовыйЭлемент);
	ЭлементКолонки.АвтоМаксимальнаяВысота = Истина;
	ЭлементКолонки.АвтоВысотаЯчейки = Истина;
	ЭлементКолонки.ТолькоПросмотр = Истина;
	ЭлементКолонки.ПутьКДанным = Реквизит.Имя + ".Значение";
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КатегорииПоНаименованию(Текст)
	
	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("СтрокаПоиска", Текст);
	ПараметрыКоманды.Вставить("ТолькоЭлементы", Истина);
	
	Отказ = Ложь;
	Результат = БизнесСетьКлиентСервер.ОписаниеРезультатаКомандыСервиса();
	ТорговыеПредложения.ВыполнитьКомандуРубрикатора("ПоискКатегорий", ПараметрыКоманды, Результат, Отказ);
	
	Если Отказ ИЛИ Результат = Неопределено ИЛИ Результат.КодСостояния <> 200
		ИЛИ ТипЗнч(Результат.Данные) <> Тип("Массив")
		ИЛИ Результат.Данные.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВозвращаемоеЗначение = Новый СписокЗначений;
	СписокКатегорий = "";
	Для Каждого Значение Из Результат.Данные Цикл
		СписокКатегорий = СписокКатегорий + ?(ПустаяСтрока(СписокКатегорий),"", ", ") + Формат(Значение.id, "ЧГ=");
	КонецЦикла;
	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("ИдентификаторыКатегории", "[" + СписокКатегорий + "]");
	РезультатЦепочкиКатегорий = БизнесСетьКлиентСервер.ОписаниеРезультатаКомандыСервиса();
	ТорговыеПредложения.ВыполнитьКомандуРубрикатора("ПолучитьЦепочкуКатегорийДоКорневой", ПараметрыКоманды, РезультатЦепочкиКатегорий, Отказ);
	
	Если Отказ ИЛИ РезультатЦепочкиКатегорий = Неопределено ИЛИ РезультатЦепочкиКатегорий.КодСостояния <> 200 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Индекс = 0;
	Для каждого Значение Из Результат.Данные Цикл
		
		Идентификатор = Формат(Значение.id, "ЧГ=");
		Наименование = Значение.title;
		
		Длина = СтрДлина(Текст);
		Позиция = СтрНайти(НРег(Наименование), НРег(Текст));
		ЛеваяЧасть = Лев(Наименование, Позиция-1);
		СредняяЧасть = Сред(Наименование, Позиция, Длина);
		ПраваяЧасть = Сред(Наименование, Позиция + Длина);
		
		ПредставлениеЭлемента = Новый Массив;
		ПредставлениеЭлемента.Добавить(ЛеваяЧасть);
		ПредставлениеЭлемента.Добавить(Новый ФорматированнаяСтрока(СредняяЧасть, Новый Шрифт(,, Истина), WebЦвета.Зеленый));
		ПредставлениеЭлемента.Добавить(ПраваяЧасть);
		
		ТекстРодитель = "";
		МассивРодителей = РезультатЦепочкиКатегорий.Данные[Индекс];
		Для Каждого Родитель Из МассивРодителей Цикл
			Если Значение.id = Родитель.id Тогда
				Продолжить;
			КонецЕсли;
			ТекстРодитель = ТекстРодитель + " - " + Родитель.title;
		КонецЦикла;
		
		ПредставлениеРодителя = Новый Массив;
		Если ПустаяСтрока(ТекстРодитель) Тогда
			ПредставлениеРодителя.Добавить("");
		Иначе
			ПредставлениеРодителя.Добавить(" ");
			ПредставлениеРодителя.Добавить(ТекстРодитель);
		КонецЕсли;
		ПредставлениеРодителя = Новый ФорматированнаяСтрока(ПредставлениеРодителя,, ЦветаСтиля.ПоясняющийТекст);
		
		Представление = Новый ФорматированнаяСтрока(ПредставлениеЭлемента, ПредставлениеРодителя);
		
		ВозвращаемоеЗначение.Добавить(Идентификатор, Представление,, БиблиотекаКартинок.БизнесСеть);
		
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

&НаСервере
Процедура ДобавитьТоварыПоИдентификатору(ПредложениеИдентификатор, Количество)
	
	Отказ = Ложь;
	
	РежимДобавления = Ложь;
	Если Количество = Неопределено Тогда
		РежимДобавления = Истина;
		Количество = 1;
	КонецЕсли;
	
	ПоискСтрок = Товары.НайтиСтроки(Новый Структура("ПредложениеИдентификатор", ПредложениеИдентификатор));
	Если ПоискСтрок.Количество() Тогда
		СтрокаТовары = ПоискСтрок[0];
		СтрокаТовары.Количество = ?(РежимДобавления, СтрокаТовары.Количество + Количество, Количество);
		Если СтрокаТовары.Количество <= 0 Тогда
			Товары.Удалить(Товары.Индекс(СтрокаТовары));
		Иначе
			ТорговыеПредложенияПереопределяемый.ПересчитатьСуммуПоСтроке(СтрокаТовары);
		КонецЕсли;
	Иначе
		Результат = ТорговыеПредложения.ПрочитатьТорговоеПредложение(ПредложениеИдентификатор, Валюта,, Отказ);
		Если Отказ ИЛИ Результат = Неопределено ИЛИ Результат.КодСостояния <> 200 Тогда
			ВызватьИсключение НСтр("ru = 'Ошибка получения карточки торгового предложения в сервисе 1С:Бизнес-сеть.'");
		КонецЕсли;
		Предложение = Результат.Данные;
		СтрокаТовары = Товары.Добавить();
		СтрокаТовары.ЕдиницаИзмерения    = Предложение.unit;
		СтрокаТовары.ЕдиницаИзмеренияКод = Предложение.basicUnit;
		СтрокаТовары.ПредложениеИдентификатор = ПредложениеИдентификатор;
		СтрокаТовары.Количество          = Количество;
		СтрокаТовары.Наименование        = Предложение.name;
		СтрокаТовары.Характеристика      = Предложение.feature;
		СтрокаТовары.Пометка             = Истина;
		СтрокаТовары.ПоставщикНаименование = Предложение.vendor.title;
		СтрокаТовары.ПоставщикИНН        = Предложение.vendor.inn;
		ПоставщикКПП                     = Предложение.vendor.kpp;
		Если ПоставщикКПП = "0" Тогда
			ПоставщикКПП = "";
		КонецЕсли;
		СтрокаТовары.ПоставщикКПП = ПоставщикКПП;
		СтрокаТовары.ПоставщикИдентификатор = Предложение.vendor.inn + ?(ПустаяСтрока(ПоставщикКПП), "",
			"_" + ПоставщикКПП);
		ТорговыеПредложенияПереопределяемый.ПолучитьЗначениеСтавкиНДС(Предложение.nds, СтрокаТовары.СтавкаНДС);
		//СтрокаТовары.Цена            = Предложение.price / 100;
		Если Предложение.Свойство("priceConverted") И ЗначениеЗаполнено(Предложение.priceConverted) Тогда
			СтрокаТовары.Цена = Предложение.priceConverted / 100;
		КонецЕсли;
		СтрокаТовары.ЦенаВключаетНДС = Предложение.campaign.saleWithNds;
		СтрокаТовары.СрокПоставки    = Предложение.deliveryTime;
		ТорговыеПредложенияПереопределяемый.ПересчитатьСуммуПоСтроке(СтрокаТовары);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// Гиперссылка списка формы Добавить.
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокГиперссылкаДобавить.Имя);
	
	ОтборГруппа = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборГруппа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ГиперссылкаДобавить");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = НСтр("ru = 'Добавить'");
	
	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ГиперссылкаДобавить");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЭДЦвет);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Добавить'"));
	
	// Надпись "в наличии".
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокСрокПоставки.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.СрокПоставки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'в наличии'"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитСнятьФлагКомандыЗапрашиватьКоличество()
	
	Элементы.КомандаЗапрашиватьКоличество.Пометка = ЗапрашиватьКоличество;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ВыполняетсяЗакрытие = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	Элементы.ГруппаПанельОтборов.Видимость = Элементы.КомандаПоказыватьОтборы.Пометка;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьРегистрациюОрганизаций()
	
	ОрганизацияЗарегистрирована = БизнесСеть.ОрганизацияЗарегистрирована();
	ТребуетсяРегистрацияОрганизации = Не ОрганизацияЗарегистрирована;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяФормыВыбораКонтрагентов()
	
	ИмяСправочника = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяПрикладногоСправочника("Контрагенты");
	ИмяФормыВыбора = "Справочник." + ИмяСправочника + ".ФормаВыбора";
	Возврат ИмяФормыВыбора;
	
КонецФункции

&НаКлиенте
Процедура ВыборКонтрагентаПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	ПоставщикНаименование = Строка(Результат);
	Поставщик = Результат;
	ЗаполнитьТорговыеПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросРегистрацииОрганизацииПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Оповещение = Новый ОписаниеОповещения("СписокВыборПродолжение", ЭтотОбъект);
		ОткрытьФорму("Обработка.БизнесСеть.Форма.РегистрацияУчастников",, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ОткрытьКарточкуТорговогоПредложения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуТорговогоПредложения()
		
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина,
		"Обработка.ТорговыеПредложения.Форма.ПоискПредложенийПоСтроке.ОткрытьКарточку");
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("УникальныйИдентификаторКорзины", УникальныйИдентификаторКорзины);
	ПараметрыОткрытия.Вставить("ПредложениеИдентификатор", ТекущиеДанные.ПредложениеИдентификатор);
	ПараметрыОткрытия.Вставить("ПоставщикИдентификатор", ТекущиеДанные.ПоставщикИдентификатор);
	ПараметрыОткрытия.Вставить("Валюта", Валюта);
	
	ИмяКолонки = Элементы.Список.ТекущийЭлемент.Имя;
	Если ИмяКолонки = "СписокГиперссылкаДобавить" Тогда
		
		СтрокаКорзины = Товары.НайтиСтроки(новый Структура("ПредложениеИдентификатор", ТекущиеДанные.ПредложениеИдентификатор));
		Если СтрокаКорзины.Количество() Тогда
			КоличествоВКорзине = СтрокаКорзины[0].Количество;
		Иначе
			КоличествоВКорзине = 0;
		КонецЕсли;
			
		Если ЗапрашиватьКоличество Тогда
			ПараметрыОткрытия.Вставить("КоличествоВКорзине", КоличествоВКорзине);
			ВводКоличестваЗавершение = Новый ОписаниеОповещения("ВводКоличестваЗавершение",
				ЭтотОбъект,
				Новый Структура("ПараметрыОповещения", ПараметрыОткрытия));
			ПоказатьВводЧисла(ВводКоличестваЗавершение, КоличествоВКорзине, НСтр("ru = 'Укажите количество'"), 15, 3);
		Иначе
			КоличествоВКорзине = КоличествоВКорзине + 1; // Добавление товара в корзину 1 шт.
			ПараметрыОткрытия.Вставить("Количество", КоличествоВКорзине);
			Оповестить("ТорговыеПредложения_ИзменитьКоличествоВКорзине", ПараметрыОткрытия);
		КонецЕсли;
		
	Иначе
		
		ОткрытьФорму("Обработка.ТорговыеПредложения.Форма.ТорговоеПредложение", ПараметрыОткрытия, ЭтотОбъект,
			ТекущиеДанные.ПредложениеИдентификатор);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтборИОбновитьСписок()
	
	// Проверка открытия из другой формы.
	РазрешитьИзменениеПоставщика = ЭтотОбъект.ВладелецФормы = Неопределено;
	Если РазрешитьИзменениеПоставщика Тогда
		Поставщик = Неопределено;
		ПоставщикНаименование = "";
		ПоставщикИНН = "";
		ПоставщикКПП = "";
		ПоставщикИдентификатор = "";
	КонецЕсли;
	
	ЦенаОт = 0;
	ЦенаДо = 0;
	ВозможнаДоставка = Ложь;
	ВозможенСамовывоз = Ложь;
	СтрокаПоиска = "";
	
	ОтборыПоКатегорииАктуальны = Ложь;
	ЗаполнитьТорговыеПредложения();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборРегионаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("РегионРежимОтбора") Тогда
		
		Результат.Свойство("РегионРежимОтбора", РегионРежимОтбора);
		Результат.Свойство("РегионПредставление", РегионПредставление);
		Результат.Свойство("РегионЗначенияПолей", РегионЗначенияПолей);
		
		ОбновитьЗаголовокРегиона(ЭтотОбъект);
		
		ЗаполнитьТорговыеПредложения();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЗаголовокРегиона(Объект)
	
	Если Объект.ТребуетсяРегистрацияОрганизации Тогда
		Объект.РегионРежимОтбора = 0;
	КонецЕсли;
	
	Если Объект.РегионРежимОтбора = 0 Тогда
		Объект.РегионЗаголовок = НСтр("ru = 'Без отбора по регионам'");
	ИначеЕсли Объект.РегионРежимОтбора = 1 Тогда
		Отказ = Ложь;
		Детализация = КоличествоРегионовЗакупкиПрофиляАбонента(Отказ);
		Если Отказ Тогда
			Объект.РегионЗаголовок = НСтр("ru = '<Ошибка данных профиля>'");
		ИначеЕсли ПустаяСтрока(Детализация) Тогда
			Объект.РегионЗаголовок = НСтр("ru = 'Регионы профиля (не выбраны)'");
		Иначе
			Объект.РегионЗаголовок = СтрШаблон(НСтр("ru = 'Регионы профиля (%1)'"), Детализация);	
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(Объект.РегионЗначенияПолей) Тогда
		Отказ = Ложь;
		Объект.РегионЗаголовок = ПоследнийУровеньКонтактнойИнформации(Объект.РегионЗначенияПолей, Отказ);
		Если Отказ Тогда
			Объект.РегионЗаголовок = НСтр("ru = 'Без отбора по регионам'");
		КонецЕсли;
	Иначе
		Объект.РегионЗаголовок = НСтр("ru = 'Без отбора по регионам'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоследнийУровеньКонтактнойИнформации(РегионЗначенияПолей, Отказ)
	
	Возврат ТорговыеПредложения.ПоследнийУровеньКонтактнойИнформации(РегионЗначенияПолей, Отказ);
	
КонецФункции

&НаСервереБезКонтекста
Функция КоличествоРегионовЗакупкиПрофиляАбонента(Отказ)
	
	ТаблицыАдресовРегионов = ТорговыеПредложения.ПолучитьТаблицыАдресовАбонента(Отказ);
	Если Отказ Тогда
		Возврат "";
	КонецЕсли;
	
	Результат = "";
	Если ТаблицыАдресовРегионов <> Неопределено И ТипЗнч(ТаблицыАдресовРегионов) = Тип("Структура")
		И ТаблицыАдресовРегионов.Свойство("РегионыЗакупки")
		И ТипЗнч(ТаблицыАдресовРегионов.РегионыЗакупки) = Тип("ТаблицаЗначений")
		И ТаблицыАдресовРегионов.РегионыЗакупки.Количество() Тогда
		Результат = ТаблицыАдресовРегионов.РегионыЗакупки.Количество();
		Если Результат = 1 Тогда
			Результат = ТорговыеПредложения.ПоследнийУровеньКонтактнойИнформации(ТаблицыАдресовРегионов.РегионыЗакупки[0].ЗначенияПолей, Отказ)
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьВалютуПоискаПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Товары.Очистить();
		ОбновитьНадписьИтогов();
		ПоказатьВводЗначения(Новый ОписаниеОповещения("ИзменитьВалютуПоискаЗавершение", ЭтотОбъект), Валюта,
			НСтр("ru = 'Валюта поиска'"), Тип("СправочникСсылка.Валюты"));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВалютуПоискаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Валюта = Результат;
		Элементы.ИзменитьВалютуПоиска.Заголовок = СтрШаблон(НСтр("ru = 'Валюта: %1'"), Валюта);
		УстановитьРежимСортировки(Элементы, РежимСортировки);
		ЗаполнитьТорговыеПредложения();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область Инициализация

ВыполняетсяЗакрытие = Ложь;

#КонецОбласти
