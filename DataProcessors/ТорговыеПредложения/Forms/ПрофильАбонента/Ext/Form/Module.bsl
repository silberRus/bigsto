
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не БизнесСеть.ОрганизацияЗарегистрирована() Тогда
		ТребуетсяРегистрацияОрганизации = Истина;
		Возврат;
	КонецЕсли;
	
	ЗагрузитьДанныеСервиса();
	
	Если Параметры.Свойство("РежимОткрытия") И Параметры.РежимОткрытия = "РегионыЗакупки" Тогда
		Элементы.ГруппаСтраниц.ТекущаяСтраница = Элементы.СтраницаЗакупки;
	КонецЕсли;
	
	ЕстьПравоСинхронизации = Пользователи.ЭтоПолноправныйПользователь()
		ИЛИ Пользователи.РолиДоступны("АдминистрированиеАбонентаБизнесСеть");
		
	Если Не ЕстьПравоСинхронизации Тогда
		// Блокировка элементов.
		Элементы.ФормаЗаписатьЗакрыть.Доступность = Ложь;
		Элементы.РегионыЗакупки.ТолькоПросмотр = Истина;
		Элементы.РегионыПродажи.ТолькоПросмотр = Истина;
		Элементы.АдресаПродажи.ТолькоПросмотр = Истина;
		Элементы.РегионыАбонентаПродажиЗаполнить.Доступность = Ложь;
		Элементы.РегионыЗакупкиЗаполнить.Доступность = Ложь;
		Элементы.АдресаАбонентаЗаполнить.Доступность = Ложь;
	КонецЕсли;
	
	ОбновитьДоступностьЭлементов(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПрограммноеЗакрытие И Модифицированность Тогда
		
		Отказ = Истина;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриОтветеНаВопросОСохраненииИзмененныхДанных",
			ЭтотОбъект);
		
		ТекстВопроса = НСтр("ru = 'Данные модифицированы. Сохранить изменения?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РегионыПродажиВозможнаДоставкаПриИзменении(Элемент)
	ОбновитьДоступностьЭлементов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура АдресаПродажиВозможенСамовывозПриИзменении(Элемент)
	ОбновитьДоступностьЭлементов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура РегионыЗакупкиВозможенСамовывозПриИзменении(Элемент)
	ОбновитьДоступностьЭлементов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура РегионыЗакупкиТребуетсяДоставкаПриИзменении(Элемент)
	ОбновитьДоступностьЭлементов(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРегионыПродажи

&НаКлиенте
Процедура РегионыПродажиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.РегионыПродажи.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаголовокВвода = НСтр("ru = 'Введите регион абонента'");
	ОткрытьФормуКонтактнойИнформации(ТекущиеДанные, ЗаголовокВвода);
	
КонецПроцедуры

&НаКлиенте
Процедура РегионыПродажиПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РегионыПродажиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ЗаголовокВвода = НСтр("ru = 'Введите регион абонента'");
	ДополнительныеПараметры = Новый Структура("Представление, ЗначенияПолей");
	ДополнительныеПараметры.Вставить("НоваяСтрока", Истина);
	ДополнительныеПараметры.Вставить("ИмяСписка", "РегионыПродажи");
	ОткрытьФормуКонтактнойИнформации(ДополнительныеПараметры, ЗаголовокВвода);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРегионыЗакупки

&НаКлиенте
Процедура РегионыЗакупкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.РегионыЗакупки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаголовокВвода = НСтр("ru = 'Введите регион абонента'");
	ОткрытьФормуКонтактнойИнформации(ТекущиеДанные, ЗаголовокВвода);
	
КонецПроцедуры

&НаКлиенте
Процедура РегионыЗакупкиПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РегионыЗакупкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ЗаголовокВвода = НСтр("ru = 'Введите регион абонента'");
	ДополнительныеПараметры = Новый Структура("Представление, ЗначенияПолей, НоваяСтрока");
	ДополнительныеПараметры.Вставить("ИмяСписка", "РегионыЗакупки");
	ОткрытьФормуКонтактнойИнформации(ДополнительныеПараметры, ЗаголовокВвода);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАдресаПродажи

&НаКлиенте
Процедура АдресаПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.АдресаПродажи.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаголовокВвода = НСтр("ru = 'Введите адрес склада самовывоза'");
	ОткрытьФормуКонтактнойИнформации(ТекущиеДанные, ЗаголовокВвода);

КонецПроцедуры

&НаКлиенте
Процедура АдресаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ЗаголовокВвода = НСтр("ru = 'Введите адрес склада самовывоза'");
	ДополнительныеПараметры = Новый Структура("Представление, ЗначенияПолей, НоваяСтрока");
	ДополнительныеПараметры.Вставить("ИмяСписка", "АдресаПродажи");
	ОткрытьФормуКонтактнойИнформации(ДополнительныеПараметры, ЗаголовокВвода);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАдресаЗакупки

&НаКлиенте
Процедура АдресаЗакупкиПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.АдресаЗакупки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаголовокВвода = НСтр("ru = 'Введите адрес склада доставки'");
	ОткрытьФормуКонтактнойИнформации(ТекущиеДанные, ЗаголовокВвода);
КонецПроцедуры

&НаКлиенте
Процедура АдресаЗакупкиПриИзменении(Элемент)
	
	Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура АдресаЗакупкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ЗаголовокВвода = НСтр("ru = 'Введите адрес склада доставки'");
	ДополнительныеПараметры = Новый Структура("Представление, ЗначенияПолей, НоваяСтрока");
	ДополнительныеПараметры.Вставить("ИмяСписка", "АдресаЗакупки");
	ОткрытьФормуКонтактнойИнформации(ДополнительныеПараметры, ЗаголовокВвода);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьРегионыПродажи(Команда)
	
	ВыборРегионовАдресовАбонента("РегионыПродажи");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРегионыЗакупки(Команда)
	
	ВыборРегионовАдресовАбонента("РегионыЗакупки");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАдресаПродажи(Команда)
	
	ВыборРегионовАдресовАбонента("АдресаПродажи");

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьАдресаЗакупки(Команда)
	
	ВыборРегионовАдресовАбонента("АдресаЗакупки");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЗакрыть(Команда)
	
	ЗаписатьДанныеИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗагрузитьДанныеСервиса();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаписатьДанныеИЗакрыть()
	
	Отказ = Ложь;
	ЗаписатьДанныеПрофиляОбменаТорговымиПредложениями(Отказ);
	Если Не Отказ Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Регионы и адреса 1С:Бизнес-сеть'"),,
			НСтр("ru = 'Регионы и адреса успешно сохранены в сервисе 1С:Бизнес-сеть'"),
			БиблиотекаКартинок.БизнесСеть);
		ПрограммноеЗакрытие = Истина;
		Оповестить("ТорговыеПредложения_СохранениеРегионовАбонента",, ЭтотОбъект);
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция АдресХранилищДанныхСписка(ИмяСписка)
	
	Возврат ПоместитьВоВременноеХранилище(ЭтотОбъект[ИмяСписка].Выгрузить());
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформации(ТекущиеДанные, ЗаголовокВвода)
	
	ВидКонтактнойИнформации = Новый Структура;
	ВидКонтактнойИнформации.Вставить("Тип", ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес"));
	ВидКонтактнойИнформации.Вставить("ВключатьСтрануВПредставление", Истина);
	
	ПараметрыОткрытия = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		ВидКонтактнойИнформации, ТекущиеДанные.ЗначенияПолей,
		ТекущиеДанные.Представление,, ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес"));
	ПараметрыОткрытия.Вставить("Заголовок", ЗаголовокВвода);
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуКонтактнойИнформацииЗавершение", ЭтотОбъект, ТекущиеДанные);
		
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, ЭтотОбъект, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И ДополнительныеПараметры <> Неопределено Тогда
		
		Если ДополнительныеПараметры.Свойство("НоваяСтрока") Тогда
			ТекущиеДанные = Элементы[ДополнительныеПараметры.ИмяСписка].ТекущиеДанные;
			Если ДополнительныеПараметры = Неопределено Тогда
				Возврат;
			КонецЕсли;
		Иначе
			ТекущиеДанные = ДополнительныеПараметры;
		КонецЕсли;
		
		Если ПустаяСтрока(Результат.Представление) И ПустаяСтрока(Результат.КонтактнаяИнформация) Тогда
			// Пустая страна, по умолчанию.
			ТекущиеДанные.Представление = ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
			
		Иначе
			
			Если СтрНайти("РегионыПродажи, РегионыЗакупки", ЭтотОбъект.ТекущийЭлемент.Имя) Тогда
				
				Если Результат.ВведеноВСвободнойФорме Тогда
					ПоказатьПредупреждение(, НСтр("ru = 'Ввод региона в свободной форме запрещен.'"));
					Если ДополнительныеПараметры.Свойство("НоваяСтрока") Тогда
						УдалитьСтрокуСписка(ДополнительныеПараметры.ИмяСписка);
					КонецЕсли;
					Возврат;
				КонецЕсли;
				СжатьАдресКонтактнойИнформации(Результат.КонтактнаяИнформация, Результат.Представление);
			КонецЕсли;
			
			Модифицированность = Истина;
			ТекущиеДанные.Представление = Результат.Представление;
			ТекущиеДанные.ЗначенияПолей = Результат.КонтактнаяИнформация;
			
		КонецЕсли;
		
		
	ИначеЕсли ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("НоваяСтрока") Тогда
		
		УдалитьСтрокуСписка(ДополнительныеПараметры.ИмяСписка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтрокуСписка(ИмяСписка, СтрокаСписка = Неопределено)
	
	Если СтрокаСписка = Неопределено Тогда
		СтрокаСписка = Элементы[ИмяСписка].ТекущиеДанные;
		Если СтрокаСписка = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Удаление текущей строки, если была отмена ввода адреса.
	ЭтотОбъект[ИмяСписка].Удалить(СтрокаСписка);
	
КонецПроцедуры


&НаСервереБезКонтекста
Процедура СжатьАдресКонтактнойИнформации(ЗначенияПолей, Представление)
	
	ТорговыеПредложения.ПолучитьРегионыКонтактнойИнформации(ЗначенияПолей, Представление);

КонецПроцедуры

&НаСервере
Процедура ЗаписатьДанныеПрофиляОбменаТорговымиПредложениями(Отказ)
	
	ОбъектРегионыПродажи = РегионыПродажи.Выгрузить();
	ОбъектРегионыПродажи.Свернуть("Представление, ЗначенияПолей");
	
	ОбъектРегионыЗакупки = РегионыЗакупки.Выгрузить();
	ОбъектРегионыЗакупки.Свернуть("Представление, ЗначенияПолей");
	
	ОбъектАдресаПродажи = АдресаПродажи.Выгрузить();
	ОбъектАдресаПродажи.Свернуть("Представление, ЗначенияПолей");
	
	ОбъектАдресаЗакупки = АдресаЗакупки.Выгрузить();
	ОбъектАдресаЗакупки.Свернуть("Представление, ЗначенияПолей");
	
	РегионыАдреса = Новый ТаблицаЗначений;
	РегионыАдреса.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	РегионыАдреса.Колонки.Добавить("ЗначенияПолей", Новый ОписаниеТипов("Строка"));
	РегионыАдреса.Колонки.Добавить("ЭтоРегион",     Новый ОписаниеТипов("Булево"));
	РегионыАдреса.Колонки.Добавить("ЭтоПродажи",    Новый ОписаниеТипов("Булево"));
	РегионыАдреса.Колонки.Добавить("ЭтоЗакупки",    Новый ОписаниеТипов("Булево"));
	РегионыАдреса.Колонки.Добавить("Активный",      Новый ОписаниеТипов("Булево"));
	
	Для Каждого СтрокаТаблицы Из ОбъектРегионыПродажи Цикл
		НоваяСтрока = РегионыАдреса.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.ЭтоРегион = Истина;
		НоваяСтрока.ЭтоПродажи = Истина;
		НоваяСтрока.Активный = РегионыПродажиВозможнаДоставка;
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ОбъектРегионыЗакупки Цикл
		НоваяСтрока = РегионыАдреса.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.ЭтоРегион = Истина;
		НоваяСтрока.ЭтоЗакупки = Истина;
		НоваяСтрока.Активный = РегионыЗакупкиВозможенСамовывоз;
	КонецЦикла;
		
	Для Каждого СтрокаТаблицы Из ОбъектАдресаПродажи Цикл
		НоваяСтрока = РегионыАдреса.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.ЭтоПродажи = Истина;
		НоваяСтрока.Активный = АдресаПродажиВозможенСамовывоз;
	КонецЦикла;
		
	Для Каждого СтрокаТаблицы Из ОбъектАдресаЗакупки Цикл
		НоваяСтрока = РегионыАдреса.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.ЭтоЗакупки = Истина;
		НоваяСтрока.Активный = АдресаЗакупкиВозможнаПриемка;
	КонецЦикла;
	
	ТорговыеПредложения.СинхронизироватьАдресныеНастройкиАбонента(РегионыАдреса, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОтветеНаВопросОСохраненииИзмененныхДанных(РезультатВопроса, ДопПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ЗаписатьДанныеИЗакрыть();
		
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		
		Модифицированность = Ложь;
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеСервиса()

	Отказ = Ложь;
	Данные = ТорговыеПредложения.ПолучитьАдресныеНастройкиАбонента(Отказ);
	Если Отказ ИЛИ ТипЗнч(Данные) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	РегионыПродажи.Очистить();
	РегионыЗакупки.Очистить();
	АдресаПродажи.Очистить();
	АдресаЗакупки.Очистить();
	
	Для Каждого СтрокаАдресаРегиона Из Данные Цикл
		
		Если СтрокаАдресаРегиона.addressType = 1 Тогда
			Если СтрокаАдресаРегиона.shipment Тогда
				НоваяСтрокаПродажи = РегионыПродажи.Добавить();
				НоваяСтрокаПродажи.Представление = СтрокаАдресаРегиона.addressInfo1;
				НоваяСтрокаПродажи.ЗначенияПолей = СтрокаАдресаРегиона.addressInfo2;
				РегионыПродажиВозможнаДоставка = Макс(РегионыПродажиВозможнаДоставка, СтрокаАдресаРегиона.active);
			КонецЕсли;
			
			Если СтрокаАдресаРегиона.entrance Тогда
				НоваяСтрокаЗакупки = РегионыЗакупки.Добавить();
				НоваяСтрокаЗакупки.Представление = СтрокаАдресаРегиона.addressInfo1;
				НоваяСтрокаЗакупки.ЗначенияПолей = СтрокаАдресаРегиона.addressInfo2;
				РегионыЗакупкиВозможенСамовывоз = Макс(РегионыЗакупкиВозможенСамовывоз, СтрокаАдресаРегиона.active);
			КонецЕсли;
		ИначеЕсли СтрокаАдресаРегиона.addressType = 2 Тогда
			Если СтрокаАдресаРегиона.shipment Тогда
				НоваяСтрокАдрес = АдресаПродажи.Добавить();
				НоваяСтрокАдрес.Представление = СтрокаАдресаРегиона.addressInfo1;
				НоваяСтрокАдрес.ЗначенияПолей = СтрокаАдресаРегиона.addressInfo2;
				АдресаПродажиВозможенСамовывоз = Макс(АдресаПродажиВозможенСамовывоз, СтрокаАдресаРегиона.active);
			КонецЕсли;
			Если СтрокаАдресаРегиона.entrance Тогда
				НоваяСтрокАдрес = АдресаЗакупки.Добавить();
				НоваяСтрокАдрес.Представление = СтрокаАдресаРегиона.addressInfo1;
				НоваяСтрокАдрес.ЗначенияПолей = СтрокаАдресаРегиона.addressInfo2;
				АдресаЗакупкиВозможнаПриемка = Макс(АдресаЗакупкиВозможнаПриемка, СтрокаАдресаРегиона.active);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборРегионовАдресовАбонента(ИмяСписка)

	ПараметрыОткрытия = Новый Структура;
	Если СтрНайти("РегионыПродажи, РегионыЗакупки", ИмяСписка) Тогда
		ПараметрыОткрытия.Вставить("РежимВыбора", "Регионы");
	КонецЕсли;
	
	ПараметрыОткрытия.Вставить("АдресТаблицыАдресов", АдресХранилищДанныхСписка(ИмяСписка));
	
	ДополнительныеПараметры = Новый Структура("ИмяСписка", ИмяСписка);
	Оповещение = Новый ОписаниеОповещения("ВыборРегионовАдресовЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.ТорговыеПредложения.Форма.ВыборРегионовАдресовАбонента",
		ПараметрыОткрытия, ЭтотОбъект,,,, Оповещение);

КонецПроцедуры
	
&НаКлиенте
Процедура ВыборРегионовАдресовЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("АдресТаблицыАдресов") Тогда
		Модифицированность = Истина;
		ОбновитьДанныеНаСервере(Результат.АдресТаблицыАдресов, ДополнительныеПараметры.ИмяСписка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеНаСервере(АдресТаблицыАдресов, ИмяСписка)
	
	// Необходимо обновить таблицу, с учетом существующих свойств.
	ТаблицаРезультата = ПолучитьИзВременногоХранилища(АдресТаблицыАдресов);
	Список = ЭтотОбъект[ИмяСписка]; // Регионы или Адреса.
	
	Для Каждого СтрокаРезультата Из ТаблицаРезультата Цикл
		
		СтрокиСписка = Список.НайтиСтроки(Новый Структура("Представление", СтрокаРезультата.Представление));
		Если СтрокиСписка.Количество() = 0 И СтрокаРезультата.Пометка Тогда
			// Добавление новой строки
			НоваяСтрока = Список.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРезультата);
		Иначе
			Если Не СтрокаРезультата.Пометка Тогда
				// Удаление не помеченной строки.
				Для Каждого УдаляемаяСтрокаСписка Из СтрокиСписка Цикл
					Список.Удалить(УдаляемаяСтрокаСписка);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДоступностьЭлементов(Объект)
	
	Объект.Элементы.РегионыПродажи.Доступность = Объект.РегионыПродажиВозможнаДоставка;
	Объект.Элементы.АдресаПродажи.Доступность =  Объект.АдресаПродажиВозможенСамовывоз;
	Объект.Элементы.РегионыЗакупки.Доступность = Объект.РегионыЗакупкиВозможенСамовывоз;
	Объект.Элементы.АдресаЗакупки.Доступность =  Объект.АдресаЗакупкиВозможнаПриемка;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ТребуетсяРегистрацияОрганизации Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Отсутствует регистрация в сервисе 1С:Бизнес-сеть.'"));
		Отказ = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
