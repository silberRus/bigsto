
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьПоУмолчаниюСервер();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоУмолчаниюСервер()
	
	Объект.АдресСайта      = "http://адрес_сайта.ru/bitrix/admin/1c_exchange.php";
	Объект.ИмяПользователя = "имя_пользователя";
	
	Объект.СоздаватьГруппы = Истина;
	Объект.СоздаватьСвойства = Истина;
	Объект.УстанавливатьЦены = Истина;
	
	Объект.Валюта = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Объект.ИнтервалОтправкиЗапросов = 2;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ВидыНоменклатуры.Ссылка
		|ИЗ
		|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|ГДЕ
		|	ВидыНоменклатуры.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|	И НЕ ВидыНоменклатуры.ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Объект.ВидНоменклатуры = Выборка.Ссылка;
	КонецЕсли;
	
	УстановитьСоздаватьХарактеристикиДоступностьПоВидуНоменклатурыСервер();
	
	Объект.СоздаватьХарактеристики = Объект.СоздаватьХарактеристикиДоступность;
	
	Объект.ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду("796");
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступностьВидимостьЭлементовСтраницы();
	ВидимостьВалюты();
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	ВыполнитьПереходДалее();
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПереходДалее()
	
	ОчиститьСообщения();
	
	ТекущаяСтраница = Элементы.ГруппаСтраницыФормы.ТекущаяСтраница;
	
	Если ТекущаяСтраница = Элементы.ГруппаСтраницаПараметрыСайта Тогда
		
		Если НЕ ПроверитьЗаполнение() Тогда
			Возврат;
		КонецЕсли;
		
		Состояние(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1 Производится установка соединения",
			Формат(ТекущаяДата(), "ДЛФ=DT"))
		,,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"с сервером ""%1""...",
			Объект.Сервер),
		БиблиотекаКартинок.ДлительнаяОперация48);
		
		Если НЕ ЕстьПодключениеСервер() Тогда
			Возврат;
		КонецЕсли;
		
		Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Элементы.ГруппаСтраницаПараметрыИмпорта;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ГруппаСтраницаПараметрыИмпорта Тогда
		
		РеквизитыЗаполнены = Истина;
		
		Если НЕ ЗначениеЗаполнено(Объект.ВидНоменклатуры) Тогда
			Сообщение = "Поле ""Вид номенклатуры"" не заполнено";
			Поле = "ВидНоменклатуры";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение,, Поле, "Объект");
			РеквизитыЗаполнены = Ложь;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.ЕдиницаИзмерения) Тогда
			Сообщение = "Поле ""Единица измерения"" не заполнено";
			Поле = "ЕдиницаИзмерения";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение,, Поле, "Объект");
			РеквизитыЗаполнены = Ложь;
		КонецЕсли;
		
		Если Объект.УстанавливатьЦены И НЕ ЗначениеЗаполнено(Объект.Валюта) Тогда
			Сообщение = "Поле ""Валюта"" не заполнено";
			Поле = "Валюта";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение,, Поле, "Объект");
			РеквизитыЗаполнены = Ложь;
		КонецЕсли;
		
		Если НЕ РеквизитыЗаполнены Тогда
			Возврат;
		КонецЕсли;
		
		Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Элементы.ГруппаСтраницаИмпорт;
		
	КонецЕсли;
	
	УстановитьДоступностьВидимостьЭлементовСтраницы();
	
КонецПроцедуры

&НаСервере
Функция ЕстьПодключениеСервер()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	СтруктураПараметровСайта = ОбработкаОбъект.ПолучитьСтруктуруПараметровСайта();
	
	Объект.Сервер = СтруктураПараметровСайта.Сервер;
	
	ОписаниеОшибки = "";
	
	Соединение = ОбработкаОбъект.ПолучитьСоединениеССервером(СтруктураПараметровСайта, ОписаниеОшибки);
	
	Если Соединение = Неопределено Тогда
		Сообщить("Ошибка при попытке установки соединения с сайтом.");
		Сообщить(ОписаниеОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	ОтветСервера = "";
	СтрокаСообщенияПользователю = "";
	
	Успешно = ОбработкаОбъект.АвторизацияВыполнена(Соединение, СтруктураПараметровСайта, ОтветСервера, СтрокаСообщенияПользователю);
	
	Если НЕ ПустаяСтрока(СтрокаСообщенияПользователю) Тогда 
		Сообщить(СтрокаСообщенияПользователю);
	КонецЕсли;
	
	Если НЕ Успешно Тогда
		Сообщить("Не удалось установить соединение с сайтом.");
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

//////////////////////////////////////////////////////////////////////////////////
&НаКлиенте
Процедура Назад(Команда)
	ВыполнитьПереходНазад();
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПереходНазад()
	
	ТекущаяСтраница = Элементы.ГруппаСтраницыФормы.ТекущаяСтраница;
	
	Если ТекущаяСтраница = Элементы.ГруппаСтраницаИмпорт Тогда
		
		Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Элементы.ГруппаСтраницаПараметрыИмпорта;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ГруппаСтраницаПараметрыИмпорта Тогда
		
		Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Элементы.ГруппаСтраницаПараметрыСайта;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ГруппаСтраницаПротокол Тогда
		
		Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Элементы.ГруппаСтраницаПараметрыИмпорта;
		
	КонецЕсли;
	
	УстановитьДоступностьВидимостьЭлементовСтраницы();
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////
&НаКлиенте
Процедура НачатьИмпорт(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Состояние(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1 выполняется импорт данных",
			Формат(ТекущаяДата(), "ДЛФ=DT"))
		,,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"с сайта ""%1""...",
			Объект.Сервер),
		БиблиотекаКартинок.ДлительнаяОперация48);
	
	Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Элементы.ГруппаСтраницаПротокол;
	УстановитьДоступностьВидимостьЭлементовСтраницы();
	
	Импорт();
	
	ПоказатьОповещениеПользователя(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1 ""%2""",
			Формат(ТекущаяДата(), "ДЛФ=DT"),
			Объект.Сервер)
		,,
		"Импорт товаров с сайта завершен",
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаКлиенте
Процедура Импорт()
	
	Протокол = "";
	
	ОбновитьОтображениеДанных();
	
	Протокол = Протокол
		+ "Получение данных с сайта:";
	
	ВсегоТоваров = 0;
	ИмпортВыполнен = ИмпортВыполненСервер(ВсегоТоваров);
	
	Если НЕ ИмпортВыполнен Тогда
		Возврат;
	КонецЕсли;
	
	Если ВсегоТоваров = 0 Тогда
		Сообщить("С сайта не получено ни одного товара.");
		Возврат;
	КонецЕсли;
	
	Ответ = Вопрос("С сайта получено " + ВсегоТоваров + " товаров."
		+ Символы.ПС
		+ "Загрузить товары в информационную базу?", РежимДиалогаВопрос.ДаНет);
	
	Если НЕ Ответ = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Состояние(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1 выполняется загрузка товаров в базу...",
			Формат(ТекущаяДата(), "ДЛФ=DT"))
		,,,
		БиблиотекаКартинок.ДлительнаяОперация48);
	
	Протокол = Протокол
		+ Символы.ПС
		+ Символы.ПС
		+ "Загрузка товаров в базу 1С:";
	
	КоличествоКартинок = 0;
	СоздатьТоварыСервер(КоличествоКартинок);
	
	Если Объект.ЗагружатьКартинки
		И КоличествоКартинок > 0 Тогда
		
		Ответ = Вопрос("Загрузка товаров завершена. Система готова к загрузке картинок."
			+ Символы.ПС + "Начать загрузку " + КоличествоКартинок + " изображений?", РежимДиалогаВопрос.ДаНет);
		
		Если НЕ Ответ = КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		
		Состояние(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"%1 выполняется загрузка изображений к товарам...",
				Формат(ТекущаяДата(), "ДЛФ=DT"))
			,,,
			БиблиотекаКартинок.ДлительнаяОперация48);
		
		ЗагрузитьКартинкиСервер();
		
	КонецЕсли;
	
	Протокол = Протокол
		+ Символы.ПС
		+ Символы.ПС
		+ "Импорт товаров завершен.";
	
КонецПроцедуры

&НаСервере
Функция ИмпортВыполненСервер(ВсегоТоваров)
	Возврат РеквизитФормыВЗначение("Объект").ИмпортВыполнен(ВсегоТоваров, АдресВХранилищеСтруктураДанных, Протокол);
КонецФункции

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура УстанавливатьЦеныПриИзменении(Элемент)
	
	ВидимостьВалюты();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидНоменклатурыПриИзменении(Элемент)
	
	УстановитьСоздаватьХарактеристикиДоступностьПоВидуНоменклатурыСервер();
	Элементы.СоздаватьХарактеристики.Доступность = Объект.СоздаватьХарактеристикиДоступность;
	Элементы.НадписьИнформацияПоХарактеристикам.Видимость = НЕ Объект.СоздаватьХарактеристикиДоступность И ЗначениеЗаполнено(Объект.ВидНоменклатуры);
	
КонецПроцедуры


#КонецОбласти


#Область ПрочиеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьВидимостьЭлементовСтраницы()
	
	Если Элементы.ГруппаСтраницыФормы.ТекущаяСтраница
		= Элементы.ГруппаСтраницаПараметрыСайта Тогда
		
		Элементы.ФормаДалее.Доступность = Истина;
		Элементы.ФормаНазад.Доступность = Ложь;
		
	ИначеЕсли Элементы.ГруппаСтраницыФормы.ТекущаяСтраница
		= Элементы.ГруппаСтраницаПараметрыИмпорта Тогда
		
		Элементы.ФормаДалее.Доступность = Истина;
		Элементы.ФормаНазад.Доступность = Истина;
		
		Элементы.СоздаватьХарактеристики.Доступность = Объект.СоздаватьХарактеристикиДоступность;
		Элементы.НадписьИнформацияПоХарактеристикам.Видимость = НЕ Объект.СоздаватьХарактеристикиДоступность И ЗначениеЗаполнено(Объект.ВидНоменклатуры);
		
	ИначеЕсли Элементы.ГруппаСтраницыФормы.ТекущаяСтраница
		= Элементы.ГруппаСтраницаИмпорт Тогда
		
		Элементы.ФормаДалее.Доступность = Ложь;
		Элементы.ФормаНазад.Доступность = Истина;
		
		Элементы.НадписьИмпортОписание.Заголовок = 
			СтрЗаменить(Элементы.НадписьИмпортОписание.Заголовок, "{Сайт}", Объект.Сервер);
			
	ИначеЕсли Элементы.ГруппаСтраницыФормы.ТекущаяСтраница
		= Элементы.ГруппаСтраницаПротокол Тогда
		
		Элементы.ФормаДалее.Доступность = Ложь;
		Элементы.ФормаНазад.Доступность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьВалюты()
	Элементы.Валюта.Видимость = Объект.УстанавливатьЦены;
КонецПроцедуры


&НаСервере
Процедура СоздатьТоварыСервер(КоличествоКартинок)
	РеквизитФормыВЗначение("Объект").СоздатьТовары(КоличествоКартинок, АдресВХранилищеСтруктураДанных, Протокол);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьКартинкиСервер()
	РеквизитФормыВЗначение("Объект").ЗагрузитьКартинки(АдресВХранилищеСтруктураДанных, Протокол);
КонецПроцедуры

&НаСервере
Процедура УстановитьСоздаватьХарактеристикиДоступностьПоВидуНоменклатурыСервер()
	
	Объект.СоздаватьХарактеристикиДоступность =
	ЗначениеЗаполнено(Объект.ВидНоменклатуры)
	И (НЕ Объект.ВидНоменклатуры.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать);
	
КонецПроцедуры

		
	#КонецОбласти
	
		