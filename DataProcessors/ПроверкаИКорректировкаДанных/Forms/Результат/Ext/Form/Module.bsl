
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.Заголовок;
	Элементы.ДекорацияПредставлениеРезультата.Заголовок = Параметры.ПредставлениеРезультата;
	
	Если ЗначениеЗаполнено(Параметры.ТабличныйДокумент) Тогда
		
		ИмяСправочника = "ИсторияПроверкиИКорректировкиДанныхПрисоединенныеФайлы";
		Если Метаданные.Справочники.Найти(ИмяСправочника) <> Неопределено
			И ТипЗнч(Параметры.ТабличныйДокумент) = Тип("СправочникСсылка." + ИмяСправочника) Тогда
			
			НачатьТранзакцию();
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.ИсторияПроверкиИКорректировкиДанныхПрисоединенныеФайлы");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Параметры.ТабличныйДокумент);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			Попытка
				Блокировка.Заблокировать();
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение СообщениеФайлУдален();
				ОписаниеОшибки = ОписаниеОшибки();
			КонецПопытки;
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("Ссылка", Параметры.ТабличныйДокумент);
			Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИсторияПроверкиИКорректировкиДанныхПрисоединенныеФайлы.Ссылка
			|ИЗ
			|	Справочник.ИсторияПроверкиИКорректировкиДанныхПрисоединенныеФайлы КАК ИсторияПроверкиИКорректировкиДанныхПрисоединенныеФайлы
			|ГДЕ
			|	ИсторияПроверкиИКорректировкиДанныхПрисоединенныеФайлы.Ссылка = &Ссылка";
			Если Запрос.Выполнить().Пустой() Тогда
				ОтменитьТранзакцию();
				ВызватьИсключение СообщениеФайлУдален();
			КонецЕсли;
			
			ТабличныйДокумент = ПроверкаИКорректировкаДанных.ТабличныйДокументИзПрисоединенногоФайла(Параметры.ТабличныйДокумент);
			ОтменитьТранзакцию();
			
		ИначеЕсли ЭтоАдресВременногоХранилища(Параметры.ТабличныйДокумент) Тогда
			
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла("mxl");
			ДвоичныеДанные = ПолучитьИзВременногоХранилища(Параметры.ТабличныйДокумент);
			ДвоичныеДанные.Записать(ИмяВременногоФайла);
			ТабличныйДокумент.Прочитать(ИмяВременногоФайла);
			УдалитьФайлы(ИмяВременногоФайла);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СообщениеФайлУдален()
	
	Возврат НСтр("ru = 'Файл уже удален, возможно в другом сеансе была запущена проверка.'");
	
КонецФункции

#КонецОбласти
