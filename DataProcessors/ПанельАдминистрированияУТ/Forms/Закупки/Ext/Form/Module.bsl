&НаКлиенте
Перем ОбновитьИнтерфейс;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Значения реквизитов формы
	СоставНабораКонстантФормы    = ОбщегоНазначенияУТ.ПолучитьСтруктуруНабораКонстант(НаборКонстант);
	ВнешниеРодительскиеКонстанты = ОбщегоНазначенияУТПовтИсп.ПолучитьСтруктуруРодительскихКонстант(СоставНабораКонстантФормы);
	РежимРаботы = Новый Структура;
	
	ВнешниеРодительскиеКонстанты.Вставить("ИспользоватьВерсионированиеОбъектов");
	ВнешниеРодительскиеКонстанты.Вставить("ИспользоватьСинхронизациюДанных");
	ВнешниеРодительскиеКонстанты.Вставить("ИспользоватьЗаказыКлиентов");
	ВнешниеРодительскиеКонстанты.Вставить("ИспользоватьЗаказыНаВнутреннееПотребление");
	ВнешниеРодительскиеКонстанты.Вставить("ИспользоватьЗаказыНаСборку");
	ВнешниеРодительскиеКонстанты.Вставить("ИспользоватьЗаявкиНаВозвратТоваровОтКлиентов");
	ВнешниеРодительскиеКонстанты.Вставить("ИспользоватьПередачиТоваровМеждуОрганизациями");
	ВнешниеРодительскиеКонстанты.Вставить("ИспользоватьНесколькоСкладов");
	ВнешниеРодительскиеКонстанты.Вставить("ПартионныйУчетВерсии22");
	ВнешниеРодительскиеКонстанты.Вставить("УчитыватьСебестоимостьТоваровПоВидамЗапасов");
	ВнешниеРодительскиеКонстанты.Вставить("ИспользоватьТоварыВПутиОтПоставщиков");
	ВнешниеРодительскиеКонстанты.Вставить("ИспользоватьНеотфактурованныеПоставки");
	
	РежимРаботы.Вставить("СоставНабораКонстантФормы",    Новый ФиксированнаяСтруктура(СоставНабораКонстантФормы));
	РежимРаботы.Вставить("ВнешниеРодительскиеКонстанты", Новый ФиксированнаяСтруктура(ВнешниеРодительскиеКонстанты));
	
	РежимРаботы = Новый ФиксированнаяСтруктура(РежимРаботы);
	
	ИспользоватьКомплекснуюАвтоматизацию = ПолучитьФункциональнуюОпцию("КомплекснаяАвтоматизация");
	ИспользоватьУправлениеПредприятием   = ПолучитьФункциональнуюОпцию("УправлениеПредприятием");
	
	Элементы.ГруппаИспользоватьОтветственноеХранениеВПроцессеЗакупки.Видимость = ИспользоватьКомплекснуюАвтоматизацию
																					Или ИспользоватьУправлениеПредприятием;
	
	
	// Обновление состояния элементов
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	ОбновитьИнтерфейсПрограммы();
КонецПроцедуры

&НаКлиенте
// Обработчик оповещения формы.
//
// Параметры:
//	ИмяСобытия - Строка - обрабатывается только событие Запись_НаборКонстант, генерируемое панелями администрирования.
//	Параметр   - Структура - содержит имена констант, подчиненных измененной константе, "вызвавшей" оповещение.
//	Источник   - Строка - имя измененной константы, "вызвавшей" оповещение.
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия <> "Запись_НаборКонстант" Тогда
		Возврат; // такие событие не обрабатываются
	КонецЕсли;
	
	// Если это изменена константа, расположенная в другой форме и влияющая на значения констант этой формы,
	// то прочитаем значения констант и обновим элементы этой формы.
	Если РежимРаботы.ВнешниеРодительскиеКонстанты.Свойство(Источник)
	 ИЛИ (ТипЗнч(Параметр) = Тип("Структура")
	 		И ОбщегоНазначенияУТКлиентСервер.ПолучитьОбщиеКлючиСтруктур(
	 			Параметр, РежимРаботы.ВнешниеРодительскиеКонстанты).Количество() > 0) Тогда
		
		ЭтаФорма.Прочитать();
		УстановитьДоступность();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьЗаказыПоставщикамПриИзменении(Элемент)
	
	Если НаборКонстант.ИспользоватьЗаказыПоставщикам И НаборКонстант.ИспользоватьЗаявкиНаРасходованиеДенежныхСредств Тогда
		
		УстановитьИспользованиеЗаказовПоставщикамИЗаявокНаРасходованиеДС(Истина);
		
	Иначе
		
		УстановитьИспользованиеЗаказовПоставщикамИЗаявокНаРасходованиеДС(Ложь);
		
	КонецЕсли;

	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьПоступлениеПоНесколькимЗаказамПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура НеЗакрыватьЗаказыПоставщикамБезПолногоПоступленияПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура НеЗакрыватьЗаказыПоставщикамБезПолнойОплатыПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАктыОРасхожденияхПослеПриемкиПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАктыОРасхожденияхПослеОтгрузкиПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьКомиссиюПриЗакупкахПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтветственноеХранениеСПравомПродажиПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАктыРасхожденийПослеПриемкиПоПриемкамТоваровНаХранениеПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАктыРасхожденийПослеОтгрузкиПоОтгрузкамТоваровСХраненияПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьИмпортныеТоварыПриИзменении(Элемент)
	
	Если НаборКонстант.ИспользоватьТоварыВПутиОтПоставщиков И НаборКонстант.ИспользоватьИмпортныеТовары Тогда
		
		УстановитьИспользованиеКонтролироватьОстаткиНомеровГТДПриИмпортеПоСхемеТоварыВПути(Истина);
		
	Иначе
		
		УстановитьИспользованиеКонтролироватьОстаткиНомеровГТДПриИмпортеПоСхемеТоварыВПути(Ложь);
		
	КонецЕсли;
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьНеотфактурованныеПоставкиПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьТоварыВПутиОтПоставщиковПриИзменении(Элемент)
	
	Если НаборКонстант.ИспользоватьТоварыВПутиОтПоставщиков И НаборКонстант.ИспользоватьИмпортныеТовары Тогда
		
		УстановитьИспользованиеКонтролироватьОстаткиНомеровГТДПриИмпортеПоСхемеТоварыВПути(Истина);
		
	Иначе
		
		УстановитьИспользованиеКонтролироватьОстаткиНомеровГТДПриИмпортеПоСхемеТоварыВПути(Ложь);
		
	КонецЕсли;
	
	Подключаемый_ПриИзмененииРеквизита(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПокупкаТоваровОблагаемыхНДСУПокупателяПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура НаборКонстантКонтролироватьОстаткиНомеровГТДПриЗакупкеУПоставщикаПоСхемеТоварыВПутиПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура НаборКонстантКонтролироватьОстаткиНомеровГТДПриИмпортеПоСхемеТоварыВПутиПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура НаборКонстантПроверятьЗаполнениеНомеровГТДПриЗакупкеУПоставщикаПоСхемеТоварыВПути(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьИмпортныеЗакупкиПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьКорректировкиПриобретенийПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура НаборКонстантЗапретитьОформлениеОперацийСИмпортнымиТоварамиБезНомеровГТДПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура НаборКонстантЗапретитьПоступлениеТоваровБезНомеровГТДПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСогласованиеЗаказовПоставщикамПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСтатусыЗаказовПоставщикамПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьПричиныОтменыЗаказовПоставщикамПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСкладыВТабличнойЧастиДокументовЗакупкиПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеЭтаповОплатыВЗакупкахПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура КонтролироватьОстаткиТоваровОрганизацийКОформлениюПоПоступлениямПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСоглашенияСПоставщикамиПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДоговорыСПоставщикамиПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура АвтоПостановкаПоставщиковНаМониторингСПАРКПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СписаниеПриПередачеВПроизводствоПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СписаниеПриРасходеНаВыпускПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьВерсионированиеДокументовЗакупки(Команда)
	
	Результат = Новый Структура;
	СохранитьЗначениеРеквизита("ВключитьВерсионированиеДокументовЗакупки", Результат);
	
	Если Результат.Свойство("ВерсионированиеВключено") Тогда
		
		Если Результат.ВерсионированиеВключено Тогда
			Пояснение = НСтр("ru = 'Для документов заказов поставщику установлен вариант версионирования ""Версионировать при проведении""'");
		Иначе
			Пояснение = НСтр("ru = 'Для документов заказов поставщику версионирование уже было включено'");
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Версионирование включено'"),, Пояснение, БиблиотекаКартинок.Информация32);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьОтветственныхЗаСогласованиеЛогистическихУсловийДокументовЗакупки(Команда)
	ОткрытьФорму("РегистрСведений.ИсполнителиЗадач.Форма.ИсполнителиРолиСОбъектомАдресации", 
		Новый Структура("ОсновнойОбъектАдресации,Роль", 
			Неопределено, 
			ПредопределенноеЗначение("Справочник.РолиИсполнителей.СогласующийЛогистическиеУсловияЗакупок")));
КонецПроцедуры

&НаКлиенте
Процедура НазначитьОтветственныхЗаСогласованиеФинансовыхУсловийДокументовЗакупки(Команда)
	ОткрытьФорму("РегистрСведений.ИсполнителиЗадач.Форма.ИсполнителиРолиСОбъектомАдресации", 
		Новый Структура("ОсновнойОбъектАдресации,Роль", 
			Неопределено, 
			ПредопределенноеЗначение("Справочник.РолиИсполнителей.СогласующийФинансовыеУсловияЗакупок")));
КонецПроцедуры

&НаКлиенте
Процедура НазначитьОтветственныхЗаСогласованиеЦеновыхУсловийДокументовЗакупки(Команда)
	ОткрытьФорму("РегистрСведений.ИсполнителиЗадач.Форма.ИсполнителиРолиСОбъектомАдресации", 
		Новый Структура("ОсновнойОбъектАдресации,Роль", 
			Неопределено, 
			ПредопределенноеЗначение("Справочник.РолиИсполнителей.СогласующийЦеновыеУсловияЗакупок")));
КонецПроцедуры

&НаКлиенте
Процедура НазначитьОтветственныхЗаСогласованиеКоммерческихУсловийДокументовЗакупки(Команда)
	ОткрытьФорму("РегистрСведений.ИсполнителиЗадач.Форма.ИсполнителиРолиСОбъектомАдресации", 
		Новый Структура("ОсновнойОбъектАдресации,Роль", 
			Неопределено, 
			ПредопределенноеЗначение("Справочник.РолиИсполнителей.СогласующийКоммерческиеУсловияЗакупок")));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Клиент

&НаКлиенте
Процедура Подключаемый_ПриИзмененииРеквизита(Элемент, ОбновлятьИнтерфейс = Истина)
	
	КонстантаИмя = ПриИзмененииРеквизитаСервер(Элемент.Имя);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Если ОбновлятьИнтерфейс Тогда
		ОбновитьИнтерфейс = Истина;
		ПодключитьОбработчикОжидания("ОбновитьИнтерфейсПрограммы", 2, Истина);
	КонецЕсли;
	
	Если КонстантаИмя <> "" Тогда
		Оповестить("Запись_НаборКонстант", Новый Структура, КонстантаИмя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнтерфейсПрограммы()
	
	Если ОбновитьИнтерфейс = Истина Тогда
		ОбновитьИнтерфейс = Ложь;
		ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВызовСервера

&НаСервере
Функция ПриИзмененииРеквизитаСервер(ИмяЭлемента)
	
	РеквизитПутьКДанным = Элементы[ИмяЭлемента].ПутьКДанным;
	
	КонстантаИмя = СохранитьЗначениеРеквизита(РеквизитПутьКДанным, Новый Структура);
	
	УстановитьДоступность(РеквизитПутьКДанным);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат КонстантаИмя;
	
КонецФункции

#КонецОбласти

#Область Сервер

&НаСервере
Функция СохранитьЗначениеРеквизита(РеквизитПутьКДанным, Результат)
	
	// Сохранение значений реквизитов, не связанных с константами напрямую (в отношении один-к-одному).
	Если РеквизитПутьКДанным = "" Тогда
		Возврат "";
	КонецЕсли;
	
	// Определение имени константы.
	КонстантаИмя = "";
	Если НРег(Лев(РеквизитПутьКДанным, 14)) = НРег("НаборКонстант.") Тогда
		// Если путь к данным реквизита указан через "НаборКонстант".
		КонстантаИмя = Сред(РеквизитПутьКДанным, 15);
	Иначе
		// Определение имени и запись значения реквизита в соответствующей константе из "НаборКонстант".
		// Используется для тех реквизитов формы, которые связаны с константами напрямую (в отношении один-к-одному).
		Если РеквизитПутьКДанным = "ВключитьВерсионированиеДокументовЗакупки" Тогда
			НаборКонстант.ИспользоватьВерсионированиеОбъектов = Истина;
			КонстантаИмя = "ИспользоватьВерсионированиеОбъектов";
		КонецЕсли;

	КонецЕсли;
	
	// Сохранения значения константы.
	Если КонстантаИмя <> "" Тогда
		КонстантаМенеджер = Константы[КонстантаИмя];
		КонстантаЗначение = НаборКонстант[КонстантаИмя];
		
		Если КонстантаМенеджер.Получить() <> КонстантаЗначение Тогда
			КонстантаМенеджер.Установить(КонстантаЗначение);
		КонецЕсли;
		
		Если ОбщегоНазначенияУТПовтИсп.ЕстьПодчиненныеКонстанты(КонстантаИмя, КонстантаЗначение) Тогда
			ЭтаФорма.Прочитать();
		КонецЕсли;
		
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "ВключитьВерсионированиеДокументовЗакупки" Тогда
		
		ОбъектыДляВерсионирования = Новый Соответствие;
		
		Если НаборКонстант.ИспользоватьЗаказыПоставщикам Тогда
			ОбъектыДляВерсионирования.Вставить("Документ.ЗаказПоставщику", "ВерсионироватьПриПроведении");
		КонецЕсли;
		
		Результат.Вставить("ВерсионированиеВключено",
			ОбщегоНазначенияУТ.ВключитьВерсионированиеОбъектов(ОбъектыДляВерсионирования));
		УстановитьДоступностьВерсионированияДокументовЗакупки();
	КонецЕсли;
	
	
	Возврат КонстантаИмя
	
КонецФункции

&НаСервере
Процедура УстановитьДоступность(РеквизитПутьКДанным = "")
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьЗаказыПоставщикам" ИЛИ РеквизитПутьКДанным = "" Тогда
		ЗначениеКонстанты = НаборКонстант.ИспользоватьЗаказыПоставщикам;
		ЕстьОбменыСФормированиемДоговоровПоЗаказам =
			Константы.ИспользоватьСинхронизациюДанных.Получить() И ОбменДаннымиУТУП.ЕстьОбменыСФормированиемДоговоровПоЗаказам();
		
		Элементы.ИспользоватьПоступлениеПоНесколькимЗаказам.Доступность 				= ЗначениеКонстанты И НЕ ЕстьОбменыСФормированиемДоговоровПоЗаказам;
		Элементы.НеЗакрыватьЗаказыПоставщикамБезПолногоПоступления.Доступность 			= ЗначениеКонстанты И НаборКонстант.ИспользоватьСтатусыЗаказовПоставщикам;
		Элементы.НеЗакрыватьЗаказыПоставщикамБезПолнойОплаты.Доступность				= ЗначениеКонстанты И НаборКонстант.ИспользоватьСтатусыЗаказовПоставщикам;
		Элементы.ИспользоватьСогласованиеЗаказовПоставщикам.Доступность 				= ЗначениеКонстанты И НаборКонстант.ИспользоватьСтатусыЗаказовПоставщикам;
		Элементы.ГруппаКомментарийИспользоватьПоступлениеПоНесколькимЗаказам.Видимость 	= ЕстьОбменыСФормированиемДоговоровПоЗаказам;
		Элементы.ИспользоватьСтатусыЗаказовПоставщикам.Доступность                      = ЗначениеКонстанты;
		Элементы.ИспользоватьПричиныОтменыЗаказовПоставщикам.Доступность                = ЗначениеКонстанты;
		Элементы.ГруппаКомментарийКонтролироватьЗакрытие.Видимость                      = ЗначениеКонстанты И НЕ НаборКонстант.ИспользоватьСтатусыЗаказовПоставщикам;
		
		УстановитьДоступностьВерсионированияДокументовЗакупки();
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьСогласованиеЗаказовПоставщикам" ИЛИ РеквизитПутьКДанным = "" Тогда
		ЗначениеКонстанты = НаборКонстант.ИспользоватьСогласованиеЗаказовПоставщикам;
		
		Элементы.НазначитьОтветственныхЗаСогласованиеЛогистическихУсловийДокументовЗакупки.Доступность = ЗначениеКонстанты;
		Элементы.НазначитьОтветственныхЗаСогласованиеФинансовыхУсловийДокументовЗакупки.Доступность    = ЗначениеКонстанты;
		Элементы.НазначитьОтветственныхЗаСогласованиеЦеновыхУсловийДокументовЗакупки.Доступность       = ЗначениеКонстанты;
		Элементы.НазначитьОтветственныхЗаСогласованиеКоммерческихУсловийДокументовЗакупки.Доступность  = ЗначениеКонстанты;
		
		УстановитьДоступностьВерсионированияДокументовЗакупки();
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьСтатусыЗаказовПоставщикам" ИЛИ РеквизитПутьКДанным = "" Тогда
		ЗначениеКонстанты = НаборКонстант.ИспользоватьСтатусыЗаказовПоставщикам;
		
		Элементы.ИспользоватьСогласованиеЗаказовПоставщикам.Доступность        = НаборКонстант.ИспользоватьЗаказыПоставщикам И ЗначениеКонстанты;
		Элементы.НеЗакрыватьЗаказыПоставщикамБезПолногоПоступления.Доступность = ЗначениеКонстанты;
		Элементы.НеЗакрыватьЗаказыПоставщикамБезПолнойОплаты.Доступность       = ЗначениеКонстанты;
		Элементы.ГруппаКомментарийКонтролироватьЗакрытие.Видимость             = НаборКонстант.ИспользоватьЗаказыПоставщикам И НЕ ЗначениеКонстанты;
			
		УстановитьДоступностьВерсионированияДокументовЗакупки();
		
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьТоварыВПутиОтПоставщиков" ИЛИ РеквизитПутьКДанным = "" Тогда
		ЗначениеКонстанты = НаборКонстант.ИспользоватьТоварыВПутиОтПоставщиков;
		
		Элементы.ПроверятьЗаполнениеНомеровГТДПриЗакупкеУПоставщикаПоСхемеТоварыВПути.Доступность   = ЗначениеКонстанты;
		Элементы.КонтролироватьОстаткиНомеровГТДПриЗакупкеУПоставщикаПоСхемеТоварыВПути.Доступность = ЗначениеКонстанты;
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьИмпортныеТовары" ИЛИ РеквизитПутьКДанным = "" Тогда
		ЗначениеКонстанты = НаборКонстант.ИспользоватьИмпортныеТовары;
		
		Элементы.ИспользоватьИмпортныеЗакупки.Доступность                                      = ЗначениеКонстанты;
		Элементы.ЗапретитьОформлениеОперацийСИмпортнымиТоварамиБезНомеровГТД.Доступность       = ЗначениеКонстанты;
		Элементы.ЗапретитьПоступлениеТоваровБезНомеровГТД.Доступность                          = ЗначениеКонстанты;
		Элементы.КонтролироватьОстаткиНомеровГТДПриИмпортеПоСхемеТоварыВПути.Доступность       = ЗначениеКонстанты;
		Элементы.КонтролироватьОстаткиТоваровОрганизацийКОформлениюПоПоступлениям.Доступность  = НаборКонстант.ИспользоватьИмпортныеЗакупки;
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьИмпортныеЗакупки" ИЛИ РеквизитПутьКДанным = "" Тогда
		ЗначениеКонстанты = НаборКонстант.ИспользоватьИмпортныеЗакупки;
		
		Элементы.КонтролироватьОстаткиТоваровОрганизацийКОформлениюПоПоступлениям.Доступность  = ЗначениеКонстанты;
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьВерсионированиеОбъектов" ИЛИ РеквизитПутьКДанным = "" Тогда
		УстановитьДоступностьВерсионированияДокументовЗакупки();
	КонецЕсли;

	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьСкладыВТабличнойЧастиДокументовЗакупки" ИЛИ РеквизитПутьКДанным = "" Тогда
		ЗначениеКонстанты = НаборКонстант.ИспользоватьНесколькоСкладов;
		
		Элементы.ИспользоватьСкладыВТабличнойЧастиДокументовЗакупки.Доступность = ЗначениеКонстанты;
		Элементы.ГруппаКомментарийИспользоватьСкладыВТабличнойЧастиДокументовЗакупки.Видимость = Не ЗначениеКонстанты;
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "" Тогда
		ЗначениеКонстанты = Константы.ИспользоватьПродажуАгентскихУслуг.Получить();
		Элементы.ИспользоватьКомиссиюПриЗакупках.Доступность = НЕ ЗначениеКонстанты;
		Элементы.ГруппаКомментарийИспользоватьКомиссионныеЗакупки.Видимость = ЗначениеКонстанты;
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьДоговорыСПоставщиками" ИЛИ РеквизитПутьКДанным = "" Тогда
		
		ЗначениеКонстантыОтветХранение 				= Ложь;
		ЗначениеКонстанты275ФЗ         				= Ложь;
		ЗначениеКонстантыТоварыВПутиОтПоставщиков 	= НаборКонстант.ИспользоватьТоварыВПутиОтПоставщиков;
		ЗначениеКонстантыНеотфактурованныеПоставки 	= НаборКонстант.ИспользоватьНеотфактурованныеПоставки;
		
		ДоговорыСПоставщикамиРедактированиеДоступно = Не ЗначениеКонстанты275ФЗ 
													И Не ЗначениеКонстантыОтветХранение
													И Не ЗначениеКонстантыТоварыВПутиОтПоставщиков
													И Не ЗначениеКонстантыНеотфактурованныеПоставки;
			
		Элементы.ИспользоватьДоговорыСПоставщиками.Доступность 										= ДоговорыСПоставщикамиРедактированиеДоступно;
		Элементы.ГруппаДоговорыСПоставщикамиНевозможноОтключить.Видимость 							= Не ДоговорыСПоставщикамиРедактированиеДоступно;
		Элементы.КомментарийИспользоватьДоговорыСПоставщиками275ФЗ.Видимость 						= ЗначениеКонстанты275ФЗ;
		Элементы.КомментарийИспользоватьДоговорыСПоставщикамиПриемкаНаХранение.Видимость 			= ЗначениеКонстантыОтветХранение;
		Элементы.КомментарийИспользоватьДоговорыСПоставщикамиТоварыВПути.Видимость 					= ЗначениеКонстантыТоварыВПутиОтПоставщиков;
		Элементы.КомментарийИспользоватьДоговорыСПоставщикамиНеотфактурованныеПоставки.Видимость 	= ЗначениеКонстантыНеотфактурованныеПоставки;
		
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьДоговорыСПоставщиками" ИЛИ РеквизитПутьКДанным = "" Тогда
		ЗначениеКонстанты = Константы.ИспользоватьДоговорыСПоставщиками.Получить();
		Элементы.ГруппаАвтоПостановкаПоставщиковНаМониторингСПАРК.Доступность = ЗначениеКонстанты;
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ИспользоватьТоварыВПутиОтПоставщиков"
		Или РеквизитПутьКДанным = "НаборКонстант.ИспользоватьНеотфактурованныеПоставки"
		Или РеквизитПутьКДанным = "НаборКонстант.ЗапретитьПоступлениеТоваровОтПартнеровБезНомеровГТД"
		Или РеквизитПутьКДанным = "" Тогда
		
		ЗначениеКонстантыСебестоимостьПоВидамЗапасов = Константы.УчитыватьСебестоимостьТоваровПоВидамЗапасов.Получить();
		ЗначениеКонстантыПартионныйУчетВерсии22 = Константы.ПартионныйУчетВерсии22.Получить();
		
		ЗначениеКонстантыИспользоватьТоварыВПутиОтПоставщиков = Константы.ИспользоватьТоварыВПутиОтПоставщиков.Получить();
		ЗначениеКонстантыИспользоватьИмпортныеЗакупки = Константы.ИспользоватьИмпортныеТовары.Получить();
		
		// Настройка товаров в пути
		Элементы.ИспользоватьТоварыВПутиОтПоставщиков.Доступность = 
				ЗначениеКонстантыСебестоимостьПоВидамЗапасов И 		ЗначениеКонстантыПартионныйУчетВерсии22;
		Элементы.ГруппаКомментарийИспользоватьТоварыВПутиОтПоставщиков.Видимость = 
			Не (ЗначениеКонстантыСебестоимостьПоВидамЗапасов И		ЗначениеКонстантыПартионныйУчетВерсии22);
		Элементы.КомментарийИспользоватьТоварыВПутиОтПоставщиков.Видимость =
			Не	ЗначениеКонстантыСебестоимостьПоВидамЗапасов И Не	ЗначениеКонстантыПартионныйУчетВерсии22;
		Элементы.КомментарийИспользоватьТоварыВПутиОтПоставщиковПоВидамЗапасов.Видимость =
			Не	ЗначениеКонстантыСебестоимостьПоВидамЗапасов И	 	ЗначениеКонстантыПартионныйУчетВерсии22;
		Элементы.КомментарийИспользоватьТоварыВПутиОтПоставщиковПартионныйУчет22.Видимость =
			 	ЗначениеКонстантыСебестоимостьПоВидамЗапасов И Не	ЗначениеКонстантыПартионныйУчетВерсии22;
		
		// Настройка неотфактурованных поставок
		Элементы.ИспользоватьНеотфактурованныеПоставки.Доступность = 
				ЗначениеКонстантыСебестоимостьПоВидамЗапасов И 		ЗначениеКонстантыПартионныйУчетВерсии22;
		Элементы.ГруппаКомментарийИспользоватьНеотфактурованныеПоставки.Видимость = 
			Не (ЗначениеКонстантыСебестоимостьПоВидамЗапасов И		ЗначениеКонстантыПартионныйУчетВерсии22);
		Элементы.КомментарийИспользоватьНеотфактурованныеПоставки.Видимость =
			Не	ЗначениеКонстантыСебестоимостьПоВидамЗапасов И Не	ЗначениеКонстантыПартионныйУчетВерсии22;
		Элементы.КомментарийИспользоватьНеотфактурованныеПоставкиПоВидамЗапасов.Видимость =
			Не	ЗначениеКонстантыСебестоимостьПоВидамЗапасов И  	ЗначениеКонстантыПартионныйУчетВерсии22;
		Элементы.КомментарийИспользоватьНеотфактурованныеПоставкиПартионныйУчет22.Видимость =
			 	ЗначениеКонстантыСебестоимостьПоВидамЗапасов И Не	ЗначениеКонстантыПартионныйУчетВерсии22;
		
		Элементы.КонтролироватьОстаткиНомеровГТДПриИмпортеПоСхемеТоварыВПути.Доступность = 
			ЗначениеКонстантыИспользоватьИмпортныеЗакупки И ЗначениеКонстантыИспользоватьТоварыВПутиОтПоставщиков;
		Элементы.ГруппаКомментарийЗапретитьПоступлениеТоваровОтПартнеровБезНомеровГТД.Видимость =
			ЗначениеКонстантыИспользоватьИмпортныеЗакупки И Не ЗначениеКонстантыИспользоватьТоварыВПутиОтПоставщиков;
		
	КонецЕсли;
	
	
	ОбменДаннымиУТУП.УстановитьДоступностьНастроекУзлаИнформационнойБазы(ЭтаФорма);
	
	ОтображениеПредупрежденияПриРедактировании(РеквизитПутьКДанным);
	
КонецПроцедуры

&НаСервере
Процедура ОтображениеПредупрежденияПриРедактировании(РеквизитПутьКДанным)
	
	СтруктураКонстант = Новый Структура(
		"ИспользоватьСтатусыЗаказовПоставщикам,
		|ИспользоватьКомиссиюПриЗакупках,
		|КонтролироватьОстаткиТоваровОрганизацийКОформлениюПоПоступлениям,
		|ИспользоватьСкладыВТабличнойЧастиДокументовЗакупки,
		|ИспользоватьИмпортныеТовары");
	
	
	Для Каждого КлючИЗначение Из СтруктураКонстант Цикл
		ОбщегоНазначенияУТКлиентСервер.ОтображениеПредупрежденияПриРедактировании(
			Элементы[КлючИЗначение.Ключ],
			НаборКонстант[КлючИЗначение.Ключ]);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Прочие

&НаСервере
Процедура УстановитьДоступностьВерсионированияДокументовЗакупки()
	ИспользоватьВерсионированиеОбъекта = ОбщегоНазначенияУТ.ИспользоватьВерсионированиеОбъекта("Документ.ЗаказПоставщику");
	Элементы.ВключитьВерсионированиеДокументовЗакупки.Доступность =
		НаборКонстант.ИспользоватьСогласованиеЗаказовПоставщикам
		И (НЕ НаборКонстант.ИспользоватьВерсионированиеОбъектов
			ИЛИ НЕ ИспользоватьВерсионированиеОбъекта);
			
	Если НЕ ИспользоватьВерсионированиеОбъекта Тогда
		Элементы.ВключитьВерсионированиеДокументовЗакупки.Заголовок = НСтр("ru='Включить версионирование'");
	Иначе
		Элементы.ВключитьВерсионированиеДокументовЗакупки.Заголовок = НСтр("ru='Версионирование включено'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере 
Процедура УстановитьИспользованиеЗаказовПоставщикамИЗаявокНаРасходованиеДС(Использование)
	
	Константы.ИспользоватьЗаказыПоставщикамИЗаявкиНаРасходованиеДС.Установить(Использование);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИспользованиеКонтролироватьОстаткиНомеровГТДПриИмпортеПоСхемеТоварыВПути(Использование)
	
	Константы.КонтролироватьОстаткиНомеровГТДПриИмпортеПоСхемеТоварыВПути.Установить(Использование);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
